//****************************************************************************
// SPHERE by : Menasoft ©1997-2016
// www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: Thursday, Dec 22, 2016
//
VERSION=0.56d


// SphereServer 0.56* Administration console
//
// Written because the old one was hardcoded, didn't look very nice
// and crashed clients on servers with alot of players on
//
// This is ment for clients with a 800x600 gameplay window size
//
// Written by Radiant
//
//Changelog:
//- Start position of dialogs set to 0,21
//- Default page of the tweaking menu set to "action"
//- + shown before account name for higher privs and - before guest accounts (like the old admin panel)
//- Removed PLevel informations in main page to get more space for char name
//- Closing tweak dialog brings back to main page
//- Added number of online clients in the main page title and changed "Administration Console" to "Admin Panel", which is shorter
//- Added "remove note" button
//- Added character updates when modifying flags
// -Added ability to hide status flags by giving them an empty title
// -Updated admin_flag_20 (statf_hovering)
// -Hidden admin_flag_22 (unused)

[DEFNAME admin_options]
// If you do not want clients of lower or equal plevel see the IP addresses
// of clients of equal or higher plevel, set this to 1
// (does not apply to plevel 7)
admin_hideips 0
// If you do not want clients to see clients with a higher plevel that are
// invisible (.invis) in the list, set this to 1
admin_hidehighpriv 0
// When a flag is set or unset, it is sometimes necessary for the character
// to be updated for the new setting to take effect. The settings below
// control whether to call UPDATEX, UPDATE or RESENDTOOLTIP on the character
// based on which flags have been modified.
admin_updatexflags      statf_invisible|statf_insubstantial|statf_hidden
admin_updateflags      statf_invul|statf_freeze|statf_stone|statf_hallucinating|statf_poisoned|statf_criminal|statf_nightsight|statf_hovering
admin_resendtooltipflags   statf_incognito

[DEFNAME admin_strings]
// Titles for privilege levels
admin_plevel_0       "Guest"
admin_plevel_1       "Player"
admin_plevel_2       "Counselor"
admin_plevel_3       "Seer"
admin_plevel_4       "Game Master"
admin_plevel_5       "Developer"
admin_plevel_6       "Administrator"
admin_plevel_7       "Owner"
// Titles for UO expansion
admin_resdisp_0      "Pre-T2A"
admin_resdisp_1      "The 2nd Age"
admin_resdisp_2      "Lord Blackthorn's Revenge"
admin_resdisp_3      "Age Of Shadows"
admin_resdisp_4      "Samurai Empire"
admin_resdisp_5      "Mondain's Legacy"
admin_resdisp_6      "Kingdom Reborn"
admin_resdisp_7      "Stygian Abyss"
admin_resdisp_8      "High Seas"
admin_resdisp_9      "Time of Legends"
// Titles for flags, copied from axis (slightly modified)
admin_flag_1         "Invulnerability"
admin_flag_2         "Dead"
admin_flag_3         "Paralyzed"
admin_flag_4         "Invisible"
admin_flag_5         "Sleeping"
admin_flag_6         "Warmode"
admin_flag_7         "Reactive Armor"
admin_flag_8         "Poisoned"
admin_flag_9         "Nightsight"
admin_flag_10        "Magic Reflect"
admin_flag_11        "Polymorphed"
admin_flag_12        "Incognito"
admin_flag_13        "Spirit Speak"
admin_flag_14        "Insubstantial"
admin_flag_15        "Emote"
admin_flag_16        "Comm Crystal"
admin_flag_17        "Has Shield"
admin_flag_18        "Can move with bow"
admin_flag_19        "Stoned"
admin_flag_20        "Hovering"
admin_flag_21        "Flying"
admin_flag_22        "" // unused
admin_flag_23        "Hallucinating"
admin_flag_24        "Hidden"
admin_flag_25        "Indoors"
admin_flag_26        "Criminal"
admin_flag_27        "Conjured (summoned)"
admin_flag_28        "Pet"
admin_flag_29        "Spawned"
admin_flag_30        "Save parity"
admin_flag_31        "Ridden"
admin_flag_32        "Mounted on horseback"
// Don't edit below this line unless you know what you are doing
ADMIN_PAGE_LIST         0
ADMIN_PAGE_ACTIONS      1
ADMIN_PAGE_CHAR         2
ADMIN_PAGE_SKILLS       3
ADMIN_PAGE_COMMENTS     4
ADMIN_PAGE_FLAGS        5
ADMIN_PAGE_ACC          6
ADMIN_PAGE_NOTES        7
ADMIN_PAGE_KILLS        8
ADMIN_PAGE_DEATHS       9
ADMIN_PAGE_JAILS        10

ADMIN_ACTION_0          Listando
ADMIN_ACTION_1          Acoes
ADMIN_ACTION_2          Informacoes sobre o personagem
ADMIN_ACTION_3          Skills do personagem
ADMIN_ACTION_4          Comentarios sobre representacao do personagem
ADMIN_ACTION_5          Flags do personagem
ADMIN_ACTION_6          Informacoes sobre a conta
ADMIN_ACTION_7          Notas sobre o jogador e sua conta
ADMIN_ACTION_8          Quem ele assassinou
ADMIN_ACTION_9          Quem ja matou ele
ADMIN_ACTION_10         Registros de prisao nao-RP

ACCOUNT_CAN_DROW        1
ACCOUNT_CAN_ORC         2


[FUNCTION admin]
if ( <getreftype> == <def.tref_serv> )
    serv.console C
    return
elseif ( !<isplayer> )
    return
endif

src.dialogclose d_sphereadmin
ctag.admin_numplayers=0
ctag.admin_page=1
serv.allclients admin_getplayers
ctag.admin_numpages=<eval (((<ctag0.admin_numplayers>-1) / 15) + 1)> // 15 players per page
ctag.admin_action=ADMIN_PAGE_LIST
ctag.admin_task=ADMIN_PAGE_LIST
dialog d_sphereadmin

[FUNCTION admin_getplayers]
// List players in CTAGs on the caller
if ((<account.plevel> > <src.account.plevel>) && (<flags> & statf_insubstantial) && (<def0.admin_hidehighpriv>))
   return
else
   src.ctag0.admin_numplayers += 1
   try src.ctag.admin_p<eval (<src.ctag0.admin_numplayers>)>=<uid>
endif

[FUNCTION admin_RenderButtons]
//button 615 5 4017 4018 1 0 0
button 585 5 4008 4009 1 0 1
dorigin 5 35
button - - 4005 4006 1 1 101
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_ACTIONS?035:0480> Acoes
button - *20 4005 4006 1 1 102
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_CHAR?035:0480> Info do char
button - *20 4005 4006 1 1 103
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_SKILLS?035:0480> Skills do char
button - *20 4005 4006 1 1 104
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_COMMENTS?035:0480> Comentarios RP
button - *20 4005 4006 1 1 105
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_FLAGS?035:0480> Flags
button - *20 4005 4006 1 1 108
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_KILLS?035:0480> Assassinatos
button - *20 4005 4006 1 1 109
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_DEATHS?035:0480> Mortes
button - *40 4005 4006 1 1 106
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_ACC?035:0480> Info da conta
button - *20 4005 4006 1 1 107
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_NOTES?035:0480> Notas da conta
button - *20 4005 4006 1 1 110
dtext +35 +2 <QVAL <ctag.admin_action>==ADMIN_PAGE_JAILS?035:0480> Prisoes da conta

[FUNCTION admin_getclienttype]
if (<clientiskr>) || (<clientissa>) 
 return "Enhanced Client"
elif (<clientis3d>)
 return "Classic Client 3D"
else
 return "Classic Client 2D"
endif

[DIALOG d_sphereadmin]
75,75
// Let's not break some (perhaps) scripts using OBJ
local.oldobj=<obj>
page 0 // Basic layout
resizepic 0 0 2620 650 397 // Background

IF (<ctag0.admin_action>==ADMIN_PAGE_LIST)
    gumppictiled 5 5 640 385 2604
    gumppictiled 5 5 640 25 2624 
    gumppictiled 5 370 640 22 2624
    gumppictiled 5 35 640 330 2624
    checkertrans 5 5 640 387
    dtext 10 7 0c1 SphereServer <src.version> Admin Panel (<serv.servname>) - Clients: <eval <ctag0.admin_numplayers>>
ELSE
    gumppictiled 5 5 640 385 2604
    gumppictiled 5 5 640 25 2624 
    gumppictiled 5 35 140 355 2624
    gumppictiled 150 35 495 355 2624
    checkertrans 5 5 640 387
    ref1=<ctag0.admin_player>
    dtext 10 7 0c1 Jogador: <ref1.tag.name> [<ref1.account>]  -  <DEF.ADMIN_ACTION_<dcTAG0.admin_action>>
ENDIF
button 615 5 4017 4018 1 0 0

IF (<ctag0.admin_action>==ADMIN_PAGE_LIST)
    //page 1 // Player data
    dtext 45 35 025 Nome da conta
    dtext 170 35 025 Nome do char
    dtext 380 35 025 IP Address
    dtext 520 35 025 Local
    //dtext 540 35 025 PLevel
    admin_renderdialog
    obj=<local.oldobj>
    if (<ctag0.admin_page> > 1)
        // Back button
        button 5 370 4014 4015 1 0 5
    endif
    if (<ctag0.admin_page> < <ctag0.admin_numpages>)
        // Forward button
        button 615 370 4005 4006 1 0 6
    endif
    dtext 275 370 0c1 Pagina <eval <ctag0.admin_page>> / <eval <ctag0.admin_numpages>>

ELIF (<ctag0.admin_action>==ADMIN_PAGE_ACTIONS)
    admin_RenderButtons
    dorigin 160 40
    button - - 4005 4006 1 0 121
    dtext +40 - 0480 Ir ao jogador
    button - *20 4005 4006 1 0 122
    dtext +40 - 0480 Ligar invis e ir ao jogador
    button - *20 4005 4006 1 0 123
    dtext +40 - 0480 Trazer o jogador ateh voce
    button - *20 4005 4006 1 0 124
    dtext +40 - 0480 Trazer o jogador ateh voce numa jaula
    button - *20 4005 4006 1 0 125
    dtext +40 - 0480 Seguir o jogador
    button - *20 4005 4006 1 0 126
    dtext +40 - 0480 Mandar uma mensagem
    button - *40 4005 4006 1 0 127
    dtext +40 - 035 Apagar personagem
    button - *20 4005 4006 1 0 128
    dtext +40 - 035 Suspender pages deste jogador por...
    button - *20 4005 4006 1 0 135
    dtext +40 - 035 Dar .aid neste personagem
    button - *20 4005 4006 1 0 129
    dtext +40 - 035 <qval (<uid.<ctag0.admin_player>.flags> & statf_dead)?Ressucitar:Desmaiar> este jogador
    if (<uid.<ctag0.admin_player>.restest i_mry_gm_jail>)
        button - *40 4005 4006 1 0 130
        dtext +40 - 025 Soltar jogador
    else
        button - *40 4005 4006 1 0 131
        dtext +40 - 025 Prender jogador
        button - *20 4005 4006 1 0 132
        dtext +40 - 025 Prender o jogador por macro
    endif
    button - *20 4005 4006 1 0 133
    dtext +40 - 025 Disconectar o jogador
    button - *20 4005 4006 1 0 134
    dtext +40 - 025 Banir o jogador
    
ELIF (<ctag0.admin_action>==ADMIN_PAGE_CHAR)
    admin_RenderButtons
    obj=<ctag0.admin_player>
    DB.QUERY "SELECT grade FROM rpGrade WHERE charUid=<eval <obj>>"
    local.notes=<DB.ROW.NUMROWS>
    DB.QUERY "SELECT round(avg(grade)) AS grade FROM rpGrade WHERE charUid=<eval <obj>>"
    local.avg=<DB.ROW.0.grade>
    DB.QUERY "SELECT grade FROM rpGrade WHERE charUid=<eval <obj>> AND gmAcct=<dtag0.idAcct>"
    if (!<DB.ROW.NUMROWS>)
        local.mynote=Vc nao avaliou
    else
        local.mynote=Sua nota: <eval (<DB.ROW.0.grade>+10)/2>
    endif
    DB.QUERY "SELECT count(idEvent) AS cnt FROM eventsLog WHERE char2Uid=<obj.duid> AND eventtype='Jail'"
    local.jails=<DB.ROW.0.cnt>
    DB.QUERY "SELECT count(idEvent) AS cnt FROM eventsLog WHERE charUID=<obj.duid> AND eventtype='Kill PC'" 
    local.pklling=<DB.ROW.0.cnt>
    DB.QUERY "SELECT count(idEvent) AS cnt FROM eventsLog WHERE char2UID=<obj.duid> AND eventtype='Kill PC'" 
    local.pklled=<DB.ROW.0.cnt>
    DB.QUERY "SELECT count(kind) AS cnt FROM background WHERE uid=<obj.duid> AND kind < 7;"
    local.ficharp=<DB.ROW.0.cnt>
    dorigin 160 35
    dtext - - 0c1 Nome:
    dtext +140 - 0480 <obj.tag.name>
    dtext +315 - 0c1 Uid:
    dtext +390 - 0480 <obj.uid>
    dtext - *20 0c1 Conta:
    dtext +140 - 0480 <obj.account.name>
    dtext - *20 0c1 Classe:
    dtext +140 - 0480 <obj.tag.classe>
    dtext +315 - 0c1 Raca:
    dtext +390 - 0480 <obj.tag0.raca>
    dtext - *20 0c1 Nota de RP:
    dtext +140 - 0480 <eval (<local.avg>+10)/2> em <dlocal.notes> avaliacoes. <local.mynote>
    dtext - *20 0c1 Pontos de alma:
    dtext +140 - 0480 <obj.dtag0.pontosdealma>/5
    dtext +315 - 0c1 Desmaios:
    dtext +390 - 0480 <obj.dtag0.morte_desmaios>/<eval <DEF.max_desmaios>>
    dtext - *20 0c1 Tempo de vida:
    dtext +140 - 0480 <f_timestring <obj.f_age>>
    dtext +315 - 0c1 Prisoes:
    dtext +390 - 0480 <dlocal.jails>
    dtext - *20 0c1 Total de skill:
    dtext +140 - 0480 <fval <obj.skilltotal>>/<fval <obj.TAG0.skill_maxSkillSumValue>>
    dtext +315 - 0c1 Skill maxima:
    dtext +390 - 0480 <fval <obj.TAG0.skill_maxSkillValue>>
    dtext - *20 0c1 PKlling:
    dtext +60 - 0480 <dlocal.pklling>
    dtext +315 - 0c1 Dinheiro:
    dtext +365 - 0480 <STRTRESURE <obj.bank_checkCash 0,1>>
    dtext - *20 0c1 PKlled:
    dtext +60 - 0480 <dlocal.pklled>
    dtext - *20 0c1 STR:
    dtext +60 - 0480 <obj.str>
    dtext - *20 0c1 DEX:
    dtext +60 - 0480 <obj.dex>
    dtext - *20 0c1 INT:
    dtext +60 - 0480 <obj.INT>
    dtext - *20 0c1 HITS:
    dtext +60 - 0480 <obj.hits>/<obj.maxhits>
    dtext - *20 0c1 STAM:
    dtext +60 - 0480 <obj.stam>/<obj.maxstam>
    dtext - *20 0c1 MANA:
    dtext +60 - 0480 <obj.mana>/<obj.maxmana>
    dtext - *20 0c1 WEIGHT:
    dtext +60 - 0480 <fval <obj.WEIGHT>>/<fval <obj.maxweight>>
    dtext - *20 0c1 FOOD:
    dtext +60 - 0480 <fval <obj.food>>/100.0
    dorigin 320 180
    dtext - - 025 Avaliar RP:
    if (<local.ficharp> >4 )
        FOR r 0 10
            dtext +23 *17 035 <dlocal.r>
            button - +4 2224 2224 1 1 <eval <dlocal.r>+300>
        ENDFOR
    else
        FOR r 0 5
            dtext +23 *17 035 <dlocal.r>
            button - +4 2224 2224 1 1 <eval <dlocal.r>+300>
        ENDFOR
        dtext - *17 025 Ficha RP incompleta!
        dtext - *17 025 Notas boas bloqueadas
    endif
    dorigin 475 200
    dtext - - 025 Morte:
    dtext +23 *17 035 + Alma
    button - +4 2224 2224 1 1 311
    dtext +23 *17 035 - Alma
    button - +4 2224 2224 1 1 312
    dtext +23 *17 035 + Desmaio
    button - +4 2224 2224 1 1 313
    dtext +23 *17 035 - Desmaio
    button - +4 2224 2224 1 1 314

ELIF (<ctag0.admin_action>==ADMIN_PAGE_SKILLS)
    admin_RenderButtons
    obj=<ctag0.admin_player>
    dorigin 160 15
    FOR s 0 54
        if (<local.s>==22)
            dorigin 320 15
        elif (<local.s>==44)
            dorigin 480 15
        endif
        dtext - *16 0c1 <serv.skill.<dlocal.s>.name>:
        local.val=<obj.<local.s>>
        local.hue=012F
        DOSWITCH <eval <local.val>/100>
            local.hue=0ee
            local.hue=0f3
            local.hue=0fd
            local.hue=0102
            local.hue=0107
            local.hue=010c
            local.hue=0111
            local.hue=0116
            local.hue=011b
            local.hue=0120
            local.hue=0125
            local.hue=012A
            local.hue=012F
        ENDDO
        dtext +95 - <local.hue> <fval <local.val>>
    END
    
ELIF (<ctag0.admin_action>==ADMIN_PAGE_COMMENTS)
    obj=<ctag0.admin_player>
    admin_RenderButtons
    ctag.admin_RP_page=<MAXIMUM 1,<ctag0.admin_RP_page>>
    DB.QUERY="SELECT count(eventType) AS cnt FROM eventsLog WHERE char2Uid=<dctag0.admin_player> AND eventType='RP note';" 
    dorigin 160 20
    IF (!<DB.ROW.0.cnt>)
        dtext - *26 0c1 Sem registro. Nenhum GM fez comentarios sobre a representacao del<obj.sex e/a>.
    else
        ctag.admin_RP_numpages=<eval (<DB.ROW.0.cnt>/9)+1>
        local.min=<eval (<ctag0.admin_RP_page>-1)*9>
        DB.QUERY="SELECT DATE_FORMAT(t1.timestamp,'%d/%m/%Y %H:%i') AS timestamp, t1.charAcct, t1.message, t2.name FROM eventsLog AS t1, accounts AS t2 WHERE t1.char2Uid=<dctag0.admin_player> AND t1.eventType='RP note' AND t2.idAcct=t1.charAcct ORDER BY timestamp DESC LIMIT <dlocal.min>,9;" 
        FOR m 0 <eval <DB.ROW.NUMROWS>-1>
            dtext - *23 025 GM <DB.ROW.<dlocal.m>.name>
            dtext +200 - 0c1 DATA: <DB.ROW.<dlocal.m>.timestamp>
            dtext - *13 0480 <DB.ROW.<dlocal.m>.message>
        ENDFOR
        if (<ctag0.admin_RP_page> > 1)
            // Back button
            button 150 370 4014 4015 1 0 700
        endif
        if (<ctag0.admin_RP_page> < <ctag0.admin_RP_numpages>)
            // Forward button
            button 615 370 4005 4006 1 0 701
        endif
    endif
    button 580 370 4030 4031 1 0 702
    dtext 325 370 0c1 Pagina <eval <ctag0.admin_RP_page>> / <eval <ctag0.admin_RP_numpages>>        
    
ELIF (<ctag0.admin_action>==ADMIN_PAGE_FLAGS)
    obj=<ctag0.admin_player>
    admin_RenderButtons
    local.flag=01
    local.ox=160
    local.oy=40
    for x 0 31
        checkbox <eval <local.ox>> <eval <local.oy>> 210 211 <hval (<obj.flags> & <local.flag>)> <eval 300 + <local.x>>
        dtext <eval <local.ox> + 40> <eval <local.oy>> 0480 <def0.admin_flag_<eval <local.x> + 1>>
        // Workaround for sphere bug - local.flag=<hval <local.flag> << 1> attempts to evaluate << !
        local.flag="<local.flag> << 1"
        local.flag=<hval <local.flag>>
        local.oy += 20
        if (<local.oy> > 360)
            local.ox += 200
            local.oy = 40
        endif
    end
    button 360 360 4005 4006 1 0 500
    dtext 400 360 0c1 Alterar flags

ELIF (<ctag0.admin_action>==ADMIN_PAGE_ACC)
    obj=<ctag0.admin_player>
    admin_RenderButtons
    dorigin 160 35
    dtext - - 0c1 Conta:
    dtext +50 - 0480 <obj.account.name>
    dtext +200 - 0c1 Tempo conectado:
    dtext +300 - 0480 <f_timestring <eval <obj.account.TOTALCONNECTTIME>*60>>
    dtext - *20 0c1 Chars:
    dtext +50 - 0480 <obj.account.chars>/<obj.account.maxchars>
    dtext +200 - 0c1 Banido:
    dtext +300 - <QVAL <obj.account.block>?025 Sim:0480 Nao>
    dtext - *20 0c1 Criacao:
    dtext +50 - 0480 <obj.account.FIRSTCONNECTDATE>
    dtext +200 - 0c1 Ultimo login:
    dtext +300 - 0480 <obj.account.LASTCONNECTDATE>
    dtext - *20 0c1 Primeiro IP:
    dtext +100 - 0480 <obj.account.FIRSTIP>
    dtext +200 - 0c1 Ultimo IP:
    dtext +300 - 0480 <obj.account.LASTIP>
    dtext - *25 025 Personagens:
    for c 0 <eval <obj.account.chars>-1>
        button - *25 2224 2224 1 0 <eval <dlocal.c>+600>
        dtext +20 -4 0480 <obj.account.char.<dlocal.c>.tag.name>
    end
    dorigin 360 95
    dtext - *25 025 Racas:
    button - *25 <QVAL <obj.account.tag0.races>&ACCOUNT_CAN_DROW?211 210:210 211> 1 0 650
    dtext +20 - 0480 Pode ser Drow
    button - *25 <QVAL <obj.account.tag0.races>&ACCOUNT_CAN_ORC?211 210:210 211> 1 0 651
    dtext +20 - 0480 Pode ser Orc

ELIF (<ctag0.admin_action>==ADMIN_PAGE_NOTES)
    obj=<ctag0.admin_player>
    admin_RenderButtons
    ctag.admin_note_page=<MAXIMUM 1,<ctag0.admin_note_page>>
    DB.QUERY="SELECT count(eventType) AS cnt FROM eventsLog WHERE char2Acct=<obj.dtag.idAcct> AND eventType='GM note';" 
    dorigin 160 20
    if (!<DB.ROW.0.cnt>)
        dtext - *26 035 Sem registro. Nenhum GM fez comentarios sobre o comportamento do jogador.
    else
        ctag.admin_note_numpages=<eval (<DB.ROW.0.cnt>/9)+1>
        local.min=<eval (<ctag0.admin_note_page>-1)*9>
        DB.QUERY="SELECT DATE_FORMAT(t1.timestamp,'%d/%m/%Y %H:%i') AS timestamp, t1.charAcct, t1.message, t2.name FROM eventsLog AS t1, accounts AS t2 WHERE t1.char2Acct=<obj.dtag.idAcct> AND t1.eventType='GM note' AND t2.idAcct=t1.charAcct ORDER BY timestamp DESC LIMIT <dlocal.min>,9;" 
        FOR m 0 <eval <DB.ROW.NUMROWS>-1>
            dtext - *23 025 GM <DB.ROW.<dlocal.m>.name>
            dtext +200 - 0c1 DATA: <DB.ROW.<dlocal.m>.timestamp>
            dtext - *13 035 <DB.ROW.<dlocal.m>.message>
        ENDFOR
        if (<ctag0.admin_note_page> > 1)
            // Back button
            button 150 370 4014 4015 1 0 800
        endif
        if (<ctag0.admin_note_page> < <ctag0.admin_note_numpages>)
            // Forward button
            button 615 370 4005 4006 1 0 801
        endif
    endif
    button 580 370 4030 4031 1 0 802
    dtext 325 370 0c1 Pagina <eval <ctag0.admin_note_page>> / <eval <ctag0.admin_note_numpages>>        

ELIF (<ctag0.admin_action>==ADMIN_PAGE_KILLS)
    obj=<ctag0.admin_player>
    admin_RenderButtons
    ctag.admin_kills_page=<MAXIMUM 1,<ctag0.admin_kills_page>>
    DB.QUERY="SELECT count(eventType) AS cnt FROM eventsLog WHERE charUid=<eval <obj>> AND eventType='Kill PC';" 
    dorigin 160 20
    if (!<DB.ROW.0.cnt>)
        dtext - *26 0c1 Sem registro. <obj.sex Ele/Ela> nunca assassinou alguem diretamente.
    else
        ctag.admin_kills_numpages=<eval (<DB.ROW.0.cnt>/8)+1>
        local.min=<eval (<ctag0.admin_kills_page>-1)*8>
        DB.QUERY="SELECT DATE_FORMAT(t1.timestamp,'%d/%m/%Y %H:%i') AS timestamp, t1.char2Uid, t1.char2Acct, t1.x, t1.y, t1.z, t2.username FROM eventsLog AS t1, accounts AS t2 WHERE t1.charUid=<eval <obj>> AND t1.eventType='Kill PC' AND t2.idAcct=t1.char2Acct ORDER BY timestamp DESC LIMIT <dlocal.min>,8;" 
        FOR m 0 <eval <DB.ROW.NUMROWS>-1>
            dtext - *23 0c1 Vitima:
            dtext +45 - 0480 <uid.<DB.ROW.<dlocal.m>.char2Uid>.tag.name> [<DB.ROW.<dlocal.m>.username>] (UID:<DB.ROW.<dlocal.m>.char2Uid>)
            dtext +300 - 0c1 Data:
            dtext +345 - 0480 <DB.ROW.<dlocal.m>.timestamp>
            dtext - *16 0c1 Local:
            dtext +45 - 0480 <serv.map(<DB.ROW.<dlocal.m>.x>,<DB.ROW.<dlocal.m>.y>,0).region.name> (<DB.ROW.<dlocal.m>.x>,<DB.ROW.<dlocal.m>.y>,<DB.ROW.<dlocal.m>.z>)
            
        ENDFOR
        if (<ctag0.admin_kills_page> > 1)
            // Back button
            button 150 370 4014 4015 1 0 900
        endif
        if (<ctag0.admin_kills_page> < <ctag0.admin_kills_numpages>)
            // Forward button
            button 615 370 4005 4006 1 0 901
        endif
    endif
    dtext 325 370 0c1 Pagina <eval <ctag0.admin_kills_page>> / <eval <ctag0.admin_kills_numpages>>      

ELIF (<ctag0.admin_action>==ADMIN_PAGE_DEATHS)
    obj=<ctag0.admin_player>
    admin_RenderButtons
    ctag.admin_deaths_page=<MAXIMUM 1,<ctag0.admin_deaths_page>>
    DB.QUERY="SELECT count(eventType) AS cnt FROM eventsLog WHERE char2Uid=<eval <obj>> AND eventType='Kill PC';" 
    dorigin 160 20
    if (!<DB.ROW.0.cnt>)
        dtext - *26 0c1 Sem registro. <obj.sex Ele/Ela> nunca morreu pelas maos de um jogador.
    else
        ctag.admin_deaths_numpages=<eval (<DB.ROW.0.cnt>/8)+1>
        local.min=<eval (<ctag0.admin_deaths_page>-1)*8>
        DB.QUERY="SELECT DATE_FORMAT(t1.timestamp,'%d/%m/%Y %H:%i') AS timestamp, t1.charUid, t1.charAcct, t1.x, t1.y, t1.z, t2.username FROM eventsLog AS t1, accounts AS t2 WHERE t1.char2Uid=<eval <obj>> AND t1.eventType='Kill PC' AND t2.idAcct=t1.charAcct ORDER BY timestamp DESC LIMIT <dlocal.min>,8;" 
        FOR m 0 <eval <DB.ROW.NUMROWS>-1>
            dtext - *23 0c1 Assassino:
            dtext +60 - 0480 <uid.<DB.ROW.<dlocal.m>.charUid>.tag.name> [<DB.ROW.<dlocal.m>.username>] (UID:<DB.ROW.<dlocal.m>.charUid>)
            dtext +300 - 0c1 Data:
            dtext +345 - 0480 <DB.ROW.<dlocal.m>.timestamp>
            dtext - *16 0c1 Local:
            dtext +45 - 0480 <serv.map(<DB.ROW.<dlocal.m>.x>,<DB.ROW.<dlocal.m>.y>,0).region.name> (<DB.ROW.<dlocal.m>.x>,<DB.ROW.<dlocal.m>.y>,<DB.ROW.<dlocal.m>.z>)
            
        ENDFOR
        if (<ctag0.admin_deaths_page> > 1)
            // Back button
            button 150 370 4014 4015 1 0 950
        endif
        if (<ctag0.admin_deaths_page> < <ctag0.admin_deaths_numpages>)
            // Forward button
            button 615 370 4005 4006 1 0 951
        endif
    endif
    dtext 325 370 0c1 Pagina <eval <ctag0.admin_deaths_page>> / <eval <ctag0.admin_deaths_numpages>>      

ELIF (<ctag0.admin_action>==ADMIN_PAGE_JAILS)
    obj=<ctag0.admin_player>
    admin_RenderButtons
    ctag.admin_jails_page=<MAXIMUM 1,<ctag0.admin_jails_page>>
    DB.QUERY "SELECT count(idEvent) AS cnt FROM eventsLog WHERE char2Acct=<obj.dtag0.idAcct> AND eventtype='Jail'"
    dorigin 160 20
    if (!<DB.ROW.0.cnt>)
        dtext - *26 0c1 Sem registro. <obj.sex Ele/Ela> nunca foi pres<obj.sex o/a> por mau comportamento.
    else
        ctag.admin_jails_numpages=<eval (<DB.ROW.0.cnt>/6)+1>
        local.min=<eval (<ctag0.admin_jails_page>-1)*6>
        DB.QUERY="SELECT DATE_FORMAT(timestamp,'%d/%m/%Y %H:%i') AS timestamp, charUid, charAcct, x, y, z, message FROM eventsLog WHERE char2Acct=<obj.dtag0.idAcct> AND eventType='Jail' ORDER BY timestamp DESC LIMIT <dlocal.min>,6;" //AND t2.idAcct=t1.charAcct 
        FOR m 0 <eval <DB.ROW.NUMROWS>-1>
            if (<uid.<DB.ROW.<dlocal.m>.charUid>.IsChar>)
                dtext - *23 035 <uid.<DB.ROW.<dlocal.m>.charUid>.tag.name> [<uid.<DB.ROW.<dlocal.m>.charUid>.account>]
            else
                dtext - *23 025 Servidor ou char de GM deletado
            endif
            dtext +300 - 0c1 Data:
            dtext +345 - 0480 <DB.ROW.<dlocal.m>.timestamp>
            dtext - *16 0c1 Local:
            dtext +45 - 0480 <serv.map(<DB.ROW.<dlocal.m>.x>,<DB.ROW.<dlocal.m>.y>,0).region.name> (<DB.ROW.<dlocal.m>.x>,<DB.ROW.<dlocal.m>.y>,<DB.ROW.<dlocal.m>.z>)
            dtext - *16 0480 <DB.ROW.<dlocal.m>.message>
        ENDFOR
        if (<ctag0.admin_jails_page> > 1)
            // Back button
            button 150 370 4014 4015 1 0 960
        endif
        if (<ctag0.admin_jails_page> < <ctag0.admin_jails_numpages>)
            // Forward button
            button 615 370 4005 4006 1 0 961
        endif
    endif
    dtext 325 370 0c1 Pagina <eval <ctag0.admin_jails_page>> / <eval <ctag0.admin_jails_numpages>>   
    
ENDIF

[FUNCTION admin_renderdialog]
local.y=55
for x <eval ((<ctag0.admin_page> - 1) * 15) + 1> <eval (<ctag0.admin_page> * 15)>
   if (<local.x> > <ctag0.admin_numplayers>)
      return
   endif
   obj=<ctag0.admin_p<eval <local.x>>>
   if (<obj.flags> & statf_insubstantial)
      local.hue=0450
   else
      local.hue=0480
   endif
   button 10 <local.y> 4005 4006 1 0 <eval (1000 + <local.x>)>
    dtext 45 <local.y> <local.hue> <QVAL <OBJ.ACCOUNT.PLEVEL>==0?(-):<QVAL <OBJ.ACCOUNT.PLEVEL>==1?:<QVAL <OBJ.IsGM>?(GM):>(P<eval <OBJ.ACCOUNT.PLEVEL>>)>> <obj.account>
    dtext 170 <local.y> <local.hue> <QVAL <OBJ.FINDID.i_rune_incognito.UID> ? <OBJ.FINDID.i_rune_incognito.NAME> INCOGNITO (<obj.tag.name> - <obj.tag.raca> - <obj.tag.classe>) : <QVAL <IsEmpty <obj.tag.name>> ? <obj.name> (novo) : <STRSUB 0 1 <obj.tag.raca>>: <obj.tag.name> (<obj.tag.classe>)>>
    // Hide IP address to people of lower or equal privilege, if desired
   if ((<obj.uid> != <uid>) && (<account.plevel> <= <obj.account.plevel>) && (<def0.admin_hideips>) && (<account.plevel> != 7))
      dtext 380 <local.y> <local.hue> x.x.x.x
   else
      dtext 380 <local.y> <local.hue> <obj.account.lastip>
   endif
   dtext 520 <local.y> <local.hue> <obj.p.x>,<obj.p.y>,<obj.p.z>,<obj.p.m>
   //  dtext 540 <local.y> <local.hue> <obj.account.plevel> (<def0.admin_plevel_<eval <obj.account.plevel>>>)
   local.y += 20
end

[DIALOG d_sphereadmin BUTTON]
on=0
IF (<ctag0.admin_action>==ADMIN_PAGE_LIST) || (<ctag0.admin_task>==ADMIN_PAGE_CHAR)
    clearctags admin_
//ELIF (<ctag0.admin_action>!=ADMIN_PAGE_CHAR)
//    ctag.admin_action=ADMIN_PAGE_CHAR
//    DIALOG d_sphereadmin
ELSE
    ADMIN
ENDIF

on=1 // List
    ADMIN
    
on=5//List -page
ctag0.admin_page -= 1
DIALOG d_sphereadmin

on=6//List +page
ctag0.admin_page += 1
DIALOG d_sphereadmin

on=100,110
    ctag.admin_action=<eval <argn>-100>
    DIALOG d_sphereadmin

on=121//go player
go <uid.<ctag.admin_player>.p>

on=122//go player invis
invis 1
go <uid.<ctag.admin_player>.p>

on=123//Bring player
try uid.<ctag.admin_player>.go <p>

on=124//Bring on cage
trysrc <uid> uid.<ctag.admin_player>.summoncage

on=125//Follow
follow <ctag.admin_player>

on=126//Message
InstantMessage <ctag.admin_player>

on=127//Delete char
if (<uid.<ctag.admin_player>.account.plevel> < <account.plevel>) || (<ctag.admin_player>==<UID>)
    ctag.chalenge=<R10000,99999>
    prompt_new Apagar personagem? Digite <dctag.chalenge> para prosseguir.
    prompt_setAction admin_removeChar
    prompt_setActionBack DIALOG d_sphereadmin
    prompt_show
else
    sysmessagered Voce nao tem privilegio para isso.
    DIALOG d_sphereadmin
endif

on=128//Suspend pages for...
prompt_new Desativar as pages deste jogador por quantos minutos? 120=2 horas. 0 desabilita.
prompt_setAction admin_suspendPages
prompt_setActionBack DIALOG d_sphereadmin
prompt_show

on=129//Kill/res
obj=<ctag0.admin_player>
if (<obj.flags>& statf_dead)
    obj.ress
elif (<obj.account.plevel> < <account.plevel>) || (<ctag.admin_player>==<UID>)
    obj.hits=0
else
    sysmessagered Voce nao tem privilegio para isso.
endif
TIMERF 1 DIALOG d_sphereadmin

on=130//Forgive
obj=<ctag0.admin_player>
obj.findid.i_mry_gm_jail.more1=1
obj.findid.i_mry_gm_jail.timerd=1
timerf 1 DIALOG d_sphereadmin

on=131//Jail
jail_Prompt <ctag0.admin_player>

on=132//Jail macroer
src.macroer <ctag0.admin_player>

on=133//Disconnect
obj=<ctag0.admin_player>
if (<obj.account.plevel> < <account.plevel>) || (<ctag.admin_player>==<UID>)
    trysrv obj.disconnect
else
    sysmessagered Voce nao tem privilegio para isso.
endif
DIALOG d_sphereadmin

on=134//Ban
if (<uid.<ctag.admin_player>.account.plevel> < <account.plevel>)
    ctag.chalenge=<R10000,99999>
    prompt_new Banir a conta <uid.<ctag.admin_player>.account.name>? Digite <dctag.chalenge> para prosseguir.
    prompt_setAction admin_banAccount
    prompt_setActionBack DIALOG d_sphereadmin
    prompt_show
else
    sysmessagered Voce nao tem privilegio para isso.
    DIALOG d_sphereadmin
endif

on=135//.aid
obj=<ctag0.admin_player>
obj.aid
DIALOG d_sphereadmin

on=300,310//Setar RP
obj=<ctag0.admin_player>
trysrc <src> obj.rp_setGrade <eval <argn>-300>
obj.rp_updateGrade
DIALOG d_sphereadmin

on=311,314//Morte e desmaios
obj=<ctag0.admin_player>
DOSWITCH <eval <argn>-311>
    obj.morte_incrementaPontos 1
    obj.morte_decrementaPontos 1
    obj.morte_incrementaDesmaios 1
    obj.morte_decrementaDesmaios 1
ENDDO
DIALOG d_sphereadmin

on=500
    obj=<ctag0.admin_player>
    local.updatexflags = <obj.flags>&<def0.admin_updatexflags>
    local.updateflags = <obj.flags>&<def0.admin_updateflags>
    local.resendtooltipflags = <obj.flags>&<def0.admin_resendtooltipflags>
    local.flag=01
    for x 0 31
        if (<argchk[<eval 300 + <local.x>>]>)
            obj.flags |= <local.flag>
        else
            obj.flags &= ~<local.flag>
        endif
        // Workaround for sphere bug - local.flag=<hval <local.flag> << 1> attempts to evaluate << !
        local.flag="<local.flag> << 1"
        local.flag=<hval <local.flag>>
    endfor
    if ((<obj.flags>&<def0.admin_updatexflags>) != <local.updatexflags>)
        obj.updatex
    elseif ((<obj.flags>&<def0.admin_updateflags>) != <local.updateflags>)
        obj.update
    elseif ((<obj.flags>&<def0.admin_resendtooltipflags>) != <local.resendtooltipflags>)
        resendtooltip
    endif
    sysmessageyellow Flags modificados!
    DIALOG d_sphereadmin
    
on=600,610//Choose other char from this acc
    obj=<ctag0.admin_player>
    local.n=<eval <argn>-600>
    ctag.admin_player=<obj.account.char.<local.n>.uid>
    ctag.admin_action=ADMIN_PAGE_CHAR
    DIALOG d_sphereadmin    
    
on=650//Can/can't Drow
    obj=<ctag0.admin_player>
    if (<obj.account.tag0.races>&ACCOUNT_CAN_DROW)
        obj.account.tag.races &= ~ACCOUNT_CAN_DROW
    else
        obj.account.tag.races |= ACCOUNT_CAN_DROW
    endif
    DIALOG d_sphereadmin
    
on=651//Can/can't Orc
    obj=<ctag0.admin_player>
    if (<obj.account.tag0.races>&ACCOUNT_CAN_ORC)
        obj.account.tag.races &= ~ACCOUNT_CAN_ORC
    else
        obj.account.tag.races |= ACCOUNT_CAN_ORC
    endif
    DIALOG d_sphereadmin

on=700//Volta página de notas RP
ctag.admin_RP_page -= 1
DIALOG d_sphereadmin

on=701//Avança página de notas RP
ctag.admin_RP_page += 1
DIALOG d_sphereadmin

on=702//Add RP note
sysmessageyellow Digite uma nota sobre o RP deste personagem e de ENTER:
promptconsole admin_RPnote

on=800//Volta página de notas RP
ctag.admin_note_page -= 1
DIALOG d_sphereadmin

on=801//Avança página de notas RP
ctag.admin_note_page += 1
DIALOG d_sphereadmin

on=802//Add GM note
sysmessageyellow Digite uma nota sobre o COMPORTAMENTO DO JOGADOR e de ENTER:
promptconsole admin_GMnote

on=900//Volta página de Kills
ctag.admin_kills_page -= 1
DIALOG d_sphereadmin

on=901//Avança página de Kills
ctag.admin_kills_page += 1
DIALOG d_sphereadmin

on=950//Volta página de Deaths
ctag.admin_deaths_page -= 1
DIALOG d_sphereadmin

on=951//Avança página de Deaths
ctag.admin_deaths_page += 1
DIALOG d_sphereadmin

on=960//Volta página de Jails
ctag.admin_jails_page -= 1
DIALOG d_sphereadmin

on=961//Avança página de Jails
ctag.admin_jails_page += 1
DIALOG d_sphereadmin

on=1001,65535 // Player buttons
    ctag.admin_action=ADMIN_PAGE_ACTIONS
    ctag.admin_player=<ctag.admin_p<eval <argn> - 1000>>
    DIALOG d_sphereadmin
    //try uid.<ctag.admin_p<eval <argn> - 1000>>.dialog d_sphereplayertweak, 2

[DIALOG d_sphereplayertweak]
0,21
src.dialogclose d_sphereplayertweak
page 0 // Basic layout
resizepic 0 0 2620 650 397 // Background
gumppictiled 5 5 640 385 2604
gumppictiled 5 5 640 25 2624 
gumppictiled 5 35 140 355 2624
gumppictiled 150 35 495 355 2624
checkertrans 5 5 640 387
dtext 10 7 0c1 SphereServer <src.version> Admin Panel (<serv.servname>) - Tweaking Client
//button 615 5 4017 4018 1 0 0
//button 585 5 4008 4009 1 0 1
//button 5 35 4005 4006 0 1 0
//dtext 40 37 0480 Info do client
//button 5 55 4005 4006 0 2 0
//dtext 40 57 0480 Acoes do client
//button 5 77 4005 4006 1 0 2
//dtext 40 77 0480 Notas da conta
//button 5 117 4005 4006 0 4 0
//dtext 40 117 0480 Info do char
//button 5 137 4005 4006 0 3 0
//dtext 40 137 0480 Flags
page 1
dtext 160 35 0c1 Name:
dtext 300 35 0480 <name>
dtext 460 35 0c1 Uid:
dtext 500 35 0480 <uid>
dtext 160 55 0c1 Account Name:
dtext 300 55 0480 <account.name>
dtext 160 75 0c1 Title:
dtext 300 75 0480 <title>
dtext 160 95 0c1 PLevel:
dtext 300 95 0480 <account.plevel> (<def0.admin_plevel_<eval <account.plevel>>>)
dtext 160 135 0c1 Client Version:
dtext 300 135 0480 <clientversion> / <reportedcliver> (<admin_getclienttype>)
dtext 160 155 0c1 Resdisp:
dtext 300 155 0480 <account.resdisp> (<def0.admin_resdisp_<eval <account.resdisp>>>)
dtext 160 195 0c1 First login:
dtext 300 195 0480 <account.firstconnectdate> from <qval ((<src.account.plevel> <= <account.plevel>) && (<def0.admin_hideips>) && (<src.account.plevel> != 7) ? "x.x.x.x" : <account.firstip>>
dtext 160 215 0c1 Last login:
dtext 300 215 0480 <account.lastconnectdate> from <qval ((<src.account.plevel> <= <account.plevel>) && (<def0.admin_hideips>) && (<src.account.plevel> != 7) ? "x.x.x.x" : <account.lastip>>
dtext 160 255 0c1 Location:
dtext 300 255 0480 <p.x>,<p.y>,<p.z>,<p.m>
dtext 160 295 0c1 Kills:
dtext 300 295 0480 <kills>
dtext 360 295 0c1 Deaths:
dtext 540 295 0480 <deaths>
dtext 160 315 0c1 Food level:
dtext 300 315 0480 <food>
page 2
button 160 40 4005 4006 1 0 21
dtext 200 40 0480 Ir ao jogador
button 160 60 4005 4006 1 0 22
dtext 200 60 0480 Ligar invis e ir ao jogador
button 160 80 4005 4006 1 0 23
dtext 200 80 0480 Trazer o jogador ateh voce
button 160 100 4005 4006 1 0 29
dtext 200 100 0480 Trazer o jogador ateh voce numa jaula
button 160 120 4005 4006 1 0 24
dtext 200 120 0480 Seguir o jogador
button 160 140 4005 4006 1 0 25
dtext 200 140 0480 Prender o char
button 160 160 4005 4006 1 0 26
dtext 200 160 0480 <qval (<flags> & statf_dead)?Dar .aid neste:Matar este> char
button 160 180 4005 4006 1 0 27
dtext 200 180 0480 Disconectar o jogador
button 160 200 4005 4006 1 0 28
dtext 200 200 025 Banir o jgador
page 3
local.flag=01
local.ox=160
local.oy=40
for x 0 31
   if !(<isempty <def.admin_flag_<eval <local.x> + 1>>>)
      checkbox <eval <local.ox>> <eval <local.oy>> 210 211 <hval (<flags> & <local.flag>)> <eval 300 + <local.x>>
      dtext <eval <local.ox> + 40> <eval <local.oy>> 0480 <def0.admin_flag_<eval <local.x> + 1>>
      local.oy += 20
      if (<local.oy> > 360)
         local.ox += 200
         local.oy = 40
      endif
   endif

   // Workaround for sphere bug - local.flag=<hval <local.flag> << 1> attempts to evaluate << !
   local.flag="<local.flag> << 1"
   local.flag=<hval <local.flag>>
end
button 360 360 4005 4006 1 0 31
dtext 400 360 0c1 Set flags
page 4
dtext 160 35 0c1 Name:
dtext 300 35 0480 <name>
dtext 460 35 0c1 Uid:
dtext 500 35 0480 <uid>
dtext 160 55 0c1 Title:
dtext 300 55 0480 <title>
dtext 160 95 0c1 Strength:
dtext 300 95 0480 <str>
dtext 360 95 0c1 Hitpoints:
dtext 540 95 0480 <hits>/<maxhits>
dtext 160 115 0c1 Dexterity:
dtext 300 115 0480 <dex>
dtext 360 115 0c1 Stamina:
dtext 540 115 0480 <stam>/<maxstam>
dtext 160 135 0c1 Intelligence:
dtext 300 135 0480 <int>
dtext 360 135 0c1 Mana:
dtext 540 135 0480 <mana>/<maxmana>
dtext 160 155 0c1 Karma:
dtext 300 155 0480 <karma>
dtext 360 155 0c1 Fame:
dtext 540 155 0480 <fame>

[DIALOG d_sphereplayertweak BUTTON]
on=0 1
   src.dialog d_sphereadmin

on=2
   dialog d_sphereplayernotes

on=21
   src.go <p>

on=22
   src.invis 1
   src.go <p>

on=23
   go <src.p>

on=24
   if ( <src.uid> != <uid> )
      src.follow <uid>
   endif
on=25
   if (<account.jail>)
      forgive
   else
      jail
   endif

on=26
   if (<flags> & statf_dead)
      resurrect
   else
      hits=0
      // Use kill here if you want the lightning effect
   endif

on=27
   disconnect

on=28
   kick

on=29
   summoncage

on=31
   local.updatexflags = <flags>&<def0.admin_updatexflags>
   local.updateflags = <flags>&<def0.admin_updateflags>
   local.resendtooltipflags = <flags>&<def0.admin_resendtooltipflags>
   local.flag=01
   for x 0 31
      if !(<isempty <def.admin_flag_<eval <local.x> + 1>>>)
         if (<argchk[<eval 300 + <local.x>>]>)
            flags |= <local.flag>
         else
            flags &= ~<local.flag>
         endif
      endif
      // Workaround for sphere bug - local.flag=<hval <local.flag> << 1> attempts to evaluate << !
      local.flag="<local.flag> << 1"
      local.flag=<hval <local.flag>>
   endfor
   if ((<flags>&<def0.admin_updatexflags>) != <local.updatexflags>)
      updatex
   elseif ((<flags>&<def0.admin_updateflags>) != <local.updateflags>)
      update
   elseif ((<flags>&<def0.admin_resendtooltipflags>) != <local.resendtooltipflags>)
      resendtooltip
   endif
   src.sysmessage Flags modified!

[DIALOG d_sphereplayernotes]
0,21
src.dialogclose d_sphereplayernotes
page 0 // Basic layout
resizepic 0 0 2620 650 397 // Background
gumppictiled 5 5 640 385 2604
gumppictiled 5 5 640 25 2624 
gumppictiled 5 35 140 355 2624
gumppictiled 150 35 495 330 2624
gumppictiled 150 370 495 20 2624
checkertrans 5 5 640 387
dtext 10 7 0c1 SphereServer <src.version> Admin Panel (<serv.servname>) - Account Notes
button 615 5 4017 4018 1 0 0
button 585 5 4008 4009 1 0 1
button 5 35 4005 4006 1 0 2
dtext 40 37 0480 Client info
button 5 55 4005 4006 1 0 3
dtext 40 57 0480 Client actions
button 5 77 4005 4006 0 1 0
dtext 40 77 0480 Client notes
button 5 117 4005 4006 1 0 4
dtext 40 117 0480 Character info
button 5 137 4005 4006 1 0 5
dtext 40 137 0480 Status flags
dtext 200 35 0c1 Here you can review and/or add notes to this player's account.
dtext 200 55 0c1 This account currently has <eval 0<account.tag0.numnotes>> notes. Showing 4 notes per page.
button 160 75 4005 4006 1 0 10
dtext 200 75 0480 Add note to this account (<account>)
if (0<account.tag0.numnotes>)
   local.page=1
   local.oy=120
   page 1
   for x 1 <account.tag0.numnotes>
      if (<local.oy> > 300)
         local.oy = 120
         local.page += 1
         button 615 370 4005 4006 0 <eval <local.page>> 0 // Forward button
         page <eval <local.page>>
         button 150 370 4014 4015 0 <eval <local.page> - 1> 0 // Back button
      endif
      resizepic 155 <eval <local.oy>> 9350 480 50
      dtext 160 <eval <local.oy>> 0 Added by <account.tag0.note_<eval <local.x>>_by> at <account.tag0.note_<eval <local.x>>_time>
      dtext 160 <eval <local.oy> + 15> 0480 <account.tag0.note_<eval <local.x>>>
      button 600 <eval <local.oy>> 4017 4018 1 0 <EVAL 10+<LOCAL.X>>
      local.oy += 60
   end
endif

[DIALOG d_sphereplayernotes BUTTON]
on=0 1
   src.dialog d_sphereadmin

on=2
   dialog d_sphereplayertweak 1

on=3
   dialog d_sphereplayertweak 2

on=4
   dialog d_sphereplayertweak 4

on=5
   dialog d_sphereplayertweak 3

on=10
   if ( <src.account.plevel> > <account.plevel> )
      src.ctag.notefor=<uid>
      src.promptconsole admin_addnote Enter note:
   else
      src.sysmessage You can't add notes to a plevel higher than you
      src.dialog d_sphereplayernotes
   endif

on=11 500
   if ( <src.account.plevel> > <account.plevel> )
      src.ctag.notefor=<uid>
      src.admin_removenote <eval (<ARGN1>-10)>
   else
      src.sysmessage You can't change notes to a plevel higher than you
      src.dialog d_sphereplayernotes
   endif

[FUNCTION admin_removenote]
local.note=<argv[0]>
local.oldobj=<obj>
obj=<ctag.notefor>
IF (<LOCAL.NOTE>==<OBJ.ACCOUNT.TAG.NUMNOTES>)//if it was the last note no need to sort
   TRYSRV OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>>
   TRYSRV OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>>_BY
   TRYSRV OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>>_TIME
ELSE //sort note list
   WHILE (<LOCAL.NOTE> < <ACCOUNT.TAG0.NUMNOTES>)
      TRYSRV OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>>=<OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>+1>>
      TRYSRV OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>>_BY=<OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>+1>_BY>
      TRYSRV OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>>_TIME=<OBJ.ACCOUNT.TAG.NOTE_<EVAL <LOCAL.NOTE>+1>_TIME>
      LOCAL.NOTE += 1
   end
endif
TRYSRV OBJ.ACCOUNT.TAG0.NUMNOTES=<EVAL <OBJ.ACCOUNT.TAG0.NUMNOTES>-1>
SYSMESSAGE Note removed from <obj.account>!
OBJ.DIALOG d_sphereplayernotes
OBJ=<LOCAL.OLDOBJ>

[FUNCTION admin_addnote]
local.oldobj=<obj>
obj=<ctag.notefor>
local.name=<name>
TRYSRV obj.account.tag.numnotes=<eval <obj.account.tag0.numnotes> + 1>
TRYSRV obj.account.tag.note_<eval <obj.account.tag0.numnotes>>=<args>
TRYSRV obj.account.tag.note_<eval <obj.account.tag0.numnotes>>_by=<local.name>
TRYSRV obj.account.tag.note_<eval <obj.account.tag0.numnotes>>_time=<serv.rtime>
sysmessage Note added to account <obj.account.name>!
obj.dialog d_sphereplayernotes
obj=<local.oldobj>

[EOF]

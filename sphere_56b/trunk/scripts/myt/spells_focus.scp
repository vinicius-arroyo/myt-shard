//###################################################################################
// Mystical Tales Shard
// MAGIAS DE CAMINHO "Rastauração"
// Galthar, o Errante
// 04/2009
//###################################################################################
//
// RANGE: 150 a 200
//
//###################################################################################

[COMMENT spells_focus]
nº Ataque
158 Raio divino
166 Tempestade divina

Buff
150 Luz
156 Cura leve
154 Remover Doença
157 Força divina
160 Armadura sagrada
162 Bênção
164 Purificação
168 Luz Divina
169 Redenção

Debuff
152 Exorcísmo
161 Julgamento

Summon
159 Conjurar servo etereo
165 Conjurar nefelibata
167 Conjurar mensageiro

Poly
153 Forma eterea
163 Forma de nefelibata

RP
155 Trovão
151 Amplificar voz

An Negar ou desjurar
Bet Pequeno
Nox Veneno
Corp Morte
Des Menor ou diminuir
Ex Libertar
Flam Chama
Grav Campo
Hur Vento
In Fazer, criar ou causar
Jux Perigo, armadilha ou injúria
Kal Conjurar ou invocar
Lor Luz
Mani Vida ou vitalidade
Ort Magico
Por Mover ou movimento
Quas Ilusão
Rel Modificar
Sanct Proteger ou proteção
Tym Tempo
Uus Maior ou aumentar
Vas Grande
Wis Conhecer ou conhecimento
Xen Criatura
Ylem Materia
Zu Dormir


//////////////////////////////////////////////////
// SPELLS
//////////////////

//***********************************************************
// SPELL RESTORATION Luz
//***********************************************************

[ITEMDEF i_scroll_luz]
ID=0E38
NAME=Pergaminho de Criar Luz
RESOURCES=i_carta,i_r_ginseng,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 20.0, Focus 10.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Luz

ON=@Create
ATTR=ATTR_MAGIC
MOREX=150

[ITEMDEF i_rune_luz]
ID=i_light_source
LAYER=layer_face
NAME=Luz

on=@create
morez=2
ATTR=attr_blessed2

on=@timer
remove
return 1

[SPELL 150]
DEFNAME=s_luz
NAME=Criar Luz
SOUND=snd_SPELL_LIGHT
RUNES=IL
CAST_TIME=20
RESOURCES=i_r_ginseng,i_r_cinza_sulfurosa
RUNE_ITEM=2245
SCROLL_ITEM=i_scroll_luz
FLAGS=spellflag_targ_char | spellflag_scripted
MANAUSE=10
DURATION=3*60,30*60
SKILLREQ=FOCUS 10.0
INTERRUPT=15.0,100.0

on=@Effect
EFFECT 3 i_fx_sparkle 0 16 0
if (<restest 1 i_rune_luz>)
findid.i_rune_luz.remove
return 1
endif
newitem i_rune_luz
new.link=<UID>
trysrc <uid> new.equip
new.timer=<DURATIONSPELL 150,<argn2>,<src.uid>>

skill_gain skill_spiritspeak
return 1

//***********************************************************
// SPELL RESTORATION Amplificar voz
//***********************************************************
//essa skill nao esta funcionando bem, problemas com sendpacket

[ITEMDEF i_scroll_ampli_voz]
ID=0E38
NAME=Pergaminho de Amplificar Voz
RESOURCES=i_carta,i_r_ginseng
SKILLMAKE=INSCRIPTION 20.0, Focus 10.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Amplificar voz

ON=@Create
ATTR=ATTR_MAGIC
MOREX=151

[ITEMDEF i_rune_ampli_voz]
ID=01ed2
name=Amplificar Voz
type=t_eq_script
layer=layer_special

on=@timer
remove
return 1

on=@equip
cont.dspeech +spk_ampli_voz
ATTR=attr_blessed2

on=@unequip
cont.sysmessageyellow Sua voz voltou ao normal.
cont.dspeech -spk_ampli_voz

[SPEECH spk_ampli_voz]
on=*
obj=<uid>
forplayers <findid.i_rune_ampli_voz.more1>
blindItemMsg <obj>, 026, <args>
end

skill_gain skill_spiritspeak
return 1

[SPELL 151]
DEFNAME=s_ampli_voz
NAME=Amplificar voz
SOUND=snd_SPELL_LIGHT
RUNES=QU
CAST_TIME=20
RESOURCES=i_r_ginseng,i_r_cinza_sulfurosa
RUNE_ITEM=23009
SCROLL_ITEM=i_scroll_ampli_voz
FLAGS=spellflag_scripted
MANAUSE=12
EFFECT=10,25
DURATION=5,50
SKILLREQ=FOCUS 10.0
INTERRUPT=15.0,100.0

on=@Effect
EFFECT 3 i_fx_sparkle 0 16 0
if (<restest 1 i_rune_ampli_voz>)
findid.i_rune_ampli_voz.timerd 1
return 1
endif
newitem i_rune_ampli_voz
trysrc <uid> new.equip
new.timer=<DURATIONSPELL 151,<argn2>,<src.uid>>
new.more1=<EFFECTSPELL 151,<argn2>,<src.uid>>
return 1

//***********************************************************
// SPELL RESTORATION Exorcizar
//***********************************************************

[ITEMDEF i_scroll_exorcizar]
ID=0E38
NAME=Pergaminho do Exorcizar
RESOURCES=i_carta,i_r_ginseng,i_r_alho
SKILLMAKE=INSCRIPTION 30.0, Focus 20.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Exorcizar

ON=@Create
ATTR=ATTR_MAGIC
MOREX=152

[ITEMDEF i_rune_exorcizar]
ID=0227B
name=Exorcizar
type=t_eq_script
layer=layer_special
tevents=e_BuffStats,e_BuffSkill

on=@timer
remove
return 1

[SPELL 152]
DEFNAME=s_exorcizar
NAME=Exorcizar
SOUND=0201
RUNES=ECX
CAST_TIME=25
RESOURCES=i_r_ginseng,i_r_alho
RUNE_ITEM=20496
SCROLL_ITEM=i_scroll_exorcizar
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_resist
MANAUSE=15
EFFECT=0,250
DURATION=20,60
SKILLREQ=FOCUS 20.0
INTERRUPT=25.0,100.0

on=@Effect
//Só contra undead?
if (<isTevent.e_undead>)

//Determinar chance de sucesso
local.karma=-<karma>
local.effect=<EFFECTSPELL 152,((<src.focus>*3) + <src.spiritspeak>)/4 ,<src.uid>>

serv.log <eval <local.effect>>
//Sucesso?
if (<local.effect> > <hits>) && !<R1>
newitem i_fx_smoke_small
new.attr=04012
new.p=<p>
new.timer 3
new.effect 2 i_fx_sparkle 0 32 0
src.effect 4 2
src.sfx 29
remove

//falha
elif (!<restest 3 i_rune_exorcizar>)
local.effect = <eval <local.effect>/166>//Tranforma effect em 30% abs
local.duration = <DURATIONSPELL 152,<src.focus>,<src.uid>>
newitem i_rune_exorcizar
new.tag.modstr -<eval (<str>*<local.effect>)/100>,<local.duration>
new.tag.moddex -<eval (<dex>*<local.effect>)/100>,<local.duration>
new.tag.modint -<eval (<int>*<local.effect>)/100>,<local.duration>
new.tag.modskill <serv.skill.<skillbest.0>.key>
ref1=<uid>
new.tag.modskillval -<eval (<ref1.<skillbest.0>>*<local.effect>)/100>,<local.duration>
new.link=<uid>
trysrc <uid> equip <new>
new.timer=<local.duration>
BuffSkill <serv.skill.<skillbest.1>.key>,-<eval (<ref1.<skillbest.1>>*<local.effect>)/100>,<local.duration>
effect 3 i_fx_sparkle 0 16 0
emotered atordoado
attack <src>
endif

//Não é undead
else
src.sysmessagered <name> nao eh um morto-vivo!
endif

skill_gain skill_spiritspeak
return 1

//***********************************************************
// SPELL RESTORATION Forma Eterea
//***********************************************************

[ITEMDEF i_scroll_forma_eterea]
ID=0E38
NAME=Pergaminho da Forma Eterea
RESOURCES=i_carta,i_r_ginseng,i_r_perola_negra
SKILLMAKE=INSCRIPTION 30.0, Focus 20.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Forma eterea

ON=@Create
ATTR=ATTR_MAGIC|attr_blessed2
MOREX=153

[SPELL 153]
DEFNAME=s_forma_eterea
NAME=Forma Eterea
SOUND=snd_SPELL_invisibility
RUNES=QX
CAST_TIME=19
RESOURCES=i_r_ginseng,i_r_perola_negra
RUNE_ITEM=2283
SCROLL_ITEM=i_scroll_forma_eterea
FLAGS=spellflag_scripted
MANAUSE=20
DURATION=5,15
SKILLREQ=FOCUS 20.0
INTERRUPT=25.0,100.0

on=@SUCCESS
serv.newitem i_fx_smoke
new.attr = attr_static|attr_decay
new.timer = 1
new.p = <act.p>

on=@Effect
newitem i_mry_invis
new.more2=<DURATIONSPELL 153,<argn2>,<src.uid>>
new.equip
new.timer 1
invis_enter
update

skill_gain skill_spiritspeak
return 1

//***********************************************************
// SPELL RESTORATION Remover doença
//***********************************************************

[ITEMDEF i_scroll_remover_doenca]
ID=0E38
NAME=Pergaminho da Cura Menor
RESOURCES=i_carta,i_r_ginseng,i_r_alho
SKILLMAKE=INSCRIPTION 40.0, Focus 30.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Remover doença

ON=@Create
ATTR=ATTR_MAGIC
MOREX=154

[SPELL 154]
DEFNAME=s_remover_doenca
NAME=Cura Menor
SOUND=snd_SPELL_CURE
RUNES=AN
CAST_TIME=16
RESOURCES=i_r_ginseng,i_r_alho
RUNE_ITEM=2250
SCROLL_ITEM=i_scroll_remover_doenca
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_good
MANAUSE=6
EFFECT=1,50
SKILLREQ=FOCUS 30.0
INTERRUPT=35.0,100.0

on=@Effect
if (<restest 1 i_veneno>)
findid.i_veneno.more2 -= <EFFECTSPELL 154,<src.focus>,<src.uid>>
endif
effect 3 i_fx_heal_effect 0 16 0

skill_gain skill_spiritspeak
return 1

//***********************************************************
// SPELL RESTORATION Trovão
//***********************************************************

[ITEMDEF i_scroll_trovao]
ID=0E38
NAME=Pergaminho do Trovao
RESOURCES=i_carta,i_r_ginseng,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 40.0, Focus 30.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Trovao

ON=@Create
ATTR=ATTR_MAGIC
MOREX=155

[SPELL 155]
DEFNAME=s_trovao
NAME=Trovao
SOUND=05CE
RUNES=KOL
CAST_TIME=1
RESOURCES=i_r_ginseng,i_r_cinza_sulfurosa
RUNE_ITEM=2269
SCROLL_ITEM=i_scroll_trovao
FLAGS=spellflag_scripted
DURATION=5,10
MANAUSE=9
SKILLREQ=FOCUS 30.0
INTERRUPT=35.0,100.0

on=@Effect
forchars 4
IF (<NPC>==brain_animal)
f_spl_trovao <argn2>,<src.uid>
endif
end

effect 4 2
timerf 1,effect,4,2



skill_gain skill_spiritspeak
return 1

[FUNCTION f_spl_trovao]
local.time=<DURATIONSPELL s_trovao,<argv0>,<argv1>>
events +e_scared
emotered assustado
timerf <local.time> events,-e_scared



//***********************************************************
// SPELL RESTORATION Cura leve
//***********************************************************

[ITEMDEF i_scroll_cura_leve]
ID=0E38
NAME=Pergaminho da Restauracao
RESOURCES=i_carta,i_r_ginseng
SKILLMAKE=INSCRIPTION 50.0, Focus 40.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Cura leve

ON=@Create
ATTR=ATTR_MAGIC
MOREX=156

[SPELL 156]
DEFNAME=s_cura_leve
NAME=Restauracao
SOUND=snd_SPELL_HEAL
RUNES=IM
CAST_TIME=25
RESOURCES=i_r_ginseng
RUNE_ITEM=2256
SCROLL_ITEM=i_scroll_cura_leve
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_good | spellflag_dir_anim
MANAUSE=12
EFFECT=7,55
SKILLREQ=FOCUS 40.0
INTERRUPT=45.0,100.0

on=@Effect
local.effect =<EFFECTSPELL 156,<argn2>,<src.uid>>
local.effect += <eval <spiritspeak>/100>
if (<isTevent.e_undead>)
local.effect=<minimumN <local.effect>,<eval <hits>-6>>
damage <eval <local.effect>/3>,<DEF.dam_energy>,<src>
else
local.effect=<minimumN <local.effect>,<eval <maxhits>-<hits>>>
hits += <local.effect>
endif
effect 3 i_fx_heal_effect 0 16 0


skill_gain skill_spiritspeak
return 1



//***********************************************************
// SPELL RESTORATION Força divina
//***********************************************************

[ITEMDEF i_scroll_forca_divina]
ID=0E38
NAME=Pergaminho da Forca Divina
RESOURCES=i_carta,i_r_ginseng,i_r_raiz_de_mandrake
SKILLMAKE=INSCRIPTION 50.0, Focus 40.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Força divina

ON=@Create
ATTR=ATTR_MAGIC
MOREX=157

[SPELL 157]
DEFNAME=s_forca_divina
NAME=Forca Divina
SOUND=snd_spell_strength
RUNES=UM
CAST_TIME=25
RESOURCES=i_r_ginseng,i_r_raiz_de_mandrake
RUNE_ITEM=23001
SCROLL_ITEM=i_scroll_forca_divina
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_good | spellflag_dir_anim
MANAUSE=20
EFFECT=
DURATION=5*60,30*60
SKILLREQ=FOCUS 40.0
INTERRUPT=45.0,100.0

on=@START
if <act.restest i_rune_forca_divina>
src.sysmessageyellow Esse alvo ja tem esse efeito.
return 1
endif

on=@SUCCESS
serv.newitem = i_fx_sparkle_2
new.attr=attr_static|attr_decay
new.timer=1
new.p=<act.p>

on=@Effect
LOCAL.EFFECT=<eval ((<src.focus>*2)+<src.spiritspeak>+750)/150> //5,25(30)
LOCAL.DURATION=<DURATIONSPELL s_forca_divina,<argn2>,<src.uid>>
newitem=i_rune_forca_divina
new.tag.modstr=<minimumN 125-<str>,<LOCAL.EFFECT>>
IF (<src.spiritspeak> > 80.0)
new.tag.modskill=Parrying
new.tag.modskillval=<eval <LOCAL.EFFECT>*5>
ENDIF
new.link <uid>
trysrc <uid> src.new.equip
new.timer=<local.duration>

skill_gain skill_spiritspeak
return 0


[ITEMDEF i_rune_forca_divina]
NAME=Forca Divina
ID=02088
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=e_BuffSkill,e_BuffStats

ON=@Create
ATTR=attr_blessed2

on=@equip
cont.sysmessagegreen Voce se sente mais forte e confiante!

on=@unequip
cont.sysmessagegreen Sua forca comeca a passar.
cont.sfx snd_SPELL_CLUMSY

on=@timer
remove
return 1

//***********************************************************
// SPELL RESTORATION Raio divino
//***********************************************************

[ITEMDEF i_scroll_raio_divino]
ID=0E38
NAME=Pergaminho do Raio Divino
RESOURCES=i_carta,i_r_ginseng,i_r_raiz_de_mandrake,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 60.0, Focus 50.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Raio divino

ON=@Create
ATTR=ATTR_MAGIC
MOREX=158

[SPELL 158]
DEFNAME=s_raio_divino
NAME=Raio Divino
SOUND=05C3
RUNES=IOJ
CAST_TIME=10
RESOURCES=i_r_ginseng,i_r_raiz_de_mandrake,i_r_cinza_sulfurosa
RUNE_ITEM=2281
SCROLL_ITEM=i_scroll_raio_divino
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_harm | spellflag_dir_anim
MANAUSE=7
EFFECT=6,14
SKILLREQ=FOCUS 50.0
INTERRUPT=55.0,100.0

on=@Effect
EFFECT 0 i_fx_energy_ray 6 16 0
local.dam=<EFFECTSPELL s_raio_divino,<argn2>,<src.uid>>
local.dam += <eval <src.spiritspeak>/80>
DAMAGE <local.dam>,DAM_ENERGY,<src.uid>

skill_gain skill_spiritspeak
return 1

//***********************************************************
//              SPELL RESTORATION Conj Servo Etereo
//***********************************************************

[ITEMDEF i_scroll_servo_etereo]
ID=0E38
NAME=Pergaminho do Invocar Servo Etereo
RESOURCES=i_carta,i_r_ginseng,i_r_raiz_de_mandrake,i_r_perola_negra
SKILLMAKE=INSCRIPTION 60.0, Focus 50.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Conj. Servo etereo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=159

[SPELL 159]
DEFNAME=s_conj_serv_etereo
NAME=Invocar Servo Etereo
SOUND=01A9
RUNES=COX
CAST_TIME=22
RESOURCES=i_r_ginseng,i_r_raiz_de_mandrake,i_r_perola_negra
RUNE_ITEM=2280
SCROLL_ITEM=i_scroll_servo_etereo
FLAGS=spellflag_scripted | spellflag_good | spellflag_summon
MANAUSE=25
EFFECT=30,100
DURATION=90,3*60
SKILLREQ=FOCUS 50.0
INTERRUPT=55.0,100.0
 
on=@Success  //on=@Effect
LOCAL.timer=<DURATIONSPELL 159,<argn2>,<UID>>
if (<BELLTEST <src.spiritspeak>,1200>) || !<R100>
	obj=<src.f_summon_creature c_ethereal_servant2_sum,<LOCAL.timer>>
else
	obj=<src.f_summon_creature c_ethereal_servant_sum,<LOCAL.timer>>
endif
obj.str=<EFFECTSPELL 159,<argn2>,<uid>>
obj.dex=<EFFECTSPELL 159,<argn2>,<uid>>
obj.int=<EFFECTSPELL 159,<argn2>,<uid>>
obj.aid

skill_gain skill_spiritspeak
return 0


//***********************************************************
// SPELL RESTORATION Armadura sagrada
//***********************************************************

[ITEMDEF i_scroll_armadura_sagrada]
ID=0E38
NAME=Pergaminho da Armadura Sagrada
RESOURCES=i_carta,i_r_ginseng,i_r_alho
SKILLMAKE=INSCRIPTION 70.0, Focus 60.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Armadura sagrada

ON=@Create
ATTR=ATTR_MAGIC
MOREX=160

[ITEMDEF i_rune_arm_sagrada]
ID=0227B
name=Armadura Sagrada
type=t_eq_script
layer=layer_special
tevents=e_BuffRes

ON=@Create
ATTR=attr_blessed2

on=@timer
remove
return 1

[SPELL 160]
DEFNAME=s_armadura_sagrada
NAME=Armadura Sagrada
SOUND=01ED
RUNES=UOS
CAST_TIME=40
RESOURCES=i_r_ginseng,i_r_alho
RUNE_ITEM=23003
SCROLL_ITEM=i_scroll_armadura_sagrada
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_good
MANAUSE=20
EFFECT=1,60
DURATION=5*60,30*60
SKILLREQ=FOCUS 60.0
INTERRUPT=65.0,100.0

ON=@Success
if <restest i_mry_RESBUFF>
src.sysmessageyellow Voce pode ter apenas um buff de protecao ativo num determinado momento.
return 1
endif

serv.newitem i_fx_sparkle
new.attr = attr_static|attr_decay
new.timer = 2
new.p = <act.p>

on=@Effect
local.time=<DURATIONSPELL s_armadura_sagrada,<argn2>,<src.uid>>
local.prot=<between 2,35,((<src.spiritspeak> +(<src.focus> * 2))/3),1250>
local.protpoison = <eval(<local.prot>/2)>
Buff_respoison <local.protpoison>,<local.time>
Buff_Armor <local.prot>,<local.time>
Buff_resenergy <local.protpoison>,<local.time>

newitem i_mry_RESBUFF
new.equip
new.timer <local.time>
new.link=<uid>

skill_gain skill_spiritspeak
return 1

[ITEMDEF i_mry_RESBUFF]
ID=i_rune_poison
name=memory buff de resistencia
type=t_eq_script
layer=layer_special

ON=@Create
ATTR=attr_blessed2

on=@equip
timer 1
cont.sysmessagegreen As resistencias do alvo foram aumentadas.

on=@timer
cont.sysmessagegreen O efeito de sua magia protetora acabou.
remove
return 1


//***********************************************************
// SPELL RESTORATION Julgamento
//***********************************************************

[ITEMDEF i_scroll_julgamento]
ID=0E38
NAME=Pergaminho do Julgamento
RESOURCES=i_carta,i_r_ginseng,i_r_musgo_sangue,i_r_olho_morto_vivo
SKILLMAKE=INSCRIPTION 70.0, Focus 60.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Julgamento

ON=@Create
ATTR=ATTR_MAGIC
MOREX=161

[SPELL 161]
DEFNAME=s_julgamento
NAME=Julgamento
SOUND=051A
RUNES=ACL
CAST_TIME=40
RESOURCES=i_r_ginseng,i_r_musgo_sangue,i_r_olho_morto_vivo
RUNE_ITEM=23004
SCROLL_ITEM=i_scroll_julgamento
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_harm | spellflag_targ_noself
MANAUSE=25
SKILLREQ=FOCUS 60.0
INTERRUPT=65.0,100.0

on=@Effect
effect 3 i_fx_heal_effect 0 16 0
if (<IsPlayer> && (<src.act.necromancy> > 100) && ((<src.act.necromancy> - 100) < <src.focus>))
timerf 1 damage,<eval (<src.act.necromancy>/15)>,dam_energy,<src.uid>
timerf 1 EFFECT 1 0
 elif (<isTevent.e_undead>)
timerf 1 damage,<R25,<eval (-<karma>/40)>>,dam_energy,<src.uid>
timerf 1 EFFECT 1 0
else
src.timerf 1 src.EFFECT 1 0
src.timerf 1 damage,<eval (<src.focus>/20)>,dam_energy,<src.uid>
src.buffskill Focus,-<src.focus>,3600
src.sysmessagered Mond fica furioso com voce por seu descaso a Seu poder. Use o julgamento com parcimonia e apenas em situacoes nas quais tem certeza que seu inimigo e mau.
endif


skill_gain skill_spiritspeak
return 1



//***********************************************************
// SPELL RESTORATION Bênção
//***********************************************************

[ITEMDEF i_scroll_bencao]
ID=0E38
NAME=Pergaminho Bencao
RESOURCES=i_carta,i_r_ginseng,i_r_raiz_de_mandrake,i_r_musgo_sangue,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 80.0, Focus 70.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Benção

ON=@Create
ATTR=ATTR_MAGIC
MOREX=162

[ITEMDEF i_rune_bencao]
ID=0227B
name=Bencao
type=t_eq_script
layer=layer_special
tevents=e_BuffStats

ON=@Create
ATTR=attr_blessed2

on=@timer
remove
return 1

[SPELL 162]
DEFNAME=s_bencao
NAME=Bencao
SOUND=01EA
RUNES=UVM
CAST_TIME=40
RESOURCES=i_r_ginseng,i_r_ginseng,i_r_raiz_de_mandrake,i_r_musgo_sangue,i_r_teia_de_aranha
RUNE_ITEM=23014
SCROLL_ITEM=i_scroll_bencao
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_good
MANAUSE=25
EFFECT=
DURATION=2*60,30*60
SKILLREQ=FOCUS 70.0
INTERRUPT=75.0,100.0


on=@START
if <act.restest i_rune_bencao>
src.sysmessageyellow Esse alvo ja tem esse efeito.
return 1
endif

on=@SUCCESS
serv.newitem = i_fx_bless_effect
new.attr=attr_static|attr_decay
new.timer=1
new.p=<act.p>

on=@Effect
LOCAL.EFFECT=<eval ((<src.focus>*2)+<src.spiritspeak>+500)/170>//3 a 20(25)
LOCAL.DURATION=<DURATIONSPELL s_bencao,<argn2>,<src.uid>>
newitem=i_rune_bencao
new.tag.modstr=<MINIMUMN <LOCAL.EFFECT>,<eval 125-<str>>>
new.tag.moddex=<MINIMUMN <LOCAL.EFFECT>,<eval 125-<dex>>>
new.tag.modint=<MINIMUMN <LOCAL.EFFECT>,<eval 125-<int>>>
new.link <uid>
trysrc <uid> src.new.equip
new.timer=<local.duration>


skill_gain skill_spiritspeak
return 0


[ITEMDEF i_rune_bencao]
NAME=Bencao
ID=02088
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=e_BuffStats

ON=@Create
ATTR=attr_blessed2

on=@equip
cont.sysmessagegreen Voce recebe os efeitos da bencao divina!

on=@unequip
cont.sysmessagegreen Os efeitos da bencao se extinguem.
cont.sfx snd_SPELL_CLUMSY

on=@timer
remove
return 1


//***********************************************************
//              SPELL RESTORATION Forma de Nefelibata
//***********************************************************

[ITEMDEF i_scroll_poly_nefelibata]
ID=0E38
NAME=Pergaminho da Forma de Celestial
RESOURCES=i_carta,i_r_ginseng,i_r_teia_de_aranha,i_r_raiz_de_mandrake
SKILLMAKE=INSCRIPTION 80.0, Focus 70.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Forma de nefelibata

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=163
        
[Spell 163]
DEFNAME=s_forma_nefelibata
NAME=Forma de Celestial
SOUND=0
RUNES=RVOX
CAST_TIME=50
RESOURCES=i_r_raiz_de_mandrake,i_r_ginseng,i_r_teia_de_aranha,i_r_raiz_de_mandrake
RUNE_ITEM=20740
SCROLL_ITEM=i_scroll_poly_nefelibata
FLAGS=spellflag_scripted | spellflag_good | spellflag_summon
MANAUSE=30
DURATION=3*60,15*60
EFFECT=15,30
SKILLREQ=FOCUS 70.0
INTERRUPT=75.0,100.0

on=@SUCCESS  //on=@Effect
//Determina se pode morphar (anti-exploit)
if (<restest 1 i_spl_polymorph>)
 sysmessayellow voce ja possui um efeito desses ativo.
 return 1
endif

//Determina corpo
local.body=c_celestial_poly

//Determina o tempo de duração e bonus de int e skills
local.time=<DURATIONSPELL s_forma_nefelibata,<argn2>,<src.uid>>
local.int=<EFFECTSPELL s_forma_nefelibata,<argn2>,<src.uid>>
local.int=<minimumN 150-<int>,<local.int>>
Buff_INT <local.int>,<local.time>
BuffSkill Meditation,<local.int>*3,<local.time>
BuffSkill Spiritspeak,<local.int>*3,<local.time>

mana += <local.int>
//src.haste <local.time>

newitem i_mry_spkmage
new.equip
new.timer <local.time>
new.link=<uid>

//Polimorpha
src.f_polymorph <local.body>,0,<local.time>

skill_gain skill_spiritspeak
return 0

//***********************************************************
// SPELL RESTORATION Purificação
//***********************************************************

[ITEMDEF i_scroll_purificacao]
ID=0E38
NAME=Pergaminho da Purificacao
RESOURCES=i_carta,i_r_ginseng,i_r_alho,i_r_perola_negra
SKILLMAKE=INSCRIPTION 90.0, Focus 80.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Purificação

ON=@Create
ATTR=ATTR_MAGIC
MOREX=164

[SPELL 164]
DEFNAME=s_purificacao
NAME=Purificacao
SOUND=snd_SPELL_CURE
RUNES=ANG
CAST_TIME=15
RESOURCES=i_r_ginseng,i_r_alho,i_r_perola_negra
RUNE_ITEM=2264
SCROLL_ITEM=i_scroll_purificacao
FLAGS=spellflag_scripted | spellflag_good
MANAUSE=14
EFFECT=1,70
SKILLREQ=FOCUS 80.0
INTERRUPT=85.0,100.0

on=@Effect
obj=<uid>
forchars 15
if !(<IsMyEnemy <obj.uid>>)
spelleffect s_remover_doenca,<argn2>,<obj.uid>
endif
end


skill_gain skill_spiritspeak
return 1



//***********************************************************
//              SPELL RESTORATION Conj Nefelibata
//***********************************************************

[ITEMDEF i_scroll_conj_nefelibata]
ID=0E38
NAME=Pergaminho do Invocar Celestial
RESOURCES=i_carta,i_r_ginseng,i_r_raiz_de_mandrake,i_r_perola_negra,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 90.0, Focus 80.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Mensageiro Celestial

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=165

[SPELL 165]
DEFNAME=s_conj_nefelibata
NAME=Invocar Celestial
SOUND=01A9
RUNES=KOX
CAST_TIME=60
RESOURCES=i_r_ginseng,i_r_raiz_de_mandrake,i_r_perola_negra,i_r_teia_de_aranha
RUNE_ITEM=2254
SCROLL_ITEM=i_scroll_conj_nefelibata
FLAGS=spellflag_scripted | spellflag_good | spellflag_summon
MANAUSE=35
EFFECT=10,40
DURATION=2*60,4*60
SKILLREQ=FOCUS 80.0
INTERRUPT=85.0,100.0
 
on=@SUCCESS  //on=@Effect
LOCAL.TIME=<DURATIONSPELL 165,<argn2>,<UID>>
LOCAL.EFFECT=<EFFECTSPELL 165,<argn2>,<UID>>
obj=<src.f_summon_creature c_warrior_ethereal_sum,<LOCAL.TIME>>
obj.Wrestling= <eval(<local.effect>*15)>
obj.tag0.moddam += <eval <local.effect>/5>

skill_gain skill_spiritspeak
return 0



//***********************************************************
// SPELL RESTORATION Tempestade divina
//***********************************************************

[ITEMDEF i_scroll_tempestade_divina]
ID=0E38
NAME=Pergaminho da Tempestade Divina
RESOURCES=i_carta,i_r_ginseng,i_r_raiz_de_mandrake,i_r_cinza_sulfurosa,i_r_perola_negra
SKILLMAKE=INSCRIPTION 100.0, Focus 90.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Tempestade divina

ON=@Create
ATTR=ATTR_MAGIC
MOREX=166

[ITEMDEF i_rune_tempestade_divina]
ID=i_gold
type=t_script

on=@timer
if (<link.flags>&statf_indoors) || (<region.flags>&region_flag_underground) //se o alvo estiver em lugar coberto.
	link.sysmessagered seus pelos se arrepiam em energia estatica estatica

else
	if (<more1>)
		link.effect 1 0
		link.sfx 05CE
		link.damage <more2>,dam_energy,<tag0.caster>
	else
		obj=<UID>
		forchars 5
			newitem <obj.defname>
			new.p=<p>
			new.more2=<R<eval <obj.more2>/2>,<obj.more2>>
			new.more1=1
			new.link=<UID>
			new.tag.caster=<obj.tag.caster>
			new.timerd=<R3,25>
		end
	endif
endif
remove
return 1

[SPELL 166]
DEFNAME=s_tempestade_divina
NAME=Tempestade Divina
SOUND=05CE
RUNES=KOJ
CAST_TIME=30
RESOURCES=i_r_ginseng,i_r_raiz_de_mandrake,i_r_cinza_sulfurosa,i_r_perola_negra
RUNE_ITEM=2288
SCROLL_ITEM=i_scroll_tempestade_divina
FLAGS=spellflag_scripted | spellflag_targ_xyz | spellflag_harm | spellflag_dir_anim
MANAUSE=25
EFFECT=18,33
SKILLREQ=FOCUS 90.0
INTERRUPT=95.0,100.0

on=@Success
if (<src.flags>&statf_indoors) || (<region.flags>&region_flag_underground)
src.sysmessageyellow Impossivel em lugares cobertos
return 1
endif
newitem i_rune_tempestade_divina
new.attr 092
new.p=<src.actp>
new.more2=<EFFECTSPELL 166,<argn2>,<src>>
new.more2 += <eval <src.spiritspeak>/75>
new.tag.caster=<src.uid>
new.timerd 1


//verificar direito, eventualmente aumentar dano, castime e mana

//***********************************************************
//              SPELL RESTORATION Conj Mensageiro
//***********************************************************

[ITEMDEF i_scroll_conj_mensageiro]
ID=0E38
NAME=Pergaminho do Celestial Superior
RESOURCES=i_carta,i_r_ginseng,i_r_raiz_de_mandrake,i_r_perola_negra,i_r_teia_de_aranha,i_r_asa_morcego
SKILLMAKE=INSCRIPTION 100.0, Focus 90.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Celestial Maior

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=167

[SPELL 167]
DEFNAME=s_conj_mensageiro
NAME=Invocar Celestial Superior
SOUND=01A9
RUNES=KVOX
CAST_TIME=80
RESOURCES=i_r_ginseng,i_r_raiz_de_mandrake,i_r_perola_negra,i_r_teia_de_aranha,i_r_asa_morcego
RUNE_ITEM=20742
SCROLL_ITEM=i_scroll_conj_mensageiro
FLAGS=spellflag_scripted | spellflag_good
MANAUSE=50
EFFECT=10,40
DURATION=60,5*60
SKILLREQ=FOCUS 90.0
INTERRUPT=95.0,100.0
 
on=@Effect
LOCAL.TIME=<DURATIONSPELL 167,<argn2>,<UID>>
LOCAL.EFFECT=<EFFECTSPELL 167,<argn2>,<UID>>
obj=<src.f_summon_creature c_Ethereal_Warrior_sum,<LOCAL.TIME>>
obj.Wrestling = <eval(<local.effect>*20)>
obj.tag0.moddam += <eval <local.effect>/5>
obj.respoison = <eval <local.effect>*2>
obj.resenergy = <eval <local.effect>*2>
obj.resfire = <eval <local.effect>*2>
obj.modar += <eval <local.effect>>


skill_gain skill_spiritspeak
return 1


//***********************************************************
// SPELL RESTORATION Luz divina
//***********************************************************

[ITEMDEF i_scroll_luz_divina]
ID=0E38
NAME=Pergaminho Luz Divina
RESOURCES=i_carta,i_r_ginseng,i_r_alho,i_r_perola_negra,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 100.0, Focus 100.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Luz divina

ON=@Create
ATTR=ATTR_MAGIC
MOREX=168

[SPELL 168]
DEFNAME=s_luz_divina
NAME=Colera Divina
SOUND=snd_SPELL_HEAL
RUNES=IGM
CAST_TIME=35
RESOURCES=i_r_ginseng,i_r_alho,i_r_perola_negra,i_r_teia_de_aranha
RUNE_ITEM=2268
SCROLL_ITEM=i_scroll_luz_divina
FLAGS=spellflag_scripted | spellflag_good
MANAUSE=21
SKILLREQ=FOCUS 100.0
INTERRUPT=100.0,100.0

on=@Effect
obj=<uid>
forchars 5
if !(<IsMyEnemy <obj.uid>>)
spelleffect s_cura_leve,<argn2>,<obj.uid>
endif
end

skill_gain skill_spiritspeak
return 1



//***********************************************************
// SPELL RESTORATION Redenção
//***********************************************************

[ITEMDEF i_scroll_redencao]
ID=0E38
NAME=Pergaminho Redencao
RESOURCES=i_carta,i_r_ginseng,i_r_alho,i_r_perola_negra,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 100.0, Focus 100.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Focus
DESCRIPTION=Redenção

ON=@Create
ATTR=ATTR_MAGIC
MOREX=169

[SPELL 169]
DEFNAME=s_redencao
NAME=Redencao
SOUND=snd_SPELL_RESURRECTION
RUNES=AC
CAST_TIME=130
RESOURCES=i_r_ginseng,i_r_alho,i_r_perola_negra,i_r_teia_de_aranha
RUNE_ITEM=2298
SCROLL_ITEM=i_scroll_redencao
FLAGS=spellflag_scripted | spellflag_good | spellflag_targ_char | spellflag_targ_item
MANAUSE=70
SKILLREQ=FOCUS 100.0
INTERRUPT=100.0,100.0

on=@Effect
if <IsItem>
	if <type>==t_corpse
		if <link.IsPlayer>
		//se morto, recupera spiritspeak/50
			if !<link.restest 1 i_mry_desmaio>
			link.morte_decrementaDesmaios <eval <src.spiritspeak>/500>
			endif
		link.f_returnToLife
		link.timerf 1 EFFECT,3,i_fx_heal_effect,0,16,0
		else
		src.sysmessagered Esta alma ja atravessou...
		endif
		else
		src.sysmessagered Isso nao eh um corpo!
	endif
	return 1
endif

if <flags>&statf_dead
	if !<restest 1 i_mry_desmaio>
	morte_decrementaDesmaios <eval <src.spiritspeak>/500>
	endif
f_returnToLife
elif (<isTevent.e_undead>) //Undead
spelleffect s_julgamento,<eval <argn2>+10.0>,<src>
elif <IsChar>
stam=<maxstam>
hits=<maxhits>
endif
timerf 1 EFFECT,3,i_fx_heal_effect,0,16,0

skill_gain skill_spiritspeak
return 1

//////////////////////////////////////////////////
// SKILL
//////////////////
[SKILL 50]
//DEFNAME=SKILL_Focus NAO PODE, PERDE SYNCH COM SKILLS.SCP
KEY=Focus
FLAGS=SKF_MAGIC
TITLE=Sacerdote
STAT_STR=20
STAT_INT=100
STAT_DEX=40
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=15
//ADV_RATE=10.0,200.0,800.0
VALUES=1,20,80
//DELAY=1.0 // based on skill and difficulty.

[COMMENT testao_focau]

on=@Start
face <act>
local.flags=<serv.spell.<actarg1>.flags>
IF (<local.flags>&spellflag_targ_xyz) && (<act>)
sysmessageyellow Voce deve selecionar um lugar no mapa com esta magia...
return 1
ELIF (<local.flags>&spellflag_targ_obj) && (!0<act>)
sysmessageyellow Voce nao pode selecionar um lugar no mapa com esta magia...
return 1
ELIF !(<local.flags>&spellflag_targ_char) && (0<act.IsChar>) && (<act>!=<uid>)
sysmessageyellow Voce nao pode selecionar uma criatura com esta magia...
return 1
ELIF !(<local.flags>&spellflag_targ_item) && (0<act.IsItem>)
sysmessageyellow Voce nao pode selecionar um item com esta magia...
return 1
ELIF (<local.flags>&spellflag_targ_xyz)
IF (<distance <targp>> > 9)
sysmessageyellow Seu alvo esta muito longe.
return 1
ENDIF
ENDIF
IF (<local.flags>&spellflag_targ_obj) || (<local.flags>&spellflag_targ_obj) || (<local.flags>&spellflag_targ_char) || (<local.flags>&spellflag_targ_item)
IF (<distance <act>> > 9)
sysmessageyellow Seu alvo esta muito longe.
return 1
ELIF !(<CanSeeLos <act>>)
sysmessageyellow Voce nao consegue ver o alvo.
return 1
ENDIF
ENDIF
if (!<IsGM>)
timerd=<serv.spell.<actarg1>.CAST_TIME>
argn2=<tag.mage_actdiff>
endif
if (<local.flags>&spellflag_dir_anim)
anim 16
else
anim 17
endif
tag0.mage_spellid=<actarg1>
TRIGGER=@spellcast,<DEF.TAT_AS_ARGN>,<tag0.mage_spellid>,<strarg <serv.spell.<actarg1>.skillreq>>,<strarg <serv.spell.<actarg1>.skillreq>>
return 2

on=@SUCCESS
if (!<BELLTEST <focus>,<eval <tag0.mage_actdiff>>,5.0>)
SRC.SYSMESSAGE The spell fizzles.
EFFECT 3 i_fx_smoke_small 0 32 0
SFX 05C
mage_fail
ACTION -1
return 1
endif
sfx <serv.spell.<actarg1>.SOUND>
local.flags=<serv.spell.<actarg1>.flags>
if !((<local.flags>&spellflag_targ_item) || (<local.flags>&spellflag_targ_char) || (<local.flags>&spellflag_targ_obj))
local.log .=SELF-
act=<uid>
targ=<act>
targp=<actp>
endif
act.spelleffect <src.actarg1>,<src.focus>,<src>
mage_success
return 1


ON=@Fail
mage_fail
mage_cleartags
SRC.SYSMESSAGE The spell fizzles.
EFFECT 3 i_fx_smoke_small 0 32 0
SFX 05C
return 1

ON=@Abort
mage_cleartags
//SRC.SYSMESSAGE You stop casting the spell.
EFFECT 3 i_fx_smoke_small 0 32 0
SFX 05C
return 1

[EOF]


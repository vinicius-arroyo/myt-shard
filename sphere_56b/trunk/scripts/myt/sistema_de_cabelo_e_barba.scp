//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE CORTE DE CABELO E BARBA
//
//  Jean Gabin
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:
//tag.ohair         cor original do cabelo
//tag.obeard        cor original da barba
//ctag.barber_uid   uid do cabelereiro
//ctag.client_uid   uid do cliente

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs & CONFIG
//*****************************************************************************
//*****************************************************************************

[SCROLL SCROLL_CABELO]
       -=Acoes de cabelo=-
Digite um dos seguintes comandos incuindo o ponto inicial
.rabo 
.tranca1
.tranca2
.delado
.coque
.samurai
.solto

[PLEVEL 0] 
rabo 
tranca1
tranca2
delado
samurai
coque
solto
cabelo

[DEFNAME sistema_cabelo_barba]
//Tempo real de crescimento em décimos de segundo.
HAIR_TIME   10*60*60*24*2       //2 dias
BEARD_TIME  10*60*60*24*3       //3 dia
SND_TIE_HAIR    048
SND_UNTIE_HAIR  058
SND_CUT_HAIR    0248
SND_BALD_HAIR   059

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// cabelo
//*****************************************************************************
[FUNCTION cabelo]
SCROLL SCROLL_CABELO
   
//*****************************************************************************
// rabo
//*****************************************************************************
[FUNCTION rabo]
obj=<src.findlayer.layer_hair>
if (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)||(<obj.baseid>==i_hair_elven_flower)||(<obj.baseid>==i_hair_elven_braid)||(<obj.baseid>==i_hair_elven_long4)||(<obj.baseid>==i_hair_elven_long3)
    src.sysmessageyellow Solte o cabelo primeiro para poder fazer um rabo.
    return 1
elif (<obj.baseid>!=i_hair_long)
    src.sysmessageyellow Voce tem que ter cabelo comprido para poder fazer um rabo.
    return 1
else
    sfx SND_TIE_HAIR
    f_change_hair i_hair_ponytail,1
    emotered prende o cabelo
endif
return 1


//*****************************************************************************
// tranca1
//*****************************************************************************
[FUNCTION tranca1]
obj=<src.findlayer.layer_hair>
if (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)||(<obj.baseid>==i_hair_elven_flower)||(<obj.baseid>==i_hair_elven_braid)||(<obj.baseid>==i_hair_elven_long4)||(<obj.baseid>==i_hair_elven_long3)
    src.sysmessageyellow Solte o cabelo primeiro para poder fazer uma tranca.
    return 1
elif (<obj.baseid>!=i_hair_long)
    src.sysmessageyellow Voce tem que ter cabelo comprido para poder fazer uma tranca.
    return 1
else
    sfx SND_TIE_HAIR
    f_change_hair i_hair_elven_braid,1
    emotered tranca os cabelos
endif
return 1

//*****************************************************************************
// tranca2
//*****************************************************************************
[FUNCTION tranca2]
obj=<src.findlayer.layer_hair>
if (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)||(<obj.baseid>==i_hair_elven_flower)||(<obj.baseid>==i_hair_elven_braid)||(<obj.baseid>==i_hair_elven_long4)||(<obj.baseid>==i_hair_elven_long3)
    src.sysmessageyellow Solte o cabelo primeiro para poder fazer uma tranca.
    return 1
elif (<obj.baseid>!=i_hair_long)
    src.sysmessageyellow Voce tem que ter cabelo comprido para poder fazer a tranca dupla.
    return 1
else
    sfx SND_TIE_HAIR
    f_change_hair i_hair_2_pigtails,1
    emotered tranca os cabelos
endif
return 1

//*****************************************************************************
// delado
//*****************************************************************************
[FUNCTION delado]
obj=<src.findlayer.layer_hair>
if (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)||(<obj.baseid>==i_hair_elven_flower)||(<obj.baseid>==i_hair_elven_braid)||(<obj.baseid>==i_hair_elven_long4)||(<obj.baseid>==i_hair_elven_long3)
    src.sysmessageyellow Solte o cabelo primeiro para poder prende-lo de lado.
    return 1
elif (<obj.baseid>!=i_hair_long)
    src.sysmessageyellow Voce tem que ter cabelo comprido para poder prende-lo de lado.
    return 1
else
    sfx SND_TIE_HAIR
    f_change_hair i_hair_elven_flower,1
    emotered prende os cabelos
endif
return 1

//*****************************************************************************
// samurai
//*****************************************************************************
[FUNCTION samurai]
obj=<src.findlayer.layer_hair>
if (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)||(<obj.baseid>==i_hair_elven_flower)||(<obj.baseid>==i_hair_elven_braid)||(<obj.baseid>==i_hair_elven_long4)||(<obj.baseid>==i_hair_elven_long3)
    src.sysmessageyellow Solte o cabelo primeiro para poder prende-lo no estilo samurai.
    return 1
elif (<obj.baseid>!=i_hair_long)
    src.sysmessageyellow Voce tem que ter cabelo comprido para poder prende-lo no estilo samurai.
    return 1
else
    sfx SND_TIE_HAIR
    f_change_hair i_hair_elven_long3,1
    emotered penteia os cabelos
endif
return 1

//*****************************************************************************
// coque
//*****************************************************************************
[FUNCTION coque]
obj=<src.findlayer.layer_hair>
if (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)||(<obj.baseid>==i_hair_elven_flower)||(<obj.baseid>==i_hair_elven_braid)||(<obj.baseid>==i_hair_elven_long4)||(<obj.baseid>==i_hair_elven_long3)
    src.sysmessageyellow Solte o cabelo primeiro para poder fazer um coque.
    return 1
elif (<obj.baseid>!=i_hair_long)
    src.sysmessageyellow Voce tem que ter cabelo comprido para poder fazer um coque.
    return 1
else
    sfx SND_TIE_HAIR
    f_change_hair i_hair_elven_long4,1
    emotered faz um coque
endif
return 1

//*****************************************************************************
// solto
//*****************************************************************************
[FUNCTION solto]
obj=<src.findlayer.layer_hair>
if (<src.findlayer.layer_hair.baseid>==i_hair_ponytail)||(<src.findlayer.layer_hair.baseid>==i_hair_2_pigtails)||(<src.findlayer.layer_hair.baseid>==i_hair_elven_flower)||(<src.findlayer.layer_hair.baseid>==i_hair_elven_braid)||(<src.findlayer.layer_hair.baseid>==i_hair_elven_long4)||(<src.findlayer.layer_hair.baseid>==i_hair_elven_long3)
    sfx SND_UNTIE_HAIR
    f_change_hair i_hair_long,1
    emotered solta os cabelos
else
    src.sysmessageyellow Voce nao tem como soltar o cabelo.
endif
return 1

//*****************************************************************************
// f_change_hair
//*****************************************************************************
[FUNCTION f_change_hair]
if (<ISEMPTY <argv0>>) || (!<serv.itemdef.<argv0>>) || (<serv.itemdef.<argv0>.type> != t_hair)
    return 0
endif
serv.newitem <argv0>
new.attr=attr_newbie
new.color=<tag.ohair>

ref2=<findlayer.layer_hair>
if <ref2> && 0<argv1>
    new.more1=<ref2.more1>
    new.more2=<ref2.more2>
else
    new.more2=<eval <serv.time>+<DEF.HAIR_TIME>>
endif

if <ref2>
    ref2.remove
endif

equip <new>
return <new>

//*****************************************************************************
// f_change_beard
//*****************************************************************************
[FUNCTION f_change_beard]
if (<ISEMPTY <argv0>>) || (!<serv.itemdef.<argv0>>) || (<serv.itemdef.<argv0>.type> != t_beard)
    return 0
endif
serv.newitem <argv0>
new.attr=attr_newbie
new.color=<tag.obeard>
findlayer.layer_beard.remove
equip <new>
new.more2=<eval <serv.time>+<DEF.BEARD_TIME>>
return <new>

//*****************************************************************************
// f_update_hair
//*****************************************************************************
[FUNCTION f_update_hair]
obj=<findlayer.layer_hair>
if (<obj.more2> < <serv.time>) && (<obj.more1>)
    f_change_hair <obj.more1>
    timerf 10,sysmessagegreen,Voce nota que seu cabelo cresceu.
endif

//*****************************************************************************
// f_update_beard
//*****************************************************************************
[FUNCTION f_update_beard]
obj=<findlayer.layer_beard>
//mulheres nao tem barba, pelo menos nao deveriam =p
if <sex 0/1> || strmatch(<tag.raca>,drow) || strmatch(<tag.raca>,elfo)
    obj.remove
elif (<obj.more2> < <serv.time>) && (<obj.more1>)
    f_change_beard <obj.more1>
    timerf 8,sysmessagegreen,Voce nota que sua barba cresceu.
endif

//*****************************************************************************
// f_hair_ground
//*****************************************************************************
[FUNCTION f_hair_ground]
serv.newitem=i_cabelo
new.color=<tag.ohair>
new.p=<p>
new.timer=<R10,20>

//*****************************************************************************
// correct_hair
//*****************************************************************************
[FUNCTION correct_hair]
forplayers 6144
    //Define a cor do cabelo
    if (<findlayer.layer_beard>) && (!<findlayer.layer_hair>)
        LOCAL.colorh=<findlayer.layer_beard.color>
        LOCAL.colorb=<findlayer.layer_beard.color>
    elif (<findlayer.layer_hair>) && (!<findlayer.layer_beard>)
        LOCAL.colorh=<findlayer.layer_hair.color>
        LOCAL.colorb=<findlayer.layer_hair.color>
    elif (!<findlayer.layer_hair>) && (!<findlayer.layer_beard>)
        LOCAL.colorh=<DEF.colors_hair>
        LOCAL.colorb=<LOCAL.colorh>
    else
        LOCAL.colorb=<findlayer.layer_beard.color>
        LOCAL.colorh=<findlayer.layer_hair.color>
    endif
    if (STRMATCH(<tag.raca>,Elfo))
        LOCAL.colorh=037
    endif

    //Define sobre cabelo
    if (!<findlayer.layer_hair>)
        obj=<f_change_hair i_cabelo_careca>
        obj.color=<LOCAL.colorh>
    else
        f_change_hair <findlayer.layer_hair.baseid>
    endif
    tag.ohair=<LOCAL.colorh>
 
    //Define sobre barba
    if (!<findlayer.layer_beard>) || (STRMATCH(<tag.raca>,Elfo)) || (<body> == c_woman)
        obj=<f_change_beard i_barba_careca>
        obj.color=<LOCAL.colorb>
    else
        f_change_beard <findlayer.layer_beard.baseid>
    endif
    tag.obeard=<LOCAL.colorb>
    LOCAL.count += 1
endfor
sysmessageyellow <dLOCAL.count> jogadores reparados.

//*****************************************************************************
//*****************************************************************************
// MENUs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// autoriza_barba
//*****************************************************************************
[MENU autoriza_barba]
Voce autoriza esta pessoa a cortar seu cabelo ou sua barba?

ON=0 SIM
uid.<ctag0.barber_uid>.menu navalha
return 1

on=0 NAO
return 1

//*****************************************************************************
// autoriza_cabelo
//*****************************************************************************
[MENU autoriza_cabelo]
Voce autoriza esta pessoa a cortar seu cabelo?

ON=0 SIM
obj=<ctag0.barber_uid>
obj.ctag.hair=<findlayer.layer_hair.baseid>
obj.skillmenu sm_cabelo_tesoura
return 1

on=0 NAO
return 1

//*****************************************************************************
// navalha
//*****************************************************************************
[MENU navalha]
O que voce deseja?
ON=0 Fazer a barba
obj=<uid.<ctag.client_uid>.findlayer.layer_beard>
if !<obj>
    sysmessagered Nao ha barba para ser feita.
else
    ctag.hair=<obj.baseid>
    skillmenu sm_navalha_barba
endif
return 1

ON=0 Corte especial de cabelo
obj=<uid.<ctag.client_uid>.findlayer.layer_hair>
if !<obj>
    sysmessageyellow Nao tem cabelo para fazer.
elif (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)
    sysmessageyellow Solte o cabelo antes de fazer o corte.
elif (<obj.baseid>!=i_hair_afro)&&(<obj.baseid>!=i_hair_buns)&&(<obj.baseid>!=i_hair_pageboy)&&(<obj.baseid>!=i_hair_long)
    sysmessageyellow Nao da para fazer nenhum corte especial com o cabelo deste tamanho. Espere ele crescer mais.
else
    menu navalha_cabelo
endif
return 1

ON=0 Raspar a cabeca
ref1=<ctag.client_uid>
obj=<ref1.findlayer.layer_hair>
if !<obj>
    sysmessageyellow Nao tem cabelo para raspar.
elif (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)
    sysmessageyellow Solte o cabelo antes de raspa-lo.
else
    sfx SND_BALD_HAIR
    ref1.f_hair_ground
    ref1.f_change_hair i_cabelo_careca
endif
return 1

//*****************************************************************************
// navalha_cabelo
//*****************************************************************************
[MENU navalha_cabelo]
Que tipo de corte especial voce deseja fazer?
ON=0 Cabelo moicano
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_mohawk
return 1

on=0 Cabelo Hare Krisna
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_krisna
return 1

//*****************************************************************************
//*****************************************************************************
// SKILLMENUs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// sm_navalha_barba
//*****************************************************************************
[SKILLMENU sm_navalha_barba]
Qual o corte que você deseja fazer?

ON=i_beard_long <name>
TESTIF=(<ctag.hair>==i_beard_long_med)
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_beard i_beard_long
return 1

ON=i_beard_short_med <name>
TESTIF=(<ctag.hair>==i_beard_long_med)
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_beard i_beard_short_med
return 1

ON=i_beard_short <name>
TESTIF=(<ctag.hair>==i_beard_long_med)||(<ctag.hair>==i_beard_long)||(<ctag.hair>==i_beard_short_med)
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_beard i_beard_short
return 1

ON=i_beard_goatee <name>
TESTIF=(<ctag.hair>==i_beard_short)||(<ctag.hair>==i_beard_short_med)||(<ctag.hair>==i_beard_long)||(<ctag.hair>==i_beard_long_med)||(<ctag.hair>==i_beard_vandyke)
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_beard i_beard_goatee
return 1

ON=i_beard_mustache <name>
TESTIF=(<ctag.hair>==i_beard_long_med)||(<ctag.hair>==i_beard_short_med)||(<ctag.hair>==i_beard_vandyke)
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_beard i_beard_mustache
return 1

ON=i_beard_vandyke <name>
TESTIF=(<ctag.hair>==i_beard_long_med)||(<ctag.hair>==i_beard_short_med)
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_beard i_beard_vandyke
return 1

ON=i_barba_careca <name>
obj=<ctag.client_uid>
sfx SND_BALD_HAIR
obj.f_hair_ground
obj.f_change_beard i_barba_careca
return 1


//*****************************************************************************
// sm_cabelo_tesoura
//*****************************************************************************
[SKILLMENU sm_cabelo_tesoura]
Qual o corte que você deseja fazer?

ON=i_hair_elven_long2 <name>
TESTIF=(<ctag.hair>==i_hair_long)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_elven_long2
return 1

ON=i_hair_elven_ML <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_elven_long2)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_elven_ML
return 1

ON=i_hair_LF <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_LF)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_LF
return 1

ON=i_hair_pageboy <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_elven_ML)||(<ctag.hair>==i_hair_LF)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_pageboy
return 1

ON=i_hair_elven_spike <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_pageboy)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_elven_ML)||(<ctag.hair>==i_hair_LF)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_elven_spike
return 1

ON=i_hair_elven_mullet <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_pageboy)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_elven_ML)||(<ctag.hair>==i_hair_LF)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_elven_mullet
return 1

ON=i_hair_buns <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_pageboy)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_elven_ML)||(<ctag.hair>==i_hair_elven_mullet)
if (<src.targ.uid>==<src.uid>)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_buns
return 1

ON=i_hair_afro <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_pageboy)||(<ctag.hair>==i_hair_buns)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_elven_ML)||(<ctag.hair>==i_hair_LF)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_afro
return 1

ON=i_hair_short <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_pageboy)||(<ctag.hair>==i_hair_buns)||(<ctag.hair>==i_hair_afro)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_elven_ML)||(<ctag.hair>==i_hair_LF)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_short
return 1

ON=i_hair_short1 <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_pageboy)||(<ctag.hair>==i_hair_buns)||(<ctag.hair>==i_hair_afro)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_elven_ML)||(<ctag.hair>==i_hair_LF)||(<ctag.hair>==i_hair_short)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_short1
return 1

ON=i_hair_receding <name>
TESTIF=(<ctag.hair>==i_hair_long)||(<ctag.hair>==i_hair_pageboy)||(<ctag.hair>==i_hair_buns)||(<ctag.hair>==i_hair_afro)||(<ctag.hair>==i_hair_elven_long2)||(<ctag.hair>==i_hair_elven_ML)||(<ctag.hair>==i_hair_LF)||(<ctag.hair>==i_hair_short)||(<ctag.hair>==i_hair_short1)
obj=<ctag.client_uid>
sfx SND_CUT_HAIR
obj.f_hair_ground
obj.f_change_hair i_hair_receding
return 1
   
//*****************************************************************************
//*****************************************************************************
// ITEMDEFs CABELOS
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_cabelo_careca
//*****************************************************************************
[ITEMDEF i_cabelo_careca]
id=i_deed
NAME=Cabeca raspada
TYPE=t_hair
WEIGHT=0
layer=layer_hair

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_receding

//*****************************************************************************
// i_hair_receding
//*****************************************************************************
[ITEMDEF 02048]
defname=i_hair_receding
NAME=Cabelo calvo
TYPE=T_HAIR
CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo calvo

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_short1

//*****************************************************************************
// i_hair_short1
//*****************************************************************************
[ITEMDEF 12225]
Defname=i_hair_short1 
Name=Cabelo bem curto
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo bem curto

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_short

//*****************************************************************************
// i_hair_short
//*****************************************************************************
[ITEMDEF 0203b]
defname=i_hair_short
NAME=Cabelo curto
TYPE=T_HAIR
CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo curto

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_afro

//*****************************************************************************
// i_hair_afro
//*****************************************************************************
[ITEMDEF 02047]
defname=i_hair_afro
NAME=Cabelo afro
TYPE=T_HAIR

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo afro

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_buns

//*****************************************************************************
// i_hair_buns
//*****************************************************************************
[ITEMDEF 02046]
defname=i_hair_buns
NAME=Cabelo cheio
TYPE=T_HAIR
CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo cheio

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_elven_mullet


//*****************************************************************************
// i_hair_elven_mullet
//*****************************************************************************
[ITEMDEF 12226]
Defname=i_hair_elven_mullet  //  vem antes do mullet com pena vai para o pageboy
Name=Cabelo Mullet
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo Mullet

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_pageboy

//*****************************************************************************
// i_hair_pageboy
//*****************************************************************************
[ITEMDEF 02045]
defname=i_hair_pageboy
NAME=Cabelo medio
TYPE=T_HAIR

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo medio

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_elven_ML

//*****************************************************************************
// i_hair_elven_ML
//*****************************************************************************
[ITEMDEF 12223]
Defname=i_hair_elven_ML
Name=Cabelo Mullet comprido
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo Mullet comprido

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_elven_long2


//*****************************************************************************
// i_hair_elven_long2
//*****************************************************************************
[ITEMDEF 12237]
Defname=i_hair_elven_long2 // mullet comprido. um depois do mullet penteado
Name=Cabelo medio comprido
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo medio comprido

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_long

//*****************************************************************************
// i_hair_long
//*****************************************************************************
[ITEMDEF 0203c]
defname=i_hair_long
NAME=Cabelo comprido
TYPE=T_HAIR

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo comprido

ON=@CREATE
ATTR=attr_newbie

//*****************************************************************************
// i_hair_ponytail
//*****************************************************************************
[ITEMDEF 0203d]
defname=i_hair_ponytail
NAME=rabo de cavalo
TYPE=T_HAIR

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo com rabo

ON=@CREATE
ATTR=attr_newbie

//*****************************************************************************
// i_hair_2_pigtails
//*****************************************************************************
[ITEMDEF 02049]
defname=i_hair_2_pigtails
NAME=Cabelo com trancas
TYPE=T_HAIR

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo com trancas

ON=@CREATE
ATTR=attr_newbie

//*****************************************************************************
// i_hair_elven_flower
//*****************************************************************************
[ITEMDEF 12236]
Defname=i_hair_elven_flower // penteado especial com cabelo comprido estilo ponytail
Name=Cabelo preso de lado
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo preso de lado

ON=@CREATE
ATTR=attr_newbie

//*****************************************************************************
// i_hair_elven_braid
//*****************************************************************************
[ITEMDEF 12239]
Defname=i_hair_elven_braid
Name=Cabelo com tranca unica // penteado especial para o mullet comprido uma espécie de ponytail
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo com tranca unica

ON=@CREATE
ATTR=attr_newbie

//*****************************************************************************
// i_hair_elven_long4
//*****************************************************************************
[ITEMDEF 12240]
Defname=i_hair_elven_long4 // coque para mulheres com cabelo longo
Name=Cabelo comprido com coque
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo comprido com coque

ON=@CREATE
ATTR=attr_newbie

//*****************************************************************************
// i_hair_elven_long3
//*****************************************************************************
[ITEMDEF 12238]
Defname=i_hair_elven_long3  // penteado especial
Name=Cabelo Samurai
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo Samurai

ON=@CREATE
ATTR=attr_newbie


//*****************************************************************************
// i_hair_mohawk
//*****************************************************************************
[ITEMDEF 02044]
defname=i_hair_mohawk
NAME=Cabelo Moicano
TYPE=T_HAIR
VALUE={8 12}

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo moicano

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_short

//*****************************************************************************
// i_hair_krisna
//*****************************************************************************
[ITEMDEF 0204a]
defname=i_hair_krisna
NAME=Cabelo hare krisna
TYPE=T_HAIR

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo hare krisna

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_receding

//*****************************************************************************
// i_hair_elven_spike
//*****************************************************************************
[itemdef 12241]
Defname=i_hair_elven_spike // corte especial quando o cabelo está afro ou buns vai para o pageboy
Name=Cabelo repicado
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo repicado

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_pageboy

//*****************************************************************************
// i_hair_LF
//*****************************************************************************
[ITEMDEF 12224]
Defname=i_hair_LF
Name=Cabelo Mullet repicado (com pena)  // Corte especial com navalha. do i_hair_elven_ML
type=t_hair

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cabelo Mullet repicado (com pena)

ON=@CREATE
ATTR=attr_newbie
more1=i_hair_elven_long2

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs BARBAS
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_barba_careca
//*****************************************************************************
[ITEMDEF i_barba_careca]
id=i_deed
NAME=Sem barba
TYPE=t_beard
weight=0
layer=layer_beard

ON=@CREATE
more1=i_beard_short_med

//*****************************************************************************
// i_beard_short
//*****************************************************************************
[ITEMDEF 0203f] // sem bigode
defname=i_beard_short
NAME=Barbicha media
TYPE=T_BEARD

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Barbicha media

ON=@CREATE
ATTR=attr_newbie
more1=i_beard_short_med


//*****************************************************************************
// i_beard_short_med
//*****************************************************************************
[ITEMDEF 0204b] // real barba curta
defname=i_beard_short_med
NAME=Barba curta
TYPE=T_BEARD

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Barba curta

ON=@CREATE
ATTR=attr_newbie
more1=i_beard_long_med

//*****************************************************************************
// i_beard_long
//*****************************************************************************
[ITEMDEF 0203e]
defname=i_beard_long
NAME=Barbicha longa
TYPE=T_BEARD

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Barbicha longa

ON=@CREATE
ATTR=attr_newbie
more1=i_beard_long_med

//*****************************************************************************
// i_beard_long_med
//*****************************************************************************
[ITEMDEF 0204c]
defname=i_beard_long_med
NAME=barba longa
TYPE=T_BEARD

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Barba longa


ON=@CREATE
ATTR=attr_newbie

//*****************************************************************************
// i_beard_goatee
//*****************************************************************************
[ITEMDEF 02040]
defname=i_beard_goatee
NAME=Barbicha
TYPE=T_BEARD

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Barbicha

ON=@CREATE
ATTR=attr_newbie
more1=i_beard_short

//*****************************************************************************
// i_beard_mustache
//*****************************************************************************
[ITEMDEF 02041]
defname=i_beard_mustache
NAME=Bigode
TYPE=T_BEARD

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Bigode

ON=@CREATE
ATTR=attr_newbie
more1=i_beard_short_med

//*****************************************************************************
// i_beard_vandyke
//*****************************************************************************
[ITEMDEF 0204d]
defname=i_beard_vandyke
NAME=Cavanhaque
TYPE=T_BEARD

CATEGORY=MyT - Items by Professions
SUBSECTION=Barber
DESCRIPTION=Cavanhaque

ON=@CREATE
ATTR=attr_newbie
more1=i_beard_short_med


//*****************************************************************************
//*****************************************************************************
// ITEMDEFs FERRAMENTAS
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_cabelo
//*****************************************************************************
[ITEMDEF i_cabelo]
NAME=cabelo cortado
ID=0dfe
type=t_script
WEIGHT=0

ON=@CREATE
attr=012

//*****************************************************************************
// i_navalha
//*****************************************************************************
[ITEMDEF i_navalha]
NAME=Navalha
ID=09a5
TYPE=t_buy_misc
SKILLMAKE=TINKERING 44.4
RESOURCES=1 i_ingot_iron, 1 i_ingot_copper
VALUE=32
WEIGHT=2

ON=@DCLICK
src.sysmessagegreen Em quem voce deseja usar isso?
target
return 1

ON=@TARGON_GROUND 
src.sysmessagered Use a navalha apenas para raspar a barba e o cabelo.
return 1  

ON=@TARGON_ITEM 
src.sysmessagered Use a navalha apenas para raspar a barba e o cabelo.
return 1 

ON=@TARGON_CHAR
if (<src.targ.body>!=c_man)&&(<src.targ.body>!=c_woman)&&(<src.targ.body>!=c_elf_male)&&(<src.targ.body>!=c_elf_female)
    src.sysmessagered Use a navalha apenas em pessoas.
    return 1
elif <src.targ.findlayer.layer_helm>
    src.sysmessagered Retire o chapeu, mascara ou elmo.
    return 1
elif (<src.targ> != <src>)
    src.ctag.client_uid=<src.targ>  // tag que contém o uid do freguês
    src.targ.ctag.barber_uid=<src>
    src.targ.menu autoriza_barba    // abre para o freguês o menu autorizacorte
    src.sysmessageyellow Se a pessoa autorizar, vai aparecer o menu.
else
    src.ctag.client_uid=<src>       // eu sou o fregues
    src.menu navalha
endif
return 1

//*****************************************************************************
// i_cabelo_tesoura
//*****************************************************************************
[ITEMDEF 0dfc]
NAME=tesoura para cortar cabelo
DEFNAME=i_cabelo_tesoura
TYPE=t_buy_misc
SKILLMAKE=TINKERING 40.3
RESOURCES=1 i_ingot_iron, 1 i_ingot_copper
VALUE=32
WEIGHT=2

ON=@DCLICK
src.sysmessagegreen Que cabelo voce deseja cortar?
target
return 1

ON=@TARGON_GROUND 
src.sysmessagered Use a tesoura de cortar cabelo apenas em cabelo.
return 1  

ON=@TARGON_ITEM 
src.sysmessagered Use a tesoura de cortar cabelo apenas em cabelo.
return 1 

ON=@TARGON_CHAR
obj=<src.targ.findlayer.layer_hair>
if (<src.targ.body>!=c_man)&&(<src.targ.body>!=c_woman)&&(<src.targ.body>!=c_elf_male)&&(<src.targ.body>!=c_elf_female)
    src.sysmessagered Use a tesoura de cortar cabelo apenas em pessoas.
    return 1
elif <src.targ.findlayer.layer_helm>
    src.sysmessagered Retire o chapeu, mascara ou o elmo para cortar o cabelo.
    return 1
elif (<obj.baseid>==i_hair_ponytail)||(<obj.baseid>==i_hair_2_pigtails)
    src.sysmessagered Solte o cabelo antes de corta-lo.
    return 1
elif (<obj.baseid>!=i_hair_long)&&(<obj.baseid>!=i_hair_pageboy)&&(<obj.baseid>!=i_hair_buns)&&(<obj.baseid>!=i_hair_afro)&&(<obj.baseid>!=i_hair_short)
    src.sysmessagered Este cabelo nao tem tamanho suficiente para ser cortado. Se quiser raspa-lo use uma navalha.
    return 1
elif (<src.targ> != <src>)
    src.ctag.client_uid=<src.targ>  // tag que contém o uid do freguês
    src.targ.ctag.barber_uid=<src>
    src.targ.menu autoriza_cabelo   // abre para o freguês o menu autorizacorte
    src.sysmessageyellow Se a pessoa autorizar, vai aparecer o menu.
else
    src.ctag.client_uid=<src>       // eu sou o fregues
    src.ctag.hair=<src.findlayer.layer_hair.baseid>
    src.skillmenu sm_cabelo_tesoura
endif
return 1

[EOF]

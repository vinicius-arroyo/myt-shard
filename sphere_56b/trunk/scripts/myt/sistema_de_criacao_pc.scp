//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE CRIACAO DE PERSONAGEMS por
//  Galthar
//  Metus
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:
//
//TAGS usadas:
//tag.agr=5
//tag.statsPoints
//tag.classe
//tag.raca
//tag.hab1=Archery
//tag.hab2=Tactics
//tag.hab3=Tracking
//tag.hab4=Camping
//tag.hab5=Camping
//tag.hab6=Camping
//tag.habesp=Camping
//tag.pontosdealma
//tag.name
//tag.drowLowLightBonus
//*****************************************************************************

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME charCreation]
//Tempo que uma conta deve ser usada entes de polher escolher a raça Drow.
//Deve ser posto em décimos de segundo.
timerDrow=10*60*60*25

//Tempo que uma conta deve ser usada entes de polher escolher a raça Orc.
//Deve ser posto em décimos de segundo.
timerOrc=10*60*60*25

//Valor inicial de skill primaria, secundaria e terciaria.
char_primarySkill=500
char_secondarySkill=200
char_tertiarySkill=50

//Valor inicial de moedas de cobre
char_initialCopperCoins=300

//Coordenadas de inicio
//char_startPnt_Humano=1,1,0
//char_startPnt_Elfo=1,1,0
//char_startPnt_Anao=1,1,0
//char_startPnt_Orc=1,1,0
//char_startPnt_Drow=1,1,0

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// char_assembleGump
//*****************************************************************************
//cria parte padrão para todos os gumps
[FUNCTION char_assembleGump]
noclose
nomove
PAGE 0
resizepic 50 74 3600 640 460
gumppic 237 99 1143
gumppic 121 95 9000
gumppic 550 93 9000
gumppic 0 0 10440
gumppic 658 0 10441

//*****************************************************************************
// char_setAsPrimarySkill( skillName )
//*****************************************************************************
//inicia skill desejada como primária
[FUNCTION char_setAsPrimarySkill]
tryp 0 <args> char_primarySkill

//*****************************************************************************
// char_setAsSecondarySkill( skillName )
//*****************************************************************************
//inicia skill desejada como secundária
[FUNCTION char_setAsSecondarySkill]
tryp 0 <args> char_secondarySkill

//*****************************************************************************
// char_setAsTertiarySkill( skillName )
//*****************************************************************************
//inicia skill desejada como terciária
[FUNCTION char_setAsTertiarySkill]
tryp 0 <args> char_tertiarySkill

//*****************************************************************************
// char_createNew
//*****************************************************************************
//chamado ON=@login pela skillclass 0
//inicia o processo de criação do personagem
[FUNCTION char_createNew]

//salva nome
if (strmatch(<tag0.name>,0)))
    tag.name=<name>
endif

//zera todas as skills,deixa com um minimo de stats e sem equipamento
str=10
dex=10
int=10
findlayer(layer_half_apron).remove
findlayer(layer_pack).remove
findlayer(layer_hand1).remove
findlayer(layer_hand2).remove

//inicia com agressividade neutra
tag.agr=5

//equipa com o basico e comum para todas as racas e classes
serv.newitem=i_backpack
new.equip

serv.newitem=i_adaga
new.equip

serv.newitem=i_candle
new.equip

serv.newitem=i_coin_copper
new.amount=char_initialCopperCoins
new.bounce

//inicia tela de criacao de novo char
dialog d_char_creation
return 1

//*****************************************************************************
// char_setSkills
//*****************************************************************************
//configura skill de acordo com a classe escolhida
[FUNCTION char_setSkills]

//limpa habilidades primarias
tag.hab1=
tag.hab2=
tag.hab3=
tag.hab4=
tag.hab5=
tag.hab6=

//inicia skills
allskills 0
Archery 15
Fencing 15
Macefighting 15
Parrying 15
Swordsmanship 15
Tactics 15
Wrestling 15
 
if (strmatch(<tag.classe>,arqueiro))
 char_setAsPrimarySkill Archery
 char_setAsPrimarySkill Tactics
 char_setAsPrimarySkill Tracking
 char_setAsPrimarySkill Camping
 char_setAsPrimarySkill Anatomy
  
 char_setAsSecondarySkill Bowcraft
 char_setAsSecondarySkill Healing
 char_setAsSecondarySkill Lumberjacking
 char_setAsSecondarySkill Herding
 char_setAsSecondarySkill Taming
 char_setAsSecondarySkill Fishing
 
 char_setAsTertiarySkill AnimalLore
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill DetectingHidden
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill SpiritSpeak
 char_setAsTertiarySkill Armslore
 char_setAsTertiarySkill Fencing
 char_setAsTertiarySkill Veterinary
 
 tag.hab1=Archery
 tag.hab2=Tactics
 tag.hab3=Tracking
 tag.hab4=Camping
 tag.hab5=Anatomy

 return 1
endif
if (strmatch(<tag.classe>,guerreiro))
 char_setAsPrimarySkill Fencing
 char_setAsPrimarySkill Macefighting
 char_setAsPrimarySkill Parrying
 char_setAsPrimarySkill Swordsmanship
 char_setAsPrimarySkill Tactics
 char_setAsPrimarySkill Wrestling
 
 char_setAsSecondarySkill Anatomy
 char_setAsSecondarySkill Camping
 char_setAsSecondarySkill Forensics
 char_setAsSecondarySkill MagicResistance
 char_setAsSecondarySkill Poisoning
 
 char_setAsTertiarySkill Armslore
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Healing
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill SpiritSpeak 

 tag.hab1=Fencing
 tag.hab2=Macefighting
 tag.hab3=Parrying
 tag.hab4=Swordsmanship
 tag.hab5=Tactics
 tag.hab6=Wrestling
 
 return 1
endif 

if (strmatch(<tag.classe>,mago))
 char_setAsPrimarySkill Alchemy
 char_setAsPrimarySkill EvaluatingIntel
 char_setAsPrimarySkill Inscription
 char_setAsPrimarySkill Magery
 char_setAsPrimarySkill Meditation
 char_setAsPrimarySkill MagicResistance
 char_setAsPrimarySkill Poisoning
 char_setAsPrimarySkill TasteID
 char_setAsPrimarySkill ItemID

 char_setAsSecondarySkill Anatomy
 char_setAsSecondarySkill Cartography
 char_setAsSecondarySkill Healing
 char_setAsSecondarySkill Macefighting
 char_setAsSecondarySkill SpiritSpeak

 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill Tactics
 char_setAsTertiarySkill Wrestling

 tag.hab1=Alchemy
 tag.hab2=EvaluatingIntel
 tag.hab3=Inscription
 tag.hab4=Magery
 tag.hab5=Meditation
 tag.hab6=MagicResistance
 
 return 1
endif 

if (strmatch(<tag.classe>,ladrao))
 char_setAsPrimarySkill Fencing
 char_setAsPrimarySkill Hiding
 char_setAsPrimarySkill Lockpicking
 char_setAsPrimarySkill RemoveTrap
 char_setAsPrimarySkill Snooping
 char_setAsPrimarySkill Stealing
 char_setAsPrimarySkill Stealth
 char_setAsPrimarySkill TasteID

 char_setAsSecondarySkill Cartography
 char_setAsSecondarySkill DetectingHidden
 char_setAsSecondarySkill Poisoning
 char_setAsSecondarySkill Tactics
 char_setAsSecondarySkill ItemID

 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill SpiritSpeak 
 char_setAsTertiarySkill Wrestling

 tag.hab1=Fencing
 tag.hab2=Hiding
 tag.hab3=Lockpicking
 tag.hab4=Snooping
 tag.hab5=Stealing
 tag.hab6=Stealth
 
 return 1
endif 



if (strmatch(<tag.classe>,bardo))
 char_setAsPrimarySkill Cartography
 char_setAsPrimarySkill Enticement
 char_setAsPrimarySkill Fencing
 char_setAsPrimarySkill Musicianship
 char_setAsPrimarySkill Peacemaking
 char_setAsPrimarySkill Provocation
 char_setAsPrimarySkill ItemID
 char_setAsPrimarySkill TasteID

 char_setAsSecondarySkill Hiding
 char_setAsSecondarySkill Lockpicking 
 char_setAsSecondarySkill Stealth
 char_setAsSecondarySkill Tactics
 char_setAsSecondarySkill Wrestling

 char_setAsTertiarySkill Anatomy
 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Lumberjacking
 char_setAsTertiarySkill Mining
 char_setAsTertiarySkill SpiritSpeak 
 

 tag.hab1=Enticement
 tag.hab2=Musicianship
 tag.hab3=Peacemaking
 tag.hab4=Provocation
  
 return 1
endif 

if (strmatch(<tag.classe>,artesao))
 char_setAsPrimarySkill Cartography
 char_setAsPrimarySkill Cooking
 char_setAsPrimarySkill Tailoring
 char_setAsPrimarySkill Tinkering

 char_setAsSecondarySkill Armslore
 char_setAsSecondarySkill Bowcraft
 char_setAsSecondarySkill Fishing
 char_setAsSecondarySkill Lumberjacking
 char_setAsSecondarySkill TasteId
 char_setAsSecondarySkill Mining

 char_setAsTertiarySkill Blacksmithing
 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Carpentry
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill Lockpicking
 char_setAsTertiarySkill SpiritSpeak 
 char_setAsTertiarySkill Wrestling
 char_setAsTertiarySkill ItemId

 tag.hab1=Cartography
 tag.hab2=Cooking
 tag.hab3=ItemId
 tag.hab4=Tailoring
 tag.hab5=TasteId
 tag.hab6=Tinkering
 
 return 1
endif 


if (strmatch(<tag.classe>,lenhador))
 char_setAsPrimarySkill Bowcraft
 char_setAsPrimarySkill Armslore
 char_setAsPrimarySkill Carpentry
 char_setAsPrimarySkill Lumberjacking
 char_setAsPrimarySkill Herding
 char_setAsPrimarySkill Taming

 char_setAsSecondarySkill Tracking
 char_setAsSecondarySkill Animallore
 char_setAsSecondarySkill Veterinary
 char_setAsSecondarySkill Fishing

 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill SpiritSpeak
 char_setAsTertiarySkill Swordsmanship
 char_setAsTertiarySkill Wrestling
 char_setAsTertiarySkill ItemID
 char_setAsTertiarySkill Macefighting
 
 tag.hab1= Bowcraft
 tag.hab2= Carpentry
 tag.hab3= Lumberjacking
 tag.hab4= Armslore
 tag.hab5= Herding
 tag.hab6= Taming
  
 return 1
endif 

if (strmatch(<tag.classe>,ferreiro))
 char_setAsPrimarySkill Blacksmithing
 char_setAsPrimarySkill Mining
 char_setAsPrimarySkill Armslore
 char_setAsPrimarySkill Anatomy

 char_setAsSecondarySkill Macefighting
 char_setAsSecondarySkill Parrying
 char_setAsSecondarySkill ItemId
 char_setAsSecondarySkill Tinkering
 char_setAsSecondarySkill Forensics
 char_setAsSecondarySkill Lockpicking
 char_setAsSecondarySkill Tactics

 char_setAsTertiarySkill Camping
 char_setAsTertiarySkill Cooking
 char_setAsTertiarySkill Fishing
 char_setAsTertiarySkill Hiding
 char_setAsTertiarySkill SpiritSpeak
 char_setAsTertiarySkill Wrestling
 char_setAsTertiarySkill Lumberjacking

 tag.hab1= Blacksmithing
 tag.hab2= Mining
 tag.hab3= Armslore
 tag.hab4= Anatomy

 return 1
endif 

//*****************************************************************************
// char_setStats
//*****************************************************************************
//configura stats de acordo com a classe escolhida
[FUNCTION char_setStats]

//configura base com valor da classe
if (strmatch(<tag.classe>,arqueiro))
 str=67
 dex=86
 int=37
elif (strmatch(<tag.classe>,guerreiro))
 str=86
 dex=67
 int=37
elif (strmatch(<tag.classe>,mago))
 str=52
 dex=52
 int=86
elif (strmatch(<tag.classe>,ladrao))
 str=52
 dex=86
 int=52
elif (strmatch(<tag.classe>,bardo))
 str=57
 dex=57
 int=76
elif (strmatch(<tag.classe>,artesao))
 str=57
 dex=57
 int=76
elif (strmatch(<tag.classe>,lenhador))
 str=76
 dex=57
 int=57
elif (strmatch(<tag.classe>,ferreiro))
 str=76
 dex=57
 int=57
endif

//pontos para distribuir nos stats
tag.statsPoints=10

//aplica bonus e penalidades de acordo com raca
if (strmatch(<tag.raca>,Anao))
 str=<str>+5
 dex=<dex>-5
 return 1
elif (strmatch(<tag.raca>,Elfo))
 str=<str>-5
 dex=<dex>+5
 return 1
elif (strmatch(<tag.raca>,Drow))
 str=<str>-10
 dex=<dex>+5
 int=<int>+5
 return 1
elif (strmatch(<tag.raca>,Orc))
 str=<str>+10
 int=<int>-10
 return 1
endif

//*****************************************************************************
// char_finishCreation
//*****************************************************************************
//finaliza a criacao do personagem
[FUNCTION char_finishCreation]

//adiciona eventos gerais
events=+e_sistema_de_morte
events=+e_sistema_de_fe
events=+e_recon
events=+e_evaluatingintel_new
events=+e_magic_resistance
events=+e_mytgeral
events=+e_combat
events=+e_dex_loss
events=+e_clima
//events=+e_parrying_new  // Problemnas
//events=+e_armor_absorv  // Problemnas

//adiciona eventos especificos de raca e classe
tryp 0 events=+e_<tag.raca>
tryp 0 events=+e_<tag.classe>

//troca o nome pela raca
name=<tag.raca>
title=

//configura outros sistemas
tag.pontosdealma=5
crafting_init
tailor_init

//prepara loot inicial baseado na melhor habilidade ou na classe escolhida
if (strmatch(<tag.habesp>, Magery))
    char_lootMagery
elif ( strmatch(<tag.classe>, mago) )
    char_lootMago
elif (strmatch(<tag.habesp>, Cooking))
    char_lootCooking
elif (strmatch(<tag.habesp>, Tinkering))
    char_lootTinkering
elif (strmatch(<tag.habesp>, Tailoring))
    char_lootTailoring
elif (strmatch(<tag.habesp>, Swordsmanship))
    char_lootSwordsmanship
elif (strmatch(<tag.habesp>, Macefighting))
    char_lootMacefighting
elif (strmatch(<tag.habesp>, Fencing))
    char_lootFencing
elif (strmatch(<tag.habesp>, Parrying)) && (strmatch(<tag.classe>, guerreiro))
    char_lootParrying
elif (strmatch(<tag.habesp>, Wrestling)) && (strmatch(<tag.classe>, guerreiro))
    char_lootWrestling
elif (strmatch(<tag.habesp>, Tactics)) && (strmatch(<tag.classe>, guerreiro))
    char_lootTactics
elif (strmatch(<tag.habesp>, Alchemy))
    char_lootAlchemy
elif (strmatch(<tag.habesp>, Inscription))
    char_lootInscription
elif (strmatch(<tag.habesp>, Healing))
    char_lootHealing
elif (strmatch(<tag.classe>, lenhador))
    char_lootLenhador
elif (strmatch(<tag.classe>, ferreiro))
    char_lootFerreiro
elif (strmatch(<tag.classe>, ladrao))
    char_lootLadrao
elif (strmatch(<tag.classe>, bardo))
    char_lootBardo
elif (strmatch(<tag.classe>, arqueiro))
    char_lootArqueiro
elif (strmatch(<tag.classe>, artesao))
    char_lootArtesao
endif

//define skillclass
//if (strmatch(<tag.classe>,arqueiro))
// skillclass=1
//elif (strmatch(<tag.classe>,guerreiro))
// skillclass=2
//elif (strmatch(<tag.classe>,mago))
// skillclass=3
//elif (strmatch(<tag.classe>,ladrao))
// skillclass=4
//elif (strmatch(<tag.classe>,bardo))
// skillclass=5
//elif (strmatch(<tag.classe>,artesao))
// skillclass=6
//elif (strmatch(<tag.classe>,lenhador))
// skillclass=7
//elif (strmatch(<tag.classe>,ferreiro))
// skillclass=8
//endif

//configura caracteristicais raciais e transporta para a 'cidade natal'
if (strmatch(<tag.raca>,Elfo))
    char_configElfo
elif (strmatch(<tag.raca>,Drow))
    char_configDrow
elif (strmatch(<tag.raca>,Orc))
    char_configOrc
elif (strmatch(<tag.raca>,Anao))
    char_configAnao
elif (strmatch(<tag.raca>,Humano))
    char_configHumano
endif

tag.char_criado=1

trigger @login

//*****************************************************************************
// char_configElfo
//*****************************************************************************
[FUNCTION char_configElfo]
    if (<findlayer(layer_hair).id> != 0203c) 
        serv.newitem i_hair_long
        new.color 037
        new.equip
    endif
    if (<findlayer(layer_beard)>)
        findlayer(layer_beard).remove
    endif
    
    if (<sex 1/0>)
        obody = c_elf_male    
    else
        obody = c_elf_female
    endif
    body=<obody>
   
    go luya

//*****************************************************************************
// char_configDrow
//*****************************************************************************
[FUNCTION char_configDrow]
    if (<findlayer(layer_hair).id> != 0203c)
        serv.newitem i_hair_long
        new.color 0
        new.equip
    endif
    if (<findlayer(layer_beard)>)
        findlayer(layer_beard).remove
    endif
    
    if (<sex 1/0>)
        obody = c_elf_male    
    else
        obody = c_elf_female
    endif
    body=<obody>
    
    tag.drowLowLightBonus=3
    go luya

//*****************************************************************************
// char_configOrc
//*****************************************************************************
[FUNCTION char_configOrc]
    if (<findlayer(layer_helm).baseid> != i_head_orc)
        findlayer(layer_helm).remove
        serv.newitem i_head_orc
        new.equip
    endif
    if (<findlayer(layer_hair)>) 
        findlayer(layer_hair).remove
    endif
    if (<findlayer(layer_beard)>)
        findlayer(layer_beard).remove
    endif
    
    obody=c_orc
    body=<obody>
    
    go luya
    
//*****************************************************************************
// char_configAnao
//*****************************************************************************
[FUNCTION char_configAnao]
    if (<findlayer(layer_hair).id> != 0203c)
        serv.newitem i_hair_long
        new.equip
    endif
    if (<findlayer(layer_beard).id> != 0203e) && (<body> != c_woman)
        serv.newitem i_beard_long_med
        new.color=<findlayer(layer_hair).color>
        new.equip
    endif
    
    if (<sex 1/0>)
        obody = c_man
    else
        obody = c_woman
    endif
    body=<obody>
    
    go luya
    
//*****************************************************************************
// char_configHumano
//*****************************************************************************
[FUNCTION char_configHumano]
    if (<findlayer(layer_hair).id> == 0)
        serv.newitem i_cabelo_careca
        new.color <findlayer(layer_beard).color>
        new.equip
    endif
    if (<findlayer(layer_beard).id> == 0) && (<body> != c_woman)
        serv.newitem i_barba_careca
        new.color <findlayer(layer_hair).color>
        new.equip
    endif
    
    if (<sex 1/0>)
        obody = c_man
    else
        obody = c_woman
    endif
    body=<obody>
    
    go luya

//*****************************************************************************
// char_lootCooking
//*****************************************************************************
[FUNCTION char_lootCooking]
 serv.newitem i_panela
 new.bounce

 serv.newitem i_frigideira
 new.bounce

 serv.newitem i_vasilha_vazia
 new.bounce

 serv.newitem i_batata
 new.amount=3
 new.bounce

 serv.newitem i_ervilha
 new.amount=3
 new.bounce

//*****************************************************************************
// char_lootTinkering
//*****************************************************************************
[FUNCTION char_lootTinkering]
 serv.newitem i_livro_projetos
 new.bounce

 serv.newitem i_tinker_tools
 new.bounce

 serv.newitem i_soprador_vidro
 new.bounce

 serv.newitem i_areia
 new.amount=10
 new.bounce

 serv.newitem {i_ar_proj_trefila i_ar_proj_lanterna i_ar_proj_balao i_ar_proj_sextante}
 new.amount=1
 new.bounce

 serv.newitem {i_ar_proj_tubo i_ar_proj_destilador i_ar_proj_espiriteira i_ar_proj_vara_pesca}
 new.amount=1
 new.bounce

 serv.newitem {i_ar_proj_chain i_ar_proj_ring i_ar_proj_term i_ar_proj_relogio i_ar_proj_misturador}
 new.amount=1
 new.bounce

 serv.newitem {i_ar_proj_lanterna i_ar_proj_recipientes i_ar_proj_misturador i_ar_proj_destilador}
 new.amount=1
 new.bounce

//*****************************************************************************
// char_lootTailoring
//*****************************************************************************
[FUNCTION char_lootTailoring]
 serv.newitem i_kit_costura
 new.bounce

 serv.newitem i_fita
 new.bounce

 serv.newitem i_thread
 new.amount=5
 new.color=colors_all
 new.bounce

 serv.newitem i_yarn_ball
 new.amount=2
 new.color=colors_all
 new.bounce

 serv.newitem i_tesoura
 new.bounce

 serv.newitem i_cloth
 new.amount=3
 new.color=colors_all
 new.bounce

//*****************************************************************************
// char_lootSwordsmanship
//*****************************************************************************
[FUNCTION char_lootSwordsmanship]
 serv.newitem i_espada_longa
 new.bounce

 serv.newitem i_pescoco_couro
 new.bounce

 serv.newitem i_peito_couro
 new.bounce

 serv.newitem i_braco_couro
 new.bounce

 serv.newitem i_cabeca_couro
 new.bounce

 serv.newitem i_perna_couro
 new.bounce

 serv.newitem i_luva_couro
 new.bounce

//*****************************************************************************
// char_lootMacefighting
//*****************************************************************************
[FUNCTION char_lootMacefighting]
 serv.newitem i_maca
 new.bounce

 serv.newitem i_pescoco_couro
 new.bounce

 serv.newitem i_peito_couro
 new.bounce

 serv.newitem i_braco_couro
 new.bounce

 serv.newitem i_cabeca_couro
 new.bounce

 serv.newitem i_perna_couro
 new.bounce

 serv.newitem i_luva_couro
 new.bounce

//*****************************************************************************
// char_lootFencing
//*****************************************************************************
[FUNCTION char_lootFencing]
 serv.newitem i_kryss
 new.bounce

 serv.newitem i_pescoco_couro
 new.bounce

 serv.newitem i_peito_couro
 new.bounce

 serv.newitem i_braco_couro
 new.bounce

 serv.newitem i_cabeca_couro
 new.bounce

 serv.newitem i_perna_couro
 new.bounce

 serv.newitem i_luva_couro
 new.bounce

//*****************************************************************************
// char_lootParrying
//*****************************************************************************
[FUNCTION char_lootParrying]
 serv.newitem i_espada_curta
 new.bounce

 serv.newitem i_escudo_madeira
 new.bounce

 serv.newitem i_pescoco_couro
 new.bounce

 serv.newitem i_peito_couro
 new.bounce

 serv.newitem i_braco_couro
 new.bounce

 serv.newitem i_cabeca_couro
 new.bounce

 serv.newitem i_perna_couro
 new.bounce

 serv.newitem i_luva_couro
 new.bounce
 
//*****************************************************************************
// char_lootWrestling
//*****************************************************************************
[FUNCTION char_lootWrestling]
 serv.newitem i_elmo_aberto
 new.bounce

 serv.newitem i_pescoco_couro
 new.bounce

 serv.newitem i_peito_couro
 new.bounce

 serv.newitem i_braco_couro
 new.bounce

 serv.newitem i_cabeca_couro
 new.bounce

 serv.newitem i_perna_couro
 new.bounce

 serv.newitem i_luva_couro
 new.bounce
 
//*****************************************************************************
// char_lootTactics
//*****************************************************************************
[FUNCTION char_lootTactics]
 serv.newitem i_espada_longa
 new.bounce

 serv.newitem i_escudo_batalha
 new.bounce

 serv.newitem i_braco_couro
 new.bounce

 serv.newitem i_luva_couro
 new.bounce

//*****************************************************************************
// char_lootMagery
//*****************************************************************************
[FUNCTION char_lootMagery]
 serv.newitem i_spellbook
 new.link=<uid>
 new.name=grimorio
 new.equip

 serv.newitem i_almofariz
 new.bounce

 serv.newitem i_tubo
 new.amount=2
 new.bounce

 serv.newitem i_bottle_empty
 new.amount=2
 new.bounce

 serv.newitem i_r_perola_negra
 new.amount=2
 new.bounce

 serv.newitem i_r_musgo_sangue
 new.amount=2
 new.bounce

 serv.newitem i_r_alho
 new.amount=2
 new.bounce

 serv.newitem i_r_ginseng
 new.amount=2
 new.bounce

 serv.newitem i_r_raiz_de_mandrake
 new.amount=2
 new.bounce

 serv.newitem i_r_nightshade
 new.amount=2
 new.bounce

 serv.newitem i_r_cinza_sulfurosa
 new.amount=2
 new.bounce

 serv.newitem i_r_teia_de_aranha
 new.amount=2
 new.bounce

//*****************************************************************************
// char_lootAlchemy
//*****************************************************************************
[FUNCTION char_lootAlchemy]
 serv.newitem i_spellbook
 new.link=<uid>
 new.name=grimorio
 new.equip

 serv.newitem i_almofariz
 new.bounce

 serv.newitem i_espiriteira
 new.bounce

 serv.newitem i_balao
 new.bounce

 serv.newitem i_tubo
 new.amount=5
 new.bounce

 serv.newitem i_bottle_empty
 new.amount=5
 new.bounce

 serv.newitem i_r_perola_negra
 new.amount=5
 new.bounce

 serv.newitem i_r_musgo_sangue
 new.amount=5
 new.bounce

 serv.newitem i_r_alho
 new.amount=5
 new.bounce

 serv.newitem i_r_ginseng
 new.amount=5
 new.bounce

 serv.newitem i_r_raiz_de_mandrake
 new.amount=5
 new.bounce

 serv.newitem i_r_nightshade
 new.amount=5
 new.bounce

 serv.newitem i_r_cinza_sulfurosa
 new.amount=5
 new.bounce


 serv.newitem i_r_teia_de_aranha
 new.amount=5
 new.bounce
 
//*****************************************************************************
// char_lootInscription
//*****************************************************************************
[FUNCTION char_lootInscription]
 serv.newitem i_spellbook
 new.link=<uid>
 new.name=grimorio
 new.equip

 serv.newitem i_book_sm
 new.bounce

 serv.newitem i_book_sm_pub
 new.bounce

 serv.newitem i_book_sm_pub
 new.bounce

 serv.newitem i_tinta
 new.bounce

 serv.newitem i_tinta
 new.bounce

 serv.newitem i_documento_branco
 new.amount=10
 new.bounce
 
//*****************************************************************************
// char_lootHealing
//*****************************************************************************
[FUNCTION char_lootHealing]
 serv.newitem i_bandage
 new.amount={10 20}
 new.bounce

 serv.newitem bottle_orange
 new.amount=2
 new.bounce

 serv.newitem bottle_yellow
 new.amount=2
 new.bounce

 serv.newitem bottle_blue
 new.amount=2
 new.bounce

 serv.newitem bottle_white
 new.amount=2
 new.bounce
 
//*****************************************************************************
// char_lootLenhador
//*****************************************************************************
[FUNCTION char_lootLenhador]
 serv.newitem i_cajado_pastor
 new.equip
 
 serv.newitem i_talhadeira
 new.bounce

 serv.newitem i_martelo
 new.bounce

 serv.newitem i_serra
 new.bounce

 serv.newitem i_serrote
 new.bounce

 serv.newitem i_raspadeira
 new.bounce

 serv.newitem i_pregos
 new.amount=100
 new.bounce

 serv.newitem i_tabua
 new.amount= {5 10}
 new.bounce

 serv.newitem i_racha
 new.bounce

 serv.newitem i_machadinha
 new.bounce

//*****************************************************************************
// char_lootFerreiro
//*****************************************************************************
[FUNCTION char_lootFerreiro]
 serv.newitem i_trefila
 new.bounce
 
 serv.newitem i_martelo_ferreiro 
 new.bounce

 serv.newitem i_alicate 
 new.bounce

 serv.newitem i_picareta 
 new.bounce

 serv.newitem i_ingot_iron
 new.amount={10 15}
 new.bounce
 
//*****************************************************************************
// char_lootLadrao
//*****************************************************************************
[FUNCTION char_lootLadrao]
 serv.newitem i_lockpick
 new.amount=5
 new.bounce

 serv.newitem i_adaga
 new.bounce

 serv.newitem i_bottle_black
 new.amount=5
 new.bounce

 serv.newitem i_bottle_yellow
 new.amount=2
 new.bounce
 
//*****************************************************************************
// char_lootBardo
//*****************************************************************************
[FUNCTION char_lootBardo]
 serv.newitem {i_lute i_lyre}
 new.bounce

 serv.newitem i_book_sm
 new.bounce

 serv.newitem i_pen_and_ink
 new.bounce
 
 serv.newitem i_documento_branco
 new.amount=2

//*****************************************************************************
// char_lootArqueiro
//*****************************************************************************
[FUNCTION char_lootArqueiro]
 serv.newitem i_bow
 new.morey=1
 new.bounce

 serv.newitem i_thread
 new.amount=2
 new.bounce

 serv.newitem i_barbante
 new.bounce
 new.dupe

 serv.newitem i_TALHADEIRA
 new.bounce

 serv.newitem i_machadinha
 new.bounce

 serv.newitem i_racha
 new.bounce

 serv.newitem i_arrow
 new.amount=30
 new.bounce

 serv.newitem i_pescoco_couro
 new.bounce

 serv.newitem i_peito_couro
 new.bounce

 serv.newitem i_braco_couro
 new.bounce

 serv.newitem i_cabeca_couro
 new.bounce

 serv.newitem i_perna_couro
 new.bounce

 serv.newitem i_luva_couro
 new.bounce
 
//*****************************************************************************
// char_lootMago
//*****************************************************************************
[FUNCTION char_lootMago]
 serv.newitem i_spellbook
 new.link=<uid>
 new.name=grimorio
 new.equip

 serv.newitem i_almofariz
 new.bounce

 serv.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 new.bounce

 serv.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 new.bounce

 serv.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 new.bounce

 serv.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 new.bounce

 serv.newitem {i_bottle_black i_bottle_yellow  i_bottle_white  i_bottle_purple  i_bottle_orange i_bottle_blue i_bottle_green i_bottle_red}
 new.bounce

 serv.newitem i_tubo
 new.amount=2
 new.update
 new.bounce

 serv.newitem i_bottle_empty
 new.amount=2
 new.update
 new.bounce

 serv.newitem i_r_perola_negra
 new.amount=2
 new.bounce

 serv.newitem i_r_musgo_sangue
 new.amount=2
 new.bounce

 serv.newitem i_r_alho
 new.amount=2
 new.bounce

 serv.newitem i_r_ginseng
 new.amount=2
 new.bounce

 serv.newitem i_r_raiz_de_mandrake
 new.amount=2
 new.bounce

 serv.newitem i_r_nightshade
 new.amount=2
 new.bounce

 serv.newitem i_r_cinza_sulfurosa
 new.amount=2
 new.bounce

 serv.newitem i_r_teia_de_aranha
 new.amount=2
 new.bounce

 serv.newitem i_documento_branco
 new.amount=3
 new.bounce

 serv.newitem i_tinta
 new.bounce

//*****************************************************************************
// char_lootArtesao
//*****************************************************************************
[FUNCTION char_lootArtesao]
 serv.newitem=i_coin_copper //cooper coins
 new.amount=100
 new.bounce

 serv.newitem i_thread
 new.amount=5
 new.bounce

 serv.newitem i_kit_costura
 new.amount=25
 new.bounce

 serv.newitem i_tesoura
 new.bounce

 serv.newitem i_panela
 new.bounce

 serv.newitem i_vasilha_vazia
 new.bounce

 serv.newitem {i_batata i_ervilha i_chicken_leg_raw i_lamb_leg_raw i_fish_cut_raw i_eggs_fresh i_eggs_fresh i_eggs_fresh i_bird_raw}
 new.amount=6
 new.bounce

 serv.newitem {i_batata i_ervilha i_chicken_leg_raw i_lamb_leg_raw i_fish_cut_raw i_eggs_fresh i_eggs_fresh i_eggs_fresh i_bird_raw}
 new.amount=6
 new.bounce
 
//*****************************************************************************
//*****************************************************************************
// DIALOGs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// d_char_creation
//*****************************************************************************
//inicia o processo de criacao do personagem mostrando a tela de apresentação
[DIALOG d_char_creation]
0, 0
char_assembleGump

text 82 76 1152 0

text 290 101 32 1
text 211 125 152 2
text 283 146 152 3
text 152 220 95 4
text 151 250 95 5
text 154 280 95 6
text 154 310 95 7
text 155 340 95 8
text 280 407 162 9
button 318 451 5033 5034 1 0 1
text 306 485 32 10

[DIALOG d_char_creation TEXT]

Mystical Tales Shard
Bem vindo ao sistema de criacao de personagens
do Mystical Tales Shard.
Atravez deste sistema, seu personagem sera recriado nos moldes
de nosso shard. Antes de prosseguir, tenha em mente que voce
esta aceitando as regras de nossa comunidade. Tenha certeza de
ter lido e entendido o funcionamento dos sistemas basicos e dos
sistemas que sao pertinentes a sua raca e classe.
Tenha um bom divertimento!
http;\\www.myt.com.br


[DIALOG d_char_creation BUTTON]
on=1
dialog d_char_creation_races

//*****************************************************************************
// d_char_creation_races
//*****************************************************************************
//selecao de raça
[DIALOG d_char_creation_races]
0, 0
char_assembleGump

button 140 230 2151 2152 1 0 1
gumppic 183 232 2445
button 140 270 2151 2152 1 0 2
button 140 310 2151 2152 1 0 3
button 140 350 2151 2152 1 0 4
button 140 390 2151 2152 1 0 5
gumppic 183 272 2445
gumppic 183 312 2445
gumppic 183 352 2445
gumppic 183 392 2445
text 290 101 32 0
text 307 133 152 1
text 194 232 95 2
text 194 272 95 3
text 194 312 95 4
text 194 352 95 5
text 194 392 95 6
text 108 468 95 7
//dtext 129 489 95 xxx
resizepic 353 213 3500 294 223
button 536 480 5033 5034 1 0 6

if (strmatch(<tag.raca>,Orc))
    LOCAL.desc=Orcs sao brutos e de habitos desagradaveis. Muito individualistas, so compartilhariam algo com seus semelhantes ainda que raramente. Detestam os Elfos mais do que tudo, mas mentem algumas relacoes com Humanos e anoes por interresse. Mortes serao consideradas PKiling.
elif (strmatch(<tag.raca>,Anao))
    LOCAL.desc=Os anoes sao uma raca especialmente orgulhosa e de personalidade muito forte. Altamente materialistas os anoes tem um facinio especial por ouro e pedras preciosas. Anoes tem fama por sua coragem e bravura, da qual tem muito orgulho. Vivem pacificamente com os Humanos e toleram os Elfos.
elif (strmatch(<tag.raca>,Elfo))
    LOCAL.desc=Elfos se consideram superiores as outras racas. Sao amigos de Humanos e vivem com anoes, mas NUNCA se esquecam do sentimento de superioridade dos Elfos. O odio pelos Drows e explicito, em caso de encontro dicussoes sao inevitaveis. Contudo, pkilling nao sera tolerado.
elif (strmatch(<tag.raca>,Drow))
    LOCAL.desc=Drows sao os senhores do medo e das trapassas. Por motivo nenhum se misturariam com Elfos mas toleram a companhia de Humanos uma vez que sao fonte de renda garantida para suas transacoes e trapassas. Sao sempre sombrios e misteriosos. Nao ha como ler a mente de um Drow. 
else //if (strmatch(<tag.raca>,Humano)) PADRAO
    tag.raca=Humano
    LOCAL.desc=Humanos sao altamente versateis e se adaptam facilmente ao ambiente. Se integram facilmente, e tratam a todos como como iguais, o que lhes fornece uma imagem neutra. Alguns sao preconceituosos, mas quando sao, nao transparecem  isso se nao houverem outros por perto ou se estao em minoria.    
endif

dhtmlgump 377 230 250 185 0 1 <LOCAL.desc>
dtext 375 188 162 <tag.raca>

[DIALOG d_char_creation_races TEXT]
Mystical Tales Shard
Selecione sua raca
Humano
Anao
Elfo
Drow *
Orc *
* Racas avancadas. Somente com um mes de jogo e autorizacao.

[DIALOG d_char_creation_races BUTTON]
on=1    //humano
tag.raca=Humano
dialog d_char_creation_races
return 1

on=2    //anao
tag.raca=Anao
dialog d_char_creation_races
return 1

on=3    //elfo
tag.raca=Elfo
dialog d_char_creation_races
return 1

on=4    //drow
if (<eval 0<account.TOTALCONNECTTIME>> >=<eval 0(timerDrow)>) && ((<tag0.advrace>)==1) || (<isgm>==1)
    tag.raca=Drow
else
    f_sendMessage <uid>,Indisponivel,SM_YELLOW
endif
dialog d_char_creation_races
return 1

on=5    //orc
if (<eval 0<account.TOTALCONNECTTIME>> >=<eval 0(timerOrc)>) && ((<tag0.advrace>)==1) || (<isgm>==1)
    tag.raca=Orc
else
    f_sendMessage <uid>,Indisponivel,SM_YELLOW
endif
dialog d_char_creation_races
return 1

on=6    //continue
dialog d_char_creation_classes
return 1


//*****************************************************************************
// d_char_creation_classes
//*****************************************************************************
//selecao de classe
[DIALOG d_char_creation_classes]
0, 0
char_assembleGump
button 140 200 2151 2152 1 0 1
gumppic 183 202 2445
button 140 240 2151 2152 1 0 2
button 140 280 2151 2152 1 0 3
button 140 320 2151 2152 1 0 4
button 140 360 2151 2152 1 0 5
gumppic 183 242 2445
gumppic 183 282 2445
gumppic 183 322 2445
gumppic 183 362 2445
resizepic 353 213 3500 294 223
gumppic 183 402 2445
gumppic 183 442 2445
gumppic 183 482 2445
button 140 400 2151 2152 1 0 6
button 140 440 2151 2152 1 0 7
button 140 480 2151 2152 1 0 8
button 322 481 5035 5036 1 0 9
button 529 481 5033 5034 1 0 10
text 290 101 32 0
text 303 133 152 1
text 194 200 95 2
text 194 242 95 3
text 194 282 95 4
text 194 322 95 5
text 194 362 95 6
text 194 402 95 7
text 194 442 95 8
text 194 482 95 9

if (strmatch(<tag.classe>,ferreiro))
    LOCAL.desc=Das minas para a oficina, e entao para as minas denovo. Esse eh o dia-a-dia de um ferreiro. Servindo sempre aos guerreiros com suas armas e armaduras, fazendo os pregos e as ferramentas do carpinteiro e barras de metal e arames para o artesao. Sempre muito requisitados por todos, entre panelas e laminas, o ferreiro tambem constroi fama e fortuna.
    LOCAL.title=Ferreiro
elif (strmatch(<tag.classe>,guerreiro))
    LOCAL.desc=A classe mais procurada entre os aventureiros se satisfaz com uma arma adequada e uma boa armadura. Guerreiros frequentemente cacam para obter mercadorias para as demais classes e sao peritos em lutas. 
    LOCAL.title=Guerreiro
elif (strmatch(<tag.classe>,mago))
    LOCAL.desc=Ser um arcano significa ter nos gestos e nas palavras o poder sobre a vida, a morte e os materiais inanimados. Eles aceitam ficar horas sentados lendo enigamas e purificando suas substancias alquimicas para criar novas formas de viver, matar e construir o que nenhuma engenharia ja mais conseguiria.
    LOCAL.title=Mago
elif (strmatch(<tag.classe>,ladrao))
    LOCAL.desc=Os ladinos nao sao sempre salteadores. A pessoa que voce mais tem como sua amiga pode tranquilamente ser um sabichao tirando proveito de voce para os seus interesses. Eles tem habilidade com as palavras e usam as sombras com seu escudo. Nunca subjulgue um ladino, pois ele pode aparecer subitamente nas suas costas, com uma adaga em seu pescoco.
    LOCAL.title=Ladino
elif (strmatch(<tag.classe>,bardo))
    LOCAL.desc=Boemios e de belas cancoes, sao os homens que espalham as noticias. Muitas aventureiros pagam para os bardos compor cancoes e versos sobre seus feitos, e executa-las em publico para lhes render fama. Os bardos, alem de bons musicos, conhecem as leis, a historia a geografia e ate os sentimentos de seu povo.
    LOCAL.title=Bardo
elif (strmatch(<tag.classe>,lenhador))
    LOCAL.desc=O homem do machado tem o tato apurado e o olho na lamina, sempre cortando e construindo. Se voce quer um objeto e ele eh feito de madeira, eh com o lenhador que voce deve falar. Construir arcos, moveis, flechas, ferramentas de madeira e materia prima para as outras classes eh o que tras a comida para a mesa de um lenhador. 
    LOCAL.title=Lenhador
elif (strmatch(<tag.classe>,artesao))
    LOCAL.desc=Artistas natos, o artesao tem otimas habilidades manuais e pode manufaturar objetos diversos como maquinas, ferramentas, roupas e utensilios. Tem o dom do comercio e de ganhar dinheiro, colecionando novos projetos e novas ferramentas de trabalho. Muitas outras classes dependem diretamente do artesao.
    LOCAL.title=Artesao
else //if (strmatch(<tag.classe>,arqueiro)) PADRAO
    tag.classe=arqueiro
    LOCAL.desc=Arqueiros sao comumente a linha de tiro de uma equipe de aventureiros. Suas epecialidades, elem do arco, eh a de rastreador e campista. Muito versateis, sao ideais para uso em florestas.
    LOCAL.title=Arqueiro
endif

dhtmlgump 377 230 250 185 0 1 <LOCAL.desc>
dtext 375 188 162 <tag.raca> - <LOCAL.title>

[DIALOG d_char_creation_classes TEXT]
Mystical Tales Shard
Selecione sua classe
Arqueiro
Guerreiro
Mago
Ladrao
Bardo
Artesao
Lenhador
Ferreiro


[DIALOG d_char_creation_classes BUTTON]
on=1    //arqueiro
if (strmatch(<tag.raca>,Anao))
    f_sendMessage <uid>,Classe nao permitida para essa raca.,SM_YELLOW
else
    tag.classe=arqueiro
endif
dialog d_char_creation_classes

on=2    //guerreiro
tag.classe=guerreiro
dialog d_char_creation_classes

on=3    //mago
if (strmatch(<tag.raca>,Anao))
    f_sendMessage <uid>,Classe nao permitida para essa raca.,SM_YELLOW
else
    tag.classe=mago
endif
dialog d_char_creation_classes

on=4    //ladrao
tag.classe=ladrao
dialog d_char_creation_classes

on=5    //bardo
if (strmatch(<tag.raca>,Anao)) || (strmatch(<tag.raca>,Orc)) || (strmatch(<tag.raca>,Drow))
    f_sendMessage <uid>,Classe nao permitida para essa raca.,SM_YELLOW
 else
    tag.classe=bardo
 endif
dialog d_char_creation_classes

on=6    //artesao
tag.classe=artesao
dialog d_char_creation_classes

on=7    //lenhador
if (strmatch(<tag.raca>,Anao)) || (strmatch(<tag.raca>,Drow))
    f_sendMessage <uid>,Classe nao permitida para essa raca.,SM_YELLOW
 else
    tag.classe=lenhador
 endif
dialog d_char_creation_classes

on=8    //ferreiro
if (strmatch(<tag.raca>,Elfo))
    f_sendMessage <uid>,Classe nao permitida para essa raca.,SM_YELLOW
 else
    tag.classe=ferreiro
 endif
dialog d_char_creation_classes

on=9    //voltar
dialog d_char_creation_races
return 1

on=10   //continue
char_setSkills
dialog d_char_creation_top_skill


//*****************************************************************************
// d_char_creation_top_skill
//*****************************************************************************
//selecao da melhor habilidade
[DIALOG d_char_creation_top_skill]
0, 0
char_assembleGump
text 290 101 32 0
text 267 133 152 1

resizepic 182 219 3500 165 135
resizepic 387 219 3500 165 135

button 140 230 2151 2152 1 0 1
text 205 233 95 2
button 140 310 2151 2152 1 0 2
text 205 313 95 3
button 568 230 2151 2152 1 0 3
text 423 233 95 4
button 568 310 2151 2152 1 0 4
text 423 313 95 5
if ( strlen( <tag.hab5> ) > 0 ) || ( strlen( <tag.hab6> ) > 0 )
    button 140 270 2151 2152 1 0 5
    text 205 273 95 6
    button 568 270 2151 2152 1 0 6
    text 423 273 95 7
endif

button 322 481 5035 5036 1 0 7  //voltar

text 176 419 1152 8
text 176 449 1152 9


[DIALOG d_char_creation_top_skill TEXT]
Mystical Tales Shard
Selecione sua melhor habilidade
<tag.hab1>
<tag.hab2>
<tag.hab3>
<tag.hab4>
<tag.hab5>
<tag.hab6>
Esta sera a pericia na qual voce ganhara 10.0 pontos
adicionais e definira seus itens iniciais.


[DIALOG d_char_creation_top_skill BUTTON]
on=1,6
try <tag.hab<argn>> =<<tag.hab<argn>>> + 100
tag.habesp=<tag.hab<argn>>
char_setStats
dialog d_char_creation_bonus
return 1

on=7
//voltar
dialog d_char_creation_classes
return 1


//*****************************************************************************
// d_char_creation_bonus
//*****************************************************************************
[DIALOG d_char_creation_bonus]
0, 0
noclose
char_assembleGump

text 290 101 32 0
text 267 133 152 1
button 270 230 2151 2152 1 0 1
button 270 270 2151 2152 1 0 2
button 270 310 2151 2152 1 0 3
button 322 481 5035 5036 1 0 4
resizepic 312 219 3500 165 135
text 335 233 1345 2
text 284 178 162 3
text 335 273 95 4
text 335 313 95 5
text 427 233 32 6
text 427 273 32 7
text 427 313 32 8
text 432 178 10 9
text 327 153 152 10

[DIALOG d_char_creation_bonus TEXT]
Mystical Tales Shard
Distribuas os pontos conforme
Forca
Pontos para gastar;
Destreza
Inteligencia
<str>
<dex>
<int>
<eval <tag.statsPoints>>
sua vontade


[DIALOG d_char_creation_bonus BUTTON]
on=1,3

if (<argn> == 1)
    str=<str>+1
elif (<argn> == 2)
    dex=<dex>+1
else
    int=<int>+1
endif

tag.statsPoints=<tag.statsPoints> - 1
if (<tag.statsPoints> == 0)
 char_finishCreation
else
 dialog=d_char_creation_bonus
endif

on=4    //voltar
char_setSkills
dialog d_char_creation_top_skill

[FUNCTION Fix_SkillID]
FORPLAYERS 6200
//Magos e Bardos
 IF (strmatch(<tag.classe>,Mago)) || (strmatch(<tag.classe>,Bardo))
  IF (<TasteID> < char_primarySkill)
   char_setAsPrimarySkill TasteID
  ENDIF
  IF (<ItemID> < char_primarySkill)
   char_setAsPrimarySkill ItemID
  ENDIF
//Ladrão
 ELIF (strmatch(<tag.classe>,Ladrao))
  IF (<TasteID> < char_primarySkill)
   char_setAsPrimarySkill TasteID
  ENDIF
  IF (<ItemID> < char_secondarySkill)
   char_setAsSecondarySkill ItemID
  ENDIF
//Artesão
 ELIF (strmatch(<tag.classe>,Artesao))
  IF (<TasteID> < char_secondarySkill)
   char_setAsSecondarySkill TasteID
  ENDIF
  IF (<ItemID> < char_tertiarySkill)
   char_setAsTertiarySkill ItemID
  ENDIF
//Ferreiro
 ELIF (strmatch(<tag.classe>,Ferreiro))
  IF (<ItemID> < char_secondarySkill)
   char_setAsSecondarySkill ItemID
  ENDIF
//Lenhador
 ELIF (strmatch(<tag.classe>,Lenhador))
  IF (<ItemID> < char_tertiarySkill)
   char_setAsTertiarySkill ItemID
  ENDIF
 ENDIF
 LOCAL.i += 1
ENDFOR
sysmessageorange Testados <dLOCAL.i> jogadores.


[EOF]
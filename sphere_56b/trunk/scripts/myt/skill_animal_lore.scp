//*****************************************************************************
//*****************************************************************************
//
// Skill Animal Lore por
//  Silver
//  UsMarine (vinicius.arroyo at gmail.com)
//
//  Chame sk_anlore sempre que a skill for utilizada. Dessa forma o player SRC
//recebe as informações sobre o alvo (SRC.targ) de acordo com sua akill.
//
// A mensagem é exibida da seguinte forma:
// "<Nome>: <sexo>, <anlr_fomA>, <temDono>" 
// A é calculado de acordo com a skill do SRC e a habilidade do TARG.
// As mensagens estão na tabela (DEFNAME) abaixo.
//*****************************************************************************
//*****************************************************************************
//TODO:

//TAGS usadas:
//*****************************************************************************

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME anlore_msg]
anlr_fom1=nao se aguenta em pe de tanta fome
anlr_fom2=esta faminto
anlr_fom3=esta faminto
anlr_fom4=esta com fome
anlr_fom5=esta com fome
anlr_fom6=esta saudavel
anlr_fom7=esta saudavel
anlr_fom8=esta satisfeito
anlr_fom9=esta satisfeito
anlr_fom10=esta contente
anlr_fom11=esta contente


anlr_t_arock=pedras
anlr_t_cloth=roupas
anlr_t_coin=moedas
anlr_t_corpse=corpos
anlr_t_crops=plantacoes
anlr_t_fire=fogo
anlr_t_fish=peixes
anlr_t_foliage=folhagem
anlr_t_food=alimentos preparados
anlr_t_fruit=frutas
anlr_t_gem=gemas
anlr_t_grain=graos
anlr_t_grass=grama
anlr_t_gold=ouro
anlr_t_ingot=lingotes
anlr_t_leather=couro
anlr_t_meat_raw=carne crua
anlr_t_ore=metais
anlr_t_water_wash=agua


//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// sk_anlore - animal lore skill
//*****************************************************************************

[FUNCTION sk_anlore]

if (!<Skill_InUse SKILL_ANIMAL_LORE>)
    src.sysmessagered Voce so pode fazer uma coisa por vez
    src.Skill_InUse 0
    return 1
elif (<src.targ.canseelos> == 0)
    src.sysmessagered Seu alvo esta fora da linha de visao...
    return 1
elif (<src.targ.uid> == <src.uid>)
    src.sysmessagered Voce nao pode usar Animal Lore em si mesmo...
    return 1
elif (<src.targ.isitem>)
    src.sysmessagered Voce nao pode usar Animal Lore em objetos...
    return 1
elif (<src.targ.flags>&statf_dead)
    src.sysmessagered Voce nao pode usar Animal Lore em almas...
    return 1
elif (<src.findid.i_anlore_mry>)
    src.sysmessagered Voce ainda nao pode usar Animal Lore...
    return 1
elif (<src.targ.npc>!=brain_animal)
    src.sysmessagered Use Animal Lore em animais somente!
    return 1
endif

src.emoteyellow Observando <src.targ.name>
src.ctag.disturb=0
src.newitem i_anlore_mry
NEW.more=<eval 11-(<src.animallore>/100)>

if ( <NEW.more> <= 0 )
    NEW.more=1
endif

NEW.link=<src.targ>
NEW.cont=<src>
NEW.timer=1

return 1

//*****************************************************************************
// anl_foodtype (uidToReturnFoodType)
//*****************************************************************************
//default: player
[FUNCTION anl_foodtype]
obj=<argv0>
split_key_val <obj.foodtype>
if !(<ctag0.spkv_keys>)
    return
endif

LOCAL.m=<eval <ctag.spkv_keys>-1>
for R 0 <LOCAL.m>
    LOCAL.f=<def.anlr_<ctag.spkv_r<LOCAL.R>_k>>
    if (strlen(<LOCAL.f>)<=1)
        serv.log [ANIMAL LORE] - tipo de item desconhecido: <obj.id>/<LOCAL.f>
        LOCAL.f=desconhecido
    endif
    
    if (<LOCAL.R>>0)
        LOCAL.s .=,<LOCAL.f>
    else
        LOCAL.s =<LOCAL.f>
    endif
endfor
return <local.s>

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

[ITEMDEF i_anlore_mry]
id=i_deed
name=AnimalLore
type=t_eq_script
layer=layer_special
weight=0

ON=@TIMER

if (<cont.canseelos <link>> == 0)
    cont.sysmessageyellow Seu alvo esta fora da linha de visao...
    remove
    return 1
// Se for perturbado ou estiver incapacitado:
elif (<cont.f_isDisturbed>) || (!<Skill_InUse SKILL_ANIMAL_LORE>)
    cont.sysmessageyellow Voce nao esta em condicoes de analisar nada.
    Skill_InUse 0
    remove
    return 1
//loop de animacao
elif (<more>)
    more=<more>-1
    timer=1
    return 1 
endif

//transforma em uma escala de 0 a 10
LOCAL.food=<eval (<link.food>*10)/<link.maxfood>>

//muda a escala para cada nivel de skill
if ( <cont.animallore> < 30.0 )
  LOCAL.food=<eval ((<LOCAL.food>/5)*5)+2>
elif ( <cont.animallore> < 70.0 )
  LOCAL.food=<eval ((<LOCAL.food>/2)*2)+1>
else
    LOCAL.food=<eval <LOCAL.food>+1>
endif

if ( <LOCAL.food> > 11 )
  LOCAL.food = 11
endif

if ( 0<link.tag.sexo>==1 )
    LOCAL.s=: macho,
elif ( 0<link.tag.sexo>==2 )
    LOCAL.s=: femea,
else
    LOCAL.s=:
endif

if (0<link.tag.born>) && (<R0,1000><<cont.animallore>)
    LOCAL.s=<LOCAL.s> <eval (<serv.time>-<link.tag.born>)/144000> dias,
endif

if (<link.memoryfindtype.memory_ipet>)
    LOCAL.d=tem dono
else
    LOCAL.d=nao tem dono
endif

cont.sysmessagegreen Alimentos: <cont.anl_foodtype <link>>
cont.sysmessagegreen <link.name><local.s> <DEF.anlr_fom<eval <LOCAL.food>>>, <LOCAL.d>.

cont.skill_gain skill_animal_lore
Skill_InUse 0
remove
return 1

[EOF]

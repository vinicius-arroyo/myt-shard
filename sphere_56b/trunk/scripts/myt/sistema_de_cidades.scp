[COMMENT sistema_de_cidades]
##################################################################
##                                                              ##
##       Mystical Tales Shard - Sistema de cidades v2.2a        ##
##              Galthar, o Errante    11/05/2009                ##
##                                                              ##
##################################################################

        -=Tabelas na DB=-

DROP TABLE IF EXISTS `mytserver`.`towns`;
CREATE TABLE  `mytserver`.`towns` (
  `region` varchar(45) NOT NULL default 'a_world',
  `capital` varchar(45) NOT NULL default 'a_world',
  `type` int(10) unsigned NOT NULL default '1',
  `master` int(10) unsigned NOT NULL default '0',
  `artefactUID` int(10) unsigned NOT NULL default '0',
  `permissions` int(10) unsigned NOT NULL default '6032',
  `tresure` int(10) unsigned NOT NULL default '1000',
  `vaultUID` int(10) unsigned NOT NULL default '0',
  `color1` int(10) unsigned NOT NULL default '4',
  `color2` int(10) unsigned NOT NULL default '54',
  `commercialfee` int(10) unsigned NOT NULL default '5',
  `buildingfee` int(10) unsigned NOT NULL default '15',
  `tilelandprice` int(10) unsigned NOT NULL default '13',
  `tilebuildprice` int(10) unsigned NOT NULL default '22',
  `tilefixtureprice` int(10) unsigned NOT NULL default '70',
  `FriendRaces` varchar(255) NOT NULL default 'Anao,Elfo,Drow,Humano,Orc',
  PRIMARY KEY  (`region`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;

DROP TABLE IF EXISTS `mytserver`.`towns_privs`;
CREATE TABLE  `mytserver`.`towns_privs` (
  `id` int(10) unsigned NOT NULL auto_increment,
  `region` varchar(45) NOT NULL,
  `position` int(10) unsigned NOT NULL default '999',
  `title` varchar(24) NOT NULL,
  `description` varchar(1024) NOT NULL,
  `privs` int(10) unsigned NOT NULL,
  PRIMARY KEY  (`id`)
) ENGINE=MyISAM;

DROP TABLE IF EXISTS `mytserver`.`towns_members`;
CREATE TABLE  `mytserver`.`towns_members` (
  `UID` int(10) unsigned NOT NULL default '0' COMMENT '''UID do player''',
  `region` varchar(45) NOT NULL default 'a_world' COMMENT '''Defname da region''',
  `priv_id` int(10) unsigned NOT NULL default '999' COMMENT '''id em towns_privs. 0 se banido''',
  `flags` int(10) unsigned NOT NULL default '0' COMMENT '''Criminal, em debito, preso e etc.'''
) ENGINE=MyISAM DEFAULT CHARSET=latin1;

DROP TABLE IF EXISTS `mytserver`.`towns_petitions`;
CREATE TABLE  `mytserver`.`towns_petitions` (
  `id` int(10) unsigned NOT NULL auto_increment,
  `type` tinyint(3) unsigned NOT NULL,
  `UID` int(10) unsigned NOT NULL,
  `region` varchar(45) NOT NULL,
  `father` int(10) unsigned NOT NULL,
  `title` varchar(45) NOT NULL,
  `text` text NOT NULL,
  `time` datetime NOT NULL,
  `gtime` varchar(45) NOT NULL,
  `flags` tinyint(3) unsigned NOT NULL,
  PRIMARY KEY  (`id`),
  KEY `region` USING BTREE (`region`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;

DROP TABLE IF EXISTS `mytserver`.`towns_petitions`;
CREATE TABLE  `mytserver`.`towns_petitions` (
  `id` int(10) unsigned NOT NULL auto_increment,
  `type` tinyint(3) unsigned NOT NULL,
  `UID` int(10) unsigned NOT NULL,
  `region` varchar(45) NOT NULL,
  `father` int(10) unsigned NOT NULL,
  `title` varchar(45) NOT NULL,
  `text` text NOT NULL,
  `time` datetime NOT NULL,
  `gtime` varchar(45) NOT NULL,
  `flags` tinyint(3) unsigned NOT NULL,
  PRIMARY KEY  (`id`),
  KEY `region` USING BTREE (`region`)
) ENGINE=MyISAM AUTO_INCREMENT=26 DEFAULT CHARSET=latin1;


        -=Tags utilizadas=-
ctag.town_ID_<dlocal.i>
ctag.town_PetitionText
ctag.town_UID
ctag.town_coloriq
ctag.town_description
ctag.town_page
ctag.town_petitionTitle
ctag.town_petition_<dlocal.i>
ctag.town_position
ctag.town_positionID
ctag.town_privid
ctag.town_privs
ctag.town_region
ctag.town_title
ctag.town_type
tag.town (no artefato da town)



        -=API=-
town [new|delete|<region.defname>]
        Dialog de interface com a town
        - new: Nova Town
        - delete: tira a town da DB
        - <region.defname>: Abre o dialog para a dada região
        - DEFAULT:  Abre o dialog para região em que o player está
        
town_TrasferMastership <region.defname>,<playerUID>
        Transfere o título de mestre da Town <argv0> para o player <argv1>
        
town_getPermissions [<region.defname>]
        Retorna o bitmask de permissividade da town (TOWN_CAN_* e TOWN_CAN_*)

town_addCash <argn>
        Adiciona <argn> ao tesouro da town e retorna o total

town_Check_Privs <argn>
        Retorna 1 se DEFAULT tem o privilégio <argn1> nessa town
        
town_CheckPrivs <argn1>
        town_Check_Privs
        
        -=TODO=-
- FRIEND setado como CRIMINOSO causa entrada dupla na DB

[DEFNAME sistema_de_cidades]
//Flags de permissions da town
TOWN_CAN_WARMODE        01      //Entrar em warmode pode?
TOWN_CAN_WEAPON         02      //Arma mais pesada que uma adaga ou cajado
TOWN_CAN_HOOD           04      //Andar de capuz pode?
TOWN_CAN_HARM           08      //QUALQUER harm. Incluindo de magia. SRC do @DAMAGE? @HIT? @SPELLSUCCESS de SPELL_HARM?
TOWN_CAN_MAGIC          010     //QUALQUER magia. Até Healing ou Fireworks
TOWN_CAN_STEAL          020     //Snooping e stealing.
TOWN_CAN_LOCKPICK       040     //No step do lockpicking. 1x por segundo
TOWN_CAN_RESOURCES      080     //Mining, lumber, @Kill BRAIN_ANIMAL, fishing, facas especiais
TOWN_CAN_SELL           0100    //Pode vender itens para NPCs
TOWN_CAN_BUY            0200    //Pode comprar itens de NPCs
TOWN_CAN_AZILUM         0400    //Pode-se pedir asilo
TOWN_AUTO_AZILUM        0800    //asilo não precisa de permissão
TOWN_CAN_PETITION       01000   //Petições abertas para membros

//Nome das permissões da town
TOWN_PERMISSION_0       Lutar na cidade
TOWN_PERMISSION_1       Andar armado
TOWN_PERMISSION_2       Andar encapuzado
TOWN_PERMISSION_3       Ferir cidadaos
TOWN_PERMISSION_4       Fazer uso de magias
TOWN_PERMISSION_5       Roubar
TOWN_PERMISSION_6       Arrombar fechaduras
TOWN_PERMISSION_7       Coletar recursos naturais
TOWN_PERMISSION_8       Vender bens ou objetos
TOWN_PERMISSION_9       Comprar bens ou objetos
TOWN_PERMISSION_10      Pedir cidadania
TOWN_PERMISSION_11      Cidadania automatica
TOWN_PERMISSION_12      Abrir peticoes

//Privs dos membros da Town
TOWN_PRIV_WAR           00000001
TOWN_PRIV_HOOD          00000002
TOWN_PRIV_HARM          00000004
TOWN_PRIV_MAGIC         00000008
TOWN_PRIV_STEAL         00000010
TOWN_PRIV_LOCKPICK      00000020
TOWN_PRIV_RESOURCES     00000040
TOWN_PRIV_MARKET        00000080
TOWN_PRIV_PETITION      00000100
TOWN_PRIV_PRIVS         00000200
TOWN_PRIV_MEMBER        00000400
TOWN_PRIV_BOOKS         00000800
TOWN_PRIV_TAXES         00001000
TOWN_PRIV_CONFISCATE    00002000
TOWN_PRIV_TRESURE       00004000
TOWN_PRIV_LAWS          00008000
TOWN_PRIV_DIPLOMACY     00010000
TOWN_PRIV_BOD           00020000
TOWN_PRIV_HIRE          00040000
TOWN_PRIV_GUARD         00080000
TOWN_PRIV_KEEPER        00100000
TOWN_PRIV_SENESCHAL     00200000
TOWN_PRIV_SBOND         00400000
TOWN_PRIV_BBOND         00800000
TOWN_PRIV_MANAGE        00019600 //TOWN_PRIV_DIPLOMACY|TOWN_PRIV_LAWS|TOWN_PRIV_TAXES|TOWN_PRIV_MEMBER|TOWN_PRIV_PRIVS

//Nomes dos privs da Town
TOWN_PRIV_0             Lutar na cidade
TOWN_PRIV_1             Usar capuz
TOWN_PRIV_2             Causar dano
TOWN_PRIV_3             Usar magica
TOWN_PRIV_4             Roubar
TOWN_PRIV_5             Arrombar
TOWN_PRIV_6             Extrair recursos
TOWN_PRIV_7             Participar do mercado
TOWN_PRIV_8             Criar/votar peticoes
TOWN_PRIV_9             Alterar cargos
TOWN_PRIV_10            Adicionar/remover membros
TOWN_PRIV_11            Gerenciar livros
TOWN_PRIV_12            Alterar taxas
TOWN_PRIV_13            Confiscar mercadoria
TOWN_PRIV_14            Acesso ao tesouro
TOWN_PRIV_15            Promulgar leis
TOWN_PRIV_16            Poderes diplomaticos
TOWN_PRIV_17            Fazer ordens de compra
TOWN_PRIV_18            Contratar funcionarios
TOWN_PRIV_19            Comandar guardas
TOWN_PRIV_20            Comandar porteiros
TOWN_PRIV_21            Comandar senescal
TOWN_PRIV_22            Fazer pequeno emprestimo
TOWN_PRIV_23            Fazer grande emprestimo

//Flags dos membros das Towns
TOWN_FLAG_BANNED        01
TOWN_FLAG_CRIMINAL      02
TOWN_FLAG_JAILED        04
TOWN_FLAG_DEBT          08
TOWN_FLAG_FRIEND        010
TOWN_FLAG_FORBIDDEN     020

//Nomes dos flags dos membros das Towns
TOWN_FLAG_0             Banido
TOWN_FLAG_1             Criminoso
TOWN_FLAG_2             Preso
TOWN_FLAG_3             Em divida
TOWN_FLAG_4             Forasteiro amigo

//Tipos de petição
TOWN_PET_VOTE           1       //Guarda um voto. 'father' é a petição
TOWN_PET_AZILUM         2       //Petição para ser cidadão. UID é o char.
TOWN_PET_GENERAL        3       //Petição geral. Depende do contesto de 'text'.

//Flags de petição
TOWN_PET_FLAG_FAVOR     1       //Voto a favor da petição se 'type' = 1. Petição aceita pelo governo se 'type'>1
TOWN_PET_FLAG_AGAINST   2       //Voto contra a petição se 'type' = 1. Petição rejeitada pelo governo se 'type'>1


//DEFNAME das páginas do gump de Towns
TOWN_PAGE_FOUND         1       //Fundação da Town. Para GMs
TOWN_PAGE_REPORT        2       //Informações gerais
TOWN_PAGE_CONFIG        3       //Configura a town (cores, raca, capital, tipo, cofre...)
TOWN_PAGE_PERMISSIONS   4       //Mostra o que é TOWN_CAN_*
TOWN_PAGE_RACES         5       //Mostra racas amigas
TOWN_PAGE_BANS          6       //Mostra banidos
TOWN_PAGE_FRIENDS       7       //Mostra forasteiros (de raças inimigas) permitidos
TOWN_PAGE_TAXES         8       //Mostra as taxas da town
TOWN_PAGE_LAWS          9       //Mostra os textos de leis RP playermade
TOWN_PAGE_LAWS_SHOW     10      //Mostra um texto de lei RP playermade
TOWN_PAGE_LAWS_EDIT     11      //Edita um texto de lei RP playermade
TOWN_PAGE_POSITIONS     12      //Mostra os cargos existentes (1=rei, 999=cidadão)
TOWN_PAGE_POSITION_DESC 13      //Altera a descrição do cargo
TOWN_PAGE_PRIVS         14      //Mostra privs dos cargos
TOWN_PAGE_MEMBERS       15      //Lista membros da town
TOWN_PAGE_MEMBER        16      //Mostra um membro
TOWN_PAGE_HIRES         17      //Mostra lista de NPCs para contratar
TOWN_PAGE_NPCS          18      //Lista NPCs contatados e custo para manter
TOWN_PAGE_NPC           19      //Mostra NPC, status, demitir...
TOWN_PAGE_POLLS         20      //Lista plebicitos
TOWN_PAGE_POLL          21      //Mostra um plebicito
TOWN_PAGE_AZILUM        22      //Pede para percenter a town
TOWN_PAGE_PRISION       23      //Página principal da prisão
TOWN_PAGE_CELLS_LIST    24      //Lista as celas e adiciona.

//tag.town_servant
TOWN_SERVANT_GATE       1
TOWN_SERVANT_GUARD      2
TOWN_SERVANT_SENESCAL   3

//Nome dos cargos de NPC
TOWN_SERVANT_1          Porteiros
TOWN_SERVANT_2          Guardas
TOWN_SERVANT_3          Senescais

//Tempo que demora para o Senescal rodar a função TOWN_TRIG em décimos de segundo
TOWN_TRIG_TIME      12*60*60*10

//Preço em mc para contratar um funcionadio com tag.town_servant N
TOWN_SERVANT_1_HIRE  100
TOWN_SERVANT_2_HIRE  TOWN_GUARD_0_HIRE
TOWN_SERVANT_3_HIRE  500

//Preço em mc para pagar diário a um funcionario com tag.town_servant N
TOWN_SERVANT_1_ORD   5
TOWN_SERVANT_2_ORD   TOWN_GUARD_0_ORD
TOWN_SERVANT_3_ORD   5


//Preço em mc para contratar um guarda de nivel N
TOWN_GUARD_0_HIRE    20
TOWN_GUARD_1_HIRE    50
TOWN_GUARD_2_HIRE    100
TOWN_GUARD_3_HIRE    200
TOWN_GUARD_4_HIRE    400
TOWN_GUARD_5_HIRE    800

//Preco em mc para pagar diario um guarda de nivel N
TOWN_GUARD_0_ORD     3
TOWN_GUARD_1_ORD     5
TOWN_GUARD_2_ORD     8
TOWN_GUARD_3_ORD     16
TOWN_GUARD_4_ORD     18
TOWN_GUARD_5_ORD     24


//########################################################################
//########################################################################
//                              EVENTS
//########################################################################
//########################################################################
//Existem mais events, mas são inerentes ao sistema de guardas e estão
//no arquivo sistema_de_guardas.scp.

[REGIONTYPE r_town]
//Regiontype para regions que tem Town
on=@enter
src.sysmessageyellow Voce esta entrando nos dominios de <region.name>
src.town_enter <region.defname>

on=@exit
src.sysmessageyellow Voce esta saindo dos dominios de <region.name>
events -e_InTown
events -e_NPCInTown

[EVENTS e_town_Seneschal]
//TEVENT do Senescal
on=@Hunger
IF (<food> < 30)
 local.food=<town_ServantFood>
 IF (!<local.food>)
  IF (<restest 3 i_coin_copper>)
   consume 3 i_coin_copper
   serv.newitem i_carne
   new.amount=<R4,9>
   trysrc <uid> src.bounce <new>
  ELIF (!<food>) && (<tag0.nopay> > 2)
   say Chega disso! Me demito!
   timerf 15 remove 1
  ENDIF
 ELSE
  argn1=<local.food>
 ENDIF
ENDIF

    
on=@Death
town_UnRegServant
serv.log Hey! Hey! Hey! Senescal <name> (<uid>) foi destruido!!!


[EVENTS e_town_gatekeeper]
//TEVENT do Porteiro
on=@Death
town_UnRegServant
TOWN_RELEASE_DOORS
serv.log Hey! Hey! Hey! Porteiro <name> (<uid>) foi destruido!!!

on=@Hunger
IF (<food> < 30)
 local.food=<town_ServantFood>
 IF (!<local.food>)
  IF (<restest 3 i_coin_copper>)
   consume 3 i_coin_copper
   serv.newitem i_carne
   new.amount=<R4,9>
   trysrc <uid> src.bounce <new>
  ELIF (!<food>) && (<tag0.nopay> > 2)
   say Chega disso! Me demito!
   timerf 15 remove 1
  ENDIF
 ELSE
  argn1=<local.food>
 ENDIF
ENDIF




//########################################################################
//########################################################################
//                              TYPEDEFS
//########################################################################
//########################################################################


[TYPEDEF t_petition_board]
//Bulletin Board de petições
on=@dclick
if (<IsEmpty <tag.town_region>>)
 if !(<town_IsTown>)
  src.sysmessgered Esta regiao nao tem governo!
  return 1
 endif
 if (<src.town_canLaws <region.defname>>)
  src.ctag.town_board=<UID>
  src.town_sanceBoard
 else
  src.sysmessageyellow Alguem com privilegios para Promulgar Leis em <region.name> precisa sancionar (ativar) este quadro.
 endif
else
 src.ctag.town_region=<tag.town_region>
 src.ctag.town_board=<uid>
 src.ctag.town_noFullMenu=1
 src.town_petitionsMenu
endif
return 1

ON=@DropOn_Ground
timerf 1 nudgeup,10
src.sysmessageyellow Voce tera mais <serv.decaytimer> minutos para ajeitar antes que ele se fixe.
update

ON=@timer
attr 04050
timer -1
return 1

[TYPEDEF t_laws_board]
//Bulletin Board de decretos
on=@dclick
if (<IsEmpty <tag.town_region>>)
 if !(<town_IsTown>)
  src.sysmessgered Esta regiao nao tem governo!
  return 1
 endif
 if (<src.town_canLaws <region.defname>>)
  src.ctag.town_board=<UID>
  src.town_sanceBoard
 else
  src.sysmessageyellow Alguem com privilegios para Promulgar Leis em <region.name> precisa sancionar (ativar) este quadro.
 endif
else
 src.ctag.town_region=<tag.town_region>
 src.ctag.town_board=<uid>
 src.ctag.town_noFullMenu=1
 src.town_Laws
endif
return 1

ON=@DropOn_Ground
timerf 1 nudgeup,10
src.sysmessageyellow Voce tera mais <serv.decaytimer> minutos para ajeitar antes que ele se fixe.
update

ON=@timer
attr 04050
timer -1
return 1

[TYPEDEF t_town_artefact]
//EVENT do item que é o artefato da town. Quem detém o artefato vira mestre da Town.
on=@CLIENTTOOLTIP
src.f_sendTooltip <name>,O detentor deste artefato é o mestre de <serv.area.<tag.town>.name>.<DEF.br>Guarde muito bem este artefato!
return 1

on=@dclick
if (<src>==<topobj>)
 local.town=<tag.town>
 DB.QUERY=SELECT master FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
 if (<src>==<hval <DB.ROW.0.master>>) && (<src>==<link>)
  src.town <tag.town>
 else
  town_TrasferMastership <local.town>,<src>
  link=<src>
 endif
endif
return 1

on=@destroy
if 0<var.wipe>
    return 0
endif
local.vault=<town_GetVault <tag.town>>
if (<local.vault>)
 obj=<local.vault>
else
 serv.newitem i_chest_metal
 new.p=<serv.area.<tag.town>.p>
 new.attr=010
 new.timer=-1
 obj=<new>
 town_SetVault <tag.town>,<obj>
endif

//if outside the city vault, move the item and forbid the removal
if <cont> != <obj>
    serv.log [TOWN] Artefato removido enviado ao bau: <name>/<tag.town>/<obj>
    REF1=<cont>
    cont=<obj>
    obj.update
    REF1.update
    return 1
endif


//########################################################################
//########################################################################
//                              ITEMDEFS
//########################################################################
//########################################################################


[ITEMDEF i_petition_board]
NAME=Quadro de peticoes
ID=i_bulletin_board
TYPE=t_petition_board
SKILLMAKE=Carpentry 65.0
RESOURCES=12 i_tabua, 2 i_bobina_papel
WEIGHT=30
VALUE=120

ON=@Create
color=033
attr=02

[ITEMDEF i_laws_board]
NAME=Quadro de decretos
ID=i_bulletin_board
TYPE=t_laws_board
SKILLMAKE=Carpentry 65.0
RESOURCES=12 i_tabua, 2 i_bobina_papel
WEIGHT=30
VALUE=120

ON=@Create
color=092
attr=02


[ITEMDEF i_mry_seneschal]
//Memory do senescal. Faz pagamenos e prune.
ID=i_deed
NAME=Timer do Senescal
TYPE=t_eq_script

on=@timer
cont.town_pruneCitzens <cont.tag.town_region>
cont.town_pruneServants <cont.tag.town_region>
local.et=<eval <serv.time>-<serv.area.<cont.tag.town_region>.tag0.lastPaid>>
if (<local.et> > <DEF.TOWN_TRIG_TIME>)
 DORAND 4
  cont.say Bem, vamos efetuar o pagamento dos funcionarios.
  cont.say Hora de gastar o dinheiro dos contribuintes.
  cont.say Pronto. O proximo servico eh a folha de pagamento.
  cont.say Tenho que pagar os servidores agora.
 END
 cont.town_payServants
 serv.area.<cont.tag.town_region>.tag0.lastPaid=<serv.time>
endif
timerd <eval <DEF.TOWN_TRIG_TIME>/3>
return 1



//########################################################################
//########################################################################
//                              FUNCTIONS
//########################################################################
//########################################################################

//########################################################################
//      Funções de interface com o sistema
//########################################################################

[FUNCTION town]
//Função principal. Fundar town ou pedir report
if (STRMATCH(new,<args>))
 if (<region.defname>==a_world) || (<region.defname>==a_dungeons) || (!<region>)
  sysmessageyellow O mundo eh selvagem!
  return 0
 elif !(<town_IsTown>)
  ctag.town_region=<region.defname>
  ctag.town_page=<DEF.TOWN_PAGE_FOUND>
 else
  sysmessagered Ja ha registro de <region.name> como cidade!
  ctag.town_page=<DEF.TOWN_PAGE_REPORT>
 endif
elif (STRMATCH(delete,<args>))
 if !(<town_IsTown>)
  sysmessagered Nao ha registro de <region.name> como cidade!
 else
  town_deleteTown <region.defname>
 endif
 return 1
elif (<serv.area.<args>.uid>&06000000)
 ctag.town_region=<args>
 ctag.town_page=<DEF.TOWN_PAGE_REPORT>
elif (<town_IsTown>)
 ctag.town_region=<region.defname>
 ctag.town_page=<DEF.TOWN_PAGE_REPORT>
else
 sysmessageyellow O mundo eh selvagem!
 return 0
endif
SDIALOG d_town
return 1


[FUNCTION town_getType]
//Retorna o tipo da Town (1 a 3) ou 0 se não for town.
if (<IsEmpty <args>>)
 local.region=<region.defname>
else
 local.region=<args>
endif
DB.QUERY=SELECT type FROM towns WHERE region="<DB.ESCAPEDATA <local.region>>"
RETURN <hval <DB.ROW.0.type>>


[FUNCTION town_GetVault]
//Retorna a UID do cofre da TOWN [<argv0>] || DEFAULT
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
if (<town_IsTown <local.town>>)
 DB.QUERY=SELECT vaultUID FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
 ref1=<DB.ROW.0.vaultUID>
 if (<ref1.IsItem>)//Ainda existe?
  return <ref1>
 endif
 return 0
endif
return 0


[FUNCTION town_SetVault]
//Seta o container <argv1> como cofre da town <argv0>
//Existe essa town?
if (!<town_IsTown <argv0>>
 return 0
endif

ref1=<argv1>
//É um item válido?
if (<ref1.IsItem>)
 if (<ref1.type>!=t_container) && (<ref1.type>!=t_container_locked)
  serv.log [TOWNS] <ref1.name> (<ref1>@<ref1.p>) nao eh valido como cofre de <argv0>
  return 0
 endif
else
 serv.log [TOWNS] <ref1.name> (<ref1>@<ref1.p>) nao eh valido como cofre de <argv0>
 return 0
endif

//Já existe um cofre na town?
local.odlvault=<town_GetVault <argv0>>
if (<local.oldvault>)
 obj=<local.oldvault>
 if (<obj.IsItem>)
  //Transfere tudo de uma para outra
  FORCONT <obj> 0
   cont=<ref1>
  END
  ref1.update
 endif
endif

//Insere na debê
DB.QUERY=UPDATE towns SET vaultUID=<eval <ref1>> WHERE region="<DB.ESCAPEDATA <argv0>>"
return 1


[FUNCTION town_getRaces]
//Pega raças amigas da town com region=<args> ou da region de DEFAULT.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT FriendRaces FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
return <DB.ROW.0.FriendRaces>


[FUNCTION town_Azilum]
//Adiciona Default no cargo 999 da town com region=<args> ou da region de DEFAULT.   6     
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT id, title FROM towns_privs WHERE region="<DB.ESCAPEDATA <local.town>>" AND position=999
local.id=<DB.ROW.0.id>
local.title=<DB.ROW.0.title>
DB.EXECUTE=INSERT INTO towns_members SET UID=<eval <UID>>, region="<DB.ESCAPEDATA <local.town>>", priv_id=<dlocal.id>
sysmessageyellow Voce eh <SEX o/a> mais nov<SEX o/a> <local.title> de <serv.area.<local.town>.name>!!!
if (<region.region.defname>==<local.town>) && (<IsOnline>)
 town_enter
endif
PSFX 05B4


[FUNCTION town_getPermissions]
//Pega as leis de permissividade da town com region=<args> ou da region de DEFAULT.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
if (<town_IsTown <local.town>>)
 DB.QUERY=SELECT permissions FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
 return <hval <DB.ROW.0.permissions>>
else
 return 0ff
endif


[FUNCTION town_getMaster]
//Retorna a UID do mestre da town de region ARGS ou da region de DEFAULT.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT master FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
RETURN <hval <DB.ROW.0.master>>


[FUNCTION town_getPrivs]
//Pega os privs de DEFAULT para a town de region ARGS ou da region de DEFAULT, incluindo suas capitais.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
IF (<IsGM>)// || (<town_getMaster <local.town>>==<UID>)
 RETURN 0FFFFFFF
ELSE
 local.type=<town_getType <local.town>>
 local.privs=<town_getRegionPrivs <local.town>>
 if (<local.type> > 1)
  local.capital=<town_getCapital <local.town>>
  local.privs |= <town_getRegionPrivs <local.capital>>
  if (<local.type>==3)
   local.capital=<town_getCapital <local.capital>>
   local.privs |= <town_getRegionPrivs <local.capital>>
  endif
 endif
 RETURN <local.privs> 
ENDIF


[FUNCTION town_getRegionPrivs]
//Pega os privs de DEFAULT para a town de region ARGS ou da region de DEFAULT.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT p.privs FROM towns_privs AS p, towns_members AS m WHERE p.id=m.priv_id AND m.uid=<eval <uid>> AND  p.region="<DB.ESCAPEDATA <local.town>>"
RETURN <hval <DB.ROW.0.privs>>


[FUNCTION town_getFirstColor]
//Pega a cor primaria da town com region=<args> ou da region de DEFAULT.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
if (<town_IsTown <local.town>>)
 DB.QUERY=SELECT color1 FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
 return <hval <DB.ROW.0.color1>>
else
 return 0
endif


[FUNCTION town_getSecondColor]
//Pega a cor secundaria da town com region=<args> ou da region de DEFAULT.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
if (<town_IsTown <local.town>>)
 DB.QUERY=SELECT color2 FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
 return <hval <DB.ROW.0.color2>>
else
 return 0
endif


[FUNCTION town_addcash]
//Adiciona <argn> moedas de cobre ao tesouro da town onde DEFAULT está e retorna o valor
if (<town_IsTown>)
 DB.QUERY=SELECT tresure FROM towns WHERE region="<DB.ESCAPEDATA <region.defname>>"
 LOCAL.TRESURE=<eval <DB.ROW.0.tresure>+<argn>>
 DB.QUERY=UPDATE towns SET tresure=<dlocal.tresure> WHERE region="<DB.ESCAPEDATA <region.defname>>"
 return <LOCAL.TRESURE>
else
 return 0
endif

[FUNCTION town_donateTresure]
//DEFAULT tenta doar <argn2> cobres para a town onde ele está. <argn1> é o NPC que esta falando com ele.
if (!<town_IsTown>)
    sysmessagered Este lugar nao tem administracao.
elif (<argn2> < 1)
    sysmessagered Valor incorreto
else
    local.cash=<bank_checkCash <argn2>,0>
    if (!<local.cash>)
     uid.<argn1>.timerf 1 say Eh melhor arranjar algum dinheiro antes...
     return 1
    endif
    bank_getCash <argn2>,0
    town_addcash <argn2>
    uid.<argn1>.timerf 1 say A transacao foi feita. Seu dinheiro agora eh publico. Algo mais?
endif

[FUNCTION town_getTresure]
//DEFAULT tenta sacar <argn2> cobres da town onde ele está. <argn1> é o NPC que esta falando com ele.
if (!<town_IsTown>)
    sysmessagered Este lugar nao tem administracao.
elif (<argn2> < 1)
    sysmessagered Valor incorreto
else
    local.cash=<town_getCash>
    if (<local.cash> < <argn2>)
     uid.<argn1>.timerf 1 say Nao temos todo este dinheiro!
     return 1
    endif
    local.g=0
    local.s=0
    local.c=<argn2>
    WHILE (<local.c> > 100)
     local.g += 1
     local.c -= 100
    ENDWHILE
    WHILE (<local.c> > 10)
     local.s += 1
     local.c -= 10
    ENDWHILE
    if (<local.g>)
        newitem i_gold
        new.amount=<local.g>
        new.bounce
    endif
    if (<local.s>)
        newitem i_coin_silver
        new.amount=<local.s>
        new.bounce
    endif
    if (<local.c>)
        newitem i_coin_copper
        new.amount=<local.c>
        new.bounce
    endif
    town_addcash -<argn2>
    uid.<argn1>.timerf 1 say Aqui esta. Use com sabedoria. Algo mais?
endif


[FUNCTION town_getCash]
//Retorna a quantidade de dinheiro que a town <args> ou da region de DAFAULT tem nos cofres.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT tresure FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
return <DB.ROW.0.tresure>

[FUNCTION TOWN_CHECK_PRIVS]
//Checa se o DEFAULT temo privilégio <argn> na town em que ele está
return <eval <town_getPrivs>&<argn>>


[FUNCTION town_CheckPrivs]
return <TOWN_CHECK_PRIVS <argn>>


[FUNCTION town_GetPositionID]
//Pega a ID do cargo de DEFAULT na town ARGS ou da region de DEFAULT.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT priv_id FROM towns_members WHERE region="<DB.ESCAPEDATA <local.town>>" AND UID=<eval <UID>>
RETURN <eval <DB.ROW.0.priv_id>>


[FUNCTION town_getTitle]
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
local.id=<town_GetPositionID <local.town>>
if (<local.id>)
 DB.QUERY=SELECT title FROM towns_privs WHERE region="<DB.ESCAPEDATA <local.town>>" AND id=<dlocal.id>
 return <DB.ROW.0.title>
else
 return Forasteiro
endif


[FUNCTION town_IsMember]
//Verifica se DEFAULT é membro da town com region=ARGS ou da region de DEFAULT.
RETURN <eval <town_GetPositionID <args>>>


[FUNCTION town_setAsFriend]
//Adiciona UID como amigo da cidade <args> ou DEFAULT.REGION priv_id=0 e flags=TOWN_FLAG_FRIEND
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
town_setFlags <DEF.TOWN_FLAG_FRIEND>,<local.town>


[FUNCTION town_setAsCriminal]
//Seta DEFAULT como criminoso na town <ARGS> ou DEFAULT.TOWN
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
town_setFlags <DEF.TOWN_FLAG_CRIMINAL>,<local.town>


[FUNCTION town_setAsBaned]
//Seta DEFAULT como criminoso na town <ARGS> ou DEFAULT.TOWN
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
//Seta DEFAULT como criminoso da cidade <args> ou DEFAULT.REGION.
town_setFlags <DEF.TOWN_FLAG_BANNED>,<local.town>


[FUNCTION town_GetPosition]
//Retorna o número da posição de 1 a 999 do cargo de DEFAULT na region ARGS ou region do DEFAULT
local.pos=<town_GetPositionID <args>>
if (!<local.pos>)
 RETURN 1000
endif
DB.QUERY=SELECT position FROM towns_privs WHERE id=<dlocal.pos>
RETURN <eval <DB.ROW.0.position>>


[FUNCTION town_getFlags]
//Retorna as flags de Town para DEFAULT na region ARGS ou region do DEFAULT
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT flags FROM towns_members WHERE region="<DB.ESCAPEDATA <local.town>>" AND UID=<eval <UID>>
RETURN <eval <DB.ROW.0.flags>>


[FUNCTION town_setFlags]
//Adiciona <argv0> às flags de Town para DEFAULT na region ARGV1 ou region do DEFAULT
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
//Testa se já existe entrada para DEFAULT na region <local.town>
DB.QUERY=SELECT uid FROM towns_members WHERE uid=<eval <uid>> AND region="<DB.ESCAPEDATA <local.town>>"
if (!<DB.ROW.NUMROWS>)
 DB.EXECUTE=INSERT INTO towns_members SET uid=<eval <uid>>, region="<DB.ESCAPEDATA <local.town>>", priv_id=0, flags=<eval <argv0>>
else
 local.flags=<eval <town_getFlags <local.town>>|<argv0>>
 DB.EXECUTE=UPDATE towns_members SET flags=<dlocal.flags> WHERE region="<DB.ESCAPEDATA <local.town>>" AND UID=<eval <UID>>
endif
IF (<IsOnline>)
 try ctag.town_<local.town>_flags = <hval <ctag0.town_<local.town>_flags> | <argv0>>
ENDIF
RETURN 1


[FUNCTION town_unsetFlags]
//Adiciona <argv0> às flags de Town para DEFAULT na region ARGV1 ou region do DEFAULT
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
//Testa se já existe entrada para DEFAULT na region <local.town>
DB.QUERY=SELECT uid FROM towns_members WHERE uid=<eval <uid>> AND region="<DB.ESCAPEDATA <local.town>>"
if (!<DB.ROW.NUMROWS>)
 return 0
else
 local.flags=<eval <town_getFlags <local.town>>^<argv0>>
 DB.EXECUTE=UPDATE towns_members SET flags=<dlocal.flags> WHERE region="<DB.ESCAPEDATA <local.town>>" AND UID=<eval <UID>>
 //Testa se não há motivo para guardar esta entrada (pode prejudicar town_Azilum e a escalabilidade)
 if (!<town_getFlags <local.town>>) && (!<town_GetPosition <local.town>>)
  DB.EXECUTE=DELETE FROM towns_members WHERE uid=<eval <uid>> AND region="<DB.ESCAPEDATA <local.town>>"
 endif
endif
IF (<IsOnline>)
 try ctag.town_<local.town>_flags = <hval <ctag0.town_<local.town>_flags>^<argv0>>
ENDIF
RETURN 1


[FUNCTION town_getArtefact]
//Retorna a UID do artefato desta town
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT artefactUID FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
return <hval <DB.ROW.0.artefactUID>>


[FUNCTION town_changeArtefact]
if (!<argo>)
 sysmessageyellow Selecione um item do mundo para ser o artefato de <serv.area.<ctag.town_region>.name>:
 targetf town_changeArtefact
else
 //Já existe um artefato?
 ref1=<town_getArtefact <ctag.town_region>>
 if (<ref1.instances>)
  ref1.name=<serv.itemdef.<ref1.baseid>.name>
  ref1.type=<serv.itemdef.<ref1.baseid>.type>
  ref1.tag.town=
 endif
 argo.type=t_town_artefact
 argo.tag.town=<ctag.town_region>
 argo.name=<argo.name> de <serv.area.<ctag.town_region>.name>
 DB.EXECUTE=UPDATE towns SET artefactUID=<eval <argo.uid>> WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
endif


[FUNCTION town_changeVault]
if (!<argo>)
 sysmessageyellow Selecione um container do mundo para ser o cofre de <serv.area.<ctag.town_region>.name>:
 targetf town_changeVault
else
 if (<argo.cont>)
  sysmessagered Este nao eh um bom lugar para o cofre...
 elif (<town_setVault <ctag.town_region>,<argo>>)
  sysmessagegreen Transferencia completa. Guarde bem seu cofre.
 else
  sysmessagered Item invalido.
 endif
 town_Configurations
endif


//########################################################################
//      Funções internas do sistema
//########################################################################


[FUNCTION town_IsTown]
//Determina se existe TOWN na region <ARGS>. Se não há ARGS, usa a region em que DEFAULT está.
if (STRMATCH(a_*,<args>))
 if (<IsAreadef <args>>)
  local.region=<args>
 else
  return 0
 endif
else
 local.region=<region.region.defname>
endif

if (<local.region>==a_world) || (<local.region>==a_dungeons) || (!<local.region>)
 return 0
endif
DB.QUERY="SELECT region FROM towns WHERE region='<DB.ESCAPEDATA <local.region>>';"
if (<DB.ROW.NUMROWS>)
 return 1
else
 return 0
endif


[FUNCTION town_AddTown]
//Adiciona como town a region <argv0> com tipo <argv1> com <argv2> como como capital
//Tipo é 1=Estado maior, 2=sub-região ou 3=região sob administração
DB.EXECUTE=INSERT INTO towns SET region="<DB.ESCAPEDATA <argv0>>", type=<eval <argv1>>, capital="<DB.ESCAPEDATA <argv2>>", tresure=0
DB.EXECUTE=INSERT INTO towns_privs SET region="<DB.ESCAPEDATA <argv0>>", position=1, title='mestre', description='Mestre. Todos os privilegios. Nao pode ser apagado.', privs=<eval 0FFFFFF>
DB.EXECUTE=INSERT INTO towns_privs SET region="<DB.ESCAPEDATA <argv0>>", position=999, title='cidadao', description='Simples membro. Sem privilegios. Nao pode ser apagado', privs=0
serv.area.<argv0>.events +r_town
serv.area.<argv0>.allclients town_enter <args>
serv.area.<argv0>.tag.permissions=<town_getPermissions <argv0>>


[FUNCTION townAddSubTown]
//Adiciona como town a region <ctag.town_region> com tipo <ctag.town_type> com <ctag.town_capital_<argn>> como como capital
//Tipo é 1=Estado maior, 2=sub-região ou 3=região sob administração
town_AddTown <ctag.town_region>,<dctag.town_type>,<ctag.town_capital_<argn>>
local.town_region=<ctag.town_region>
town <ctag.town_region>


[FUNCTION town_selectCapital]
// LIST para seleção da capital da nova Town
list_new Selecione a capital para <serv.area.<ctag.town_region>.name>:
list_newDbCol 20,Nome,Nome
list_newDbCol 200,Tipo,Tipo
list_setAction townAddSubTown
list_loadFromDb SELECT region AS Nome, type AS tipo, capital from towns WHERE type < <eval <argn1>>

//Apurar os itens para melhor identificação
for i <ctag.list_numitems>
 //Salvar defname da capital em outra lista
 TRY ctag.town_capital_<dlocal.i>=<ctag.list_row<local.i>_01>
 TRY ctag.list_row<local.i>_01=<serv.area.<ctag.list_row<local.i>_01>.name>
 DOSWITCH <ctag.list_row<local.i>_02>
  local.null
  TRY ctag.list_row<local.i>_02=Estado maior
  TRY ctag.list_row<local.i>_02=Sub-Regiao de <serv.area.<DB.ROW.<eval <local.i>-1>.capital>.name>
  TRY ctag.list_row<local.i>_02=Regiao sob administracao de <serv.area.<DB.ROW.<eval <local.i>-1>.capital>.name>
 ENDDO
end

//Salva o tipo da town para a próxima função
ctag.town_type=<argn1>
list_show 1


[FUNCTION town_clearTable]
//Limpa os dados de cache referentes à tabela de cargos da Town
local.r=1
WHILE (!<IsEmpty <ctag.list_row<local.r>_01>>)
 try ctag.list_row<local.r>_01=
 try ctag.list_row<local.r>_02=
 try ctag.list_id_<local.r>=
 ctag.town_petition_<dlocal.i>=
 ctag.town_region_<dlocal.r>=
 local.r += 1
END 


[FUNCTION town_clearctags]
ctag.town_PetitionText
ctag.town_UID
ctag.town_coloriq
ctag.town_description
ctag.town_page
ctag.town_petitionTitle
ctag.town_position
ctag.town_positionID
ctag.town_privid
ctag.town_privs
ctag.town_region
ctag.town_title
ctag.town_type
ctag.town_noFullMenu
ctag.town_board
town_clearTable


[FUNCTION town_TrasferMastership]
//Transfere o dono da town de DEFNAME=<argv0> para o player de UID=<argv1>
if (!<town_IsTown <argv0>>)
 return 0
endif
ref1=<argv1>
//Avisa mestre atual.
DB.QUERY=SELECT master FROM towns WHERE region="<DB.ESCAPEDATA <argv0>>"
IF (0<DB.ROW.0.master>)
 local.oldmaster=<DB.ROW.0.master>
 f_sendMessage <local.oldmaster>,Voce nao eh mais mestre de <serv.area.<argv0>.name>!,<DEF.SM_RED>
 //Seta mestre atual como membro comum.
 DB.QUERY=SELECT id FROM towns_privs WHERE position=999 AND region="<DB.ESCAPEDATA <argv0>>"
 local.newpos=<DB.ROW.0.id>
 DB.EXECUTE=UPDATE towns_members SET priv_id=<dlocal.newpos> WHERE UID=<dlocal.oldmaster>
ENDIF

//Seta ref1 como mestre
DB.QUERY=SELECT id FROM towns_privs WHERE position=1 AND region="<DB.ESCAPEDATA <argv0>>"
local.masterid=<DB.ROW.0.id>
if (<ref1.town_isMember <argv0>>)
 DB.EXECUTE=UPDATE towns_members SET priv_id=<dlocal.masterid>, flags=0 WHERE UID=<eval <ref1>>
else
 DB.EXECUTE=INSERT INTO towns_members SET UID=<eval <ref1>>, priv_id=<dlocal.masterid>, region="<DB.ESCAPEDATA <argv0>>"
endif
DB.EXECUTE=UPDATE towns SET master=<eval <ref1>> WHERE region="<DB.ESCAPEDATA <argv0>>"

f_sendMessage <ref1>,Voce eh o novo mestre de <serv.area.<argv0>.name>!,<DEF.SM_GREEN>
if (<ref1.IsOnline>)
 ref1.psfx 05B4
endif


[FUNCTION town_deleteTown]
//deleta a town de DEFNAME=<argv0> da DB
prompt_new Excluir <serv.area.<argv0>.name>?<DEF.BR>Isso removera <serv.area.<argv0>.name> do sistema de cidades permanentemente. (S/N)?
prompt_setAction town_deleteTown2 <argv0>,
prompt_show


[FUNCTION town_deleteTown2]
//Continuação de town_deleteTown
IF (STRMATCH(S,<argv1>))
 DB.EXECUTE=DELETE FROM towns_members WHERE region="<DB.ESCAPEDATA <argv0>>"
 DB.EXECUTE=DELETE FROM towns_privs WHERE region="<DB.ESCAPEDATA <argv0>>"
 DB.EXECUTE=DELETE FROM towns WHERE region="<DB.ESCAPEDATA <argv0>>"
 DB.EXECUTE=UPDATE towns SET type=1, capital=region WHERE capital="<DB.ESCAPEDATA <argv0>>"
 serv.area.<argv0>.events -r_town
 serv.area.<argv0>.allclients events -e_InTown
 serv.area.<argv0>.tag.permissions=
 sysmessagered <serv.area.<argv0>.name> excuida do sistema de cidades!
 return 1
ENDIF
return 0


[FUNCTION town_getPositions]
//Retorna o número de cargos existentes na Town.
DB.QUERY=SELECT COUNT(id) as positions FROM towns_privs WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
return <DB.ROW.0.positions>


[FUNCTION town_getMembers]
//Retorna o número de membros/cidadãos existentes na Town.
DB.QUERY=SELECT COUNT(UID) as members FROM towns_members WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
return <DB.ROW.0.members>


[FUNCTION town_setColor]
//Seta a cor primária ou secundária da town. argv0=1|2 (primária, secundária) argv1=cor
IF (!<argv[1]>)
 sysmessageyellow Selecione um item do mundo para escolher sua cor:
 ctag.town_coloriq=<argv[0]>
 targetf town_TargColor
ELSE
 DB.QUERY=UPDATE towns SET color<eval <argv[0]>>=<eval <argv[1]>> WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
 SDIALOG d_town
ENDIF


[FUNCTION town_changePostitionTitle]
//Troca o título do cargo de ID=ctag.town_positionID
IF (!<argv>)
 prompt_new Qual o novo titlo?
 prompt_setAction town_changePostitionTitle
 prompt_setActionBack SDIALOG d_town
 prompt_show
ELSE
 if (<dctag.town_positionID>)
  DB.QUERY=UPDATE towns_privs SET title="<DB.ESCAPEDATA <args>>" WHERE id=<dctag.town_positionID>
 else
  ctag.town_title=<args>
 endif
 SDIALOG d_town
ENDIF


[FUNCTION town_changePositionDesc]
//Troca a descrição do cargo de ID=ctag.town_positionID
IF (<argv>)
 IF (<ctag.town_positionID>)
  DB.QUERY=UPDATE towns_privs SET description="<DB.ESCAPEDATA <args>>" WHERE id=<dctag.town_positionID>
 ELSE
  ctag.town_description=<args>
 ENDIF
 ctag.town_page=<DEF.TOWN_PAGE_POSITIONS>
ELSE
 ctag.town_page=<DEF.TOWN_PAGE_POSITION_DESC>
ENDIF
SDIALOG d_town


[FUNCTION town_changePosition]
//Troca a posição de 2 a 998 do cargo de ID=ctag.town_positionID
if (!<IsGm>)
 local.maxpos=<eval <MAXIMUM 1,<town_getPosition <ctag.town_region>>>+1>
else
 local.maxpos=2
endif
IF (!<argv>)
 prompt_new Digite uma posicao hierarquica de <dlocal.maxpos> a 998 sendo <dlocal.maxpos> a mais influente.
 prompt_setAction town_changePosition
 prompt_setActionBack SDIALOG d_town
 prompt_show
ELSE
 if (<argn> < <local.maxpos>) || (<argn> > 998)
  sysmessagered Posicao invalida.
 elif (<dctag.town_positionID>)
  DB.QUERY=UPDATE towns_privs SET position=<eval <argn>> WHERE id=<dctag.town_positionID>
 else
  ctag.town_position=<argn>
 endif
 SDIALOG d_town
ENDIF


[FUNCTION town_deletePosition]
//Exclui permanentemente o cargo de ID=ctag.town_positionID caso não haja jogadores no cargo.
DB.QUERY=SELECT COUNT(UID) AS chars FROM towns_members WHERE priv_id=<dctag.town_positionID>
IF (<DB.ROW.0.chars>)
 sysmessagered Impossivel excluir cargo <ctag.town_title>:
 sysmessageyellow Existem atualmente <eval <DB.ROW.0.chars>> pessoas ocupando este cargo. Troque-as de cargo.
 SDIALOG d_town
else
 DB.EXECUTE=DELETE FROM towns_privs WHERE id=<dctag.town_positionID>
 sysmessageyellow O cargo <ctag.town_title> foi extinto.
 town_listPositions
endif


[FUNCTION town_addPosition]
//Insere um novo cargo para a town de region=ctag.town_region conforme ctags de cache do dialog.
DB.EXECUTE=INSERT INTO towns_privs SET region="<DB.ESCAPEDATA <ctag.town_region>>", position=<dctag.town_position>, title="<DB.ESCAPEDATA <ctag.town_title>>", description="<DB.ESCAPEDATA <ctag.town_description>>", privs=<dctag.town_privs>
town_listPositions


[FUNCTION town_changeCriminalFlag]
//Alterna flag de criminal
local.flags=<town_getFlags <src.ctag.town_region>>
if (<local.flags>&<DEF.TOWN_FLAG_CRIMINAL>)
 town_unsetFlags <DEF.TOWN_FLAG_CRIMINAL>,<src.ctag.town_region>
else
 town_setFlags <DEF.TOWN_FLAG_CRIMINAL>,<src.ctag.town_region>
endif


[FUNCTION town_changeBannedFlag]
//Alterna flag de banned
local.flags=<town_getFlags <src.ctag.town_region>>
if (<local.flags>&<DEF.TOWN_FLAG_BANNED>)
 town_unsetFlags <DEF.TOWN_FLAG_BANNED>,<src.ctag.town_region>
else
 town_setFlags <DEF.TOWN_FLAG_BANNED>,<src.ctag.town_region>
endif


[FUNCTION town_changeDebtFlag]
//Altera flag de inatimplente
local.flags=<town_getFlags <src.ctag.town_region>>
if (<local.flags>&<DEF.TOWN_FLAG_DEBT>)
 town_unsetFlags <DEF.TOWN_FLAG_DEBT>,<src.ctag.town_region>
else
 town_setFlags <DEF.TOWN_FLAG_DEBT>,<src.ctag.town_region>
endif


[FUNCTION town_expurgeMember]
//Expurga o membro da Town, sem banir, mantendo os flags se houverem.
ref1=<ctag.town_uid>
if (STRMATCH(s,<args>))
 DB.EXECUTE=UPDATE towns_members SET priv_id=0 WHERE region="<DB.ESCAPEDATA <ctag.town_region>>" AND uid=<eval <ref1>>
 ref1.town_unsetFlags <DEF.TOWN_FLAG_FRIEND>,<ctag.town_region>
 town_listMembers
else
 prompt_new Remover permanentemente <ref1.tag.name> da cidade? Se el<ref1.sex a/e> tiver pendencias legais, elas continuarao validas. (s/n)
 prompt_setAction town_expurgeMember
 prompt_setActionBack SDIALOG d_town
 prompt_show
endif


[FUNCTION town_NPCs]
//Retorna quantos NPCs a town tem
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT COUNT(UID) as cnt FROM towns_servants WHERE region="<DB.ESCAPEDATA <local.town>>"
RETURN <DB.ROW.0.cnt>


[FUNCTION town_targAddFriend]
if (!<argo>)
 sysmessageyellow Selecione um jogador de uma raca proibida em <serv.area.<ctag.town_region>.name> para ser aceito na cidade:
 targetf town_targAddFriend
else
 //Isso eh um jogador?
 if (!<argo.IsPlayer>)
  sysmessagered Isso nao eh um jogador valido!
  town
 endif
 //É de raça proibida?
 if (STRMATCH(*<argo.tag.raca>*,<town_getRaces <ctag.town_region>>))
  sysmessagered Ess<argo.sex e/a> <argo.tag.raca> nao pode ser amigo! Sua raca eh aceita na cidade!
 else
  DB.QUERY=SELECT uid,flags FROM towns_members WHERE uid=<eval <argo.uid>> AND region="<DB.ESCAPEDATA <ctag.town_region>>"
  if (0<DB.ROW.0.flags>&<DEF.TOWN_FLAG_FRIEND>)
   sysmessagetellow Ess<argo.sex e/a> <argo.tag.raca> ja eh amig<argo.sex o/a> da cidade!
  elif (0<DB.ROW.0.flags>&<DEF.TOWN_FLAG_BANNED>)
   sysmessagered Ess<argo.sex e/a> <argo.tag.raca> nao pode ser amigo da cidade enquanto estiver banido!
  else
   argo.town_setAsFriend <ctag.town_region>
   argo.sysmessagegreen Voce agora eh um amigo em <serv.area.<ctag.town_region>.name>!
  endif
 endif
 town
endif


[FUNCTION town_targAddCriminal]
if (!<argo>)
 sysmessageyellow Selecione um jogador para ser marcado como criminoso de <serv.area.<ctag.town_region>.name>:
 targetf town_targAddCriminal
else
 //Isso eh um jogador?
 if (!<argo.IsPlayer>)
  sysmessagered Isso nao eh um jogador valido!
  town
 endif
 //Você pode dar este comando?
 local.pos=<argo.town_getPosition <ctag.town_region>>
 if (<town_canMember <local.pos>>) && (<local.pos>)
  sysmessagered Este jogador tem uma posicao hierarquica maior que a sua!
  town
 endif
 local.flags=<argo.town_getFlags <ctag.town_region>>
 //Já está banido?
 if (<local.flags>&<DEF.TOWN_FLAG_CRIMINAL>)
  sysmessagered Este jogador ja esta banido da cidade!
  town
 endif
 argo.town_setAsCriminal <ctag.town_region>
 town
endif


[FUNCTION town_targInviteCitzen]
if (!<argo>)
 sysmessageyellow Selecione um jogador para convidar a cidadao de <serv.area.<ctag.town_region>.name>:
 targetf town_targInviteCitzen
else
 //Isso eh um jogador?
 local.flags=<argo.town_getFlags <ctag.town_region>>
 if (!<argo.IsPlayer>)
  sysmessagered Isso nao eh um jogador valido!
 elif (!<argo.IsOnline>)//Está online?
  sysmessagered El<argo.sex e/a> nao esta online para responder...
 elif !(STRMATCH(*<argo.tag.raca>*,<town_getRaces <ctag.town_region>>))//Raça válida na Town?
  sysmessagered Ess<argo.sex e/a> <argo.tag.raca> nao pode ser cidadao! Sua raca eh proibida na cidade!
 elif (<argo.town_IsMember <ctag.town_region>>) //Ele ja faz parte da Town?
  sysmessagered El<argo.sex e/a> ja faz parte da cidade.
 elif (<local.flags>&<DEF.TOWN_FLAG_BANNED>) || (<local.flags>&<DEF.TOWN_FLAG_CRIMINAL>)//Está banido?
  sysmessagered Este jogador eh um crimonoso procurado na cidade! Nao pode ser um cidadao!
 else
  argo.ctag.town_region=<ctag.town_region>
  argo.ctag.town_uid=<uid>
  trysrc <argo> src.town_prompInviteCitzen
  say Voce aceita ser um cidadao de <serv.area.<ctag.town_region>.name>?
  timerf 1,sysmessageorange Aguarde a resposta del<argo.sex e/a>.
  return 1
 endif
 town
endif


[FUNCTION town_prompInviteCitzen]
//Pergunta se o player quer pertencer a town <ctag.town_region>
ref1=<ctag.town_uid>
if (STRMATCH(s,<args>))
 town_Azilum <ctag.town_region>
else
 prompt_new Voce deseja ser cidadao de <serv.area.<ctag.town_region>.name>? (s/n)
 prompt_setAction town_prompInviteCitzen
 prompt_show
endif


[FUNCTION town_getCapital]
//Retorna a capital da region <args> ou DEFAULT.REGION.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT capital FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
RETURN <DB.ROW.0.capital>


[FUNCTION town_MassMessage]
//Manda a mensagem <argv0> para todos os cidadãos da region <argv1> ou DEFAULT.REGION.
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
local.capital=<town_getCapital <local.town>>
DB.QUERY=SELECT uid FROM towns_members  WHERE region="<DB.ESCAPEDATA <local.town>>" OR region="<DB.ESCAPEDATA <local.capital>>"
for p 0 <eval <DB.ROW.NUMROWS>-1>
 f_sendMessage <DB.ROW.<dlocal.p>.uid>,[<serv.area.<local.town>.name>] <argv0>,<DEF.SM_ORANGE>
end


[FUNCTION town_sanceBoard]
//Ativa um quadro de petições ou decretos
obj=<ctag.town_board>
if (STRMATCH(s,<args>))
 obj.tag.town_region=<region.defname>
elif (STRMATCH(r,<args>))
 obj.remove
else
 prompt_new Deseja sacionar (ativar) <obj.name>?<DEF.BR>(S)im ou (R)emover
 prompt_setAction town_sanceBoard
 prompt_show
endif


//########################################################################
//      Funções de chamada do gump
//########################################################################

[FUNCTION town_Configurations]
//Abre a página de Características da Town
ctag.town_page=<DEF.TOWN_PAGE_CONFIG>
SDIALOG d_town


[FUNCTION town_PromptFirstColor]
//Pergunta a cor primária da Town. 0 para TARGET em item
prompt_new Escolha a cor primaria de <serv.area.<ctag.town_region>.name>:<DEF.BR>(numero da cor ou '0' para target)
prompt_setAction town_setColor 1,
prompt_setActionBack SDIALOG d_town
prompt_show


[FUNCTION town_PromptSecondColor]
//Pergunta a cor secundária da Town. 0 para TARGET em item
prompt_new Escolha a cor secundaria de <serv.area.<ctag.town_region>.name>:<DEF.BR>(numero da cor ou '0' para target)
prompt_setAction town_setColor 2,
prompt_setActionBack SDIALOG d_town
prompt_show


[FUNCTION town_TargColor]
//Pede um item como alvo para as cores da town.
if (!<argo>) || (!<argo.IsItem>)
 sysmessagered Item invalido.
 SDIALOG d_town
else
 town_setColor <ctag.town_coloriq>,<argo.color>
 ctag.town_coloriq=
endif


[FUNCTION town_showMember]
//Abre o dialog de town mostrando detalhes sobre o cidadão escolhido em town_listMembers.
ctag.town_UID=<ctag0.list_id_<hval <argn>>>
ctag.town_page=<DEF.TOWN_PAGE_MEMBER>
SDIALOG d_town

[FUNCTION town_showNPC]
//Abre o dialog de town mostrando detalhes sobre o cidadão escolhido em town_listMembers.
ctag.town_UID=<ctag0.list_id_<hval <argn>>>
ctag.town_page=<DEF.TOWN_PAGE_NPC>
SDIALOG d_town


[FUNCTION town_showCriminal]
//Abre o dialog de town mostrando detalhes sobre o criminoso escolhido em town_listEnemies.
ctag.town_UID=<ctag0.list_id_<hval <argn>>>
ctag.town_page=<DEF.TOWN_PAGE_BANS>
SDIALOG d_town


[FUNCTION town_showFriend]
//Abre o dialog de town mostrando detalhes sobre o criminoso escolhido em town_listEnemies.
ctag.town_UID=<ctag0.list_id_<hval <argn>>>
ctag.town_page=<DEF.TOWN_PAGE_FRIENDS>
SDIALOG d_town


[FUNCTION town_petitionsMenu]
//Abre o menu de petições
ctag.town_page=<DEF.TOWN_PAGE_POLLS>
SDIALOG d_town


[FUNCTION town_PageNPCs]
//Abre o menu de NPCs
ctag.town_page=<DEF.TOWN_PAGE_NPCs>
SDIALOG d_town


[FUNCTION town_hireNPC]
//Abre o menu de NPCs para contratar
ctag.town_uid=0
ctag.town_page=<DEF.TOWN_PAGE_NPC>
SDIALOG d_town


[FUNCTION town_hireGuard]
//Abre o menu de NPCs guardas para contratar
ctag.town_uid=-1
ctag.town_page=<DEF.TOWN_PAGE_NPC>
SDIALOG d_town


[FUNCTION town_showPetition]
//Abre uma petição para ser apreciada
ctag.town_petitionID=<argn>
ctag.town_page=<DEF.TOWN_PAGE_POLL>
SDIALOG d_town


[FUNCTION town_Taxes]
//Abre a pagina de impostos
ctag.town_page=<DEF.TOWN_PAGE_TAXES>
SDIALOG d_town


[FUNCTION town_Laws]
//Abre a pagina de decretos
if (<town_canLaws <ctag.town_region>>)
 ctag.town_page=<DEF.TOWN_PAGE_LAWS>
 SDIALOG d_town
else
 town_lawsList
endif


[FUNCTION town_showPosition]
//Abre o dialog de town mostrando detalhes sobre a posição escolhida em town_listPositions.
ctag.town_positionID=<ctag0.list_id_<hval <argn>>>
ctag.town_page=<DEF.TOWN_PAGE_POSITIONS>
SDIALOG d_town


[FUNCTION town_ShowPositionPrivs]
//Mostra os privilégios para o cargo de ID=ctag.town_positionID
IF (<argv>)
 IF (<ctag.town_positionID>)
  DB.QUERY=UPDATE towns_privs SET privs=<eval <argn>> WHERE id=<dctag.town_positionID>
 ELSE
  ctag.town_privs=<argn>
 ENDIF
 ctag.town_page=<DEF.TOWN_PAGE_POSITIONS>
ELSE
 ctag.town_page=<DEF.TOWN_PAGE_PRIVS>
ENDIF
SDIALOG d_town


[FUNCTION town_listMembers]
//Lista todos os membros e cidadãos da town com region=<ctag.town_region>
town_clearTable
list_new "Lista de cidadaos de <serv.area.<ctag.town_region>.name>"
list_newDbCol 25,uid,Nome
list_newDbCol 200,title,Titulo
list_setAction town_showMember
list_setActionBack town
list_loadFromDb SELECT t1.uid, t2.title FROM towns_members as t1, towns_privs as t2 WHERE t1.region='<DB.ESCAPEDATA <ctag.town_region>>' AND t1.priv_id=t2.id ORDER BY t2.position
//Transformar em decimal as posições deve você, Anakyn.
FOR t 1 <ctag.list_numitems>
 ctag.list_id_<local.t>=<DB.ROW.<eval <LOCAL.t>-1>.UID>
 ctag.list_row<local.t>_01=<UID.<DB.ROW.<eval <LOCAL.t>-1>.UID>.tag.name>
end
list_show 1

[FUNCTION town_listServants]
//Lista todos os membros e cidadãos da town com region=<ctag.town_region>
town_pruneServants <ctag.town_region>//Pruna primeiro
town_clearTable
list_new "Lista de funcionarios de <serv.area.<ctag.town_region>.name>"
list_newDbCol 25,uid,Nome
list_newDbCol 200,uid,Cargo
list_setAction town_showNPC
list_setActionBack town_PageNPCs
list_loadFromDb SELECT uid FROM towns_servants WHERE region='<DB.ESCAPEDATA <ctag.town_region>>'
//Transformar em decimal as posições deve você, Anakyn.
FOR t 1 <ctag.list_numitems>
 obj=<DB.ROW.<eval <LOCAL.t>-1>.UID>
 ctag.list_id_<local.t>=<obj>
 ctag.list_row<local.t>_01=<obj.name>
 DOSWITCH <obj.tag0.servant>
  ctag.list_row<local.t>_02=Demitido. Saira da lista logo.
  ctag.list_row<local.t>_02=Porteiro
  ctag.list_row<local.t>_02=Guarda
  ctag.list_row<local.t>_02=Senescal
 END
 if (<obj.tag0.servant>==2)
  DOSWITCH <obj.tag0.guard_level>
   ctag.list_row<local.t>_02 .= " - Aspirante"
   ctag.list_row<local.t>_02 .= " - Soldado"
   ctag.list_row<local.t>_02 .= " - Cabo"
   ctag.list_row<local.t>_02 .= " - Sargento"
   ctag.list_row<local.t>_02 .= " - Capitao"
   ctag.list_row<local.t>_02 .= " - Major"
  END
 endif
end
list_show 1


[FUNCTION town_listMembersPosition]
//Lista todos os membros e cidadãos da town com region=<ctag.town_region> e cargo=<ctag.town_positionID>
town_clearTable
list_new "Lista de cidadaos de <serv.area.<ctag.town_region>.name> com cargo de <ctag.town_title>"
list_newDbCol 25,uid,Nome
list_newDbCol 200,title,Titulo
list_setAction town_showMember
list_setActionBack SDIALOG d_town
list_loadFromDb SELECT t1.uid, t2.title FROM towns_members as t1, towns_privs as t2 WHERE t1.region='<DB.ESCAPEDATA <ctag.town_region>>' AND t1.priv_id=t2.id AND t2.id=<dctag.town_positionID> ORDER BY t2.id
//Transformar em decimal as posições deve você, Anakyn.
FOR t 1 <ctag.list_numitems>
 ctag.list_id_<local.t>=<DB.ROW.<eval <LOCAL.t>-1>.UID>
 ctag.list_row<local.t>_01=<UID.<DB.ROW.<eval <LOCAL.t>-1>.UID>.tag.name>
end
list_show 1


[FUNCTION town_listChangePosition]
//Lista todos os cargos existentes na town para trocar o cargo de ctag.town_uid.
IF (!<argv>)
 town_clearTable
 if (!<IsGM>)
  local.pos=<town_getPosition <ctag.town_region>>
 else
  local.pos=1
 endif
 list_new "Qual sera o novo cargo de <uid.<ctag.town_uid>.tag.name> em <serv.area.<ctag.town_region>.name>?"
 list_newDbCol 25,title,Titulo
 list_newDbCol 200,position,Posicao
 list_setAction town_listChangePosition
 list_setActionBack SDIALOG d_town
 list_loadFromDb "SELECT id,title,position FROM towns_privs WHERE region='<DB.ESCAPEDATA <ctag.town_region>>' AND position > <dlocal.pos> ORDER BY position"
 //Transformar em decimal as posições deve você, Anakyn.
 FOR t 1 <ctag.list_numitems>
  ctag.list_id_<local.t>=<DB.ROW.<eval <LOCAL.t>-1>.id>
  ctag.list_row<local.t>_02=<dctag.list_row<local.t>_02> de 1000
 end
 list_show 1
ELSE
 ref1=<dctag.town_uid>
 local.oldpos=<ref1.town_getTitle <ctag.town_region>>
 local.newpos=<ctag.list_id_<hval <argn>>>
 DB.EXECUTE=UPDATE towns_members SET priv_id=<dlocal.newpos> WHERE UID=<dctag.town_uid> AND region="<DB.ESCAPEDATA <ctag.town_region>>"
 if (<local.newpos> > <ctag.town_positionID>)
  local.w=promovido
 elif (<local.newpos> < <ctag.town_positionID>)
  local.w=rebaixado
 else
  SDIALOG d_town
  return 1
 endif
 local.newpos=<ref1.town_getTitle <ctag.town_region>>
 f_sendMessage <tag.town_uid>,[<serv.area.<ctag.town_region>.name>]Voce foi <local.w> de <local.oldpos> a <local.newpos>!,<DEF.SM_ORANGE>
 SDIALOG d_town
ENDIF


[FUNCTION town_getEnemies]
//Retorna o numero de jogadores criminosos, banidos ou presos da town com region <args> ou region do DEFAULT.
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT COUNT(uid) as criminals FROM towns_members WHERE region="<DB.ESCAPEDATA <local.town>>" AND flags&7
return <DB.ROW.0.criminals>


[FUNCTION town_getFriends]
//Retorna o numero de jogadores forasteiros (raças inimigas) marcados como amigos da town com region <args> ou region do DEFAULT.
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT COUNT(uid) as friends FROM towns_members WHERE region="<DB.ESCAPEDATA <local.town>>" AND flags&16
return <DB.ROW.0.friends>

[FUNCTION town_getPrisioners]
//Retorna o numero de jogadores presos da town com region <args> ou region do DEFAULT.
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT COUNT(uid) as criminals FROM towns_members WHERE region="<DB.ESCAPEDATA <local.town>>" AND flags&<eval <DEF.TOWN_FLAG_JAILED>>
return <DB.ROW.0.criminals>


[FUNCTION town_listEnemies]
//Lista todos os jogadores com flag de banido ou criminoso ou inadimplente.
town_clearTable
list_new "Lista de inimigos publicos de <serv.area.<ctag.town_region>.name>"
list_newDbCol 25,uid,Nome
list_newDbCol 200,flags,Estado
list_setAction town_showCriminal
list_setActionBack town
list_loadFromDb SELECT uid, flags FROM towns_members WHERE region='<DB.ESCAPEDATA <ctag.town_region>>' AND flags&15
//Transformar em decimal as posições deve você, Anakyn.
FOR t 1 <ctag.list_numitems>
 ctag.list_id_<local.t>=<DB.ROW.<eval <LOCAL.t>-1>.UID>
 ctag.list_row<local.t>_02="Registrado como "
 local.flags=<eval <DB.ROW.<eval <LOCAL.t>-1>.flags>>
 if (<local.flags>&01)
  ctag.list_row<local.t>_02 .= " banido"
 endif
 if (<local.flags>&02)
  ctag.list_row<local.t>_02 .= " criminoso"
 endif
 if (<local.flags>&04)
  ctag.list_row<local.t>_02 .= " preso"
 endif
 if (<local.flags>&08)
  ctag.list_row<local.t>_02 .= " devedor"
 endif
 try ctag.list_row<local.t>_01=<UID.<DB.ROW.<eval <LOCAL.t>-1>.UID>.tag.name>
end
list_show 1

[FUNCTION town_listPrisioners]
//Lista todos os jogadores com flag de preso.
town_clearTable
list_new "Lista de prisioneiros de <serv.area.<ctag.town_region>.name>"
list_newDbCol 25,uid,Nome
list_newDbCol 200,flags,Estado
list_setAction town_showCriminal
list_setActionBack town_PrisionMenu
list_loadFromDb SELECT uid, flags FROM towns_members WHERE region='<DB.ESCAPEDATA <ctag.town_region>>' AND flags&<EVAL <DEF.TOWN_FLAG_JAILED>>
if (!<ctag0.list_numitems>)
 sysmessageyellow Nada para mostrar.
 town
 return 1
endif
//Transformar em decimal as posições deve você, Anakyn.
FOR t 1 <ctag.list_numitems>
 ctag.list_id_<local.t>=<DB.ROW.<eval <LOCAL.t>-1>.UID>
 ctag.list_row<local.t>_02="Registrado como "
 local.flags=<eval <DB.ROW.<eval <LOCAL.t>-1>.flags>>
 if (<local.flags>&01)
  ctag.list_row<local.t>_02 .= " banido"
 endif
 if (<local.flags>&02)
  ctag.list_row<local.t>_02 .= " criminoso"
 endif
 if (<local.flags>&04)
  ctag.list_row<local.t>_02 .= " preso"
 endif
 if (<local.flags>&08)
  ctag.list_row<local.t>_02 .= " devedor"
 endif
 try ctag.list_row<local.t>_01=<UID.<DB.ROW.<eval <LOCAL.t>-1>.UID>.tag.name>
end
list_show 1


[FUNCTION town_listFriends]
//Lista todos os jogadores com flag de Friend
town_clearTable
list_new "Lista de extrangeiros amigos de <serv.area.<ctag.town_region>.name>"
list_newDbCol 25,uid,Nome
list_newCol 200,Raca
list_setAction town_showFriend
list_setActionBack town
list_loadFromDb SELECT uid FROM towns_members WHERE region='<DB.ESCAPEDATA <ctag.town_region>>' AND flags&16
//Transformar em decimal as posições deve você, Anakyn.
if (!<ctag0.list_numitems>)
 sysmessageyellow Nada para mostrar.
 town
 return 1
endif
FOR t 1 <ctag.list_numitems>
 try ctag.list_id_<local.t>=<DB.ROW.<eval <LOCAL.t>-1>.UID>
 try ctag.list_row<local.t>_01=<UID.<DB.ROW.<eval <LOCAL.t>-1>.UID>.tag.name>
 try ctag.list_row<local.t>_02=<UID.<DB.ROW.<eval <LOCAL.t>-1>.UID>.tag.raca>
end
list_show 1


[FUNCTION town_listMembersTransfer]
//Lista todos os membros e cidadãos da town com region=<ctag.town_region> para transferir a tirularidade
IF (!<argn>)
 town_clearTable
 list_new "Para quem deseja transferir a titularidade de <serv.area.<ctag.town_region>.name>?"
 list_newDbCol 25,uid,Nome
 list_newDbCol 200,position,Hierarquia
 list_newDbCol 275,title,Titulo
 list_setAction town_listMembersTransfer
 list_setActionBack town_Configurations
 list_loadFromDb SELECT t1.uid, t2.title, t2.position FROM towns_members as t1, towns_privs as t2 WHERE t1.region='<DB.ESCAPEDATA <ctag.town_region>>' AND t1.priv_id=t2.id AND t2.position > 1 ORDER BY t2.position
 //Transformar em decimal as posições deve você, Anakyn.
 FOR t 1 <ctag.list_numitems>
  ctag.list_id_<local.t>=<DB.ROW.<eval <LOCAL.t>-1>.UID>
  ctag.list_row<local.t>_01=<UID.<DB.ROW.<eval <LOCAL.t>-1>.UID>.tag.name>
  ctag.list_row<local.t>_02=.<eval <ctag.list_row<local.t>_02>>
 end
 list_show 1
else
 ref1=<ctag0.list_id_<hval <argn>>>
 town_TrasferMastership <ctag.town_region>,<ref1>
endif



[FUNCTION town_prompIndependency]
//Pergunta se quer declarar independencia de <ctag.town_region>
if (STRMATCH(s,<args>))
 town_Independecy <ctag.town_region>
else
 local.capital=<serv.area.<town_getCapital <ctag.town_region>>.name>
 prompt_new Deseja tornar <serv.area.<ctag.town_region>.name> independente? A alta cupola de <local.capital> pode nao gostar da ideia! (s/n)
 prompt_setAction town_Independecy <ctag.town_region>,
 prompt_setActionBack town_Configurations
 prompt_show
endif


[FUNCTION town_Independecy]
//Seta esta town como type=1 (região soberana) e a capital para ela mesma.
if (!<IsEmpty <argv0>>)
 local.town=<argv0>
else
 local.town=<region.defname>
endif
DB.EXECUTE=UPDATE towns SET type=1, capital="<DB.ESCAPEDATA <local.town>" WHERE region="<DB.ESCAPEDATA <local.town>>"
town_MassMessage Foi declarada a independencia de <serv.area.<local.town>.name>!!!
town_Configurations

[FUNCTION town_PrisionMenu]
ctag.town_page=<DEF.TOWN_PAGE_PRISION>
SDIALOG d_town

[FUNCTION town_PrisionCells]
ctag.town_page=<DEF.TOWN_PAGE_CELLS_LIST>
SDIALOG d_town

[FUNCTION town_listDemences]
//Lista as sub=regiões da town ASRG ou DEFAULT.REGION
if (!<IsNum <argv0>>)
 if (!<IsEmpty <args>>)
  local.town=<args>
 else
  local.town=<region.defname>
 endif
 town_clearTable
 list_new Selecione uma regiao para regenciar:
 list_newDbCol 20,Nome,Nome
 list_newDbCol 200,Tipo,Tipo
 list_setAction town_listDemences
 list_loadFromDb SELECT region AS Nome, type AS tipo FROM towns WHERE capital='<DB.ESCAPEDATA <local.town>>' AND capital<>region
 
 //Apurar os itens para melhor identificação
 for i <ctag.list_numitems>
  //Salvar defname da capital em outra lista
  TRY ctag.town_region_<dlocal.i>=<ctag.list_row<local.i>_01>
  TRY ctag.list_row<local.i>_01=<serv.area.<ctag.list_row<local.i>_01>.name>
  DOSWITCH <ctag.list_row<local.i>_02>
   local.null
   TRY ctag.list_row<local.i>_02=Estado maior
   TRY ctag.list_row<local.i>_02=Sub-Regiao
   TRY ctag.list_row<local.i>_02=Regiao sob administracao
  ENDDO
 end
 list_show 1
else
 town <ctag0.town_region_<eval <argn>>>
endif


[FUNCTION town_getDemences]
//Retorna o numero de subregioes que a town ASRG ou DEFAULT.REGION tem
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT COUNT(region) AS cnt FROM towns WHERE capital="<DB.ESCAPEDATA <local.town>>"
RETURN <eval <DB.ROW.0.cnt>|<IsGM>>


[FUNCTION town_listProCapitals]
//Lista cidades que podem ser capitais desta town.
if (!<IsNum <argv0>>)
 if (!<IsEmpty <args>>)
  local.town=<args>
 else
  local.town=<region.defname>
 endif
 town_clearTable
 list_new Selecione uma regiao para regenciar:
 list_newDbCol 20,Nome,Nome
 list_newDbCol 200,Tipo,Tipo
 list_setAction town_listProCapitals
 ctag.town_demences=<town_getDemences <local.town>>
 if (<ctag0.demences>)//Se tiver subregiões, não pode ter capital tipo2.
  list_loadFromDb SELECT region AS Nome, type AS tipo FROM towns WHERE type=1
 else
  list_loadFromDb SELECT region AS Nome, type AS tipo FROM towns WHERE type<3
 endif
 ctag.town_region=<local.town>
 //Apurar os itens para melhor identificação
 for i <ctag.list_numitems>
  //Salvar defname da capital em outra lista
  TRY ctag.town_region_<dlocal.i>=<ctag.list_row<local.i>_01>
  TRY ctag.list_row<local.i>_01=<serv.area.<ctag.list_row<local.i>_01>.name>
  DOSWITCH <ctag.list_row<local.i>_02>
   local.null
   TRY ctag.list_row<local.i>_02=Estado maior
   TRY ctag.list_row<local.i>_02=Sub-Regiao
  ENDDO
 end
 list_show 1
else
 local.capital=<ctag0.town_region_<eval <argn>>>
 local.capital_type=<town_getType <local.capital>>
 local.type=<hval <local.capital_type>+1>
 DB.EXECUTE=UPDATE towns SET capital="<DB.ESCAPEDATA <local.capital>>", type=<dlocal.type> WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
 DB.EXECUTE=UPDATE towns SET capital="<DB.ESCAPEDATA <local.capital>>" WHERE capital="<DB.ESCAPEDATA <ctag.town_region>>" AND type<=<local.type>
 town_MassMessage A regiao <serv.area.<ctag.town_region>.name> foi anexada a <serv.area.<local.capital>.name>!,<local.capital>
endif


[FUNCTION town_newPetition]
//Adiciona uma petição
if (<IsEmpty <args>>)
 ctag.town_petitionTitle=
 ctag.town_petitionText=
 ctag.town_petitionID=0
 prompt_new Digite um titulo para a sua peticao
 prompt_setAction town_newPetition <args>
 if !(<ctag.town_noFullMenu>)
  prompt_setActionBack town_Configurations
 else
  prompt_setActionBack
 endif
 prompt_show
else
 ctag.town_petitionTitle=<args>
 ctag.town_page=<DEF.TOWN_PAGE_POLL>
 SDIALOG d_town
endif
 

[FUNCTION town_sendPetition]
//Envia a nova petição
ctag.gtime = ".<eval <var.tDay>>/<eval <var.tMonth>>/<eval <var.tYear>>"
DB.EXECUTE=INSERT INTO towns_petitions SET title="<DB.ESCAPEDATA <ctag.town_petitionTitle>>", text="<DB.ESCAPEDATA <args>>", father=0, type=<eval <DEF.TOWN_PET_GENERAL>>, UID=<eval <UID>>, gtime='<DB.ESCAPEDATA <STRSUB 1 10 <ctag.gtime>>>', time=CURRENT_TIMESTAMP(), region="<DB.ESCAPEDATA <ctag.town_region>>", flags=0
town_petitionsMenu
sysmessagegreen Peticao enviada. A resposta pode demorar dias se nenhum responsavel aparecer.


[FUNCTION town_sendCitzenshipPetition]
//Envia uma nova petição para ser cidadão
ctag.gtime = ".<eval <var.tDay>>/<eval <var.tMonth>>/<eval <var.tYear>>"
local.text=Declaro-me candidato a ser um cidadao de <serv.area.<ctag.town_region>.name> esperando a apreciacao do governo.
DB.EXECUTE=INSERT INTO towns_petitions SET title="Peticao de asilo de <DB.ESCAPEDATA <tag.name>>", text="<DB.ESCAPEDATA <local.text>>", father=0, type=<eval <DEF.TOWN_PET_AZILUM>>, UID=<eval <UID>>, gtime='<DB.ESCAPEDATA <STRSUB 1 10 <ctag.gtime>>>', time=CURRENT_TIMESTAMP(), region="<DB.ESCAPEDATA <ctag.town_region>>", flags=0
sysmessagegreen Peticao de cidadania enviada. A resposta pode demorar dias se nenhum responsavel aparecer.



[FUNCTION town_listOpenPetitions]
//Lista as petições não respondidas da town ARGS
if (!<IsNum <argv0>>)
 if (!<IsEmpty <args>>)
  local.town=<args>
 else
  local.town=<region.defname>
 endif
 town_clearTable
 list_new Peticoes abertas em <serv.area.<local.town>.name>
 list_newDbCol 20,title,Titulo
 list_newDbCol 300,UID,requisitado por
 list_setAction town_listOpenPetitions
 list_setActionBack town_petitionsMenu
 list_loadFromDb SELECT title, UID, id FROM towns_petitions WHERE region='<DB.ESCAPEDATA <local.town>>' AND flags=0 ORDER BY time DESC
 if (!<ctag0.list_numitems>)
   sysmessageyellow Nao ha novas peticoes.
   town_petitionsMenu
   return 1
 endif
 //Apurar os itens para melhor identificação
 for i <ctag.list_numitems>
  //Salvar id das petições
  TRY ctag.town_petition_<dlocal.i>=<DB.ROW.<eval <local.i>-1>.id>
  TRY ctag.list_row<local.i>_02=<UID.<ctag.list_row<local.i>_02>.tag.name>
 end
 list_show 1
else
 town_showPetition <ctag0.town_petition_<eval <argn>>>
endif


[FUNCTION town_listOldPetitions]
if (!<IsNum <argv0>>)
 if (!<IsEmpty <args>>)
  local.town=<args>
 else
  local.town=<region.defname>
 endif
 town_clearTable
 list_new Peticoes ja votadas em <serv.area.<local.town>.name>
 list_newDbCol 20,title,Titulo
 list_newDbCol 300,UID,requisitado por
 list_setAction town_listOpenPetitions
 list_setActionBack town_petitionsMenu
 list_loadFromDb SELECT title, UID, id FROM towns_petitions WHERE region='<DB.ESCAPEDATA <local.town>>' AND flags>0 ORDER BY time DESC
 
 //Apurar os itens para melhor identificação
 for i <ctag.list_numitems>
  //Salvar id das petições
  TRY ctag.town_petition_<dlocal.i>=<DB.ROW.<eval <local.i>-1>.id>
  TRY ctag.list_row<local.i>_02=<UID.<ctag.list_row<local.i>_02>.tag.name>
 end
 list_show 1
else
 town_showPetition <ctag0.town_petition_<eval <argn>>>
endif


[FUNCTION town_getPetitions]
//Retorna o numero de peticoes abertas para a town <args>
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT COUNT(id) AS cnt FROM towns_petitions WHERE region="<DB.ESCAPEDATA <local.town>>" AND flags=0 AND type<>1
RETURN <DB.ROW.0.cnt>


[FUNCTION town_petitionVote]
//DEFAULT vota 1 ou 0 na petição <dctag.town_petitionID>
if (0<argn>)
 local.flags=<DEF.TOWN_PET_FLAG_FAVOR>
else
 local.flags=<DEF.TOWN_PET_FLAG_AGAINST>
endif
DB.EXECUTE=INSERT INTO towns_petitions SET UID=<eval <uid>>, region="<DB.ESCAPEDATA <local.town>>", title = "none", text="none", type=1, father=<dctag.town_petitionID>, gtime='<dvar.tDay>/<dvar.tMonth>/<dvar.tYear>', flags=<dlocal.flags>, time=CURRENT_TIMESTAMP()
SDIALOG d_town


[FUNCTION town_petitionDelete]
//Remove a petição <dctag.town_petitionID> e todas as votações.
if (<IsEmpty <args>>)
 prompt_new Deseja realmente remmover a peticao '<ctag.town_petitionTitle>'?<DEF.BR>(s/n)
 prompt_setAction town_petitionDelete
 prompt_setActionBack SDIALOG d_town
 prompt_show
elif (STRMATCH(s,<args>))
 DB.EXECUTE=DELETE FROM towns_petitions WHERE id=<dctag.town_petitionID> OR father=<dctag.town_petitionID>
 sysmessagered Peticao excuida.
 town_listOpenPetitions
else
 SDIALOG d_town
endif


[FUNCTION town_petitionJudge]
//DEFAULT julga derradeiramente 1 ou 0 a petição <dctag.town_petitionID>
if (0<argn>)
 local.flags=<DEF.TOWN_PET_FLAG_FAVOR>
 local.word=aceita
else
 local.flags=<DEF.TOWN_PET_FLAG_AGAINST>
 local.word=rejeitada
endif
DB.EXECUTE=UPDATE towns_petitions SET flags=<dlocal.flags> WHERE id=<dctag.town_petitionID>
DB.QUERY=SELECT UID FROM towns_petitions WHERE id=<dctag.town_petitionID> OR father=<dctag.town_petitionID>
FOR p 0 <eval <DB.ROW.NUMROWS>-1>
 f_sendMessage <DB.ROW.<dlocal.p>.uid>,A peticao '<ctag.town_petitionTitle>' foi declarada <local.word>!!!,<DEF.SM_ORANGE>
END
//Eh uma peticao de pedido de cidadania?
DB.QUERY=SELECT UID, type FROM towns_petitions WHERE id=<dctag.town_petitionID>
IF (<DB.ROW.0.type>==<DEF.TOWN_PET_AZILUM>)
 ref1=<DB.ROW.0.UID>
 ref1.town_Azilum <ctag.town_region>
ENDIF
SDIALOG d_town


[FUNCTION town_loadTaxes]
//CALL Pega as taxas da town <args> e guarda em locals.
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.QUERY=SELECT * FROM towns WHERE region="<DB.ESCAPEDATA <local.town>>"
local.commercialfee=<DB.ROW.0.commercialfee>
local.buildingfee=<DB.ROW.0.buildingfee>
local.tilelandprice=<DB.ROW.0.tilelandprice>
local.tilebuildprice=<DB.ROW.0.tilebuildprice>
local.tilefixtureprice=<DB.ROW.0.tilefixtureprice>


[FUNCTION town_saveTaxes]
//Salva as taxas modificadas no gump
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.EXECUTE=UPDATE towns SET commercialfee=<eval <ARGTXT[1]>>, buildingfee=<eval <ARGTXT[2]>>, tilelandprice=<eval <ARGTXT[3]>>, tilebuildprice=<eval <ARGTXT[4]>>, tilefixtureprice=<eval <ARGTXT[5]>> WHERE region="<DB.ESCAPEDATA <local.town>>"


[FUNCTION town_NewLaw]
//Cria uma nome lei peguntando o nome
if (<IsEmpty <args>>)
 ctag.town_lawTitle=
 ctag.town_lawText=
 ctag.town_lawID=0
 prompt_new Digite um titulo para o decreto
 prompt_setAction town_NewLaw <args>
 prompt_setActionBack SDIALOG d_town
 prompt_show
else
 ctag.town_lawTitle=<args>
 ctag.town_page=<DEF.TOWN_PAGE_LAWS_EDIT>
 SDIALOG d_town
endif


[FUNCTION town_lawSendNew]
//Manda uma nova lei para a DB
if (<EVAL STRLEN(<ARGS>)> < 19)
 SYSMESSAGERED Seu decreto deve ter entre 20 e 1024 caracteres. Tente de novo!
 ctag.town_lawText=<args>
 SDIALOG d_town
 return 1
endif
DB.EXECUTE=INSERT INTO towns_laws SET sign="<DB.ESCAPEDATA <tag.name>>", title="<DB.ESCAPEDATA <ctag.town_lawTitle>>", time=CURRENT_TIMESTAMP(), gtime='<dvar.tDay>/<dvar.tMonth>/<dvar.tYear>', text="<DB.ESCAPEDATA <args>>", region="<ctag.town_region>", flags=1
SYSMESSAGEGREEN Novo decreto promulgado!
town_Laws


[FUNCTION town_lawsList]
//Lista as leis da town
town_clearTable
list_new Decretos promulgados em <serv.area.<ctag.town_region>.name>:
list_newDbCol 20,id,No.
list_newDbCol 100,gtime,data
list_newDbCol 300,title,Titulo
list_setAction town_lawShow
if (!<ctag0.town_noFullMenu>)
 list_setActionBack town <ctag.town_region>
else
 list_setActionBack
endif
CTAG.list_numItems=0
DB.QUERY=SELECT id, gtime, title FROM towns_laws WHERE region="<DB.ESCAPEDATA <ctag.town_region>>" ORDER BY time DESC
if (<DB.ROW.NUMROWS> > 0)
    for R 0 <eval <DB.ROW.NUMROWS>-1>
        CTAG.list_numItems += 1
        TRY CTAG.list_row<CTAG.list_numItems>_01=.<DB.ROW.<dLOCAL.R>.id>
        TRY CTAG.list_id_<dCTAG.list_numItems>=<DB.ROW.<dLOCAL.R>.id>
        TRY CTAG.list_row<CTAG.list_numItems>_02=.<DB.ESCAPEDATA <DB.ROW.<dLOCAL.R>.gtime>>
        TRY CTAG.list_row<CTAG.list_numItems>_03=<DB.ESCAPEDATA <DB.ROW.<dLOCAL.R>.title>>
    end
endif

CTAG.list_numPages=<eval (((<CTAG.list_numItems>-1) / list_itemsPerPage) + 1)>
list_Show 1


[FUNCTION town_listPositions]
//Lista todos os cargos existentes na town.
town_clearTable
ctag.town_position=
ctag.town_title=
ctag.town_description=
ctag.town_positionID=
ctag.town_privs=
list_new "Lista de cargos de <serv.area.<ctag.town_region>.name>"
list_newDbCol 25,title,Titulo
list_newDbCol 200,position,Posicao
list_setAction town_showPosition
list_setActionBack town_Configurations
list_loadFromDb "SELECT id,title,position FROM towns_privs WHERE region='<DB.ESCAPEDATA <ctag.town_region>>' ORDER BY position"
//Transformar em decimal as posições deve você, Anakyn.
FOR t 1 <ctag.list_numitems>
 ctag.list_id_<local.t>=<DB.ROW.<eval <LOCAL.t>-1>.id>
 ctag.list_row<local.t>_02=<dctag.list_row<local.t>_02> de 1000
end
IF (<town_getPrivs <ctag.town_region>>&<DEF.TOWN_PRIV_PRIVS>)
 ctag.list_numitems += 1
 ctag.list_row<ctag.list_numitems>_01=Adicionar
 ctag.list_row<ctag.list_numitems>_02=.
ENDIF
list_show 1



[FUNCTION town_lawShow]
//Mostra uma lei
ctag.town_lawID=<ctag.list_id_<argn>>
town_clearTable
ctag.town_page=<DEF.TOWN_PAGE_LAWS_SHOW>
SDIALOG d_town


[FUNCTION town_lawsListRevoke]
//Lista leis para revogar
town_clearTable
list_new Desejo revogar o seguinte decreto de <serv.area.<ctag.town_region>.name>:
list_newDbCol 20,id,No.
list_newDbCol 100,gtime,data
list_newDbCol 300,title,Titulo
list_setAction town_lawRevoke
list_setActionBack town <ctag.town_region>
CTAG.list_numItems=0
DB.QUERY=SELECT id, gtime, title FROM towns_laws WHERE region="<DB.ESCAPEDATA <ctag.town_region>>" ORDER BY time DESC
if (<DB.ROW.NUMROWS> > 0)
    for R 0 <eval <DB.ROW.NUMROWS>-1>
        CTAG.list_numItems += 1
        TRY CTAG.list_row<CTAG.list_numItems>_01=.<DB.ROW.<dLOCAL.R>.id>
        TRY CTAG.list_id_<dCTAG.list_numItems>=<DB.ROW.<dLOCAL.R>.id>
        TRY CTAG.list_row<CTAG.list_numItems>_02=.<DB.ESCAPEDATA <DB.ROW.<dLOCAL.R>.gtime>>
        TRY CTAG.list_row<CTAG.list_numItems>_03=<DB.ESCAPEDATA <DB.ROW.<dLOCAL.R>.title>>
    end
endif

CTAG.list_numPages=<eval (((<CTAG.list_numItems>-1) / list_itemsPerPage) + 1)>
list_Show 1


[FUNCTION town_lawRevoke]
//Revoga uma lei
if (<IsNum <argv0>>)
 ctag.town_lawID=<ctag.list_id_<argn>>
 town_clearTable
 prompt_new Deseja mesmo revogar o decreto '<CTAG.list_row<hval <argn>>_03>'?(s/n)
 prompt_setAction town_lawRevoke <args>
 prompt_setActionBack town <ctag.town_region>
 prompt_show
elif (STRMATCH(*s*,<args>))
 DB.EXECUTE=DELETE FROM towns_laws WHERE id=<dctag.town_lawID>
 sysmessagered Decreto revogado!
 town_laws
else
 sysmessageyellow Decreto mantido.
 town_laws
endif


[FUNCTION town_lawsListEdit]
//Lista decretos para editar
town_clearTable
list_new Desejo modificar o seguinte decreto de <serv.area.<ctag.town_region>.name>:
list_newDbCol 20,id,No.
list_newDbCol 100,gtime,data
list_newDbCol 300,title,Titulo
list_setAction town_lawEdit
list_setActionBack town <ctag.town_region>
CTAG.list_numItems=0
DB.QUERY=SELECT id, gtime, title FROM towns_laws WHERE region="<DB.ESCAPEDATA <ctag.town_region>>" ORDER BY time DESC
if (<DB.ROW.NUMROWS> > 0)
    for R 0 <eval <DB.ROW.NUMROWS>-1>
        CTAG.list_numItems += 1
        TRY CTAG.list_row<CTAG.list_numItems>_01=.<DB.ROW.<dLOCAL.R>.id>
        TRY CTAG.list_id_<dCTAG.list_numItems>=<DB.ROW.<dLOCAL.R>.id>
        TRY CTAG.list_row<CTAG.list_numItems>_02=.<DB.ESCAPEDATA <DB.ROW.<dLOCAL.R>.gtime>>
        TRY CTAG.list_row<CTAG.list_numItems>_03=<DB.ESCAPEDATA <DB.ROW.<dLOCAL.R>.title>>
    end
endif

CTAG.list_numPages=<eval (((<CTAG.list_numItems>-1) / list_itemsPerPage) + 1)>
list_Show 1


[FUNCTION town_lawEdit]
//Carrega uma lei para editar
ctag.town_lawID=<ctag.list_id_<argn>>
DB.QUERY=SELECT title, text FROM towns_laws WHERE id=<dctag.town_lawID>
ctag.town_lawTitle=<DB.ROW.0.title>
ctag.town_lawText=<DB.ROW.0.text>
ctag.town_page=<DEF.TOWN_PAGE_LAWS_EDIT>
SDIALOG d_town


[FUNCTION town_lawChangeTitle]
//Troca o título de um decreto
if (<IsEmpty <args>>)
 prompt_new Qual ser o novo titulo para '<ctag.town_lawTitle>'?
 prompt_setAction town_lawChangeTitle <args>
 prompt_setActionBack town_lawEdit <ctag.town_region>
 prompt_show
else
 ctag.town_lawTitle=<args>
 ctag.town_page=<DEF.TOWN_PAGE_LAWS_EDIT>
 SDIALOG d_town
endif


[FUNCTION town_lawSendEdit]
//Envia para DB um decreto editado
if (<EVAL STRLEN(<ARGS>)> < 19)
 SYSMESSAGERED Seu decreto deve ter entre 20 e 1024 caracteres. Tente de novo!
 ctag.town_lawText=<args>
 SDIALOG d_town
 return 1
endif
DB.EXECUTE=UPDATE towns_laws SET sign="<DB.ESCAPEDATA <tag.name>>", title="<DB.ESCAPEDATA <ctag.town_lawTitle>>", time=CURRENT_TIMESTAMP(), gtime='<dvar.tDay>/<dvar.tMonth>/<dvar.tYear>', text="<DB.ESCAPEDATA <args>>", region="<DB.ESCAPEDATA <ctag.town_region>>", flags=1 WHERE id=<dctag.town_lawID>
SYSMESSAGEGREEN Edicao do decreto promulgada!
town_Laws


[FUNCTION town_hireKeeper]
//Contrata um NPC
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
if (<ctag0.town_tryHire> > 5)
 sysmessagered Nao ha porteiros disponiveis para contratar. Tente mais tarde.
 ctag.town_tryHire=
 return 1
endif
ctag.town_tryHire=<ctag0.town_tryHire>+1
local.races=<town_getRaces <local.town>>
local.racen=<R3>
DOSWITCH <local.racen>
 local.race=Humano
 local.race=Elfo
 local.race=Anao
end
if (strmatch(*<local.race>*,<local.races>))
 if (<town_getCash> < <DEF.TOWN_SERVANT_1_HIRE>)
  sysmessageyellow <serv.area.<local.town>.name> nao tem dinheiro para pagar por um porteiro.
  town_PageNPCs
  retuen 1
 endif
 town_AddCash -<DEF.TOWN_SERVANT_1_HIRE>
 DOSWITCH <local.racen>
  local.npc=c_h_gatekeeper
  local.npc=c_e_gatekeeper
  local.npc=c_a_gatekeeper
 end
 src.newnpc=<local.npc>
 new.movenear <uid>,2
 town_PageNPCs
else
 town_hireKeeper <local.town>
endif


[FUNCTION town_hireSeneschal]
//Contrata um senescal
if (!<IsEmpty <argv1>>)
 local.town=<argv1>
else
 local.town=<region.defname>
endif
if (<ctag0.town_tryHire> > 5)
 sysmessagered Nao ha administradores disponiveis para contratar. Tente mais tarde.
 ctag.town_tryHire=
 return 1
endif
ctag.town_tryHire=<ctag0.town_tryHire>+1
local.races=<town_getRaces <local.town>>
local.racen=<R3>
DOSWITCH <local.racen>
 local.race=Humano
 local.race=Elfo
 local.race=Anao
end
if (strmatch(*<local.race>*,<local.races>))
 if (<town_getCash> < <DEF.TOWN_SERVANT_3_HIRE>)
  sysmessageyellow <serv.area.<local.town>.name> nao tem dinheiro para pagar por um senescal.
  town_PageNPCs
  return 1
 endif
 town_AddCash -<DEF.TOWN_SERVANT_3_HIRE>
 DOSWITCH <local.racen>
  local.npc=c_h_senescal
  local.npc=c_e_senescal
  local.npc=c_a_senescal
 end
 src.newnpc=<local.npc>
 new.movenear <uid>,2
 town_PageNPCs
else
 town_hireSeneschal <local.town>
endif


[FUNCTION town_hireThisGuard]
//Contrata para a town <argv0> um guarda de nível <argv1>
if (!<IsEmpty <argv0>>)
 local.town=<argv0>
 if (<IsEmpty <argv1>>)
  local.level=<argv1>
 else
  local.level=0
 endif
else
 local.town=<region.defname>
 local.level=0
endif
if (<ctag0.town_tryHire> > 5)
 sysmessagered Nao ha guardas disponiveis para contratar. Tente mais tarde.
 ctag.town_tryHire=
 return 1
endif
ctag.town_tryHire=<ctag0.town_tryHire>+1
local.races=<town_getRaces <local.town>>
local.racen=<R3>
DOSWITCH <local.racen>
 local.race=Humano
 local.race=Elfo
 local.race=Anao
end
if (strmatch(*<local.race>*,<local.races>))
 if (<town_getCash> < <DEF.TOWN_GUARD_<dlocal.level>_HIRE>)
  sysmessageyellow <serv.area.<local.town>.name> nao tem dinheiro para pagar por este guarda.
  town_PageNPCs
  retuen 1
 endif
 town_AddCash -<DEF.TOWN_GUARD_<dlocal.level>_HIRE>
 DOSWITCH <local.racen>
  local.npc=c_h_guard
  local.npc=c_e_guard
  local.npc=c_a_guard
 end
 src.newnpc=<local.npc>
 new.movenear <uid>,2
 new.tag.servant=<DEF.TOWN_SERVANT_GUARD>
 new.town_RegServant <local.town>
 new.guard_level <argv1>
 town_PageNPCs
else
 town_hireGuard <local.town>,<local.level>
endif


[FUNCTION town_fireNPC]
//Demite o NPC de UID=<ctag.town_UID>
if (STRMATCH(s,<args>))
 obj=<ctag0.town_UID>
 sysmessagered <obj.name> foi demitido!
 obj.remove
 town_PageNPCs
elif (STRMATCH(n,<args>))
 sysmessageyellow Demicao cancelada.
elif (<IsEmpty <args>>)
 obj=<ctag0.town_UID>
 prompt_new Deseja realmente demitir <obj.name>?<DEF.BR>(S)im/(N)ao
 prompt_setAction town_fireNPC
 prompt_setActionBack SDIALOG d_town
 prompt_show
 return 0
endif
SDIALOG d_town


[FUNCTION town_PrisionActivate]
//Ativa ou desativa o sistema de prisão com <argn> minutos de detenção mínima
if (<IsEmpty <args>>)
 prompt_new Quantos minutos de detencao minima?
 prompt_setAction town_PrisionActivate <args>
 prompt_setActionBack SDIALOG d_town
 prompt_show
else
 serv.area.<ctag.town_region>.tag.prisionTime=<eval <argn>*60>
 SDIALOG d_town
endif


[FUNCTION town_ChangePrisionFee]
//Muda a fuança mínima da prisão
if (<IsEmpty <args>>)
 prompt_new Digite um valor em cobre para fianca minima:<DEF.BR>(0 = inafiancavel)
 prompt_setAction town_ChangePrisionFee <args>
 prompt_setActionBack SDIALOG d_town
 prompt_show
else
 serv.area.<ctag.town_region>.tag.prisionFee=<argn>
 SDIALOG d_town
endif


[FUNCTION town_AddCell]
//Adiciona uma cela ao sistema de prisão
sysmessageyellow Selecione um ponto dentro da nova cela:
targetfg town_AddCell2

[FUNCTION town_AddCell2]
 local.c=0
 WHILE (!<IsEmpty <serv.area.<ctag.town_region>.tag.cell_<dlocal.c>>>)
  local.c += 1
 END
 //local.c += 1
 serv.area.<ctag.town_region>.tag.cell_<dlocal.c>=<targp>
 sysmessage args: <args> targp: <targp>
 town_PrisionCells

 
[FUNCTION town_RemoveCell]
//Remove da Town a cela número <argn>

//Prepara o Cache e apaga o atual log
local.t=0
FOR c 0 5
 if (<local.c> != <argn>)
  local.cache_<dlocal.t>=<serv.area.<ctag.town_region>.tag.cell_<dlocal.c>>
  local.t += 1
 endif
 serv.area.<ctag.town_region>.tag.cell_<dlocal.c>=
END

//Repopula a lista
FOR n 0 5
 if (!<IsEmpty <local.cache_<dlocal.n>>>) && (<EVAL STRLEN(<local.cache_<dlocal.n>>)> > 2)
  serv.area.<ctag.town_region>.tag.cell_<dlocal.n>=<local.cache_<dlocal.n>>
 endif
END
town_PrisionCells


[FUNCTION town_ChangeJailTime]
//Muda o tempo de prisão do player <ctag.town_UID>
ref1=<ctag.town_UID>
ref2=<ref1.town_GetJailMemory <ctag.town_region>>
if (!<argv>)
 prompt_new Alterar a pena para quantos minutos?<DEF.BR>(Atual: <f_timestring <ref2.timer>>)
 prompt_setAction town_ChangeJailTime
 prompt_setActionBack SDIALOG d_town
 prompt_show
else
 town_AdjustJailTime <ref1>,<ctag.town_region>,<argn>
 serv.log town_AdjustJailTime <ref1>,<ctag.town_region>,<argn>
 SDIALOG d_town
endif

[FUNCTION town_changeReleasePoint]
//Target no ponto de banimento/soltura da town
sysmessageyellow Selecione um ponto no mapa 'FORA DOS MUROS' de <serv.area.<ctag.town_region>.name>
targetfg town_changeReleasePoint2

[FUNCTION town_changeReleasePoint2]
serv.area.<ctag.town_region>.tag.town_releasePoint = <TARGP>
SDIALOG d_town

[FUNCTION town_manageBook]
if (<region.defname> != <ctag.town_region>) || (<region.defname> != <ctag.town_region>)
 sysmessagered Voce so poderia fazer isso em <serv.area.<ctag.town_region>.name>.
 return 1
elif (!<argo>)
 sysmessageyellow Selecione um livro dentro de uma estante:
 targetf town_manageBook
elif (<argo.type>!= t_book) && (<argo.type>!= t_book_scroll)
 sysmessagered Apenas livros e cartas.
elif !(STRMATCH(*bookcase*,<argo.topobj.defname>) )
 sysmessagered O livro deve estar numa estante no chao.
elif !(<argo.topobj.attr>&010)
 sysmessagered Esta estante nao parece muito confiavel... [nao esta presa]
elif (<argo.attr>&010)
 sysmessageyellow O livro foi solto.
 argo.attr &= ~010
 argo.update
 SDIALOG d_town
else
 sysmessageyellow O livro esta preso agora.
 argo.attr |= 010
 argo.update
 SDIALOG d_town
endif

 
 
 

//*****************************
// Função town_Can* para privs
//*****************************

[FUNCTION town_canPriv]
//Testa se DEFAULT tem priviléguio para alterar um cargo de posição <argn> (1 a 999)
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_PRIVS>?1:0>
local.pos=<town_getPosition <local.region>>
IF (<argn> > <local.pos>) || (<local.pos>==1) || <IsGM> || (<town_getMaster <local.region>>==<UID>)
 local.priv &= 1
ELSE
 local.priv &= 0
ENDIF
RETURN <local.priv>


[FUNCTION town_canMember]
//Testa se DEFAULT tem priviléguio para modificar membros e flags de membros com cargos de posição <argn> (1 a 999)
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_MEMBER>?1:0>
local.pos=<town_getPosition <local.region>>
IF (<argn> > <local.pos>) || (<local.pos>==1) || <IsGM> || (<town_getMaster <local.region>>==<UID>)
 local.priv &= 1
ELSE
 local.priv &= 0
ENDIF
RETURN <local.priv>


[FUNCTION town_canLaws]
//Testa se DEFAULT tem priviléguio para modificar leis
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_LAWS>?1:0>
RETURN <local.priv>


[FUNCTION town_canHood]
//Testa se DEFAULT tem priviléguio para usar capuz
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_HOOD>?1:0>
RETURN <local.priv>


[FUNCTION town_canWarmode]
//Testa se DEFAULT tem priviléguio para usar capuz
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_WARMODE>?1:0>
RETURN <local.priv>


[FUNCTION town_canWeapon]
//Testa se DEFAULT tem priviléguio para usar armas
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_WEAPON>?1:0>
RETURN <local.priv>


[FUNCTION town_canHire]
//Testa se DEFAULT tem priviléguio para comandar porteiro
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_HIRE>?1:0>
RETURN <local.priv>


[FUNCTION town_canGuard]
//Testa se DEFAULT tem priviléguio para comandar porteiro
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_GUARD>?1:0>
RETURN <local.priv>


[FUNCTION town_canKeeper]
//Testa se DEFAULT tem priviléguio para comandar porteiro
if (<IsEmpty <ctag.town_region>>)
 local.region=<region.defname>
else
 local.region=<ctag.town_region>
endif
local.priv=<QVAL <town_getPrivs <local.region>>&<DEF.TOWN_PRIV_KEEPER>?1:0>
RETURN <local.priv>


[FUNCTION town_canGate]
OBJ=<argn>
//Testa se DEFAULT pode pedir para abrir a porta
local.ret=1
if !(<rec_CanSee <OBJ>>)
 DORAND 3
  local.ret E quem eh voce?
  local.ret Nao consigo ver seu rosto.
  local.ret E quem gostaria de passar?
  local.ret Nao sei quem voce eh.
 END
elif (<OBJ.ctag0.town_<region.defname>_flags>&<DEF.TOWN_FLAG_BANNED>) || (<OBJ.ctag0.town_<region.defname>_flags>&<DEF.TOWN_FLAG_FORBIDDEN>) || (<OBJ.ctag0.town_<region.defname>_flags>&<DEF.TOWN_FLAG_CRIMINAL>)
 local.ret Va embora! Voce nao eh bem vind<sex o/a> aqui!
endif
RETURN <local.ret>



//########################################################################
//########################################################################
//                              CHARDEFS
//########################################################################
//########################################################################



//******************************************************************************
//******************************************************************************
//############################################################################**
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//                                                                            **
//############################################################################**
//                                                                            **
//                              SENESCAIS                                     **
//                                                                            **
//############################################################################**
//                                                                            **
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//############################################################################**
//******************************************************************************
//******************************************************************************

//*****************************************************************************
// Senescal Homem
//*****************************************************************************
[CHARDEF c_h_senescal]
ID=0190
Name=#NAMES_HUMANMALE, o Senescal
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat
TEVENTS=e_town_Seneschal
TSPEECH=spk_senescal
TEVENTS=e_Human_HearUnk

CATEGORY=MYT - Town
SUBSECTION=Senescal
DESCRIPTION=Senescal (Humano)

ON=@Create
NPC=brain_towncrier
COLOR=colors_skin

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_SENESCAL

SPEECHCOLOR=colors_blue
TAG.RACA=Humano
tag.servant=<DEF.TOWN_SERVANT_SENESCAL>

ON=@NPCRestock
    ITEM={ i_hair_ponytail 1 i_hair_short 1 i_hair_pageboy 1}
    COLOR=colors_hair
    ITEM { i_beard_vandyke 1 i_beard_goatee 1 i_beard_mustache 1 0 2}
    COLOR=match_hair
    ITEM=RANDOM_LIGHT
    ITEM=i_shirt_fancy
    COLOR=colors_all
    ITEM=i_pants_long
    COLOR=colors_all
    ITEM=i_cape
    ITEM=i_SASH
    ITEM=random_boots
    ITEM={ i_hat_feather 1 i_cap 1 0 2}
    COLOR=COLORS_ALL
    
    

//*****************************************************************************
// Senescal Elfo
//*****************************************************************************
[CHARDEF c_e_senescal]
ID=0190
Name=#NAMES_HUMANMALE, o Senescal
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TEVENTS=e_town_Seneschal
TSPEECH=spk_senescal
TEVENTS=e_Human_HearUnk

CATEGORY=MYT - Town
SUBSECTION=Senescal
DESCRIPTION=Senescal (Elfo)

ON=@Create
NPC=brain_towncrier
COLOR=083EA

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_SENESCAL

SPEECHCOLOR=colors_blue
TAG.RACA=Elfo
tag.servant=<DEF.TOWN_SERVANT_SENESCAL>

ON=@NPCRestock
    ITEM=i_hair_long
    COLOR=035
    ITEM=i_shirt_plain
    COLOR=colors_all
    ITEM=i_pants_short
    COLOR=colors_all
    ITEM=i_cape
    ITEM=i_SASH
    ITEM=i_sandals


//*****************************************************************************
// Senescal Anão
//*****************************************************************************
[CHARDEF c_a_senescal]
ID=0190
Name=#NAMES_HUMANMALE, o Senescal
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TEVENTS=e_town_Seneschal
TSPEECH=spk_senescal
TEVENTS=e_Human_HearUnk

CATEGORY=MYT - Town
SUBSECTION=Senescal
DESCRIPTION=Senescal (Anao)

ON=@Create
NPC=brain_towncrier
COLOR=083EA

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_SENESCAL

SPEECHCOLOR=colors_blue
TAG.RACA=Anao
tag.servant=<DEF.TOWN_SERVANT_SENESCAL>

ON=@NPCRestock
    ITEM={ i_hair_long 1 i_hair_ponytail 1  i_hair_2_pigtails 1 }
    COLOR=COLORS_HAIR
    ITEM={ i_beard_long 1 i_beard_short_med 1 i_beard_long_med 1 }
    COLOR=match_hair
    ITEM=i_shirt_fancy
    COLOR=colors_all
    ITEM=i_pants_long
    COLOR=colors_all
    ITEM=i_cape
    ITEM=i_SASH
    ITEM=RANDOM_BOOTS
        

//******************************************************************************
//******************************************************************************
//############################################################################**
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//                                                                            **
//############################################################################**
//                                                                            **
//                                   PORTEIROS                                **
//                                                                            **
//############################################################################**
//                                                                            **
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//############################################################################**
//******************************************************************************
//******************************************************************************



//*****************************************************************************
// Porteiro Homem
//*****************************************************************************
[CHARDEF c_h_gatekeeper]
ID=0190
Name=#NAMES_HUMANMALE, o Porteirol
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=jobTOWNdoor
TEVENTS=e_Human_HearUnk
TEVENTS=e_town_gatekeeper

CATEGORY=MYT - Town
SUBSECTION=Porteiros
DESCRIPTION=Porteiro (Humano)

ON=@Create
NPC=brain_towncrier
COLOR=colors_skin

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_GATEKEEPER

SPEECHCOLOR=colors_blue
TAG.RACA=Humano
tag.servant=<DEF.TOWN_SERVANT_GATE>

ON=@NPCRestock
    ITEM={ i_hair_ponytail 1 i_hair_short 1 i_hair_pageboy 1}
    COLOR=colors_hair
    ITEM { i_beard_vandyke 1 i_beard_goatee 1 i_beard_mustache 1 0 2}
    COLOR=match_hair
    ITEM=RANDOM_LIGHT
    ITEM=i_shirt_fancy
    COLOR=colors_all
    ITEM=i_pants_long
    COLOR=colors_all
    ITEM=i_SASH
    ITEM=random_boots
    ITEM={ i_hat_feather 1 i_cap 1 0 2}
    COLOR=COLORS_ALL
    

//*****************************************************************************
// Porteiro Elfo
//*****************************************************************************
[CHARDEF c_e_gatekeeper]
ID=0190
Name=#NAMES_HUMANMALE, o Porteiro
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=jobTOWNdoor
TEVENTS=e_Human_HearUnk
TEVENTS=e_town_gatekeeper

CATEGORY=MYT - Town
SUBSECTION=Porteiros
DESCRIPTION=Porteiro (Elfo)

ON=@Create
NPC=brain_towncrier
COLOR=083EA

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_GATEKEEPER

SPEECHCOLOR=colors_blue
TAG.RACA=Elfo
tag.servant=<DEF.TOWN_SERVANT_GATE>

ON=@NPCRestock
    ITEM=i_hair_long
    COLOR=035
    ITEM=i_shirt_plain
    COLOR=colors_all
    ITEM=i_pants_short
    COLOR=colors_all
    ITEM=i_SASH
    ITEM=i_sandals
    

//*****************************************************************************
// Porteiro Anão
//*****************************************************************************
[CHARDEF c_a_gatekeeper]
ID=0190
Name=#NAMES_HUMANMALE, o Porteiro
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=jobTOWNdoor
TEVENTS=e_Human_HearUnk
TEVENTS=e_town_gatekeeper

CATEGORY=MYT - Town
SUBSECTION=Porteiros
DESCRIPTION=Porteiro (Anao)

ON=@Create
NPC=brain_towncrier
COLOR=083EA

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_GATEKEEPER

SPEECHCOLOR=colors_blue
TAG.RACA=Anao
tag.servant=<DEF.TOWN_SERVANT_GATE>

ON=@NPCRestock
    ITEM={ i_hair_long 1 i_hair_ponytail 1  i_hair_2_pigtails 1 }
    COLOR=COLORS_HAIR
    ITEM={ i_beard_long 1 i_beard_short_med 1 i_beard_long_med 1 }
    COLOR=match_hair
    ITEM=i_shirt_fancy
    COLOR=colors_all
    ITEM=i_pants_long
    COLOR=colors_all
    ITEM=i_SASH
    ITEM=RANDOM_BOOTS




//########################################################################
//########################################################################
//                              SPEECHS
//########################################################################
//########################################################################

[SPEECH jobTOWNdoor]
//Speech do porteiro
on=*venha*
on=*vamos*
on=ANDE
on=siga-me
IF (<src.town_canKeeper <tag.town>>)
 flags=<flags>&~04
 timerf 1,say,Pois nao, <src.sex senhor/senhora>.
 act=<src.uid>
 action=064
ENDIF
return 1

on=*fique*
on=*permaneca*
IF (<src.town_canKeeper <tag.town>>)
 flags = <flags>|04
 timerf 1,say,Cuidarei dos portoes aqui entao, <src.sex senhor/senhora>.
 act=
 action=065
ENDIF
return 1

on=*vigie*porta
IF (<src.town_canKeeper <tag.town>>)
 SAY Que porta quer que eu vigie?
 targetf TOWN_ADD_DOOR <UID>
ENDIF
return 1

on=*pare*vigiar*porta
IF (<src.town_canKeeper <tag.town>>)
 SAY Que porta quer que eu deixe de vigiar?
 targetf TOWN_REM_DOOR <UID>
ENDIF
return 1

on=*abra*porta*
on=*abra*portao*
on=*abra*portal*
on=*abra*portoes*
on=*abra*portais*
local.ret=<town_canGate <src>>
IF (<IsNum <local.ret>>)
 TOWN_OPEN_DOOR
 timerf 7,TOWN_CLOSE_DOOR
ELSE
 timerf 1,say <local.ret>
ENDIF
return 1

on=*mantenha*porta*aberta
on=*mantenha*portao*aberto
on=*mantenha*portal*aberto
on=*mantenha*portoes*abertos
on=*mantenha*portais*abertos
IF (<src.town_canKeeper <tag.town>>)
 TOWN_OPEN_DOOR
ENDIF
return 1

on=*feche*porta*
on=*feche*portao*
on=*feche*portal*
on=*feche*portoes*
on=*feche*portais*
//IF (<SRC.TOWN_CHECK_SPK_PRIVS <UID>,1>)
 TOWN_CLOSE_DOOR
//ENDIF
return 1

ON=oi
ON=ola
ON=olá
ON=*servico*mim*
ON=*trabalho*mim*

    quest_closeDialogs
    SRC.CTAG.quest_selectedNpc=<uid>
    quest_createNpcQuestList
    if <SRC.CTAG0.quest_numQuests> > 0
            message O que deseja fazer?
            dialog d_quest_list
    else            
        say Gostaria de comprar ou vender algo? Retirar talvez?
    endif


[SPEECH spk_senescal]
on=*venha*
on=*vamos*
on=ANDE
on=siga-me
IF (<src.town_getPrivs <tag.town_region>>&<DEF.TOWN_PRIV_SENESCHAL>)
 flags=<flags>&~04
 timerf 1,say,Certamente, <src.sex senhor/senhora>.
 act=<src.uid>
 action=064
ELSE
 say So posso obedecer os comandos de meus superiores, senho<src.sex r/ra>.
ENDIF
return 1

on=*fique*
on=*permaneca*
IF (<src.town_getPrivs <tag.town_region>>&<DEF.TOWN_PRIV_SENESCHAL>)
 flags = <flags>|04
 timerf 1,say,De certo que ficarei aqui, <src.sex senhor/senhora>.
 act=
 action=065
ELSE
 say So posso obedecer os comandos de meus superiores, senho<src.sex r/ra>.
ENDIF
return 1

on=*como*esta*reino*
on=*como*esta*cidade*
on=*como*esta*vila*
timerf 1,say Oh, sim! Veja meus relatos.
timerf 2,emoteyellow,entrega uns papeis
src.timerf 2,town

on=*contratar*
on=*contrate*
IF (<src.town_getPrivs <tag.town_region>>&<DEF.TOWN_PRIV_HIRE>)
 say Ainda nao podemos ter funcionarios, senho<src.sex r/ra>.
ELSE
 say Voce nao tem privilegio para contratar funcionarios, senho<src.sex r/ra>.
ENDIF

ON=oi
ON=ola
ON=olá
ON=*servico*mim*
ON=*trabalho*mim*

    quest_closeDialogs
    SRC.CTAG.quest_selectedNpc=<uid>
    quest_createNpcQuestList
    if <SRC.CTAG0.quest_numQuests> > 0
            message O que deseja fazer?
            dialog d_quest_list
    else            
        say Gostaria de comprar ou vender algo? Retirar talvez?
    endif

//########################################################################
//      Funções dos NPCs
//########################################################################

[FUNCTION town_RegServant]
//Põe NPC na DB
if (!<IsEmpty <args>>)
 local.town=<args>
else
 local.town=<region.defname>
endif
DB.EXECUTE=INSERT INTO towns_servants SET uid=<eval <uid>>, type=<eval <tag0.servant>>, region="<DB.ESCAPEDATA <local.town>>"
DORAND 3
 say Nao ha lugar como o lar. E <serv.area.<local.town>.name> eh meu novo lar!
 say <serv.area.<local.town>.name> eh meu novo lar!
 say Eh bom estar em <serv.area.<local.town>.name>. Morarei aqui.
 say Ja me instalei em <serv.area.<local.town>.name>. Agora aqui eh meu lar.
END
tag.town_region=<local.town>


[FUNCTION town_UnRegServant]
//Tira NPC da DB
local.town=<tag.town_region>
DB.EXECUTE=DELETE FROM towns_servants WHERE UID=<eval <UID>>
serv.log [TOWNS] Servidor <name> (<dtag.servant>) de <local.town> :: UnRegServant

[FUNCTION town_ServantFood]
//Faz o NPC comer
ref1=<findtype t_food>
if (<ref1>)
 anim 022
 SFX 03b
 emotegreen comendo <ref1.name>
 ref1.decrement
 return <eval <food>+<R15,75>>
endif
return 0


[FUNCTION TOWN_START_SENESCAL]
//Inicia um char senescal
town_RegServant
FINDID.i_cape.color=<town_GetFirstColor>
FINDID.i_sash.color=<town_GetSecondColor>
serv.newitem=i_mry_seneschal
trysrc <uid> new.equip
new.timerd=<DEF.TOWN_TRIG_TIME>


[FUNCTION TOWN_START_GATEKEEPER]
TOWN_REGSERVANT
FINDID.i_sash.color=<town_getFirstColor <tag.town>>


[FUNCTION town_payServants]
//Paga periodicamente os NPCs
if !(<IsTEvent.e_town_Seneschal>) || (<IsEmpty <tag.town_region>>)
 serv.log [TOWN] Erro! <uid> tentando efetuar pagamento. Event? <eval <IsTEvent.e_town_Seneschal>> - Town: <tag.town_region>
 return 0
endif
DB.QUERY=SELECT uid FROM towns_servants WHERE region="<DB.ESCAPEDATA <tag.town_region>>"
local.total=0
local.i=0

//Salva a lista de NPCs
FOR s 0 <eval <DB.ROW.NUMROWS>-1>
 try local.servant_<dlocal.i>=<DB.ROW.<dlocal.s>.uid>
 local.i +=1
END

FOR s 0 <eval <DB.ROW.NUMROWS>-1>
 obj=<local.servant_<dlocal.s>>
 
 //Calcular salário
 if (<obj.ischar>)
  local.servant=<obj.tag0.servant>
  if (<local.servant>!=2)//Não é guarda
   local.ord=<DEF.TOWN_SERVANT_<dlocal.servant>_ORD>
  else //É guarda. Pagar por level.
   local.ord=<DEF.TOWN_GUARD_<obj.dtag.guard_level>_ORD>
  endif
  
  //Dá pra pagar?
  if (<town_getCash <tag.town_region>> >= <local.ord>)
   //Pagar
   serv.newitem=i_coin_copper
   new.amount=<local.ord>
   trysrc <obj> src.bounce <new>
   town_AddCash -<local.ord>
   local.total += <local.ord>
   
   //Comemorar
   dorand 4
    local.cheer=Hum... salario.
    local.cheer=Que bom que recebi.
    local.cheer=Mais dinheiro, mais bebida!
    local.cheer=Pagamento, jah?
   end
   obj.timerf <R15>,say <local.cheer>
  else
   obj.tag.nopay = <obj.tag0.nopay>+1
   obj.timerf <R15> Sem salario dessa vez? Vai mal...
  endif
  
 endif
END

//Emote final
DORAND 4
 local.emote=Pronto. <STRTRESURE <local.total>> de pagamentos. Agora vamos contabilizar o comercio.
 local.emote=Proximo servico? Vejamos...
 local.emote=E agora, aonde deixei os papeis da fazenda?
 local.emote=Foram gastos <dlocal.total> cobres dessa vez.
END

timerf 3 say <local.emote>

serv.area.<tag.town_region>.tag.town_cost=<local.total>


[FUNCTION town_pruneServants]
//Pruna periodicamente os NPCs
if (!<argn>)
 if !(<IsTEvent.e_town_Seneschal>) || (<IsEmpty <tag.town_region>>)
  serv.log [TOWN] Erro! <uid> tentando efetuar prune. Event? <eval <IsTEvent.e_town_Seneschal>> - Town: <tag.town_region>
  return 0
 endif
 DB.QUERY=SELECT uid FROM towns_servants WHERE region="<DB.ESCAPEDATA <tag.town_region>>"
else
 DB.QUERY=SELECT uid FROM towns_servants
endif
local.total=0
local.i=0

//Salva a lista de NPCs
FOR s 0 <eval <DB.ROW.NUMROWS>-1>
 try local.servant_<dlocal.i>=<DB.ROW.<dlocal.s>.uid>
 local.i +=1
END

FOR s 0 <eval <DB.ROW.NUMROWS>-1>
 obj=<local.servant_<dlocal.s>>
 //Silent prune
 if (!<obj.ischar>) && (<local.i> < 20)//Foi limitado a 20 jogadores fantasma por vez para não exceder o tamanho da string. O Sphere não gostou de 757 jogadores sairem da TOWN.
  if (<eval strlen(<local.prunes>)> < 2)
   local.prunes = "UID=<dlocal.servant_<dlocal.s>>"
   local.pruneN=1
  else
   local.prunes .= " OR UID=<dlocal.servant_<dlocal.s>>"
   local.pruneN += 1
  endif
 endif
END

//Prune
if (<local.pruneN>)
 DB.EXECUTE=DELETE FROM towns_servants WHERE <local.prunes>
 if (<argn>)
  return <local.pruneN>
 else
  serv.log [TOWN] Silent Prune removeu <dlocal.pruneN> NPCs inexistentes de <tag.town_region>.
 endif
endif


[FUNCTION town_prunePetitions]
//Remove petições de player não mais existentes
DB.QUERY=SELECT ID, UID, father FROM towns_petitions
local.n = 0
for p 0 <eval <DB.ROW.NUMROWS>-1>
 //Char existe?
 obj=<DB.ROW.<local.p>.uid>
 if (!<obj.IsPlayer>) && (<local.n> < 20)//Foi limitado a 20 jogadores fantasma por vez para não exceder o tamanho da string. O Sphere não gostou de 757 jogadores sairem da TOWN.
  if (<eval STRLEN(<local.q>)> < 3)
   local.q = "WHERE (uid = <eval <DB.ROW.<dlocal.p>.uid>>"
  else
   local.q .= " OR uid = <eval <DB.ROW.<dlocal.p>.uid>>"
  endif
  local.n += 1
 endif
end

if (!<local.n>)
 return 0
endif

DB.EXECUTE=DELETE FROM towns_petitions <local.q>
serv.log [TOWNS] PRUNE :: <dlocal.n> petições e votos retirados do sistema por <topobj.tag.name>-<topobj.name>.
return <local.n>


[FUNCTION town_pruneCitzens]
//Remove jogadores inválidos da town <args>. Se ARGS for vazia, remove geral.
if (<IsEmpty <args>>)
 DB.QUERY=SELECT DISTINCT uid FROM towns_members
else
 DB.QUERY=SELECT DISTINCT uid FROM towns_members WHERE region="<DB.ESCAPEDATA <args>>"
endif
local.i=0
FOR p 0 <eval <DB.ROW.NUMROWS>-1>
 if (!<uid.<db.row.<dlocal.p>.uid>.IsPlayer>) && (<local.i> < 20)//Foi limitado a 20 jogadores fantasma por vez para não exceder o tamanho da string. O Sphere não gostou de 757 jogadores sairem da TOWN.
  if (<eval STRLEN(<local.q>)> < 3)
   local.q = " uid=<eval <db.row.<dlocal.p>.uid>>"
  else
   local.q .= " OR uid=<eval <db.row.<dlocal.p>.uid>>"
  endif
  local.i += 1
 endif
END
if (<local.i>)
 try DB.EXECUTE=DELETE FROM towns_members WHERE<local.q>
 if (<IsEmpty <args>>)
  serv.log [TOWNS] PRUNE :: <dlocal.i> jogadores retirados do sistema por <topobj.tag.name>-<topobj.name>.
 else
  return <local.i>
 endif
endif

[FUNCTION TOWN_ADD_DOOR]
//Adiciona a porta TARGET (argo) a lista de portas do NPC
OBJ=<argv[0]>
say Esta aqui.
if (<argo.type> != t_door) && (<argo.type> != t_door_locked) && (<argo.type> != t_portculis)
 obj.timerf 1,say,Isso nao me parece com uma porta <sex senhor/senhora>.
 return 1
endif
IF (STRMATCH(*<argo.uid>*,<obj.tag.TOWN_DOORS>)
 Obj.timerf 1,say,Esta porta ja esta sendo vigiada.
 return 1
endif
obj.timerf 1,say,Muito bem <sex senhor/senhora>. Guardarei para que seja segura.
obj.tag.TOWN_DOORS .=;<argo.uid>


[FUNCTION TOWN_REM_DOOR]
//Retira uma porta da lista de portas
OBJ=<argv[0]>
say Esta aqui.
if (<argo.type> != t_door) && (<argo.type> != t_door_locked) && (<argo.type> != t_portculis)
 obj.timerf 1,say,Isso nao me parece com uma porta <sex senhor/senhora>.
 return 1
endif
IF !(STRMATCH(*<argo.uid>*,<obj.tag.TOWN_DOORS>)
 Obj.timerf 1,say,Mas eu nao estou vigiando esta porta...
 return 1
endif
obj.timerf 1,say,Certo <sex senhor/senhora>. Nao guardarei mais esta porta.
obj.tag.TOWN_DOORS =<f_RemoveList <argo.uid>,<obj.tag.TOWN_DOORS>>


[FUNCTION TOWN_OPEN_DOOR]
//Abre as portas da lista de portas do NPC porteiro
LOCAL.doors=<EXPLODE ;,<tag.TOWN_DOORS>>
LOCAL.doors=<STRSUB 0 <eval STRLEN(<LOCAL.doors>)> <LOCAL.doors>>
_TOWN_OPEN_DOOR <LOCAL.doors>

[FUNCTION _TOWN_OPEN_DOOR]
for i 0 <eval <argv>-1>
 IF (<UID.<argv[<LOCAL.i>]>.type> != t_portculis)
  if (!<UID.<argv[<LOCAL.i>]>.tag0.open>)
   UID.<argv[<LOCAL.i>]>.timerd=1
   UID.<argv[<LOCAL.i>]>.tag.open=1
   UID.<argv[<LOCAL.i>]>.timerf 1,timer,-1
  endif
 ELSE
  IF (!<UID.<argv[<LOCAL.i>]>.moreX>) && (!<UID.<argv[<LOCAL.i>]>.morey>)
   //Esta fechada E nao esta em uso
   UID.<argv[<LOCAL.i>]>.f_portculis
  ENDIF
 ENDIF
endfor


[FUNCTION TOWN_RELEASE_DOORS]
//Abre as portas da lista de portas do NPC porteiro para sempre.
LOCAL.doors=<EXPLODE ;,<tag.TOWN_DOORS>>
LOCAL.doors=<STRSUB 0 <eval STRLEN(<LOCAL.doors>)> <LOCAL.doors>>
_TOWN_RELEASE_DOOR <LOCAL.doors>


[FUNCTION _TOWN_RELEASE_DOOR]
//Tira uma porta do sistema
for i 0 <eval <argv>-1>
 IF (<UID.<argv[<LOCAL.i>]>.type> != t_portculis)
  UID.<argv[<LOCAL.i>]>.type=t_door
 ELSE
  IF (!<UID.<argv[<LOCAL.i>]>.moreX>) && (!<UID.<argv[<LOCAL.i>]>.morey>)
   //Esta fechada E nao esta em uso
   UID.<argv[<LOCAL.i>]>.f_portculis
  ENDIF
  UID.<argv[<LOCAL.i>]>.timerf 10,type,t_quebrado
 ENDIF
endfor


[FUNCTION TOWN_CLOSE_DOOR]
//Fecha as portas da lista de portas do NPC porteiro
LOCAL.doors=<EXPLODE ;,<tag.TOWN_DOORS>>
LOCAL.doors=<STRSUB 0 <eval STRLEN(<LOCAL.doors>)> <LOCAL.doors>>
_TOWN_CLOSE_DOOR <LOCAL.doors>

[FUNCTION _TOWN_CLOSE_DOOR]
for i 0 <eval <argv>-1>
 IF (<UID.<argv[<LOCAL.i>]>.type> != t_portculis)
  if (<UID.<argv[<LOCAL.i>]>.tag0.open>)
   UID.<argv[<LOCAL.i>]>.timerd=1
   UID.<argv[<LOCAL.i>]>.tag.open=
  endif
 ELSE
  IF (<UID.<argv[<LOCAL.i>]>.moreX>) && (!<UID.<argv[<LOCAL.i>]>.morey>)
   //Esta aberta E nao esta em uso
   UID.<argv[<LOCAL.i>]>.f_portculis
  ENDIF
 ENDIF
endfor




//########################################################################
//########################################################################
//                              DIALOGS
//########################################################################
//########################################################################

//Dividido em funções de construção, funções de botão e o Dialog

//########################################################################
//      Funções para os Botões de Okay
//########################################################################

[FUNCTION d_town_okay_found]
//OKAY da página TOWN_PAGE_FOUND. ARGN é o tipo da town.
if (<ARGCHK[1]>)
 town_AddTown <ctag.town_region>,1,<ctag.town_region>
 town
else
 town_selectCapital <eval <ARGCHK[1]>+(<ARGCHK[2]>*2)+(<ARGCHK[3]>*3)>
endif


[FUNCTION d_town_okay_races]
//OKAY da página TOWN_PAGE_RACES
local.races=
for r 1 5
 if (<ARGCHK[<dlocal.r>]>)
  DOSWITCH <dlocal.r>
   local.null
   local.races .= Anao,
   local.races .= Elfo,
   local.races .= Drow,
   local.races .= Humano,
   local.races .= Orc,
  ENDDO
 endif
end

local.races=<STRSUB 1 <eval STRLEN(<local.races>)-2> <local.races>>
DB.EXECUTE=UPDATE towns SET FriendRaces="<DB.ESCAPEDATA <local.races>>" WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
town


[FUNCTION d_town_okay_laws]
//OKAY da página TOWN_PAGE_PERMISSIONS
if (<town_getPrivs <ctag.town_region>>&<DEF.TOWN_PRIV_MEMBER>)
 local.flags=0
 local.r=0
 WHILE (!<IsEmpty <DEF.TOWN_PERMISSION_<dlocal.r>>>)
  local.flags |= <eval (2@<local.r>)*<ARGCHK[<dlocal.r>]>>
  local.r += 1
 END
 DB.EXECUTE=UPDATE towns SET permissions=<dlocal.flags> WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
 serv.area.<ctag.town_region>.tag.permissions=<local.flags>
endif
ctag.town_page=<DEF.TOWN_PAGE_REPORT>
SDIALOG d_town


[FUNCTION d_town_okay_privs]
//OKAY da página TOWN_PAGE_PRIVS
IF (<town_canPriv <ctag.town_position>>)
 local.flags=0
 local.r=0
 WHILE (!<IsEmpty <DEF.TOWN_PRIV_<dlocal.r>>>)
  local.flags |= <eval (2@<local.r>)*<ARGCHK[<dlocal.r>]>>
  local.r += 1
 END
 IF (<ctag.town_positionID>)
  DB.EXECUTE=UPDATE towns_privs SET privs=<dlocal.flags> WHERE id=<dctag.town_positionID>
 ELSE
  ctag.town_privs=<local.flags>
 ENDIF
ENDIF
ctag.town_page=<DEF.TOWN_PAGE_POSITIONS>
SDIALOG d_town


//########################################################################
//      Funções para contrução dos gumps
//########################################################################

[FUNCTION town_buildReport]
        //CALL para contruir html e valores de TOWN_PAGE_REPORT
        DB.QUERY=SELECT * FROM towns WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
        local.html=<DEF.BFONT_LPURPLE>    <serv.area.<ctag.town_region>.name><DEF.BFONT_white> eh<addspace>
        DOSWITCH <DB.ROW.0.type>
         local.html .= UM TREMENDO BUG (reportar no forum)
         local.html .= uma regiao soberana
         local.html .= uma sub-regiao de <serv.area.<DB.ROW.0.capital>.name>
         local.html .= uma regiao sob a administracao de <serv.area.<DB.ROW.0.capital>.name>
        ENDDO
        local.html .= <addspace>cujo responsavel eh <DEF.BFONT_LPURPLE><uid.<DB.ROW.0.master>.tag.name><DEF.BFONT_white>
        if (<DB.ROW.0.artefactUID>)
         local.html .= ", o detentor de <DEF.BFONT_LPURPLE><UID.<DB.ROW.0.artefactUID>.name><DEF.BFONT_white>"
        endif
        local.html .= ".<DEF.BR>"
        local.html .= - Tesouro: <strtresure <DB.row.0.tresure>><DEF.BR>
        local.html .= - Gasto periodico: <strtresure <serv.area.<ctag.town_region>.tag0.town_cost>> a cada <f_timestring <eval <DEF.TOWN_TRIG_TIME>/10>>
        local.races=<DB.ROW.0.FriendRaces>


[FUNCTION d_town_buildMemberHtml]
        local.html=<DEF.BFONT_GREEN><uid.<ctag.town_uid>.tag.name><DEF.BFONT_WHITE>, <ctag.town_title> de <serv.area.<ctag.town_region>.name>.
        local.html .= "<DEF.BR>Posicao hierarquica: <dctag.town_position><DEF.BFONT_LPURPLE>"
        if (!<ctag0.town_member_flags>)
         local.html .= "<DEF.BR> - Em dia com o governo"
        else
         local.f=0
         WHILE (!<IsEmpty <DEF.TOWN_FLAG_<dlocal.f>>>)
          if (<ctag0.town_member_flags>&<eval 2@<dlocal.f>>)
           local.html .= "<DEF.BR> - <DEF.TOWN_FLAG_<dlocal.f>>"
          endif
          local.f += 1
         END
        endif
        local.html .= <DEF.BR><DEF.BFONT_WHITE>Privilegios:<DEF.BFONT_LGREEN>
        if (!<ctag0.town_privs>)
         local.html .= "<DEF.BR> - Sem privilegios"
        else
         local.p=0
         WHILE (!<IsEmpty <DEF.TOWN_PRIV_<dlocal.p>>>)
          if (<ctag0.town_privs>&<eval 2@<dlocal.p>>)
           local.html .= "<DEF.BR> - <DEF.TOWN_PRIV_<dlocal.p>>"
          endif
          local.p += 1
         END
        endif
        if (<IsGM>)
         local.html .="<DEF.BR><DEF.BFONT_LCYAN>[Conta: <UID.<ctag.town_UID>.account>]<DEF.BR>"
         local.html .="[Ultima conexao: <serv.account.<UID.<ctag.town_UID>.account>.LASTCONNECTDATE>]"
        endif
        

[FUNCTION d_town_buildCriminalHtml]
        local.html=<DEF.BFONT_GREEN><uid.<ctag.town_uid>.tag.name><DEF.BFONT_WHITE> eh um criminoso em <serv.area.<ctag.town_region>.name>.<DEF.BFONT_LPURPLE>
        if (!<ctag0.town_member_flags>)
         local.html .= "<DEF.BR> - Em dia com o governo"
        else
         local.f=0
         WHILE (!<IsEmpty <DEF.TOWN_FLAG_<dlocal.f>>>)
          if (<ctag0.town_member_flags>&<eval 2@<dlocal.f>>)
           local.html .= "<DEF.BR> - <DEF.TOWN_FLAG_<dlocal.f>>"
          endif
          local.f += 1
         END
         if (<ctag0.town_member_flags>&<DEF.TOWN_FLAG_JAILED>)
          ref1=<UID.<ctag.town_uid>.town_GetJailMemory <ctag.town_region>>
          local.html .= <DEF.BR><DEF.BFONT_WHITE>Tempo para ser solto: <f_timestring <ref1.timer>><DEF.BR>
          if (<UID.<ctag.town_uid>.distance <ref1.morep>> > 20)
           local.html .= <DEF.BFONT_GREEN><DEF.BR> - Fugitivo!
          endif
         endif
        endif


[FUNCTION d_town_buildFriendHtml]
        ref1=<ctag0.town_uid>
        local.html=<DEF.BFONT_GREEN><ref1.tag.name>, <ref1.sex o/a> <ref1.tag.raca> <DEF.BFONT_WHITE> eh um extrangeiro com permissao de permanecer em <serv.area.<ctag.town_region>.name>.<DEF.BFONT_LPURPLE>
        if (!<ctag0.town_member_flags>)
         local.html .= "<DEF.BR> - Em dia com o governo"
        else
         local.f=0
         WHILE (!<IsEmpty <DEF.TOWN_FLAG_<dlocal.f>>>)
          if (<ctag0.town_member_flags>&<eval 2@<dlocal.f>>)
           local.html .= "<DEF.BR> - <DEF.TOWN_FLAG_<dlocal.f>>"
          endif
          local.f += 1
         END
        endif
        
[FUNCTION d_town_buildPetition]
        DB.QUERY=SELECT * FROM towns_petitions WHERE id=<dctag.town_petitionID>
        ref1=<DB.ROW.0.UID>
        ctag.town_petitionTitle=<DB.ROW.0.title>
        ctag.town_petitionFlags=<DB.ROW.0.flags>
        local.html=<DEF.BFONT_DCYAN>Peticao n. <eval <DB.ROW.0.id>> de <DB.ESCAPEDATA <DB.ROW.0.gtime>> por <ref1.tag.name><DEF.BR><DEF.BFONT_WHITE>
        local.html .=<DB.ROW.0.text>
        DB.QUERY=SELECT count(id) AS yes FROM towns_petitions WHERE father=<dctag.town_petitionID> AND type=1 AND flags&<eval <DEF.TOWN_PET_FLAG_FAVOR>>
        local.favor=<DB.ROW.0.yes>
        DB.QUERY=SELECT count(id) AS no FROM towns_petitions WHERE father=<dctag.town_petitionID> AND type=1 AND flags&<eval <DEF.TOWN_PET_FLAG_AGAINST>>
        local.against=<DB.ROW.0.no>
        DB.QUERY=SELECT count(id) AS can FROM towns_petitions WHERE father=<dctag.town_petitionID> AND UID=<EVAL <UID>>
        local.canvote=<eval !<DB.ROW.0.can>>
        
        
[FUNCTION d_town_buildLaw]
        DB.QUERY=SELECT * FROM towns_laws WHERE id=<dctag.town_lawID>
        ctag.town_lawTitle=<DB.ROW.0.title>
        ctag.town_lawFlags=<DB.ROW.0.flags>
        local.html=<DEF.BFONT_DCYAN>Decreto n. <eval <DB.ROW.0.id>> de <DB.ESCAPEDATA <DB.ROW.0.gtime>> por <DB.ROW.0.sign><DEF.BR><DEF.BFONT_WHITE>
        local.html .=<DB.ROW.0.text>
        
[FUNCTION d_town_buildNPCHtml]
        DB.QUERY=SELECT type FROM towns_servants WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
        FOR n 0 <eval <DB.ROW.NUMROWS>-1>
         try local.npc_<eval <DB.ROW.<dlocal.n>.type>> = 0<local.npc_<eval <DB.ROW.<dlocal.n>.type>>>+1
        END
        local.t = <serv.area.<ctag.town_region>.name> tem no momento <eval <DB.ROW.NUMROWS>> servidores, gerando um gasto de <strtresure <serv.area.<ctag.town_region>.tag0.town_cost>> a cada <f_timestring <eval <DEF.TOWN_TRIG_TIME>/10>><DEF.BR>
        local.s=1
        WHILE (!<IsEmpty <DEF.TOWN_SERVANT_<dlocal.s>>>)
         local.t .= " - <DEF.TOWN_SERVANT_<dlocal.s>>: <dlocal.npc_<dlocal.s>><DEF.BR>"
         local.s += 1
        END
        return <local.t>
        
[FUNCTION d_town_buildServantHtml]
        obj=<ctag.town_UID>
        DOSWITCH <obj.tag0.servant>
         local.position=bug feio (reporte)
         local.position=porteiro
         local.position=guarda
         local.position=senescal
        END
        if (<obj.tag0.servant>==2)
         local.position .= " cuja patente eh de "
         DOSWITCH <obj.tag0.guard_level>
          local.position .= aspirante
          local.position .= soldado
          local.position .= cabo
          local.position .= tenente
          local.position .= capitao
          local.position .= major
         END
         local.ord=<STRTRESURE <DEF.TOWN_GUARD_<obj.dtag0.guard_level>_ORD>>
         local.guard=1
        ELSE
         local.ord=<STRTRESURE <DEF.TOWN_SERVANT_<obj.dtag0.SERVANT>_ORD>>
         local.guard=0
        ENDIF
        local.html=<DEF.BFONT_LBLUE><obj.name><DEF.BFONT_WHITE> eh um <local.position> de <serv.area.<obj.tag.town_region>.name><DEF.BR>
        local.html .= Seu salario eh de <local.ord><DEF.BR>
        local.html .= Este funcionario esta contratado ha <f_timestring <eval <obj.f_age>*6>><DEF.BR>
        local.html .= Atualmente ele se encontra em <obj.region.name> e sua posicao atual eh <QVAL <IsGM>?<obj.p>:<obj.sextantp>>.
        return <local.html>
        
[FUNCTION d_town_buildPrisionHtml]
        local.fee=<serv.area.<ctag.town_region>.tag0.prisionFee>
        local.time=<f_timestring <serv.area.<ctag.town_region>.tag0.prisionTime>>
        local.prisions=<serv.area.<ctag.town_region>.tag0.prisions>
        local.cells=0
        local.prisioners=<town_getPrisioners <ctag.town_region>>
        for c 0 4
         if (!<IsEmpty <serv.area.<ctag.town_region>.tag.cell_<dlocal.c>>>)
          local.cells += 1
         endif
        end
        if (!<serv.area.<ctag.town_region>.tag0.prisionTime>)
         local.html = O sistema de prisao esta desabilitado. Os delinquentes sao atacados e banidos de <serv.area.<ctag.town_region>.name>.
        else
         local.html = "Celas ativas: <dlocal.cells> <DEF.BR>Tempo minimo de cativeiro: <local.time> <DEF.BR>Fianca minima: "
         if <serv.area.<ctag.town_region>.tag0.prisionFee>
          local.html .= "<STRTRESURE <local.fee>> "
         else
          local.html .= "Inafiancavel "
         endif
         local.html .= "<DEF.BR>Total de prisoes feitas: <dlocal.prisions><DEF.BR>Total de prisioneiros: <dlocal.prisioners>"
        endif
        
[FUNCTION d_town_solve]
if (<ctag.town_page>==<DEF.TOWN_PAGE_FOUND>)
        resizepic 92 89 2620 416 82
        dhtmlgump 98 97 403 65 0 0 O que eh?
        resizepic 92 182 2620 416 189
        Group 0
        radio 105 200 210 211 1 1
        dtext 130 200 152 Estado maior
        dtext 130 214 62 Pode ter sub-regioes as quais o mesmo mestre controla
        dtext 130 228 62 e acumulara todas as suas riquezas.
        radio 105 250 210 211 0 2
        dtext 130 250 152 Sub-regiao
        dtext 130 264 62 Deve pertencer a um estado maior e herdara todas as leis e
        dtext 130 278 62 e caracteristicas do estado maior, incluindo hierarquia.
        radio 105 300 210 211 0 3
        dtext 130 300 152 Regiao sob administracao
        dtext 130 314 62 Nao tem administracao propria e depende totalmente de um
        dtext 130 328 62 estado maior ou sub-regiao, como uma extencao de
        dtext 130 342 62 territorio. (minas, florestas, vilarejos)
        button 445 385 247 248 1 0 1
        button 90 385 241 243 1 0 0

elif (<ctag.town_page>==<DEF.TOWN_PAGE_REPORT>)
        local.permissions=<town_getPermissions <ctag.town_region>>
        call town_buildReport
        resizepic 92 102 2620 416 132
        resizepic 92 241 2620 416 82
        resizepic 92 332 2620 416 82
        dhtmlgump 98 110 403 115 0 1 <addspace>   <local.html>
        if (<town_canMember 0>)
          button 101 251 30008 30009 1 0 2
        endif
        button 101 267 30008 30009 1 0 3
        button 101 283 30008 30009 1 0 4
        button 101 299 30008 30009 1 0 5
        dtext 120 248 66 Racas amigas:
        dtext 256 248 4 <local.races>
        dtext 120 264 66 Extrangeiros amigos:
        dtext 256 264 4 <town_getFriends>
        dtext 120 280 66 Inimigos publicos:
        dtext 256 280 4 <town_getEnemies <ctag.town_region>>
        dtext 120 296 66 Cidadaos:
        dtext 256 296 4 <town_getMembers>
        if (<town_canMember 0>)
         button 291 267 2224 2224 1 0 52
         dtext 310 264 993 Adicionar extrangeiro amigo
         button 291 283 2224 2224 1 0 53
         dtext 310 280 993 Adicionar como criminoso
         button 291 299 2224 2224 1 0 54        
         dtext 310 296 993 Convidar a ser cidadao         
        endif
        button 101 342 30008 30009 1 0 6
        button 101 358 30008 30009 1 0 7
        button 101 374 30008 30009 1 0 8
        button 101 390 30008 30009 1 0 9
        dtext 119 340 66 Administracao
        dtext 119 355 66 Leis
        dtext 119 371 66 Decretos
        dtext 119 387 66 Impostos
        button 319 342 30008 30009 1 0 10
        //button 319 358 30008 30009 1 0 11
        button 319 358 30008 30009 1 0 12
        dtext 337 340 66 Funcionarios:
        dtext 425 340 4 <town_NPCs <ctag.town_region>>
        //dtext 337 356 66 Plebicitos:
        //dtext 425 356 4 (n_polls)
        dtext 337 356 66 Peticoes:
        dtext 425 356 4 <town_getPetitions <ctag.town_region>>
        if (<local.permissions>&<DEF.TOWN_CAN_AZILUM>) && (!<town_IsMember <ctag.town_region>>) && (STRMATCH(*<tag.raca>*,<town_getRaces <ctag.town_region>>))
         dtext 337 387 66 Pedir direito de cidadania
         button 319 390 30008 30009 1 0 13
        endif
        if !(STRMATCH(<ctag.town_region>,<town_getCapital <ctag.town_region>>))
         button 100 85 30008 30009 1 0 55
         dtext 120 82 66 Visualizar capital
        endif
        if (<town_getDemences <ctag.town_region>>)
         button 373 84 30008 30009 1 0 56
         dtext 393 82 66 Listar sub-regioes
        endif
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_FRIENDS>)
        DB.QUERY=SELECT flags FROM towns_members WHERE UID=<dctag.town_UID> AND region="<DB.ESCAPEDATA <ctag.town_region>>"
        ctag.town_member_flags=<DB.ROW.0.flags>
        local.priv=<town_canMember 0>
        resizepic 92 87 2620 416 147
        call d_town_buildFriendHtml
        dhtmlgump 98 95 403 130 0 1 <local.html>
        dorigin 101 221
        IF (<local.priv>) && (<ctag0.town_position>!=1)
         button - *20 30008 30009 1 0 47
         dtext +19 -1 66 <QVAL <ctag.town_member_flags>&<DEF.TOWN_FLAG_CRIMINAL>?Retirar da lista de inimigos publicos:Adicionar a lista de inimigos publicos>
         button - *20 30008 30009 1 0 48
         dtext +19 -1 66 <QVAL <ctag.town_member_flags>&<DEF.TOWN_FLAG_BANNED>?Retirar da lista de inimigos publicos banidos:Adicionar a lista de inimigos publicos banidos>
         button - *20 30008 30009 1 0 49
         dtext +19 -1 66 <QVAL <ctag.town_member_flags>&<DEF.TOWN_FLAG_FRIEND>?Retirar da lista de amigos:Adicionar a lista de amigos>
        ENDIF
        button 92 385 4014 4015 1 0 1

elif (<ctag.town_page>==<DEF.TOWN_PAGE_TAXES>)
        call town_loadTaxes
        resizepic 92 89 2620 416 282
        dorigin 153 75
        local.priv=<eval <town_getPrivs>&<DEF.TOWN_PRIV_TAXES>>
        dtext +127 *30 53 Impostos
        //--
        dtext - *30 66 Imposto comercial:
        gumppic +119 -2 2443
        if (<local.priv>)
         dtextentry +126 - 48 20 0 1 <dlocal.commercialfee>
        else
         dtext +126 - 0 <dlocal.commercialfee>
        endif
        dtext +185 - 66 %
        //--
        dtext +52 *30 66 Laudemio:
        gumppic +119 -2 2443
        if (<local.priv>)
         dtextentry +126 - 48 20 0 2 <dlocal.buildingfee>
        else
         dtext +126 - 0 <dlocal.buildingfee>
        endif
        dtext +185 - 66 %
        //--
        dtext +13 *30 66 Tile de terreno:
        gumppic +119 -2 2443
        if (<local.priv>)
         dtextentry +126 - 48 20 0 3 <dlocal.tilelandprice>
        else
         dtext +126 - 0 <dlocal.tilelandprice>
        endif
        dtext +185 - 66 mc
        //--
        dtext -5 *30 66 Tile de construcao:
        gumppic +119 -2 2443
        if (<local.priv>)
         dtextentry +126 - 48 20 0 4 <dlocal.tilebuildprice>
        else
         dtext +126 - 0 <dlocal.tilebuildprice>
        endif
        dtext +185 - 66 mc
        //--
        dtext -22 *30 66 Tile de porta/telepad:
        gumppic +119 -2 2443
        if (<local.priv>)
         dtextentry +126 - 48 20 0 5 <dlocal.tilefixtureprice>
        else
         dtext +126 - 0 <dlocal.tilefixtureprice>
        endif
        dtext +185 - 66 mc
        if (<local.priv>)
         dtext +20 *40 66 Aplicar modificacoes
         button - +3 30008 30009 1 0 57
        endif
        //--
        
        button 92 385 4014 4015 1 0 1

elif (<ctag.town_page>==<DEF.TOWN_PAGE_BANS>)
        DB.QUERY=SELECT flags FROM towns_members WHERE UID=<dctag.town_UID> AND region="<DB.ESCAPEDATA <ctag.town_region>>"
        ctag.town_member_flags=<DB.ROW.0.flags>
        local.priv=<town_canMember <uid.<ctag.town_UID>.town_GetPosition <ctag.town_region>>>
        resizepic 92 87 2620 416 147
        call d_town_buildCriminalHtml
        dhtmlgump 98 95 403 130 0 1 <local.html>
        dorigin 101 221
        IF (<local.priv>) && (<ctag0.town_position>!=1)
         button - *20 30008 30009 1 0 47
         dtext +19 -1 66 <QVAL <ctag0.town_member_flags>&<DEF.TOWN_FLAG_CRIMINAL>?Retirar da lista de inimigos publicos:Adicionar a lista de inimigos publicos>
         button - *20 30008 30009 1 0 48
         dtext +19 -1 66 <QVAL <ctag0.town_member_flags>&<DEF.TOWN_FLAG_BANNED>?Retirar da lista de inimigos publicos banidos:Adicionar a lista de inimigos publicos banidos>
         button - *20 30008 30009 1 0 51
         dtext +19 -1 66 <QVAL <ctag0.town_member_flags>&<DEF.TOWN_FLAG_DEBT>?Retirar da lista de inadimplentes:Adicionar a lista de inadimplentes>
         if (<src.town_CheckPrivs <DEF.TOWN_PRIV_LAWS>>) && (<ctag0.town_member_flags>&<DEF.TOWN_FLAG_JAILED>)
          button - *20 30008 30009 1 0 107
          dtext +19 -1 66 Libertar da prisao
          button - *20 30008 30009 1 0 108
          dtext +19 -1 66 Ajustar pena          
         endif
        ENDIF
        button 92 385 4014 4015 1 0 1

elif (<ctag.town_page>==<DEF.TOWN_PAGE_CONFIG>)
        resizepic 92 89 2620 416 282
        dorigin 107 80
        dtext +18 *20 5 Cor primaria:
        gumppic +118 +1 1896 <eval <town_getFirstColor <ctag.town_region>>>
        IF (<src.town_CheckPrivs <DEF.TOWN_PRIV_LAWS>>)
         button - +3 30008 30009 1 0 31
        endif
        dtext +18 *20 5 Cor secundaria:
        gumppic +118 +1 1896 <eval <town_getSecondColor <ctag.town_region>>>
        IF (<src.town_CheckPrivs <DEF.TOWN_PRIV_LAWS>>)
         button - +3 30008 30009 1 0 32
        endif
        dtext +18 *20 5 Cargos existentes: (<town_getPositions>)
        button - +3 30008 30009 1 0 39
        //dtext +18 *20 5 Diplomacia
        //button - +3 30008 30009 1 0 34
        IF (<src.town_getMaster <ctag.town_region>>==<UID>) || (<src.IsGM>)
         dtext +18 *20 45 Transferir titularidade
         button - +3 30008 30009 1 0 33
         dtext +18 *20 45 <QVAL <town_getArtefact <ctag.town_region>>?Transferir artefato:Criar artefato>
         button - +3 30008 30009 1 0 35
        ENDIF
        IF (<src.town_CheckPrivs <DEF.TOWN_PRIV_TRESURE>>)
         dtext +18 *20 45 Transferir cofre
         button - +3 30008 30009 1 0 36
        ENDIF
        IF (<src.town_CheckPrivs <DEF.TOWN_PRIV_DIPLOMACY>>)
         if (<town_getType <ctag.town_region>> > 1)
          dtext +18 *20 45 Declarar independencia
          button - +3 30008 30009 1 0 38
         else
          dtext +18 *20 45 Tornar <serv.area.<ctag.town_region>.name> subordinada a outra regiao.
          button - +3 30008 30009 1 0 37
         endif
        ENDIF
        dtext +18 *20 5 Prisao
        button - +3 30008 30009 1 0 100
        IF (<src.town_CheckPrivs <DEF.TOWN_PRIV_LAWS>>)
         dtext +18 *20 45 Mudar ponto de soltura/banimento (<QVAL <IsGM>?<serv.area.<ctag.town_region>.tag0.town_releasePoint>:<SEXTANTP <serv.area.<ctag.town_region>.tag0.town_releasePoint>>>)
         button - +3 30008 30009 1 0 109
        else
         dtext +18 *20 5 Ponto de soltura/banimento (<SEXTANTP <serv.area.<ctag.town_region>.tag0.town_releasePoint>>)
        endif
        IF (<src.town_CheckPrivs <DEF.TOWN_PRIV_BOOKS>>)
         dtext +18 *20 45 Prender/soltar um livro
         button - +3 30008 30009 1 0 58
        endif
        button 92 385 4014 4015 1 0 1
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_RACES>)
        DB.QUERY=SELECT FriendRaces FROM towns WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
        local.anao=<qval STRMATCH(*Anao*,<DB.ROW.0.FriendRaces>)?1:0>
        local.elfo=<qval STRMATCH(*Elfo*,<DB.ROW.0.FriendRaces>)?1:0>
        local.drow=<qval STRMATCH(*Drow*,<DB.ROW.0.FriendRaces>)?1:0>
        local.humano=<qval STRMATCH(*humano*,<DB.ROW.0.FriendRaces>)?1:0>
        local.orc=<qval STRMATCH(*orc*,<DB.ROW.0.FriendRaces>)?1:0>
        resizepic 110 128 9200 178 224
        resizepic 304 128 9200 178 224
        dtext 156 136 62 Racas amigas
        dtext 348 136 32 Racas inimigas
        dtext 152 168 92 Anoes
        dtext 344 168 92 Anoes
        dtext 152 200 92 Elfos
        dtext 344 200 92 Elfos
        dtext 152 232 92 Drows
        dtext 344 232 92 Drows
        dtext 152 264 92 Humanos
        dtext 344 264 92 Humanos
        dtext 152 296 92 Orcs
        dtext 344 296 92 Orcs
        
        //Anões
        Group 1
        radio 128 168 208 209 <local.anao> 1
        radio 320 168 208 209 <eval !<local.anao>> 0
        
        //Elfos
        Group 2
        radio 128 200 208 209 <local.elfo> 2
        radio 320 200 208 209 <eval !<local.elfo>> 0
        
        //Drows
        Group 3
        radio 128 232 208 209 <local.drow> 3
        radio 320 232 208 209 <eval !<local.drow>> 0
        
        //Humanos
        Group 4
        radio 128 264 208 209 <local.humano> 4
        radio 320 264 208 209 <eval !<local.humano>> 0
        
        //Orcs
        Group 5
        radio 128 296 208 209 <local.orc> 5
        radio 320 296 208 209 <eval !<local.orc>> 0
        
        button 96 384 241 242 1 0 0
        button 440 384 247 248 1 0 1
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_PERMISSIONS>)
        DB.QUERY=SELECT permissions FROM towns WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
        local.permissions=<DB.row.0.permissions>
        local.priv=<eval <town_getPrivs <ctag.town_region>>&<DEF.TOWN_PRIV_MEMBER>>
        resizepic 92 89 2620 416 289
        if (<local.priv>)
         dtext 105 100 036 O que deve ser permitido?
        else
         dtext 105 100 036 So eh permitido em <serv.area.<ctag.town_region>.name> as atividades assinaladas:
        endif
        dorigin 105 100
        local.r=0
        local.c=11
        WHILE (!<IsEmpty <DEF.TOWN_PERMISSION_<dlocal.r>>>)
         if (<local.r> > <local.c>)
          dorigin *200 100
          local.c += 12
         endif
         if (<local.priv>)
          checkbox - *20 210 211 <eval <local.permissions>&<eval 2@<local.r>>> <dlocal.r>
         else
          gumppic - *20 <QVAL <eval <local.permissions>&<eval 2@<local.r>>>?211:210>
         endif
         dtext +22 +1 2100 <DEF.TOWN_PERMISSION_<dlocal.r>>
         local.r += 1
        END
        //button 96 384 241 242 1 0 0
        button 440 384 247 248 1 0 1

elif (<ctag.town_page>==<DEF.TOWN_PAGE_POSITIONS>)
        if (<ctag0.town_positionID>)
         DB.QUERY=SELECT title,description,position FROM towns_privs WHERE id=<dctag.town_positionID>
         ctag.town_title=<DB.ROW.0.title>
         ctag.town_position=<DB.ROW.0.position>
         ctag.town_description=<DB.ROW.0.description>
        else
         ctag.town_title=<QVAL <IsEmpty <ctag.town_title>>?Novo cargo:<ctag.town_title>>
         ctag.town_position=<QVAL <IsEmpty <ctag.town_position>>?998:<ctag.town_position>>
         ctag.town_description=<QVAL <IsEmpty <ctag.town_description>>?Modifique Titulo, Descricao e Posicao para criar um novo cargo.:<ctag.town_description>>
        endif
        local.priv=<town_canPriv <ctag.town_position>>
        resizepic 92 87 2620 416 147
        dhtmlgump 98 95 403 130 0 1 <DEF.BFONT_GREEN><ctag.town_title><DEF.BFONT_WHITE> (posicao <dctag.town_position>)<DEF.BR><ctag.town_description>
        dorigin 101 221
        IF (<local.priv>)
         button - *20 30008 30009 1 0 40
         dtext +19 -1 66 Trocar titulo
         button - *20 30008 30009 1 0 41
         dtext +19 -1 66 Trocar descricao
        ENDIF
        IF (<ctag.town_position>!=1)
         button - *20 30008 30009 1 0 42
         dtext +19 -1 66 Ver privilegios
         if (<ctag0.town_positionID>)
          button - *20 30008 30009 1 0 46
          dtext +19 -1 66 Listar cidadaos com cargo de <ctag.town_title>
         endif
        ENDIF
        IF (<local.priv>)
         if (<ctag.town_position>!=1) && (<ctag.town_position>!=999) && (<ctag.town_positionID>)
          button - *20 30008 30009 1 0 43
          dtext +19 -1 66 Apagar
         endif
         if (<ctag0.town_position>!=1) && (<ctag0.town_position>!=999)
          button - *20 30008 30009 1 0 44
          dtext +19 -1 66 Trocar posicao (<dctag.town_position>)
         endif
         if (!<ctag.town_positionID>)
          button - *20 30008 30009 1 0 45
          dtext +19 -1 4 Salvar novo cargo
         endif
        ENDIF
        button 92 385 4014 4015 1 0 1
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_PRIVS>)
        local.priv=<town_canPriv <ctag.town_position>>
        IF (<ctag.town_positionID>)
         DB.QUERY=SELECT privs FROM towns_privs WHERE id=<dctag.town_positionID>
         local.privs=<DB.row.0.privs>
        ELSE
         local.privs=<ctag0.town_privs>
        ENDIF
        resizepic 92 89 2620 416 289
        dtext 105 100 036 Privilegios para <ctag.town_title> de <serv.area.<ctag.town_region>.name>:
        dorigin 105 100
        local.r=0
        local.c=11
        WHILE (!<IsEmpty <DEF.TOWN_PRIV_<dlocal.r>>>)
         if (<local.r> > <local.c>)
          dorigin *200 100
          local.c += 12
         endif
         if (<local.priv>)
          checkbox - *20 210 211 <eval <local.privs>&<eval 2@<local.r>>> <dlocal.r>
         else
          gumppic - *20 <QVAL <local.privs>&<eval 2@<local.r>>?211:210>
         endif
         dtext +22 +1 2100 <DEF.TOWN_PRIV_<dlocal.r>>
         local.r += 1
        END
        //button 96 384 241 242 1 0 0
        button 92 385 4014 4015 1 0 1
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_POSITION_DESC>)
        resizepic 92 87 2620 416 147
        dtextentry 98 95 403 130 2100 1 <ctag.town_description>
        dorigin 101 221
        button 101 240 30008 30009 1 0 41
        dtext 120 239 66 Trocar descricao
        button 92 385 4014 4015 1 0 1
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_LAWS>)
        resizepic 92 89 2620 416 282
        dorigin 230 65
        dtext +18 *60 66 Ver os decretos
        button - +3 30008 30009 1 0 70
        dtext +18 *60 66 Promulgar novo decreto
        button - +3 30008 30009 1 0 71
        dtext +18 *60 66 Editar um decreto
        button - +3 30008 30009 1 0 72
        dtext +18 *60 66 Revogar um decreto
        button - +3 30008 30009 1 0 73
        
        IF (!<ctag0.town_noFullMenu>)
         button 92 385 4014 4015 1 0 1
        endif


elif (<ctag.town_page>==<DEF.TOWN_PAGE_LAWS_EDIT>)
        resizepic 92 117 2620 416 147
        dtext 100 97 45 Decreto:
        if (!<ctag0.town_lawID>)    //Criando
         dtext 160 97 66 <ctag.town_lawTitle>
         dtextentry 98 125 403 130 2100 1 <ctag.town_lawText>
         dorigin 100 250
         dtext - *20 66 Descreva com precisao e pompa o seu decreto.
         dtext - *20 66 Promulgar este decreto
         button -20 +3 30008 30009 1 0 74
        else    //Vendo
         dtext 160 97 66 <dctag0.town_lawID> - <ctag.town_lawTitle>
         dtextentry 98 125 403 130 2100 1 <ctag.town_lawText>
         dorigin 100 250
         dtext - *20 66 Descreva com precisao e pompa o seu decreto.
         dtext - *20 66 Modificar o titulo deste decreto
         button -20 +3 30008 30009 1 0 76
         dtext - *20 66 Promulgar a edicao deste decreto
         button -20 +3 30008 30009 1 0 75
        endif
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_MEMBER>)
        DB.QUERY=SELECT t1.id,t1.title,t1.position,t1.privs,t2.flags FROM towns_privs as t1, towns_members as t2  WHERE t2.UID=<dctag.town_UID> AND t1.id=t2.priv_id AND t1.region=t2.region AND t1.region="<DB.ESCAPEDATA <ctag.town_region>>"
        ctag.town_title=<DB.ROW.0.title>
        ctag.town_positionID=<DB.ROW.0.id>
        ctag.town_privs=<DB.ROW.0.privs>
        ctag.town_position=<DB.ROW.0.position>
        ctag.town_member_flags=<DB.ROW.0.flags>
        ctag.town_description=<DB.ROW.0.description>
        local.priv=<town_canMember <ctag.town_position>>
        resizepic 92 87 2620 416 147
        call d_town_buildMemberHtml
        dhtmlgump 98 95 403 130 0 1 <local.html>
        dorigin 101 221
        IF (<local.priv>) && (<ctag0.town_position>!=1)
         button - *20 30008 30009 1 0 47
         dtext +19 -1 66 <QVAL <ctag.town_member_flags>&<DEF.TOWN_FLAG_CRIMINAL>?Retirar da lista de inimigos publicos:Adicionar a lista de inimigos publicos>
         button - *20 30008 30009 1 0 48
         dtext +19 -1 66 <QVAL <ctag.town_member_flags>&<DEF.TOWN_FLAG_BANNED>?Retirar da lista de inimigos publicos banidos:Adicionar a lista de inimigos publicos banidos>
         button - *20 30008 30009 1 0 49
         dtext +19 -1 66 Retirar cidadania
         button - *20 30008 30009 1 0 50
         dtext +19 -1 66 Trocar cargo
        ENDIF
        if (<ctag0.town_positionID>)
         button - *20 30008 30009 1 0 46
         dtext +19 -1 66 Listar cidadaos com cargo de <ctag.town_title>
        endif
        button 92 385 4014 4015 1 0 1
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_NPCs>)  
        local.priv=<town_getPrivs <ctag.town_region>>
        resizepic 92 87 2620 416 147
        call d_town_buildNPCHtml
        dhtmlgump 98 95 403 130 0 1 <local.t>
        dorigin 101 221
        IF (<local.priv>&<DEF.TOWN_PRIV_HIRE>)
         button - *20 30008 30009 1 0 80
         dtext +19 -1 66 Contratar novo servidor
        ENDIF
        button - *20 30008 30009 1 0 81
        dtext +19 -1 66 Listar servidores
      
        button 92 385 4014 4015 1 0 1
        
elif (<ctag.town_page>==<DEF.TOWN_PAGE_NPC>)  
        if (!<ctag.town_UID>)        //vamos comprar
         local.priv=<town_getPrivs <ctag.town_region>>
         resizepic 92 89 2620 416 282
         dorigin 130 65
         if (<local.priv>&<DEF.TOWN_PRIV_KEEPER>)
          dtext +18 *60 66 Contratar porteiro (<STRTRESURE <DEF.TOWN_SERVANT_1_HIRE>>)
          button - +3 30008 30009 1 0 85
          dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_SERVANT_1_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)
         else
          dtext +18 *60 32 Contratar porteiro (negado)
         endif
         if (<local.priv>&<DEF.TOWN_PRIV_GUARD>)
          dtext +18 *60 66 Contratar guarda (<STRTRESURE <DEF.TOWN_GUARD_0_HIRE>> ate <STRTRESURE <DEF.TOWN_GUARD_5_HIRE>>)
          button - +3 30008 30009 1 0 86
          dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_GUARD_0_ORD>> ate <STRTRESURE <DEF.TOWN_GUARD_5_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)
         else
          dtext +18 *60 32 Contratar guarda (negado)
         endif
         if (<local.priv>&<DEF.TOWN_PRIV_SENESCHAL>)
          dtext +18 *60 66 Contratar senescal (<STRTRESURE <DEF.TOWN_SERVANT_3_HIRE>>)
          button - +3 30008 30009 1 0 87
          dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_SERVANT_3_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)
         else
          dtext +18 *60 32 Contratar senescal (negado)
         endif

        elif (<ctag.town_UID>==-1) //Vamos comprar um guarda
         resizepic 92 89 2620 416 282
         dorigin 130 65
         dtext +18 *40 66 Contratar aspirante (<STRTRESURE <DEF.TOWN_GUARD_0_HIRE>>)
         button - +3 30008 30009 1 0 90
         dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_GUARD_0_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)
         dtext +18 *20 66 Contratar soldado (<STRTRESURE <DEF.TOWN_GUARD_1_HIRE>>)
         button - +3 30008 30009 1 0 91
         dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_GUARD_1_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)
         dtext +18 *20 66 Contratar cabo (<STRTRESURE <DEF.TOWN_GUARD_2_HIRE>>)
         button - +3 30008 30009 1 0 92
         dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_GUARD_2_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)
         dtext +18 *20 66 Contratar tenente (<STRTRESURE <DEF.TOWN_GUARD_3_HIRE>>)
         button - +3 30008 30009 1 0 93
         dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_GUARD_3_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)
         dtext +18 *20 66 Contratar capitao (<STRTRESURE <DEF.TOWN_GUARD_4_HIRE>>)
         button - +3 30008 30009 1 0 94
         dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_GUARD_4_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)
         dtext +18 *20 66 Contratar major (<STRTRESURE <DEF.TOWN_GUARD_5_HIRE>>)
         button - +3 30008 30009 1 0 95
         dtext +18 *20 66 Ordenado de <STRTRESURE <DEF.TOWN_GUARD_5_ORD>> a cada <f_timestring <DEF.TOWN_TRIG_TIME>>(reais)

        else //Vamos mostrar um
         local.priv=<town_canHire <ctag.town_region>>
         resizepic 92 87 2620 416 147
         call d_town_buildServantHtml
         if (<local.guard>) && (<town_canGuard <ctag.town.region>>)
          local.priv=1
         endif
         dhtmlgump 98 95 403 130 0 1 <local.html>
         dorigin 101 221
         if (<IsGM>)
          button - *20 30008 30009 1 0 82
          dtext +19 -1 66 [GM] Teleportar para o NPC
          button - *20 30008 30009 1 0 83
          dtext +19 -1 66 [GM] Teleportar o NPC para ca
         endif         
         IF (<local.priv>)
          button - *20 30008 30009 1 0 84
          dtext +19 -1 66 Demitir este <QVAL <local.guard>?guarda:funcionario>
         ENDIF

        endif      
        button 92 385 4014 4015 1 0 1 

elif (<ctag.town_page>==<DEF.TOWN_PAGE_POLLS>)
        DB.QUERY=SELECT permissions FROM towns WHERE region="<DB.ESCAPEDATA <ctag.town_region>>"
        local.permissions=<DB.row.0.permissions>
        resizepic 92 89 2620 416 282
        dorigin 230 65
        dtext +18 *60 66 Listar peticoes abertas
        button - +3 30008 30009 1 0 60
        dtext +18 *60 66 Listar historico de peticoes
        button - +3 30008 30009 1 0 61
        if (<local.permissions>&<DEF.TOWN_CAN_PETITION>) || (<town_getPrivs <ctag.town_region>>&<DEF.TOWN_PRIV_PETITION>)
         dtext +18 *60 66 Nova peticao
         button - +3 30008 30009 1 0 62
        endif
        if (<local.permissions>&<DEF.TOWN_CAN_AZILUM>) && (!<town_IsMember <ctag.town_region>>) && (STRMATCH(*<tag.raca>*,<town_getRaces <ctag.town_region>>))
         dtext +18 *60 66 Pedir cidadania
         button - +3 30008 30009 1 0 13
        endif

        IF (!<ctag0.town_noFullMenu>)
         button 92 385 4014 4015 1 0 1
        ENDIF

elif (<ctag.town_page>==<DEF.TOWN_PAGE_POLL>)
        resizepic 92 117 2620 416 147
        dtext 100 97 45 Peticao:
        if (!<ctag0.town_petitionID>)    //Criando
         dtext 160 97 66 <ctag.town_petitionTitle>
         dtextentry 98 125 403 130 2100 1 <ctag.town_description>
         dorigin 100 250
         dtext - *20 66 Descreva com precisao o que voce deseja do governo.
         dtext - *20 66 Enviar esta peticao
         button -20 +3 30008 30009 1 0 63
         button 92 385 4014 4015 1 0 12
        else    //Vendo
         call d_town_buildPetition
         local.privs=<src.town_getPrivs <ctag.town_region>>
         dtext 160 97 66 <ctag.town_petitionTitle>
         dhtmlgump 98 125 403 130 0 1 <local.html>
         dorigin 120 241
         dtext +100 *20 53 favor   <dlocal.favor> x <dlocal.against>   contra
         dtext +125 *20 4 Estado:
         if (!<ctag.town_petitionFlags>)
          dtext +175 - 53 Aberta
          if ((<town_getPermissions>&<DEF.TOWN_CAN_PETITION>) || (<local.privs>&<DEF.TOWN_PRIV_PETITION>)) && (<local.canvote>)
           dtext - *20 66 Votar a favor
           button -20 +3 30008 30009 1 0 64
           dtext - *20 66 Votar contra
           button -20 +3 30008 30009 1 0 65
          endif
          if (<local.privs>&<DEF.TOWN_PRIV_LAWS>)
           dtext - *20 66 Dar peticao como aceita
           button -20 +3 30008 30009 1 0 66
           dtext - *20 66 Dar peticao como rejeitada
           button -20 +3 30008 30009 1 0 67
          endif
          button 92 385 4014 4015 1 0 60
         else
          if (<ctag.town_petitionFlags>&01)
           dtext +175 - 66 Aceita
          else
           dtext +175 - 26 Rejeitada
          endif
          button 92 385 4014 4015 1 0 61
         endif
         if (<IsGM>)
          dtext +40 *20 38 [GM] Remover peticao e votos
          button +20 +3 30008 30009 1 0 68
         endif
         
        endif
        IF (!<ctag0.town_noFullMenu>)
         button 92 385 4014 4015 1 0 1
        //else
        // button 92 385 4014 4015 1 0 12
        endif

elif (<ctag.town_page>==<DEF.TOWN_PAGE_LAWS_SHOW>)
        call d_town_buildLaw
        resizepic 92 117 2620 416 147        
        dtext 160 97 66 <ctag.town_lawTitle>
        dhtmlgump 98 125 403 130 0 1 <local.html>
        dorigin 120 241
        
        if (!<ctag0.town_noFullMenu>)
         button 92 385 4014 4015 1 0 1
        else
         button 92 385 4014 4015 1 0 70
        endif

elif (<ctag.town_page>==<DEF.TOWN_PAGE_PRISION>)
        resizepic 92 87 2620 416 147
        call d_town_buildPrisionHtml
        dhtmlgump 98 95 403 130 0 1 <local.html>
        dorigin 101 220
        IF (<town_getPrivs <local.town>>&<DEF.TOWN_PRIV_LAWS>)
         if (<local.time>)
          dtext +19 *20 66 Desabilitar sistema de prisao
         else
          dtext +19 *20 24 Habilitar sistema de prisao
         endif
         button - +1 30008 30009 1 0 101
         if (<local.time>)
          dtext +19 *20 66 Mudar tempo minimo de prisao
          button - +1 30008 30009 1 0 102
          dtext +19 *20 66 Mudar fianca minima
          button - +1 30008 30009 1 0 103
          dtext +19 *20 66 Gerenciar celas
          button - +1 30008 30009 1 0 104
         endif
        ENDIF
        dtext +19 *20 66 Listar prisioneiros
        button - +1 30008 30009 1 0 105

        button 92 385 4014 4015 1 0 1 

elif (<ctag.town_page>==<DEF.TOWN_PAGE_CELLS_LIST>)
        dorigin 101 100
        local.c=0
        dtext +19 *30 47 Precioneo botao do lado da cela para remove-la.
        WHILE (!<IsEmpty <serv.area.<ctag.town_region>.tag.cell_<dlocal.c>>>)
         if (<IsGM>)
          dtext +19 *20 56 Cela <eval <dlocal.c>+1> (<serv.area.<ctag.town_region>.tag.cell_<dlocal.c>>)
         else
          dtext +19 *20 56 Cela <eval <dlocal.c>+1> (<sextantp <serv.area.<ctag.town_region>.tag.cell_<dlocal.c>>>)
         endif
         button - +1 30008 30009 1 0 <eval 120+<local.c>>
         local.c += 1
        END
        if (<local.c> < 6)
         dtext +19 *30 66 Adicionar cela (<dlocal.c>/6)
         button - +1 30008 30009 1 0 106
        endif
        button 92 385 4014 4015 1 0 1

endif


//########################################################################
//                                   DIALOG
//########################################################################

[DIALOG d_town]
//Dialog de interface do sistema
100,75
page 0
resizepic 50 31 2620 500 400
checkertrans 55 38 490 385
gumppic 0 0 10400
gumppic 0 160 10401
gumppic 0 356 10402
gumppic 518 0 10410
gumppic 518 160 10411
gumppic 518 356 10412
dhtmlgump 92 43 416 38 1 0 <DEF.CENTER><DEF.BFONT_RED><serv.area.<ctag.town_region>.name>
call d_town_solve

[DIALOG d_town text]

[DIALOG d_town button]
ON=0
if (!<ctag0.town_noFullMenu>)
 //Não é board. É senescal
 town_clearctags
elif (<ctag.town_page>!=<DEF.TOWN_PAGE_LAWS>) && (<ctag.town_page>!=<DEF.TOWN_PAGE_POSITIONS>)
 //Eh board. Volta pro board.
 town_clearctags
 obj=<ctag0.town_board>
 obj.use
else
 town_clearctags
endif

ON=1
// Okay geral
DOSWITCH <ctag0.town_page>
 local.null             //Não tem TOWN_PAGE_*
 call d_town_okay_found //TOWN_PAGE_FOUND
 local.null             //TOWN_PAGE_REPORT
 town <ctag.town_region>//TOWN_PAGE_CONFIG
 call d_town_okay_laws  //TOWN_PAGE_PERMISSIONS
 call d_town_okay_races //TOWN_PAGE_RACES
 town_listEnemies       //TOWN_PAGE_BANS
 town <ctag.town_region>//TOWN_PAGE_FRIENDS
 town <ctag.town_region>//TOWN_PAGE_TAXES
 town <ctag.town_region>//TOWN_PAGE_LAWS
 town_lawsList          //TOWN_PAGE_LAWS_SHOW
 town <ctag.town_region>//TOWN_PAGE_LAWS_EDIT
 town_listPositions     //TOWN_PAGE_POSITIONS
 town <ctag.town_region>//TOWN_PAGE_POSITION_DESC
 call d_town_okay_privs //TOWN_PAGE_PRIVS
 town <ctag.town_region>//TOWN_PAGE_MEMBERS
 town_listMembers       //TOWN_PAGE_MEMBER
 town <ctag.town_region>//TOWN_PAGE_HIRES
 town <ctag.town_region>//TOWN_PAGE_NPCS
 town <ctag.town_region>//TOWN_PAGE_NPC
 town <ctag.town_region>//TOWN_PAGE_POLLS
 town_petitionsMenu     //TOWN_PAGE_POLL
 town <ctag.town_region>//TOWN_PAGE_AZILUM
 town_Configurations    //TOWN_PAGE_PRISION
 town_PrisionMenu       //TOWN_PAGE_CELLS_LIST

ENDDO


ON=2
// Raças amigas
ctag.town_page=<DEF.TOWN_PAGE_RACES>
SDIALOG d_town

ON=3
// Friends
town_listFriends

ON=4
// Banned
town_listEnemies

ON=5
// Citzens
town_listMembers

ON=6
// Config
town_Configurations

ON=7
// Permissions
ctag.town_page=<DEF.TOWN_PAGE_PERMISSIONS>
SDIALOG d_town

ON=8
// Laws
town_Laws

ON=9
// Taxes
town_Taxes

ON=10
// NPCs
town_PageNPCs

ON=11
// Polls
// 

ON=12
// Petitions
town_petitionsMenu

ON=13
// Azilum
IF (<town_GetPermissions <ctag.town_region>>&<DEF.TOWN_AUTO_AZILUM>)
 town_Azilum <ctag.town_region>
ELSE
 town_sendCitzenshipPetition <ctag.town_region>
ENDIF

ON=31
// Change color 1
town_PromptFirstColor

ON=32
// Change color 2
town_PromptSecondColor

ON=33
// Transfer mastership
town_listMembersTransfer

ON=34
// Diplomacy
// 

ON=35
// Change artefact
town_changeArtefact

ON=36
// Tranfer vault
town_changeVault

ON=37
// Turn sub-region
town_listProCapitals

ON=38
// Independency
town_prompIndependency

ON=39
// Show Positions
town_listPositions

ON=40
//Change Possition title
town_changePostitionTitle

ON=41
//Change position Description
IF (<ctag.town_page>==<DEF.TOWN_PAGE_POSITION_DESC>)
 town_changePositionDesc <ARGTXT[1]>
ELSE
 town_changePositionDesc
ENDIF

ON=42
//View pirvs
town_ShowPositionPrivs

ON=43
//Delete prositionID
town_deletePosition

ON=44
//Change position
town_changePosition

ON=45
//Create position
town_addPosition

ON=46
//List members in this position
town_listMembersPosition

ON=47
//(UN)Criminal
ref1=<ctag.town_uid>
ref1.town_changeCriminalFlag
SDIALOG d_town

ON=48
//(UN)Banned
ref1=<ctag.town_uid>
ref1.town_changeBannedFlag
SDIALOG d_town

ON=49
//Expurge member
town_expurgeMember

ON=50
//Change position
town_listChangePosition

ON=51
//(UN)Debt
ref1=<ctag.town_uid>
ref1.town_changeDebtFlag
SDIALOG d_town

ON=52
//Add friend
town_targAddFriend

ON=53
//Add criminal
town_targAddCriminal

ON=54
//Add citzen
town_targInviteCitzen

ON=55
//Manage capital
town <town_getCapital <ctag.town_region>>

ON=56
//List demences
town_listDemences <ctag.town_region>

ON=57
//Apply taxes
call town_saveTaxes
town <ctag.town_region>

ON=58
//Prender/soltar livros
town_manageBook

////////////////  Petições
ON=60
//List open petitions
town_listOpenPetitions

ON=61
//List closed petitions
town_listOldPetitions

ON=62
//New petition
town_newPetition

ON=63
//Apply new petition
town_sendPetition <argtxt[1]>

ON=64
//Vote in favor of petition
town_petitionVote 1

ON=65
//Vote against petition
town_petitionVote 0

ON=66
//Accept petition
town_petitionJudge 1

ON=67
//Negate petition
town_petitionJudge 0

ON=68
//Delete petition
if (<IsGm>)
 town_petitionDelete
else
 SDIALOG d_town
endif

//////////////////  Decretos

ON=70
//Ver decretos
town_lawsList

ON=71
//Promulgar decreto
town_NewLaw

ON=72
//Editar decreto
town_lawsListEdit

ON=73
//Revogar decreto
town_lawsListRevoke

ON=74
//Enviar promulgação de decreto
town_lawSendNew <ARGTXT[1]>

ON=75
//Modificar um decreto em edição
town_lawSendEdit <ARGTXT[1]>

ON=76
//Modificar o titulo de um decreto em edição
town_lawChangeTitle

//////////////////  NPCs

ON=80
//Contratar servidor
town_hireNPC

ON=81
//Listar servidores
town_listServants

ON=82
//GM Help. Tele para até o NPC.
obj=<ctag0.town_UID>
go <obj.p>

ON=83
//GM Help. Tele NPC até aqui
obj=<ctag0.town_UID>
obj.p=<p>
obj.update
obj.timerf 1,say Huh? Huh? [Q ISO/ [VO BANI!!1(onze)

ON=84
//Demitir servidor
town_fireNPC

ON=85
//Contratar porteiro
town_hireKeeper

ON=86
//Contratar guarda
town_hireGuard

ON=87
//Contratar senescal
town_hireSeneschal

ON=90 95
//Contratar um guarda em específico
local.level=<eval <argn>-90>
town_hireThisGuard <ctag.town_region>,<local.level>

///////////////// Prisão
ON=100
//Chamar o quadro de prisão
town_PrisionMenu

ON=101
//Ativa/desativar prisão
if (<serv.area.<ctag.town_region>.tag0.prisionTime>)
 town_PrisionActivate 0
else
 town_PrisionActivate
endif

ON=102
//Mudar tempo de prisão
town_PrisionActivate

ON=103
//Mudar fiança
town_ChangePrisionFee

ON=104
//Gerenciar celas
town_PrisionCells

ON=105
//Listar prisioneiros
town_listPrisioners

ON=106
//Adicionar cela
town_AddCell

ON=107
//Libertar da prisão
uid.<ctag.town_uid>.town_forgiveJail <ctag.town_region>
sysmessagered Prisioneiro libertado!!!
town_PrisionMenu

ON=108
//Ajustar pena
town_ChangeJailTime

ON=109
//Mudar área de soltura/banimento
town_changeReleasePoint

ON=120 130
//Deletar cela
local.n=<eval <argn>-120>
town_RemoveCell <local.n>

[EOF]

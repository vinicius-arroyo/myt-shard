     /////////////////////////////////////////////////////
    ///////  sistema de armadilha (MyT)  ////////////////
   ///////         Jean Gabin           ////////////////
  ///////       Galthar, o Errante     ////////////////
 ///////      Versão beta test 0.5    ////////////////
/////////////////////////////////////////////////////



//|||||||||||||||||||||||||||||||||
// ---------# TYPEDEFS #---------//
//|||||||||||||||||||||||||||||||||



[TYPEDEF t_trap]

//more1 spell
//more2 força (%skill magery)
//morex cargas (morex>04fffffff = infinito, coisa de GM)
//morey dificuldade de desarmar/detectar (%skill detect hidden e disarm trap)
//morez typedef original
//morem tempo de rearme
//link chave
//attr_magic Se for armadilha de chão, ela pode mudar d elugar quando resetada.
//color=026 foi detectada
//tag.color A ultima cor da armadilha.

on=@dclick
type=<morez>
if (<morex>==0)
 src.dclick
 return 1
endif
if (<morez>==t_container)		//Se a armadilha era um cantainer
 if (<more1>==5)				//Magic Arrow
  src.effect 3 i_fx_smoke_small 0 32 0
  src.sfx 0223
 elif (<more1>==18)			//Fireball
  src.effect 3 i_fx_explode 0 14 0
  src.sfx 015e
 elif (<more1>==20)			//Poison
  src.effect 2 i_trap_gas_3 0 24 0
  src.sfx 022f
 elif (<more1>==28)			//Fire Field
  LOCAL.time=<eval <more2>/10>
  src.f_cela_fogo <LOCAL.time>
  more2=0
 elif (<more1>==30)			//Lighting
  src.effect 1 0 0 24 0
  src.sfx 028
 elif (<more1>==39)			//Poison Field
  LOCAL.time=<eval <more2>/10>
  src.f_cela_veneno <LOCAL.time>
  more2=0
 elif (<more1>==43)			//Explosion
  effect 3 i_fx_explode 0 14 0
  src.sfx 011b
 elif (<more1>==42)			//Energy Blast
  src.effect 3 036cb 0 9 0 0b90 0
  src.sfx 0213
 elif (<more1>==51)			//Flame Bolt
  src.effect 3 i_fire_column 0 16
  src.sfx 011b
 endif
endif
if (<morex> == 04fffffff)		//Carga infinita
 var.blank=
else					//decrementa carga
 morex=<morex>-1
endif
f_resetTrap
src.spelleffect <more1> <more2> <uid>	//aplica efeito hardcoded
return 1

on=@step
type=<morez>
if (<morex>==0)
 src.remove
 return 1
endif
if (<more1>==5)				//Magic Arrow
 src.effect 3 i_fx_smoke_small 0 32 0
 src.sfx 0223
elif (<more1>==18)			//Fireball
 src.effect 3 i_fx_explode 0 14 0
 src.sfx 015e
elif (<more1>==20)			//Poison
 src.effect 2 i_trap_gas_3 0 24 0
 src.sfx 022f
elif (<more1>==28)			//Fire Field
 LOCAL.time=<eval <more2>/10>
 src.f_cela_fogo <LOCAL.time>
 more2=0
elif (<more1>==30)			//Lighting
 src.effect 1 0 0 24 0
 src.sfx 028
elif (<more1>==39)			//Poison Field
 LOCAL.time=<eval <more2>/10>
 src.f_cela_veneno <LOCAL.time>
 more2=0
elif (<more1>==43)			//Explosion
 effect 3 i_fx_explode 0 14 0
 src.sfx 011b
elif (<more1>==42)			//Energy Blast
 src.effect 3 036cb 0 9 0 0b90 0
 src.sfx 0213
elif (<more1>==51)			//Flame Bolt
 src.effect 3 i_fire_column 0 16
 src.sfx 011b
endif
if (<morex> == 04fffffff)		//Carga infinita
 var.blank=
else					//decrementa carga
 morex=<morex>-1
endif
if (<attr>&080)				//Se estiver invis (testa antes de mudar attr para não dar erro no console do Sphere)
 attr==<attr>&~080			//Apareça
 update
endif
f_resetTrap
src.spelleffect <more1> <more2> <uid>	//aplica efeito hardcoded
return 1


[TYPEDEF t_trap_active]
//animação das traps
//morex contador do timer
//morey ID do quadro que começa o loop
//morez ID do quadro que termina o loop
//morem tempo antes de sumir em décimos de segundo
//more1l dano
//more1h tipo do dano
//more2 som do step

on=@step
sfx <more2>
src.damage <more1l> <more1h>
timerd 1
return 1

on=@dclick
timerd=1
return 1

on=@timer
if (<morez> > <morem>)
 remove
 return 1
endif
if (<dispiddec> == <morex>) 
 dispiddec=<morey>
else
 dispiddec=<dispiddec>+1
endif
update
morez=<morez>+1
timerd 1
return 1

[ITEMDEF i_tat]
ID=011a7
NAME=teste de trap active
TYPE=t_trap_active


on=@create
morey=011aa
morex=011ab
morem=60
more1l=5
more1h=010
more2=01dc

//morex cotador do timer
//morey ID do quadro que começa o loop
//morez ID do quadro que termina o loop
//morem tempo antes de sumir em décimos de segundo
//more1l dano
//more1h tipo do dano
//more2 som do step



//|||||||||||||||||||||||||||||||||
// ---------# ITEMDEFS #---------//
//|||||||||||||||||||||||||||||||||




[ITEMDEF i_reset_trap]
NAME=reset de armadilha
ID=i_worldgem_bit
TYPE=t_script

on=@create
attr=092
color=028

on=@timer
f_resetTrap 1

on=@dclick
f_resetTrap 1
remove


[ITEMDEF i_forbiden]
NAME=forbiden
ID=i_gold
TYPE=t_script

on=@create
color=07fff
attr=092

on=@step
if (!<src.isgm>)
 src.f_retreat
endif
return 1


[ITEMDEF i_mry_pegandofogo]
id=i_memory
NAME=pegando fogo
TYPE=T_eq_script
WEIGHT=0

ON=@Create
 ATTR=attr_newbie | 080
 timer=1
 more2={12 30}

ON=@TIMER
cont.effect 3 i_fire 0 64 0
cont.bark
if (<cont.tag.pegandofogo>==1)
 dorand 5
  cont.emotered pegando fogo
  cont.anim 9
  cont.anim 10
  cont.emotered se debate
 enddo
 if (<cont.hits> >= 4)
  LOCAL.dam={0 6 1 3 2 2 3 1}
  cont.damage <eval <LOCAL.dam>> 010 <UID>
  timer=1
 else
  cont.tag.pegandofogo=   //apaga a tag pegando fogo
  remove			// remove a memory pegando fogo
 endif
else
 remove
endif
more2=<more2>-1
if (!<more2>)
 remove
endif
return 1


[ITEMDEF i_fogo_armadilha_lo]
name=Cela de fogo
id=0398c
TYPE=t_fire
CATEGORY=MyT - Quest Items
SUBSECTION=Traps
DESCRIPTION=Cela de fogo LO
DUPELIST=0398d,0398e,0398f,03990,03991,03992,03993,03994,03995,03e27

ON=@Create
MOREZ=1
ATTR=012

on=@step
src.f_pega_fogo
return 1


[ITEMDEF i_fogo_armadilha_ns]
name=Cela de fogo
id=03996
TYPE=t_fire
CATEGORY=MyT - Quest Items
SUBSECTION=Traps
DESCRIPTION=Cela de fogo NS
DUPELIST=03997,03998,03999,0399a,0399b,0399c,0399d,0399e,0399f,03e31

ON=@Create
MOREZ=1
ATTR=012

on=@step
src.f_pega_fogo
return 1


[ITEMDEF i_veneno_armadilha_lo]
name=Cela de veneno
id=03915
TYPE=t_script
CATEGORY=MyT - Quest Items
SUBSECTION=Traps
DESCRIPTION=Cela de veneno LO

ON=@Create
MOREZ=1
ATTR=012

on=@step
src.sfx 05b
src.veneno <eval {75 85}>
return 1


[ITEMDEF i_veneno_armadilha_ns]
name=Cela de veneno
id=03921
CATEGORY=MyT - Quest Items
SUBSECTION=Traps
DESCRIPTION=Cela de veneno NS

ON=@Create
MOREZ=1
ATTR=012

on=@step
src.sfx 05b
src.veneno <eval {75 85}>
return 1




//||||||||||||||||||||||||||||||||||
// ---------# FUNCTIONS #---------//
//||||||||||||||||||||||||||||||||||




[FUNCTION f_resetTrap]
if (!<argn>) 			//Trap cria o resetador de trap
 serv.newitem i_reset_trap
 new.link=<UID>
 new.p=<p>
 new.timer=<eval <morem>>
else				//resetador de trap reseta a trap
 link.type=t_trap
 if (<link.attr>&020)
  link.movenear <UID> <eval {0 12 1 5 2 2 3 1}>
 endif
 try link.color=0<link.tag.color>
 if (<link.morez>==0)		//Se é uma armadilha de chão. Morez=t_normal=0
  link.REMOVEFROMVIEW		//some pros chars que estão perto
  link.attr=<link.attr>|080	//volta a ficar invis
 endif
 link.update
endif


[FUNCTION dh]
FORITEMS 8
 if (<type>==t_trap)			//Para todas as traps no campo de visao.
  if (<morez>==0) && (<attr>&080)	//Se for uma armadilha do chão (morez=t_normal=0)
   attr=<attr>&~080			//Apareça
   update
  endif
  if (!<canseelos>) && (<morez>==0)
   attr=<attr>|080
   removefromview
  elif (<canseelos>)
   if (<color>!=026)			//se eu não estiver detectada
    tag.color=0<color>
    color=026
    update
   endif
   say=@028, [armadilha]
   f_resetTrap
  endif
 endif
ENDFOR


[FUNCTION f_cela_fogo]
f_cela i_fogo_armadilha_ns, i_fogo_armadilha_lo, <argn>
sfx 015f
return 1


[FUNCTION f_cela_veneno]
f_cela i_veneno_armadilha_ns, i_veneno_armadilha_lo, <argn>
sfx 0117
return 1


[FUNCTION f_cela]
if (<argv2>==0)
 local.timer=<eval {20 30}>
else
 local.timer=<argv2>
endif

serv.newitem=<argv1>
new.p=<p>
new.move 0 2 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move 0 -1 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move 1 -1 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move -1 -1 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move -1 2 0
new.timer=<local.timer>

serv.newitem=<argv1>
new.p=<p>
new.move 1 2 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move 2 1 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move 2 0 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move 2 -1 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move -1 -1 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move -1 0 0
new.timer=<local.timer>

serv.newitem=<argv0>
new.p=<p>
new.move -1 1 0
new.timer=<local.timer>

serv.newitem=i_forbiden
new.p=<p>
new.move 2 2 0
new.timer=<local.timer>


[FUNCTION f_pega_fogo]
if (!<src.RESTEST 1 i_mry_pegandofogo>)
 src.effect 3 i_fire 0 64 0
 src.tag.pegandofogo=1
 src.newitem=i_mry_pegandofogo
 src.act.equip
 src.update
 src.sfx 015f
 LOCAL.dam=<eval {5 15}>
 src.damage <LOCAL.dam> 010
endif
//*****************************************************************************
//*****************************************************************************
//
// SKILL HIDING e STEALTH por
//
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//TODO:

//TAGS usadas:
//tag.light_lvl     //0=escuro, 30=claro

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// sk_hiding
//*****************************************************************************
[FUNCTION sk_hiding]

if <src.findid.i_hiding> || <src.findid.i_mry_invis>
    src.sysmessageyellow Nao pode se esconder ainda.
    return 1
elif (<src.flags>&statf_onhorse)
    src.sysmessageyellow Voce nao pode se esconder com um cavalo...
    return 1
elif (<src.region.flags>&region_flag_underground)

elif (<src.hiding> < 500) && (<var.tHour> > 4) && (<var.tHour> < 20)
    src.sysmessageyellow Voce nao possui habilidade para se esconder com esta luminosidade...
    return 1
elif (<src.hiding> < 700) && (<var.tHour> > 5) && (<var.tHour> < 19)
    src.sysmessageyellow Voce nao possui habilidade para se esconder com esta luminosidade...
    return 1
elif (<src.hiding> < 900) && (<var.tHour> > 6) && (<var.tHour> < 18)
    src.sysmessageyellow Voce nao possui habilidade para se esconder com esta luminosidade...
    return 1
endif

src.act=0
src.ctag.disturb=0
serv.newitem i_hiding
new.cont=<src>

return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_hiding
//*****************************************************************************
[ITEMDEF i_hiding]
ID=i_deed
NAME=Hiding
TYPE=t_eq_script
LAYER=layer_special

ON=@create
//not hidden
more1=0
timer=1

ON=@timer
timer=-1

if (!<cont.isonline>)
    cont.hiding_leave
    timerf 5,remove
    return 1
elif (<cont.npc>)
    //
elif (<cont.findlayer.layer_hand1.type> == t_light_lit) || (<cont.findlayer.layer_hand2.type> == t_light_lit)
    cont.sysmessageyellow Apague tochas e lampioes...
    cont.hiding_leave
    timerf 5,remove
    return 1
//statf_dead|statf_freeze|statf_sleeping|statf_war|statf_poisoned|statf_polymorph|statf_stone|statf_hallucinating|statf_onhorse|statf_spiritspeak
elif (<cont.flags>&0804414b6) || (<cont.ctag0.disturb>)
    cont.sysmessageyellow Voce esta visivel// 2! <hval <cont.flags>&0804404b6>
    cont.hiding_leave
    timerf 5,remove
    return 1
endif

if !(<more1>)
    cont.skill_gain SKILL_HIDING
    
    //dif base entre 10% e 90% de acordo com luz
    LOCAL.dif=<between 100,900,<cont.tag0.light_lvl>,30>
    LOCAL.dif=<eval 1000-<LOCAL.dif>>
    
//    serv.log [hiding] testando <eval <cont.hiding>> contra dif <dLOCAL.dif> e light <eval <cont.tag0.light_lvl>>
    if ( !<belltest <cont.hiding>,<LOCAL.dif>,200> )
        cont.sysmessagered Voce nao conseguiu se esconder
        cont.hiding_leave
        timerf 5,remove
        return 1
    else
        cont.sysmessagegreen Conseguiu se esconder
        cont.hiding_enter
        more1=1
    endif
else
    
    LOCAL.d=<cont.distance <tag0.p>>
    if (<LOCAL.d> > 0)
        cont.skill_gain SKILL_STEALTH

        //dif base entre 10% e 90% de acordo com luz
        LOCAL.dif=<between 100,900,<cont.tag0.light_lvl>,30>
        LOCAL.dif=<eval 1000-<LOCAL.dif>>
    
        //soma com 15% por passo dado em 1 segundo
        LOCAL.dif=<eval <LOCAL.dif>+(<LOCAL.d>*150)>
        
//        serv.log [stealth] testando <eval <cont.stealth>> contra dif <dLOCAL.dif> e light <eval <cont.tag0.light_lvl>>
        if ( !<belltest <cont.stealth>,<LOCAL.dif>,200> )
            cont.sysmessageyellow Voce esta visivel!
            cont.hiding_leave
            timerf 5,remove
            return 1
        endif    
    endif
endif

tag.p=<cont.p>
timer=1
return 1

//*****************************************************************************
// i_mry_invis
//*****************************************************************************
//more2 = tempo em segundos de invisibilidade
[ITEMDEF i_mry_invis]
ID=i_memory
NAME=pocao da invisibilidade
TYPE=t_eq_script
layer=layer_special

ON=@CREATE
timer=1

ON=@TIMER
timer = 1
if <more2>
    more2 -= 1
else
    cont.invis_leave
    cont.sysmessageyellow O efeito da pocao passou.
    remove
endif
return 1

//*****************************************************************************
// i_speak_disturb
//*****************************************************************************
[ITEMDEF i_speak_disturb]
ID=i_worldgem_bit
NAME=disturb on speak
TYPE=t_comm_crystal
LAYER=layer_special

ON=@Create
SPEECH=spk_disturb
TYPE=t_eq_script

ON=@Equip
//TYPE=t_comm_crystal
//LAYER=99
SRC.FLAGS=<SRC.FLAGS>|statf_commcrystal
ATTR=<ATTR>|attr_newbie|attr_move_never

//*****************************************************************************
// spk_disturb
//*****************************************************************************
[SPEECH spk_disturb]
ON=*
src.ctag.disturb=1

//*****************************************************************************
// hiding_enter
//*****************************************************************************
[FUNCTION hiding_enter]
invis 1
flags |= statf_hidden
events +e_no_attack
events +e_no_dclick
serv.newitem i_speak_disturb
new.cont=<uid>
update

//*****************************************************************************
// hiding_leave
//*****************************************************************************
[FUNCTION hiding_leave]
invis 0
flags &= ~statf_hidden
events -e_no_attack
events -e_no_dclick
consume 1 i_speak_disturb
update

//*****************************************************************************
// invis_enter
//*****************************************************************************
[FUNCTION invis_enter]
invis 1
flags |= statf_hidden
update

serv.newitem i_mry_invis
new.cont=<uid>
new.more2=(<int>/10)

sysmessagegreen Ninguem pode lhe ver.

//*****************************************************************************
// invis_leave
//*****************************************************************************
[FUNCTION invis_leave]
invis 0
flags &= ~statf_hidden
update

[EOF]
//*****************************************************************************
//*****************************************************************************
//
// EVENTOS RACIAIS por
//  Galthar
//  Metus
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:
//
//TAGS usadas:
//src.tag.drowLowLightBonus //1 with bonus active, 0 otherwise
//src.tag.resisteaoclima
//src.tag.raca
//src.tag.bank.weight
//*****************************************************************************


//*****************************************************************************
//*****************************************************************************
// DEFNAMEs 
//*****************************************************************************
//*****************************************************************************

[DEFNAME charCreation]
//check drow bonuses every n seconds
ev_drow_check_timer=5

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// Eventos ligados a armas
//*****************************************************************************
[EVENTS e_silver_weapon]
ON=@CDAMAGE
if (<src.npc>==brain_undead) || (<src.npc>==BRAIN_DRAGON)
    src.damage <eval <R<dam>>/2>,<def.dam_magic>,<uid>
    src.EFFECT 3,i_fx_curse,6,15,1
    SOUND snd_spell_harm
endif

[EVENTS T_WEAPON_FIRE]
ON=@CDAMAGE
if !(<attr>&01) //se não tá identificado
    return 0
endif

if (<tag0.flame_timer> < <serv.time>)
    src.damage <R4,10>,dam_fire,<topobj>
    src.EFFECT 2 i_fire_column 6 30 0
    sfx 05CF
    IF !<R19>
        src.f_pega_fogo <R5,7>
    endif        
    tag.flame_timer=<eval <serv.time>+<R15.0,65.0>>
endif

[EVENTS T_WEAPON_COLD]
ON=@CDAMAGE
if !(<attr>&01) //se não tá identificado
    return 0
endif

if (<tag0.cold_timer> < <serv.time>)
    src.damage <R4,10>,dam_cold,<topobj>
    src.EFFECT 3 i_fx_smoke 0 24 0 99 2
    sfx 05C7
    IF !<R19>
        src.stonned <R5,8>
    endif        
    tag.cold_timer=<eval <serv.time>+<R15.0,65.0>>
endif

[EVENTS T_WEAPON_PESTILENCE]
ON=@CDAMAGE
if !(<attr>&01) //se não tá identificado
    return 0
endif

if (<tag0.plage_timer> < <serv.time>)
    src.timerf 2 damage <R4,10>,dam_poison,<topobj>
    src.timerf 2 EFFECT 3 i_fx_explode 0 26 0 177 2
    sfx 0474
    IF !<R19>
        src.poison <R35.0,45.0>
    endif        
    tag.plage_timer=<eval <serv.time>+<R15.0,65.0>>
endif

[EVENTS T_WEAPON_TEMPEST]
ON=@CDAMAGE
if !(<attr>&01) //se não tá identificado
    return 0
endif

if (<tag0.lighting_timer> < <serv.time>)
    IF <R19>
        src.damage <R4,10>,dam_lightning,<topobj>
        src.EFFECT 2 i_fx_field_paralyze 6 16 0
        sfx 05C3
    ELSE
        src.damage <R10,17>,dam_lightning,<topobj>
        src.effect 1 0 0 0 0
        sfx SND_THUNDER
    ENDIF
    tag.lighting_timer=<eval <serv.time>+<R15.0,65.0>>
endif

[EVENTS e_machado_luna]
ON=@CDAMAGE
//Recarregar a cada 3 horas
if !(<attr>&01) //se não tá identificado
    return 0
endif

//Recarregar a cada 3 horas
if (!<morex>) && (!<tag0.loading>)
    tag.loading 1
    cont.sysmessageorange O Machado de Luna esta exaurido de suas forcas... Mas Luna ha de recarrega-lo em seu tempo!
    timerf 3600,wp_machado_luna_reload
endif

[FUNCTION wp_machado_luna_reload]
tag.loading=0
morex=2
more2=30

[EVENTS e_crescente_mond]
ON=@CDAMAGE
//Recarregar a cada 3 horas
if !(<attr>&01) //se não tá identificado
    return 0
endif

//Recarregar a cada 3 horas
if (!<morex>) && (!<tag0.loading>)
    tag.loading 1
    cont.sysmessageorange O Crescente de Mond esta exaurido de suas forcas... Mas Mond ha de recarrega-lo em seu tempo!
    timerf 3600,wp_crescente_mond_reload
endif

[FUNCTION wp_crescente_mond_reload]
tag.loading=0
morex=5
more2=350

[EVENTS e_pique_atila]
ON=@CDAMAGE
//Recarregar a cada 3 horas
if !(<attr>&01) //se não tá identificado
    return 0
endif

//Recarregar a cada 3 horas
if (!<morex>) && (!<tag0.loading>)
    tag.loading 1
    cont.sysmessageorange O Pique de Atila esta exaurido de suas forcas... Mas Atila ha de recarrega-lo em seu tempo!
    timerf 3600,wp_pique_atila_reload
endif

[FUNCTION wp_pique_atila_reload]
tag.loading=0
morex=2000
more2=20

[FUNCTION wp_spell]
ATTR=attr_magic
MORE2=<argn1> //spell
MOREX=<argn2> //charges

[TYPEDEF e_NoUntilID]
on=@equiptest
if !(<attr>&01) && (!<SRC.IsGM>)
 src.sysmessageorange <name> parece ter vontade propria.
 src.unequip
 return 1
endif

on=@DCLick
if !(<attr>&01) && (!<SRC.IsGM>)
 src.sysmessageorange Voce nao pode usar isto antes de saber o que realmente eh.
 return 1
endif

//*****************************************************************************
// Item quebra ao ser pisado
//*****************************************************************************
[TYPEDEF e_fragile]
ON=@STEP
src.emotered quebrou
sfx 040
remove
return 1

//*****************************************************************************
// Vira Merda
//*****************************************************************************
//a ideia é ser uma praga de yarret
[FUNCTION f_viraMerda]
src.emote Fedendo merda
newitem i_horse_dung
new.amount = 1
DORAND 4
	begin
		sfx 0431
		sysmessagered Acha mesmo que isso te percente?
	end
	begin
		sfx 0431
		sysmessagered Como eh Tolo 
	end
	begin
		sfx 0431
		sysmessagered Voce eh soh mais um no tabuleiro dos meus jogos 
	end
	begin
		sfx 0431
		sysmessagered Eh tao facil te enganar 
	end
ENDDO
effect 2 i_fx_sparkle_2 2 20 0 0Af4 
if (<cont>)
	new.p = <src.p>
else
	new.p = <p>
endif
	new.decay
remove 

[EVENTS e_viraMerda]
ON=@Equip
timerf 30,f_viraMerda
ON=@DropOn_Char
timerf 30,f_viraMerda
ON=@DropOn_Ground
timerf 30,f_viraMerda
ON=@DropOn_Item
timerf 30,f_viraMerda


//*****************************************************************************
// Vira Areia
//*****************************************************************************
[FUNCTION f_viraAreia]
src.emote ao tocar, ve <name> se desfazendo em areia
newitem i_areia
new.amount = 2
effect 2 i_fx_sparkle_2 2 20 0 031
if (<cont>)
    new.p = <src.p>
else
    new.p = <p>
endif
	new.decay
remove 

[TYPEDEF e_viraAreia]
ON=@Equip
f_viraAreia
ON=@DropOn_Char
f_viraAreia
ON=@DropOn_Ground
f_viraAreia
ON=@DropOn_Item
f_viraAreia

//*****************************************************************************
// ALJAVAS
//*****************************************************************************
[TYPEDEF t_quiver]
ON=@DROPON_SELF
if (<argo.type>!=t_weapon_arrow) && (<argo.type>!=t_weapon_bolt)
    src.sysmessageyellow Somente flechas aqui dentro!
    return 1
endif

//*****************************************************************************
// ELFOS
//*****************************************************************************
//Elfos possuem visao noturna e mana acelerada
[EVENTS e_elfo]

ON=@LOGIN
nightsight 0
f_elfSight

ON=@ITEMEQUIP
//remove visao elfica se carregar fonte de luz
if <act.layer>==layer_face
    f_noElfSight
endif

//mantem o layer light com visao noturna
ON=@ITEMUNEQUIP
if <act.layer>==layer_face
    f_elfSight
endif

//carrega um light source no layer light em q soh o player (src) veja
[FUNCTION f_elfSight]
SENDPACKET 02e D040000001 W<serv.itemdef.i_light_source.id> B0 B<def.layer_face> D<uid> W00

//f_noElfSight <force>
[FUNCTION f_noElfSight]
if (<argn>) || (!strmatch(<tag.raca>,Elfo)) && !strmatch(<tag.raca>,Drow))
    SENDPACKET 01d D040000001
endif
//*****************************************************************************
// DROWS
//*****************************************************************************
//Drows tem visao noturna preferenciada, ganham bonus no escuro e sofrem danos
//no claro.
[EVENTS e_drow]

ON=@LOGIN
src.nightsight 0
f_elfSight
src.findid.i_drow_check.remove
serv.newitem=i_drow_check
new.link <src>
new.cont <src>

//mantem o layer light livre para visao noturna
ON=@ITEMUNEQUIP
if ( <act.layer> != layer_face ) //&& ( <act.layer> != layer_hand2 )
    return 0
endif
f_elfSight
return 1

[ITEMDEF i_drow_check]
id=i_deed
name=Drow - Check
type=t_eq_script
layer=layer_special
weight=0

on=@create
timer=ev_drow_check_timer

on=@timer

if ( <var.tHour> >= 19 ) || ( <var.tHour> <= 5 ) || (<region.flags>&region_flag_underground)
  if (<link.tag.drowLowLightBonus> != 1)
    link.tag.drowLowLightBonus=1
//    link.nightsight 1
    link.hiding=<link.hiding>+10.0
    link.stealth=<link.stealth>+5.0
    link.magicresistance=<link.magicresistance>+5.0
    link.dex=<link.dex>+20
    link.sysmessageyellow Voce se sente mais forte no escuro...
  endif
else
  if (<link.tag.drowLowLightBonus> != 0)
    link.tag.drowLowLightBonus=0
//    link.nightsight 0
    link.hiding=<link.hiding>-10.0
    link.stealth=<link.stealth>-5.0
    link.magicresistance=<link.magicresistance>-5.0
    link.dex=<link.dex>-20
    link.sysmessageyellow A luz esta debilitando seu corpo...
  endif
 
  if (<link.hits> > 5)
    link.damage <R10> dam_poison
  endif
endif

timer=ev_drow_check_timer
return 1

//*****************************************************************************
// ORCS
//*****************************************************************************
//Orcs possuem mascara fixa (cara de orc) e não podem usar capacetes. 
//Quando são atingidos e estão com poucos pontos de vida dão dano no inimigo.
[EVENTS e_orc]

ON=@LOGIN
//src.findlayer(layer_hair).remove
//src.findlayer(layer_beard).remove
//src.findlayer(layer_helm).hitpoints 999
//src.update

on=@hit
if (<hits> < 20)
  emotered em furia
  src.damage {5 10}
endif

[ITEMDEF i_head_orc]
id=0141b
name=Orc
TYPE=t_armor
VALUE=0
WEIGHT=0
ARMOR=50

ON=@Create
COLOR=0597
HITPOINTS=9999
attr=attr_static|attr_move_never

//*****************************************************************************
// ANOES
//*****************************************************************************
//Anoes resistem ao clima e tem regeneracao de stamina acelerada
//-Caem ao subir em cavalos
//-Se queimao ao tocar itens magicos
[EVENTS e_anao]

ON=@LOGIN
src.tag.resitenteaoclima=10

//*****************************************************************************
// HUMANOS
//*****************************************************************************
[EVENTS e_humano]

ON=@LOGIN

//*****************************************************************************
// MAGO
//*****************************************************************************
[EVENTS e_mago]



//*****************************************************************************
// EVENTO GERAL PARA TODOS CHARS
//*****************************************************************************
[EVENTS e_mytgeral]
//*************************************
ON=@LOGIN
//*************************************
//    src.skill_init chamado pelo rp_updateGrade
    src.name=<src.tag.raca>
    src.PUT_RP
    src.fame=0
    src.karma=0
    src.title " "
    
    src.flags &= ~statf_freeze
    src.f_update_hair
    src.f_update_beard
    
    src.consume 10 i_mry_clima
    serv.newitem i_mry_clima
    new.equip

    //log event
    event_login

    //entrega mensagens pendentes
    f_deliverPendingMessages

    src.f_correctLight <src.region.flags>
	
	var.manutencao = 0 // coloque 1 aqui para ativar a manutencao
	
    if <var.manutencao> && (<src.account.plevel> <= 1)
        prompt_new Servidor em manutencao. Voce sera desconectado em 30 segundos.
        ynprompt_show
        src.warnAndDisconnect
        return
    endif
    
    if (!<checkVersion>) && (<src.account.plevel> <= 1)
        serv.log [EVENTS] Cliente desconectado por versao invalida: <CLIENTVERSION>
        prompt_new Versao de client deve ser 5.0.8 ou maior. Voce sera desconectado em 30 segundos.
        ynprompt_show
        src.warnAndDisconnect
    endif

    src.news 1//Chama notícias ignorado se não há novas  

    broadup <src.account.plevel>, Login: <src.account>
    
    src.resistanceInit
    
    //atualiza slots
    src.tam_playerSlots 
    
    //atualiza nota RP
    src.rp_updateGrade
    
    src.timerf 4,form_checkAccStatus
    
    src.fixweight

    src.morte_init
    
//*************************************
ON=@LOGOUT
//*************************************

    //Limpa buffs
    //Limpa buffs e debuffs
    f_CleanBuffs
    
    broadup <src.account.plevel>, Logout: <src.account>
    
    if (<src.account.plevel> <= 1)
        src.emoteyellow [desconectou]
    endif
    
    //log event
    event_logout    
//*************************************
ON=@USERQUESTBUTTON
//*************************************
  quest_userQuests
  return 1

//on=@itemClientTooltip
//SRC.addcliloc 1042971,Race: Goblin
//return 0

//on=@ClientTooltip 
//SRC.addcliloc 1042970,Race: Goblin
//return 0

//*************************************
ON=@ITEMDCLICK
//*************************************

 if (<src.act.type> == t_container) || (<src.act.type> == t_container_locked) || (<src.act.type> == t_door) || (<src.act.type> == t_door_locked) || (<src.act.type> == t_ship_hold) || (<src.act.type> == t_ship_hold_lock)
  if !(<src.isgm>) && (<src.act.distance> > 1)
   src.sysmessage Chegue mais perto.
   return 1
  elif (<src.act.attr>&attr_forsale)
   return <f_aluguel>
  endif
 endif

//*************************************
ON=@ITEMCLICK
//*************************************
  //bloqueia descricao se fora do raio de visao
  if (!<src.act.canseelos>) && (!<src.gm>)
   return 1
  endif

  //testa pra ver se é roupa e checa tamanho da roupa
  if (<src.act.type>==t_clothing)
    src.ctag.csize = <src.clothes_checkSize <SRC.ACT>>
    if ( <src.ctag.csize> == 1 )
      src.act.message <src.act.name> (pequena)
      return 1
    elif ( <src.ctag.csize> == 3 )
      src.act.message <src.act.name> (grande)
      return 1
    else
      return 0
    endif
  endif

//*************************************
ON=@ITEMEQUIP
//*************************************
//Novo sistema de magias
if <act.type>==t_wand
 dspeech +spk_mage
endif

return 0
 //testa pra ver se é roupa e checa tamanho da roupa
 if (<src.act.type>==t_clothing)
   src.ctag.csize = <src.clothes_checkSize <SRC.ACT>>
   if ( <src.ctag.csize> == 2 )
      return 0
   else
     src.sysmessage Nao serve...
     src.act.bounce
     return 1
   endif
 endif

//*************************************
on=@ITEMUNEQUIP
//*************************************
if <act.type>==t_wand
 dspeech -spk_mage
endif

//*************************************
on=@ITEMDROPON_ITEM
//*************************************


//forca correcao das posicoes dos itens no container.
//itens com posicao x=0 estao fazendo com que remocoes da pilha
//nao sejam tratadas corretamente
if <argo.type>==t_container
    argo.timerf 1,fixStackDupBug
endif        

//Vai Stackar?
if (<act.defname>==<argo.defname>)
    if (<argo.cont>)
        if <topobj.findlayer.layer_bankbox> && <argo.inContainer <topobj.findlayer.layer_bankbox>>
            REF2 = <topobj.findlayer.layer_bankbox>
            return <REF2.bank_checkWeight <eval <argo.weight>+<act.weight>>>
        else
            REF1=<argo.cont>
            REF2=<REF1>
            WHILE (<REF1.IsItem>)
//                serv.log [CONT] Tratando de <REF1> com <fval <ref1.cont_Weight>> => <eval <ref1.cont_Weight>+<act.weight>>
                if (<ref1.cont_checkWeight <act.weight>>
                    sysmessagered <REF1.name> esta na sua capacidade máxima. Tire alguma coisa.
                    return 1
                endif
                REF1=<REF1.CONT>
            END
            IF (!<ref2.cont>)
                REF2.timerf 1,update
            endif
        endif
    endif
endif

//*************************************
ON=@SKILLSELECT
//*************************************
//use custom skills aqui, return 0 para aceitar skill ou 1 para anular o uso
//serv.log [SKILLSELECT] Skill: <src.account>/<src.action>
doswitch <argn1>
return 0                    // SKILL_ALCHEMY
return 0                    // SKILL_ANATOMY
return 0                    // SKILL_ANIMAL_LORE
return <sk_itemid>          // SKILL_APPRAISE
return <sk_armslore>        // SKILL_ARMSLORE
return 0                    // SKILL_PARRYING
return 0                    // SKILL_BEGGING
return 0                    // SKILL_Blacksmith
return 0                    // SKILL_BOWCRAFT
return 0                    // SKILL_PEACEMAKING
return 0                    // SKILL_CAMPING
return 0                    // SKILL_CARPENTRY
return 0                    // SKILL_CARTOGRAPHY
return 0                    // SKILL_COOKING
return 0                    // SKILL_DETECTHIDDEN
return 0                    // SKILL_ENTICEMENT
return <sk_evalint>         // Skill_EvalInt
return 0                    // SKILL_HEALING
return 0                    // SKILL_FISHING
return 0                    // SKILL_FORENSICS
return 0                    // SKILL_HERDING
return <sk_hiding>          // SKILL_HIDING 
return 0                    // SKILL_PROVOCATION
return <sk_inscription>     // SKILL_INSCRIPTION
return 0                    // Skill_LockPick
return 0                    // SKILL_MAGERY
return 0                    // Skill_MagicResist
return 0                    // SKILL_TACTICS
return 0                    // SKILL_SNOOPING
return 0                    // SKILL_MUSICIANSHIP
return 0                    // SKILL_POISONING
return 0                    // SKILL_ARCHERY
return 0                    // SKILL_SPIRITSPEAK
return <sk_stealing>        // SKILL_STEALING
return 0                    // SKILL_TAILORING
return 0                    // SKILL_TAMING
return 0                    // SKILL_TASTEID
return 0                    // SKILL_TINKERING
return <sk_tracking>        // SKILL_TRACKING
return 0                    // Skill_Vet
return 0                    // SKILL_SWORDSMANSHIP
return 0                    // SKILL_MACEFIGHTING
return 0                    // SKILL_FENCING
return 0                    // SKILL_WRESTLING
return 0                    // SKILL_LUMBERJACK
return 0                    // SKILL_MINING
return <sk_meditation>      // SKILL_MEDITATION
return 0                    // SKILL_STEALTH
return <sk_removetrap>      // SKILL_REMOVE_TRAP
return 0                    // SKILL_NECROMANCY
return 0                    // SKILL_FOCUS
return 0                    // SKILL_CHIVALRY
return 0                    // SKILL_BUSHIDO
return 0                    // SKILL_NINJITSU
return 0                    // SKILL_SPELLWEAVING
enddo
return 0
 
//*************************************
ON=@SKILLSTART
//*************************************
//use custom skills aqui, return 0 para funcionamento hardcoded ou 1 para anular o uso da skill
//serv.log [SKILLSTART] Skill: <src.account>/<src.action>

doswitch <src.action>
return 0                    // SKILL_ALCHEMY
return <sk_anatomy>         // SKILL_ANATOMY
return <sk_anlore>          // SKILL_ANIMAL_LORE
return 0                    // SKILL_APPRAISE
return <sk_armslore>        // SKILL_ARMSLORE
return 0                    // SKILL_PARRYING
return 1                    // SKILL_BEGGING
return 0                    // SKILL_Blacksmith
return 0                    // SKILL_BOWCRAFT
return 0                    // SKILL_PEACEMAKING
return 0                    // SKILL_CAMPING
return 0                    // SKILL_CARPENTRY
return 0                    // SKILL_CARTOGRAPHY
return 0                    // SKILL_COOKING
return <sk_DetectingHidden> // SKILL_DETECTHIDDEN
return 0                    // SKILL_ENTICEMENT
return 0                    // Skill_EvalInt
return 0                    // SKILL_HEALING
return 0                    // SKILL_FISHING
return 0                    // SKILL_FORENSICS
return 0                    // SKILL_HERDING
return 1                    // SKILL_HIDING 
return 0                    // SKILL_PROVOCATION
return 0                    // SKILL_INSCRIPTION
return 0                    // Skill_LockPick
return 0                    // SKILL_MAGERY
return 0                    // Skill_MagicResist
return 0                    // SKILL_TACTICS
return <sk_snooping>        // SKILL_SNOOPING
return 0                    // SKILL_MUSICIANSHIP
return <sk_poisoning>       // SKILL_POISONING
return 0                    // SKILL_ARCHERY
return 0                    // SKILL_SPIRITSPEAK
return 1                    // SKILL_STEALING
return 0                    // SKILL_TAILORING
return <sk_taming>          // SKILL_TAMING
return <sk_TasteID>         // SKILL_TASTEID
return 0                    // SKILL_TINKERING
return 1                    // SKILL_TRACKING
return 0                    // Skill_Vet
return 0                    // SKILL_SWORDSMANSHIP
return 0                    // SKILL_MACEFIGHTING
return 0                    // SKILL_FENCING
return 0                    // SKILL_WRESTLING
return 0                    // SKILL_LUMBERJACK
return 0                    // SKILL_MINING
return 1                    // SKILL_MEDITATION
return <sk_hiding>          // SKILL_STEALTH
return 1                    // SKILL_REMOVE_TRAP
return 0                    // SKILL_NECROMANCY
return 0                    // SKILL_FOCUS
return 1                    // SKILL_CHIVALRY
return 1                    // SKILL_BUSHIDO
return 1                    // SKILL_NINJITSU
return 0                    // SKILL_SPELLWEAVING
enddo
return 0

//*************************************
ON=@SKILLSUCCESS
//*************************************
//serv.log [SKILLSUCCESS] <src.account>/<src.action>/<src.action>/<argn1> <serv.skill.<argn1>.name>

doswitch <argn1>
return 0                    // SKILL_ALCHEMY
return 0                    // SKILL_ANATOMY
return 0                    // SKILL_ANIMAL_LORE
return 0                    // SKILL_APPRAISE
return 0                    // SKILL_ARMSLORE
return 0                    // SKILL_PARRYING
return 0                    // SKILL_BEGGING
return 0                    // SKILL_Blacksmith
return 0                    // SKILL_BOWCRAFT
skill_gain <src.action>     // SKILL_PEACEMAKING
return 0                    // SKILL_CAMPING
return 0                    // SKILL_CARPENTRY
skill_gain <src.action>     // SKILL_CARTOGRAPHY
return 0                    // SKILL_COOKING
return 0                    // SKILL_DETECTHIDDEN
skill_gain <src.action>     // SKILL_ENTICEMENT
skill_gain <src.action>     // Skill_EvalInt
return 0                    // SKILL_HEALING
skill_repeat <argn1>,1      // SKILL_FISHING
skill_gain <src.action>     // SKILL_FORENSICS
return 0                    // SKILL_HERDING
return 0                    // SKILL_HIDING 
skill_gain <src.action>     // SKILL_PROVOCATION
skill_gain <src.action>     // SKILL_INSCRIPTION
return 0                    // Skill_LockPick
return <mage_success>       // SKILL_MAGERY
skill_gain <src.action>     // Skill_MagicResist
return 0                    // SKILL_TACTICS
skill_gain <src.action>     // SKILL_SNOOPING
return 0                    // SKILL_MUSICIANSHIP
return 0                    // SKILL_POISONING
return 0                    // SKILL_ARCHERY
skill_gain <src.action>     // SKILL_SPIRITSPEAK
return 0                    // SKILL_STEALING
return 0                    // SKILL_TAILORING
return 0                    // SKILL_TAMING
return 0                    // SKILL_TASTEID
return 0                    // SKILL_TINKERING
return 0                    // SKILL_TRACKING
skill_gain <src.action>     // Skill_Vet
skill_gain <src.action>     // SKILL_SWORDSMANSHIP
skill_gain <src.action>     // SKILL_MACEFIGHTING
skill_gain <src.action>     // SKILL_FENCING
skill_gain <src.action>     // SKILL_WRESTLING
skill_repeat <argn1>,1      // SKILL_LUMBERJACK
skill_repeat <argn1>,1      // SKILL_MINING
return 0                    // SKILL_MEDITATION
return 0                    // SKILL_STEALTH
skill_gain <src.action>     // SKILL_REMOVE_TRAP
return <mage_success>       // SKILL_NECROMANCY
return <mage_success>       // SKILL_FOCUS
return 0                    // SKILL_CHIVALRY
return 0                    // SKILL_BUSHIDO
return 0                    // SKILL_NINJITSU
return <mage_success>       // SKILL_SPELLWEAVING
enddo
return 0

//*************************************
ON=@SKILLFAIL
//*************************************
//MANTER!!! Sera usado tambem nas skills de magia!
//serv.log [SKILLFAIL] <src.account>/<src.action>

doswitch <argn1>
return 0                    // SKILL_ALCHEMY
return 0                    // SKILL_ANATOMY
return 0                    // SKILL_ANIMAL_LORE
return 0                    // SKILL_APPRAISE
return 0                    // SKILL_ARMSLORE
return 0                    // SKILL_PARRYING
return 0                    // SKILL_BEGGING
return 0                    // SKILL_Blacksmith
return 0                    // SKILL_BOWCRAFT
return 0                    // SKILL_PEACEMAKING
return 0                    // SKILL_CAMPING
return 0                    // SKILL_CARPENTRY
return 0                    // SKILL_CARTOGRAPHY
return 0                    // SKILL_COOKING
return 0                    // SKILL_DETECTHIDDEN
return 0                    // SKILL_ENTICEMENT
return 0                    // Skill_EvalInt
return 0                    // SKILL_HEALING
skill_repeat <argn1>,1      // SKILL_FISHING
return 0                    // SKILL_FORENSICS
return 0                    // SKILL_HERDING
return 0                    // SKILL_HIDING 
return 0                    // SKILL_PROVOCATION
return 0                    // SKILL_INSCRIPTION
return 0                    // Skill_LockPick
mage_fail                   // SKILL_MAGERY
return 0                    // Skill_MagicResist
return 0                    // SKILL_TACTICS
return 0                    // SKILL_SNOOPING
return 0                    // SKILL_MUSICIANSHIP
return 0                    // SKILL_POISONING
return 0                    // SKILL_ARCHERY
return 0                    // SKILL_SPIRITSPEAK
return 0                    // SKILL_STEALING
return 0                    // SKILL_TAILORING
return 0                    // SKILL_TAMING
return 0                    // SKILL_TASTEID
return 0                    // SKILL_TINKERING
return 0                    // SKILL_TRACKING
return 0                    // Skill_Vet
return 0                    // SKILL_SWORDSMANSHIP
return 0                    // SKILL_MACEFIGHTING
return 0                    // SKILL_FENCING
return 0                    // SKILL_WRESTLING
skill_repeat <argn1>,1      // SKILL_LUMBERJACK
skill_repeat <argn1>,1      // SKILL_MINING
return 0                    // SKILL_MEDITATION
return 0                    // SKILL_STEALTH
return 0                    // SKILL_REMOVE_TRAP
mage_fail                   // SKILL_NECROMANCY
mage_fail                   // SKILL_FOCUS
return 0                    // SKILL_CHIVALRY
return 0                    // SKILL_BUSHIDO
return 0                    // SKILL_NINJITSU
mage_fail                   // SKILL_SPELLWEAVING
enddo
return 0

//*************************************
ON=@SKILLABORT
//*************************************
//serv.log [ABORT] Skill: <src.account>/<src.action>

doswitch <argn1>
return 0                    // SKILL_ALCHEMY
return 0                    // SKILL_ANATOMY
return 0                    // SKILL_ANIMAL_LORE
return 0                    // SKILL_APPRAISE
return 0                    // SKILL_ARMSLORE
return 0                    // SKILL_PARRYING
return 0                    // SKILL_BEGGING
return 0                    // SKILL_Blacksmith
return 0                    // SKILL_BOWCRAFT
return 0                    // SKILL_PEACEMAKING
return 0                    // SKILL_CAMPING
return 0                    // SKILL_CARPENTRY
return 0                    // SKILL_CARTOGRAPHY
return 0                    // SKILL_COOKING
return 0                    // SKILL_DETECTHIDDEN
return 0                    // SKILL_ENTICEMENT
return 0                    // Skill_EvalInt
return 0                    // SKILL_HEALING
return 0                    // SKILL_FISHING
return 0                    // SKILL_FORENSICS
return 0                    // SKILL_HERDING
return 0                    // SKILL_HIDING 
return 0                    // SKILL_PROVOCATION
return 0                    // SKILL_INSCRIPTION
return 0                    // Skill_LockPick
mage_clearTags              // SKILL_MAGERY
return 0                    // Skill_MagicResist
return 0                    // SKILL_TACTICS
return 0                    // SKILL_SNOOPING
return 0                    // SKILL_MUSICIANSHIP
return 0                    // SKILL_POISONING
return 0                    // SKILL_ARCHERY
return 0                    // SKILL_SPIRITSPEAK
return 0                    // SKILL_STEALING
return 0                    // SKILL_TAILORING
return 0                    // SKILL_TAMING
return 0                    // SKILL_TASTEID
return 0                    // SKILL_TINKERING
return 0                    // SKILL_TRACKING
return 0                    // Skill_Vet
return 0                    // SKILL_SWORDSMANSHIP
return 0                    // SKILL_MACEFIGHTING
return 0                    // SKILL_FENCING
return 0                    // SKILL_WRESTLING
return 0                    // SKILL_LUMBERJACK
return 0                    // SKILL_MINING
return 0                    // SKILL_MEDITATION
return 0                    // SKILL_STEALTH
return 0                    // SKILL_REMOVE_TRAP
mage_clearTags              // SKILL_NECROMANCY
mage_clearTags              // SKILL_FOCUS
return 0                    // SKILL_CHIVALRY
return 0                    // SKILL_BUSHIDO
return 0                    // SKILL_NINJITSU
mage_clearTags              // SKILL_SPELLWEAVING
enddo
return 0

//*************************************
//ON=@SKILLUSEQUICK
//*************************************
//serv.log [SKILLUSEQUICK] <src.account>/<argn1>

//*************************************
ON=@USERVIRTUE
//*************************************
//estatisticas do char
virtue_bt

//*************************************
ON=@RENAME
//*************************************
if <argo.isonline>
    timerf 1,uid.<argo>.update
endif

//*************************************
ON=@HUNGER
//*************************************
//disabled since sphere v18-02-2014
f_feelHungy
return 1

ON=@RegenStat
if (<local.StatID> == 3) //food
//    serv.log regenfood <src.tag0.name> <serv.time> <dlocal.Value> / <dlocal.StatLimit>
    f_feelHungy
    return 1
endif

//*************************************
ON=@PROFILE
//*************************************
//if !<src.rec_canSee <uid>> && !<isgm>
//    src.sysmessageyellow Nao consegue ver...
//    return 1
//endif  
src.ctag.ficharp_uid=<uid>
src.dialogclose d_ficharp   //both dialogs use the same uid
src.dialogclose d_profile
src.dialog d_profile
return 1

//*************************************
ON=@MOUNT
//*************************************
//verifica se o PC esta sobre efeito do SM dismount
if <serv.time> < <ctag0.nomount>
	sysmessageyellow O animal esta muito agitado
	return 1
endif
//mounts sem exlusividade
if (<argo.id>==c_llama) || (<argo.id>!=c_bear_polar)
    return 0
//mounts exclusivas de anao
elif (<argo.id>==c_besouro_deserto) || (<argo.id>==c_cao_jathiir)
    if !strmatch(<tag.raca>,anao)
        emotered Caiu do animal
        sysmessageyellow O animal nao te aceita...
        return 1
    endif
//mounts exclusivas de drow    
elif <argo.id>==c_besouro_gigante
    if !strmatch(<tag.raca>,drow)
        emotered Caiu do besouro
        sysmessageyellow O animal nao te aceita...
        return 1
    endif
//nao eh mount exclusiva    
else
    //anoes nao podem montar
    if strmatch(<tag.raca>,anao)
        emotered Caiu da montaria
        sysmessageyellow Suas pernas sao pequenas demais para montar o animal...
        return 1
    endif
endif

//*************************************
on=@SpellSelect
//*************************************
//Aumenta a dificuldade da magia em 4.5 por tile andado. Ver função MAGE_SUCCESS em sistema_de_magia.scp

//Novo sistema de magias
IF (!<IsGM>)

 IF (<tag0.mage_spellid>) && (<argn3>==3)//Foi cancelada e começada outra magia posteriormente?
//  serv.log SPELLSELECT 3. Aborta. <account>
  mage_cleartags
  argn3=2        //Começar a castar de novo
  return 6
 elif (<argn1> < 99 ) && (<account.plevel> < 2)
//  serv.log SPELLSELECT Aborta cast por macro. <account>
  return 1
 endif
 
 ENDIF
 
 IF (<argn3>==3)           //Iniciar
 
  //Guarda se o char sabe a spell e quam é a spell
  tag.mage_know=<mage_knowSpell <argn1>>
  tag.mage_spellid=<argn1>
  
  //Define bônus/penalidade por modalidade de cast (WoP, wand/orb, scroll)
  if (<argo>==<uid>)      //Mago castando sozinho
   tag.mage_source=<DEF.MAGE_SELF>
   if !(<tag0.mage_know>)
    argn2=(<argn2>*125)/100       //consimir 25% mais de mana
   endif
  else
   tag.mage_uid=<argo>            //Salva item
   argn2=0                        //Castando por item. Não usar mana.
   if (<argo.type>==t_wand)//Mago castando por varinha ou gema
    tag.mage_source=<DEF.MAGE_WAND>
   else
    if (<argo.type>==t_SCROLL)//Mago castando por pergaminho
     tag.mage_source=<DEF.MAGE_SCROLL>
     if (!<tag0.mage_know>)//Se mago não sabe magia do pergaminho, gasta mana.
      argn2=<serv.spell.<argn1>.manause>
     endif
    endif
   endif
  endif
  IF (<Mana> < <argn2>)
   sysmessageyellow Falta-lhe mana suficiente para esta magia...
   return 1
  endif
  tag.mage_mana=<argn2>
  tag.mage_p=<p>
 ENDIF
// serv.log SPELLSELECT <argn1> (<serv.spell.<argn1>.name>) - <argn2> - <argo.name> - Fase: <argn3>
 return 6
ENDIF

//*************************************
on=@spellcast
//*************************************

IF (!<IsGM>)
 if (<flags>&statf_invisible)
  flags ^= statf_invisible
  sysmessagegreen Voce foi revelado!
  emotered aparece
  update
 endif
 
 //Novo sistema de magias
 local.skill=<src.<serv.skill.<DEF.<streat <serv.spell.<argn1>.skillreq>>>.key>>
 IF <tag0.mage_source>==<DEF.MAGE_SELF>  //Mago castando via WoP
  if !<tag0.mage_know>                   //Não sabe a magia
   argn2=(<argn2>*125)/100
  endif
 ELIF <tag0.mage_source>==<DEF.MAGE_SCROLL>//Mago castando via scroll
   if <tag0.mage_know>                                                            //Sabe a magia
//   serv.log sabe a spell
   argn2=(<strarg <serv.spell.<argn1>.skillreq>>*100)/125                         //Bônus de 25%
  elif (<inscription> > <local.skill>)   //Não sabe a magia. Usar Inscription?
   if (<skillcheck inscription <eval <strarg <serv.spell.<argn1>.skillreq>>/10>>)
    argn2=0      //Passou no teste com inscription
   else
    argn2=1500   //Não passou no teste de inscription
   endif
  else
//   serv.log nao sabe a spell
   argn2=<eval <strarg <serv.spell.<argn1>.skillreq>>/10>        //Barra a diff hard coded de t_scroll
  endif
 ELIF (<tag0.mage_source>==<DEF.MAGE_GEM>) || (<tag0.mage_source>==<DEF.MAGE_WAND>)//Mago castando via gema/varinha encantada
 argn2=0//Não tem chance de falha
//  serv.log Gema/varinha
 ENDIF
 //Define chance de falha por andar
 
 tag.mage_actdiff=<argn2>
// serv.log SPELLCAST s: <argn1> (<serv.spell.<argn1>.name>) - d: <argn2> - ad: <argn3> - tag.actdiff: <dtag.mage_actdiff> - <argo.name>
 return 6
ENDIF

//*************************************
//on=@destroy
//*************************************

//*************************************
ON=@MurderMark
//*************************************
argn1=0         //Sem murders
argn2=0         //Não fica criminal

//*************************************
ON=@Criminal
//*************************************
return 1

//*****************************************************************************
// e_scared
//*****************************************************************************
//usado tanto em NPCs como em PCs
[EVENTS e_scared]
on=@NPCActFight
argn2=-20

on=@USERWARMODE
sysmessagered Voce esta muito assustado para lutar.
flags &= ~statf_war
src.emotered aterrorizado
return 1

on=@SpellSelect
sysmessagered Voce esta muito assustado para se concentrar.
src.emotered com muito medo
return 1

ON=@SKILLSTART
sysmessagered Voce esta muito assustado para se concentrar.
src.emotered tremendo de medo 
return 1

//*****************************************************************************
// e_exceptional_item
//*****************************************************************************
[TYPEDEF e_exceptional_item]

//*****************************************************************************
// e_weapon_silver
//*****************************************************************************
[TYPEDEF e_weapon_silver]
ON=@CDAMAGE
//SRC é quem toma o dano.
if (<src.npc>==brain_undead)
//    serv.log [<name>] -> <src.name> (<R2,<BETWEEN 2,15,-<src.karma>,5000>>)
    src.damage <R2,<BETWEEN 2,15,-<src.karma>,5000>>
    src.effect 3 i_fx_bless_effect 0 32 0 53
endif

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// f_onaccount_login
//*****************************************************************************
[FUNCTION f_onaccount_login]

if (<argn1>==3)

    LOCAL.chars=<SERV.ACCOUNT.<args>.CHARS>
    
    //troca os nomes dos chars pelos verdadeiros
    if (<LOCAL.chars>>0)
        LOCAL.chars=<eval <LOCAL.chars>-1>
        for R 0 <LOCAL.chars>
            OBJ=<SERV.ACCOUNT.<args>.CHAR.<LOCAL.R>>
            if (!<IsEmpty <OBJ.tag.name>>)
                OBJ.name=<OBJ.tag.name>
//            else
//                OBJ.tag.name=<OBJ.name>
            endif
        end
        
        LOCAL.chars += 1
    endif
    
    //cria char extras para facilitar criacao do personagem
//    if <LOCAL.chars> < <SERV.ACCOUNT.<args>.MAXCHARS>
//        LOCAL.chars = <eval <SERV.ACCOUNT.<args>.MAXCHARS>-<LOCAL.chars>>
//
//        for R 0 <LOCAL.chars>
//            serv.newnpc c_man
//            new.account=<args>
//            new.p=<DEF.NEWCHAR_SP>
//            new.name=Novo Personagem
//            new.tag.name=Novo Personagem
//        end
//    endif
    
endif

//*****************************************************************************
// fixStackDupBug
//*****************************************************************************
//forca correcao das posicoes dos itens no container.
//intes com posicao x=0 estao fazendo com que remocoes da pilha
//nao sejam tratadas corretamente
[FUNCTION fixStackDupBug]
    
FORCONT <uid>
    LOCAL.fix=0
    LOCAL.x=<p.x>
    LOCAL.y=<p.y>
    if (!<p.x>) || (<p.x>>250)
        LOCAL.x=1
        LOCAL.fix=1
    endif
    if (!<p.y>) || (<p.y>>250)
        LOCAL.y=1
        LOCAL.fix=1
    endif

    if <LOCAL.fix>
//        f_gmLog fix <p> para <LOCAL.x>,<LOCAL.y>
            contp=<LOCAL.x>,<LOCAL.y>
//        f_gmLog para verify <contp>
        endif
ENDFOR

[EOF]

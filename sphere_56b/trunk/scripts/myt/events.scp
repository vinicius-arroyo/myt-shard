//*****************************************************************************
//*****************************************************************************
//
// EVENTOS RACIAIS por
//  Galthar
//  Metus
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:
//
//TAGS usadas:
//src.tag.drowLowLightBonus //1 with bonus active, 0 otherwise
//src.tag.resisteaoclima
//src.tag.raca
//src.tag.bank.weight
//*****************************************************************************


//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME charCreation]
//regenerates one mana point every n seconds
ev_elf_mana_timer=3
//check drow bonuses every n seconds
ev_drow_check_timer=5
//regenerates one stam point every n seconds
ev_dwarf_stam_timer=3

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// T_EQ_HORSE
//*****************************************************************************
[TYPEDEF T_EQ_HORSE]
ON=@DESTROY
//limpa modificador de regeneracao de stamina
//tempo
LINK.TAG.OVERRIDE.REGEN_2  
//amount
LINK.TAG.OVERRIDE.REGENVAL_2

//o sphere nao chama @CREATE do t_eq_horse, entao chamamos essa funcao direto do item
//para ajustar a regeneracao de stamina
[FUNCTION T_EQ_HORSE_CREATE]
//tempo
SRC.ACT.TAG.OVERRIDE.REGEN_2 1
//amount
SRC.ACT.TAG.OVERRIDE.REGENVAL_2 10

//*****************************************************************************
// ELFOS
//*****************************************************************************
//Elfos possuem visao noturna e mana acelerada
[EVENTS e_elfo]

ON=@LOGIN
src.nightsight 0
f_elfSight
findid.i_elfo_mana.remove
newitem=i_elfo_mana
act.link <src.uid>
act.cont <src.uid>

//mantem o layer light livre para visao noturna
ON=@ITEMUNEQUIP
if ( <act.layer> != layer_light )&& ( <act.layer> != layer_hand2 )
    return 0
endif
f_elfSight
return 1

[ITEMDEF i_elfo_mana]
id=i_deed
name=Elfo - Mana
type=t_eq_script
layer=layer_special
weight=0

on=@create
timer=ev_elf_mana_timer
return 1

on=@timer
if (<link.mana> < <eval <link.int>-10>)
  link.mana=<link.mana>+1
endif
timer=ev_elf_mana_timer
return 1

//carrega um light source no layer light em q soh o player (src) veja
[FUNCTION f_elfSight]
SENDPACKET 02e D040000001 W<serv.itemdef.i_light_source.id> B0 B<def.layer_light> D<src> W00

//*****************************************************************************
// DROWS
//*****************************************************************************
//Drows tem visao noturna preferenciada, ganham bonus no escuro e sofrem danos
//no claro.
[EVENTS e_drow]

ON=@LOGIN
src.nightsight 0
f_elfSight
src.findid.i_drow_check.remove
serv.newitem=i_drow_check
new.link <src>
new.cont <src>

//mantem o layer light livre para visao noturna
ON=@ITEMUNEQUIP
if ( <act.layer> != layer_light )&& ( <act.layer> != layer_hand2 )
    return 0
endif
f_elfSight
return 1

[ITEMDEF i_drow_check]
id=i_deed
name=Drow - Check
type=t_eq_script
layer=layer_special
weight=0

on=@create
timer=ev_drow_check_timer

on=@timer

if ( <var.tHour> >= 19 ) || ( <var.tHour> <= 5 ) || (<region.flags>&region_flag_underground)
  if (<link.tag.drowLowLightBonus> != 1)
    link.tag.drowLowLightBonus=1
//    link.nightsight 1
    link.hiding=<link.hiding>+10.0
    link.stealth=<link.stealth>+5.0
    link.magicresistance=<link.magicresistance>+5.0
    link.dex=<link.dex>+20
    link.sysmessageyellow Voce se sente mais forte no escuro...
  endif
else
  if (<link.tag.drowLowLightBonus> != 0)
    link.tag.drowLowLightBonus=0
//    link.nightsight 0
    link.hiding=<link.hiding>-10.0
    link.stealth=<link.stealth>-5.0
    link.magicresistance=<link.magicresistance>-5.0
    link.dex=<link.dex>-20
    link.sysmessageyellow A luz esta debilitando seu corpo...
  endif
 
  if (<link.hits> > 5)
    link.damage <R10> dam_poison
  endif
endif

timer=ev_drow_check_timer
return 1

//*****************************************************************************
// ORCS
//*****************************************************************************
//Orcs possuem mascara fixa (cara de orc) e não podem usar capacetes. 
//Quando são atingidos e estão com poucos pontos de vida dão dano no inimigo.
[EVENTS e_orc]

on=@login
src.findlayer(layer_hair).remove
src.findlayer(layer_beard).remove
src.findlayer(layer_helm).hitpoints 999
src.update

on=@hit
if (<hits> < 20)
  say * Em Furia *
  src.damage {5 10}
endif

[ITEMDEF i_head_orc]
id=0141b
name=Orc
TYPE=t_armor
VALUE=0
WEIGHT=0
ARMOR=50

ON=@Create
COLOR=0597
HITPOINTS=9999
attr=attr_static|attr_move_never

//*****************************************************************************
// ANOES
//*****************************************************************************
//Anoes resistem ao clima e tem regeneracao de stamina acelerada
//-Caem ao subir em cavalos
//-Se queimao ao tocar itens magicos
[EVENTS e_anao]

on=@login
src.tag.resisteaoclima=1

src.findid.i_anao_stam.remove
src.newitem=i_anao_stam
src.act.link <src.uid>
src.act.cont <src.uid>

src.findid.i_no_mounting.remove
src.newitem=i_no_mounting
src.act.link <src.uid>
src.act.cont <src.uid>

on=@itemDclick

if (<act.attr>&attr_magic) && (!<src.isgm>)
 src.say * Se queimou *
 src.damage {10 15}
 act.p=<src.p>
endif

on=@itemEquip
if (<act.attr>&attr_magic) && (<src.isgm>==0)
 src.say * Se queimou *
 src.damage {10 15}
 act.p=<src.p>
endif


[ITEMDEF i_anao_stam]
id=i_deed
name=Anao - Stam
type=t_eq_script
layer=layer_special
weight=0

on=@create
timer=ev_dwarf_stam_timer
return 1

on=@timer
if (<link.stam> < <eval <link.dex>-10>)
  link.stam=<link.stam>+1
endif
timer=ev_dwarf_stam_timer
return 1


[ITEMDEF i_no_mounting]
id=i_deed
name=No - Mounting
type=t_eq_script
layer=layer_special
weight=0

on=@create
timer=2
return 1

on=@timer
if (<link.findlayer(layer_horse)>)
 link.say * Caiu da montaria *
 link.sleep
 link.sysmessage Suas pernas sao pequenas demais para montar o animal...
endif
timer=2
return 1

//*****************************************************************************
// HUMANOS
//*****************************************************************************
[EVENTS e_humano]


//*****************************************************************************
// ARQUEIRO
//*****************************************************************************
[EVENTS e_arqueiro]

//*****************************************************************************
// GUERREIRO
//*****************************************************************************
[EVENTS e_guerreiro]

//*****************************************************************************
// MAGO
//*****************************************************************************
[EVENTS e_mago]

on=@SpellSelect
//Aumenta a dificuldade da magia em 10.0% por tile andado.
IF (<argn3>==3)
 ctag.actp=<p>
ELIF (<argn3>==2)
 LOCAL.penalty=<eval (<distance <ctag.actp>>*100)+<strarg <serv.spell.<argn1>.skillreq>>>
 IF (!<BELLTEST <magery>,<dLOCAL.penalty>>) && (<LOCAL.penalty>!=<actdiff>) //O ultimo teste representa SE o playa andou ou não. Se não andou, ignorar o teste.
  action=-1
  return 1
 ENDIF
endif

on=@spellcast
if (<flags>&statf_invisible) && (!<IsGM>)
 flags ^= statf_invisible
 sysmessagegreen Voce foi revelado!
 emotered aparece
 update
endif

//*****************************************************************************
// BARDO
//*****************************************************************************
[EVENTS e_bardo]

//*****************************************************************************
// LADRAO
//*****************************************************************************
[EVENTS e_ladrao]

//*****************************************************************************
// ARTESAO
//*****************************************************************************
[EVENTS e_artesao]

//*****************************************************************************
// LENHADOR
//*****************************************************************************
[EVENTS e_lenhador]

//*****************************************************************************
// FERREIRO
//*****************************************************************************
[EVENTS e_ferreiro]

//*****************************************************************************
// EVENTO GERAL PARA TODOS CHARS
//*****************************************************************************
[EVENTS e_mytgeral]
//*************************************
ON=@LOGIN
//*************************************
    src.skill_init
    src.name=<src.tag.raca>
    src.PUT_RP
    src.fame=0
    src.karma=0
//    src.title " "
    
    src.flags=<src.flags>&~statf_freeze
    src.f_update_hair
    src.f_update_beard

    //log event
    event_login

    //entrega mensagens pendentes
    f_deliverPendingMessages

    src.f_correctLight <src.region.flags>
    update    
    
    if (!<checkVersion>)
        serv.log [EVENTS] Cliente desconectado por versao invalida: <CLIENTVERSION>
        src.sysmessagered ****************************************************Versao de client deve ser 5.0.8 ou maior.
        src.sysmessagered ****************************************************
        src.warnAndDisconnect
    endif

    src.scroll SCROLL_MOTD  

    if (<src.account.plevel> > 1)
        serv.allclients broadstaff GM LOGIN,"<src.account>"
    endif
    
//*************************************
ON=@LOGOUT
//*************************************
    
    if (<src.account.plevel> > 1)
        serv.allclients broadstaff GM LOGOUT,"<src.account>"
    else
        src.emoteyellow [desconectou]
    endif
    
    //log event
    event_logout    
//*************************************
ON=@USERQUESTBUTTON
//*************************************
  quest_userQuests
  return 1

//on=@itemClientTooltip
//SRC.addcliloc 1042971,Race: Goblin
//return 0

//on=@ClientTooltip 
//SRC.addcliloc 1042970,Race: Goblin
//return 0

//*************************************
ON=@ITEMDCLICK
//*************************************

 if (<src.act.type>==t_meat_raw) && !(strmatch(<src.tag.raca>,Orc))
   src.sysmessagered Isso deve ser cozinhado antes de consumido.
   return 1
 endif

 if (<src.act.type> == t_container) || (<src.act.type> == t_container_locked) || (<src.act.type> == t_door) || (<src.act.type> == t_door_locked) || (<src.act.type> == t_ship_hold) || (<src.act.type> == t_ship_hold_lock)
  if !(<src.isgm>) && (<src.act.distance> > 1)
   src.sysmessage Chegue mais perto.
   return 1
  elif (<src.act.attr>&attr_forsale)
   return <f_aluguel>
  elif (<src.act.type>==t_door_locked) || (<src.act.type>==t_container_locked) //Barrar o HC de abrir se tiver chave.
   sysmessageyellow Trancado...
   return <eval !<src.isgm>>
  endif
 endif

//*************************************
ON=@ITEMCLICK
//*************************************
  //bloqueia descricao se fora do raio de visao
  if (!<src.act.canseelos>) && (!<src.gm>)
   return 1
  endif

  //testa pra ver se é roupa e checa tamanho da roupa
  if (<src.act.type>==t_clothing)
    src.ctag.csize = <src.clothes_checkSize <SRC.ACT>>
    if ( <src.ctag.csize> == 1 )
      src.act.message <src.act.name> (pequena)
      return 1
    elif ( <src.ctag.csize> == 3 )
      src.act.message <src.act.name> (grande)
      return 1
    else
      return 0
    endif
  endif

//*************************************
ON=@ITEMEQUIP
//*************************************
return 0
 //testa pra ver se é roupa e checa tamanho da roupa
 if (<src.act.type>==t_clothing)
   src.ctag.csize = <src.clothes_checkSize <SRC.ACT>>
   if ( <src.ctag.csize> == 2 )
      return 0
   else
     src.sysmessage Nao serve...
     src.act.bounce
     return 1
   endif
 endif
 
//*************************************
ON=@SKILLSTART
//*************************************
//use custom skills aqui, return 0 para funcionamento hardcoded ou 1 para anular o uso da skill
//serv.log [SKILLSTART] Skill: <src.account>/<src.action>
doswitch <src.action>
return 0                    // SKILL_ALCHEMY
return <sk_anatomy>         // SKILL_ANATOMY
return <sk_anlore>          // SKILL_ANIMALLORE
return 0                    // SKILL_ITEMID
return <sk_armslore>        // SKILL_ARMSLORE
return 0                    // SKILL_PARRYING
return 1                    // SKILL_BEGGING
return 0                    // SKILL_BLACKSMITHING
return 0                    // SKILL_BOWCRAFT
return 0                    // SKILL_PEACEMAKING
return 0                    // SKILL_CAMPING
return 0                    // SKILL_CARPENTRY
return 0                    // SKILL_CARTOGRAPHY
return 0                    // SKILL_COOKING
return <sk_DetectingHidden> // SKILL_DETECTHIDDEN
return 0                    // SKILL_ENTICEMENT
return 0                    // SKILL_EVALUATINGINTEL
return 0                    // SKILL_HEALING
return 0                    // SKILL_FISHING
return 0                    // SKILL_FORENSICS
return 0                    // SKILL_HERDING
return <sk_hiding>          // SKILL_HIDING 
return 0                    // SKILL_PROVOCATION
return 0                    // SKILL_INSCRIPTION
return 0                    // SKILL_LOCKPICKING
return 0                    // SKILL_MAGERY
return 0                    // SKILL_MAGICRESISTANCE
return 0                    // SKILL_TACTICS
return 0                    // SKILL_SNOOPING
return 0                    // SKILL_MUSICIANSHIP
return <sk_poisoning>       // SKILL_POISONING
return 0                    // SKILL_ARCHERY
return 0                    // SKILL_SPIRITSPEAK
return 0                    // SKILL_STEALING
return 0                    // SKILL_TAILORING
return <sk_taming>          // SKILL_TAMING
return <sk_TasteID>         // SKILL_TASTEID
return 0                    // SKILL_TINKERING
return 0                    // SKILL_TRACKING
return 0                    // SKILL_VETERINARY
return 0                    // SKILL_SWORDSMANSHIP
return 0                    // SKILL_MACEFIGHTING
return 0                    // SKILL_FENCING
return 0                    // SKILL_WRESTLING
return 0                    // SKILL_LUMBERJACK
return 0                    // SKILL_MINING
return 0                    // SKILL_MEDITATION
return <sk_hiding>          // SKILL_STEALTH
return 0                    // SKILL_REMOVETRAP
return 1                    // SKILL_NECROMANCY
return 1                    // SKILL_FOCUS
return 1                    // SKILL_CHIVALRY
return 1                    // SKILL_BUSHIDO
return 1                    // SKILL_NINJITSU
return 1                    // SKILL_SPELLWEAVING
enddo
return 0

//*************************************
ON=@SKILLSUCCESS
//*************************************
serv.log [SKILLSUCCESS] <src.account>/<src.action>/<eval <src.action>>

doswitch <src.action>
return 0                    // SKILL_ALCHEMY
return 0                    // SKILL_ANATOMY
return 0                    // SKILL_ANIMALLORE
return 0                    // SKILL_ITEMID
return 0                    // SKILL_ARMSLORE
return 0                    // SKILL_PARRYING
return 0                    // SKILL_BEGGING
return 0                    // SKILL_BLACKSMITHING
return 0                    // SKILL_BOWCRAFT
skill_gain <src.action>     // SKILL_PEACEMAKING
return 0                    // SKILL_CAMPING
return 0                    // SKILL_CARPENTRY
skill_gain <src.action>     // SKILL_CARTOGRAPHY
return 0                    // SKILL_COOKING
return 0                    // SKILL_DETECTHIDDEN
skill_gain <src.action>     // SKILL_ENTICEMENT
skill_gain <src.action>     // SKILL_EVALUATINGINTEL
return 0                    // SKILL_HEALING
return 0                    // SKILL_FISHING
skill_gain <src.action>     // SKILL_FORENSICS
return 0                    // SKILL_HERDING
return 0                    // SKILL_HIDING 
return 0                    // SKILL_PROVOCATION
skill_gain <src.action>     // SKILL_INSCRIPTION
return 0                    // SKILL_LOCKPICKING
return 0                    // SKILL_MAGERY
skill_gain <src.action>     // SKILL_MAGICRESISTANCE
return 0                    // SKILL_TACTICS
skill_gain <src.action>     // SKILL_SNOOPING
return 0                    // SKILL_MUSICIANSHIP
return 0                    // SKILL_POISONING
return 0                    // SKILL_ARCHERY
skill_gain <src.action>     // SKILL_SPIRITSPEAK
return 0                    // SKILL_STEALING
return 0                    // SKILL_TAILORING
return 0                    // SKILL_TAMING
return 0                    // SKILL_TASTEID
return 0                    // SKILL_TINKERING
skill_gain <src.action>     // SKILL_TRACKING
skill_gain <src.action>     // SKILL_VETERINARY
skill_gain <src.action>     // SKILL_SWORDSMANSHIP
skill_gain <src.action>     // SKILL_MACEFIGHTING
skill_gain <src.action>     // SKILL_FENCING
skill_gain <src.action>     // SKILL_WRESTLING
skill_gain <src.action>     // SKILL_LUMBERJACK
skill_gain <src.action>     // SKILL_MINING
skill_gain <src.action>     // SKILL_MEDITATION
return 0                    // SKILL_STEALTH
skill_gain <src.action>     // SKILL_REMOVETRAP
return 0                    // SKILL_NECROMANCY
return 0                    // SKILL_FOCUS
return 0                    // SKILL_CHIVALRY
return 0                    // SKILL_BUSHIDO
return 0                    // SKILL_NINJITSU
return 0                    // SKILL_SPELLWEAVING
enddo
return 0

//*************************************
//ON=@SKILLFAIL
//*************************************
//serv.log [SKILLFAIL] <src.account>/<src.action>

//*************************************
//ON=@SKILLUSEQUICK
//*************************************
//serv.log [SKILLUSEQUICK] <src.account>/<argn1>

//*************************************
ON=@USERVIRTUE
//*************************************
//Diálogos de magia ou combate
IF (STRMATCH(<tag.classe>,Mago))
 mago
ELIF (STRMATCH(<tag.classe>,Bardo))
 bardo
ELSE
 combate
ENDIF

//*************************************
ON=@RENAME
//*************************************
timerf 1,uid.<argo>.update

//*************************************
ON=@HUNGER
//*************************************
f_feelHungy
return 1

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// f_onaccount_login
//*****************************************************************************
[FUNCTION f_onaccount_login]
//apos senha (argn1=3) troca os nomes dos chars pelos verdadeiros
if (<argn1>==3)
    LOCAL.chars=<SERV.ACCOUNT.<args>.CHARS>
    if (<LOCAL.chars>>0)
        LOCAL.chars=<eval <LOCAL.chars>-1>
        for R 0 <LOCAL.chars>
            OBJ=<SERV.ACCOUNT.<args>.CHAR.<LOCAL.R>>
            OBJ.name=<OBJ.tag.name>
        end
    endif
endif


[EOF]

//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE FICHA por 
//      Galthar 
//      Pomp
//      UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:

//TAGS usadas:


//CREATE TABLE  `mytserver`.`rpGrade` (
//  `idGrade` int(10) unsigned NOT NULL auto_increment,
//  `timestamp` timestamp NOT NULL default CURRENT_TIMESTAMP on update CURRENT_TIMESTAMP,
//  `charUid` int(10) unsigned NOT NULL,
//  `charAcct` int(10) unsigned NOT NULL,
//  `gmUid` int(10) unsigned NOT NULL,
//  `gmAcct` int(10) unsigned NOT NULL,
//  `grade` smallint(6) NOT NULL,
//  PRIMARY KEY  (`idGrade`),
//  UNIQUE KEY `Index_2` USING BTREE (`charUid`,`gmAcct`)
//) ENGINE=MyISAM AUTO_INCREMENT=1 DEFAULT CHARSET=latin1;

//DROP TABLE IF EXISTS `mytserver`.`background`;
//CREATE TABLE  `mytserver`.`background` (
//  `UID` int(10) unsigned NOT NULL auto_increment,
//  `KIND` int(10) unsigned NOT NULL,
//  `VALUE` varchar(255) NOT NULL,
//  `TIMESTAMP` timestamp NOT NULL default CURRENT_TIMESTAMP,
//  KEY `UID` (`UID`)
//) ENGINE=MyISAM AUTO_INCREMENT=8167 DEFAULT CHARSET=latin1;

[DEFNAME ficha_emote]
f_emote1    vomitar
f_emote2    arrotar
f_emote3    tossir
f_emote4    espirrar
f_emote5    peidar
f_emote6    rir
f_emote7    observar
f_emote8    gritar
f_emote9    chamar
f_emote10   afastar
f_emote11   ola
f_emote12   saudar
f_emote13   comemorar
f_emote14   susto
f_emote15   confuso
f_emote16   surpreso
f_emote17   ops
f_emote18   cospir
f_emote19   erguer

[DEFNAME ficharp]
//Pages
FICHARP_PAGE_FACE       1
FICHARP_PAGE_BODY       2
FICHARP_PAGE_MARKS      3
FICHARP_PAGE_PSYCHO     4
FICHARP_PAGE_NATURE     5
FICHARP_PAGE_RACE       6
FICHARP_PAGE_CLASS      7
FICHARP_PAGE_QUEST      8
FICHARP_PAGE_PAST       9
FICHARP_PAGE_EARLYS     10
FICHARP_PAGE_MEMO       11
FICHARP_PAGE_CRITICS    12

//Tasks
FICHARP_TASK_SHOW       1
FICHARP_TASK_ADD        2
FICHARP_TASK_EDIT       3

//HEADERS
FICHARP_1_HEADER        Descricao do rosto
FICHARP_2_HEADER        Descricao do corpo
FICHARP_3_HEADER        Marcas visiveis no corpo
FICHARP_4_HEADER        Manias e peculiaridades
FICHARP_5_HEADER        Natureza e comportamento
FICHARP_6_HEADER        Estereotipos de raca
FICHARP_7_HEADER        Estereotipos de classe
FICHARP_8_HEADER        Busca pessoal
FICHARP_9_HEADER        Passado distante
FICHARP_10_HEADER       Passado recente
FICHARP_11_HEADER       Memorias
FICHARP_12_HEADER       Criticas

//INSTRUCOES
FICHARP_1_INFO        Descreva em ate 230 caracteres sem acento como eh o seu rosto. Pressione o botao "enviar" quando estiver acabado.
FICHARP_2_INFO        Selecione as opcoes que mais se adequam ao seu tipo fisico. Pressione o botao "enviar" quando estiver acabado.
FICHARP_3_INFO        Descreva em ate 230 caracteres sem acento as marcas visiveis no seu corpo como cicatrizes, tatuagens, deficiencias visiveis, pintas, pelagem e outras anomalias que sao visiveis para qualquer um que olhe para voce por mais de 5 segundos. Pressione o botao "enviar" quando estiver acabado.
FICHARP_4_INFO        Descreva em ate 230 caracteres sem acentuacao as peculiaridades do seu personagem, manias, transtornos, personalidade, etc. Pressione o botao "enviar" quando estiver acabado.
FICHARP_5_INFO        De uma breve descricao da natureza, comportamento e alinhamento do seu personagem em ate 230 caracteres sem acentuacao. Pressione o botao "enviar" quando estiver acabado.
FICHARP_6_INFO        Seu personagem foge do esteriotipo padrao da raca? Como? Porque? (nao necessario para humanos) (230 caracteres sem acentucao) Pressione o botao "enviar" quando estiver acabado.
FICHARP_7_INFO        Seu personagem foge do esteriotipo padrao da classe? Como? Porque? (230 caracteres sem acentuacao) Pressione o botao "enviar" quando estiver acabado.
FICHARP_8_INFO        Cite alguns objetivos de vida do seu personagem, sejam eles grandiosos ou comuns. Atualize esta secao de vez em quando com as novidades sobre a sua busca. (230 caracteres sem acentuacao) Pressione o botao "enviar" quando estiver acabado.
FICHARP_9_INFO        Conte passagens de seu passado distante como infancia, inicio da vida adulta e historias que fizeram de voce o que eh hoje (PRE-JOGO) (230 caracteres sem acentuacao).
FICHARP_10_INFO       Conte passagens de seu passado recente vivido no jogo. Voce pode utilizar esse espaco como um diario de aventuras (230 caracteres sem acentuacao).
FICHARP_11_INFO       Use este espaco para anotacoes tais como senhas, posicoes de sextante, pedidos de compra e quaisquer informacoes ingame que julgue necessario guardar (230 caracteres sem acentuacao).
FICHARP_12_INFO       ATENCAO GM!<BR>Use frases claras, precisas e imparciais para dar dicas de interpretacao SEM ACENTUACAO. Nao utilize este espaco para fazer comentarios a um jogador tais como:<BR> - Elogios<BR> - Criticas destrutivas<BR> - Criticas rudes<BR> - Bajulacao<BR> - Informacoes meta-game

//COOLDOWN DE ATUALIZAÇÃO
FICHARP_1_COOLDOWN      15*24*60*60
FICHARP_2_COOLDOWN      15*24*60*60
FICHARP_3_COOLDOWN      5*24*60*60
FICHARP_4_COOLDOWN      30*24*60*60
FICHARP_5_COOLDOWN      30*24*60*60
FICHARP_6_COOLDOWN      30*24*60*60
FICHARP_7_COOLDOWN      10*24*60*60
FICHARP_8_COOLDOWN      1*24*60*60
FICHARP_9_COOLDOWN      1*24*60*60
FICHARP_10_COOLDOWN     1*24*60*60
FICHARP_11_COOLDOWN     1*24*60*60
FICHARP_12_COOLDOWN     15*24*60*60

//Constantes de corpo
FICHARP_BODY_0_0        Muito baixo(a)
FICHARP_BODY_0_1        Baixo(a)
FICHARP_BODY_0_2        Estatura normal
FICHARP_BODY_0_3        Alto(a)
FICHARP_BODY_0_4        Muito alto(a)
FICHARP_BODY_1_0        Muito magro(a)
FICHARP_BODY_1_1        Magro(a)
FICHARP_BODY_1_2        Esbelto(a)
FICHARP_BODY_1_3        Forte
FICHARP_BODY_1_4        Tronculento(a)
FICHARP_BODY_1_5        Muito forte
FICHARP_BODY_1_6        Acima do peso
FICHARP_BODY_1_7        Gordo(a)
FICHARP_BODY_1_8        Obeso(a)
FICHARP_BODY_2_0        Lento(a) e tropego(a)
FICHARP_BODY_2_1        Pouco agail
FICHARP_BODY_2_2        Delicado(a)
FICHARP_BODY_2_3        Bruto(a)
FICHARP_BODY_2_4        Agilidade normal
FICHARP_BODY_2_5        Agil
FICHARP_BODY_2_6        Muito agil

[PLEVEL 0]
ficharp

[PLEVEL 2]
ficha
prender

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// ficha
//*****************************************************************************
//usado pelos GMs
[FUNCTION ficha]
IF (!<argo>)
    sysmessageyellow Selecione o Jogador:
    targetf ficha
ELIF (!<argo.IsPlayer>)
    sysmessagered Jogador invalido!
ELSE
    ctag.admin_player=<argo>
    ctag0.admin_action=ADMIN_PAGE_CHAR
    ctag.admin_task=ADMIN_PAGE_CHAR
    dialogclose d_sphereadmin
    dialog d_sphereadmin
ENDIF


//*****************************************************************************
[FUNCTION admin_banAccount]
//*****************************************************************************
//Function para banir player
obj=<ctag0.admin_player>
if (<argn>==<ctag0.chalenge>)
    if (<obj.account.plevel> < <account.plevel>)
        if (!<obj.account.block>)
            trysrv obj.kick
            sb Conta <obj.account> banida!
            serv.log [BANIMENTO] Conta <obj.account.name> banida por '<src.account.name>'!!!
        else
            obj.account.block=0
            sb Conta '<obj.account>' reincluida!
            serv.log [BANIMENTO] Conta '<obj.account.name>' DESbanida por '<src.account.name>'!!!
    else
        sysmessagered Voce nao tem privilegio para isso.
    endif
else
    sysmessagered Codigo errado.
    dialog d_sphereadmin
endif

//*****************************************************************************
[FUNCTION admin_RPnote]
//*****************************************************************************
//Adiciona uma nota <args> ao RP do char <ctag.admin_player>
if (<eval strlen(<args>)> < 6)
    sysmessagered Nota muito curta!
else
    event_RPnote <ctag.admin_player>,<args>
endif
DIALOG d_sphereadmin

//*****************************************************************************
[FUNCTION admin_GMnote]
//*****************************************************************************
//Adiciona uma nota <args> à conta do char <ctag.admin_player>
if (<eval strlen(<args>)> < 6)
    sysmessagered Nota muito curta!
else
    event_note <ctag.admin_player>,<args>
endif
DIALOG d_sphereadmin

//*****************************************************************************
// prender ([motivo])
//*****************************************************************************
//usado pelos GMs
[FUNCTION prender]
sysmessageyellow Selecione o Jogador:
targetfg prender_

[FUNCTION prender_]
if (<argo.isplayer>)
    ctag.admin_player=<argo>
    DB.QUERY "SELECT count(idEvent) AS cnt FROM eventsLog WHERE char2Acct=<argo.dtag0.idAcct> AND eventType='Jail'"
    ctag.admin_jail_h=<MAXIMUM <DB.ROW.0.cnt>,1>
    dialog d_prender
else
    sysmessageyellow Nao eh um jogador valido.
endif

//*****************************************************************************
// jail_Prompt (UID)
//*****************************************************************************
[FUNCTION jail_Prompt]
//Dialog para prender o jogador <argn>
obj=<argn>
if (!<obj.IsPlayer>)
    return
elif (!<obj.IsOnline>)
    return
endif
ctag.admin_player=<obj>
DB.QUERY "SELECT count(idEvent) AS cnt FROM eventsLog WHERE char2Acct=<obj.dtag0.idAcct> AND eventType='Jail'"
ctag.admin_jail_h=<MAXIMUM <DB.ROW.0.cnt>,1>
dialog d_prender

//***********************************************************
[FUNCTION macroer]
//***********************************************************
//Prende e penaliza um jogador por macro
if (!<argo>) && (!<argn>)
    sysmessageyellow Selecione o delinquente:
    targetf macroer
elif (!<argo.IsPlayer>) && (!<uid.<argn>.IsPlayer>)
    sysmessagered Jogador invalido.
else
    if (<argo>)
        obj=<argo>
    else
        obj=<argn>
    endif
    ctag.macroer_hours=<eval 4+<obj.account.tag0.macro_count>>
    ctag.macroer_minutes=0
    ctag.macroer_penalty=-100
    ctag.macroer=<obj>
    dialog d_macroer
endif

//***********************************************************
[FUNCTION macroer_Resume]
//***********************************************************
// Cria resumo do dialog de macroer
ref1=<ctag.macroer>
local.t = <ref1.tag.name> [<ref1.account>] ja foi preso e penalizado por macro <ref1.account.dtag0.macro_count> vezes.<DEF.BR>
local.t .= Isso fara ele ficar preso pelo tempo descrito abaixo, ganhar uma anotacao na ficha e perder skill, na quantidade descrita abaixo e das skills escolhidas abaixo.
return <local.t>

//***********************************************************
[FUNCTION macroer_skill]
//***********************************************************
//Lista skills para descer no macroer
ref1=<ctag.macroer>
list_new Escolha uma skill para penalizar o macroer
list_newCol 20,Skill
list_newCol 200,valor atual
cTAG.list_action=macroer_addSkill
cTAG.list_numItems=55
cTAG.list_numPages=4
for s 0 54
 try cTAG.list_row<hval <local.s>+1>_01=<serv.skill.<dlocal.s>.name>
 try cTAG.list_row<hval <local.s>+1>_02=<ref1.<dlocal.s>>
end
list_show 1

//***********************************************************
[FUNCTION macroer_addSkill]
//***********************************************************
// Adiciona a skill <argn> no dialog do macroer
local.i=0
WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
    local.i += 1
ENDWHILE
TRY cTAG.macroer_skill_<dlocal.i>=<eval <ARGN>-1>
dialogclose d_macroer
dialog d_macroer

//***********************************************************
[FUNCTION macroer_DeleteSkill]
//***********************************************************
//Remove a skill de indice <argn> da lista de skills de um macroer
local.n=<argn>

//Grava cash excluindo o item
local.i=0
local.j=0
WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
    IF (<local.i>!=<local.n>)
        TRY local.skill_<dlocal.j>=<cTAG.macroer_skill_<dlocal.i>>
        local.j += 1
    ENDIF
    TRY cTAG.macroer_skill_<dlocal.i>=
    local.i += 1
ENDWHILE

//Joga o cash nas cTAGS
if (<local.j>)
    FOR i 0 <eval <local.j>-1>
        TRY cTAG.macroer_skill_<dlocal.i>=<local.skill_<dlocal.i>>
    ENDFOR
ENDIF
dialogclose d_macroer
dialog d_macroer

//*****************************************************************************
// detem(tempo,jailer,motivo)
//*****************************************************************************
//usado pelos sistemas
[FUNCTION detem]
if (<restest 1 i_mry_GM_jail>)
    sysmessagered Infracao detectada. Prisao aumentada em 10 minutos.
    findid.i_mry_gm_jail.more1=<findid.i_mry_gm_jail.more1>+600
    return 1
endif
LOCAL.t=<eval (<tag0.prisao_total>/2)+<argv0>>
sysmessagered Preso por <f_timestring <LOCAL.t>>.
sysmessagered Motivo: <argv2>

serv.newitem i_mry_gm_jail
new.cont=<uid>
new.morep=<p>
new.more1=<LOCAL.t>
new.timer=1
tag.prisao_total=<eval <tag0.prisao_total>+<argv0>>
//dorand (3)
// go=5291,1166
// go=5284,1166
// go=5277,1166
//enddo
go 5275,1164

if (<DB.connected>)
    REF1=<argv1>
    if (<REF1>)
        LOCAL.u=<REF1>
        LOCAL.n=<REF1.account>
    else
        LOCAL.u=0
        LOCAL.n=Servidor
    endif
    //DB.EXECUTE "INSERT INTO jail SET timestamp=NOW(),uidJailed=<eval <uid>>,jailed='<account>',duration=<eval <argv0>>,reason='<argv2>',uidJailer=<eval <LOCAL.u>>,jailer='<LOCAL.n>'"
    DB.EXECUTE "INSERT INTO eventsLog SET timestamp=NOW(), eventType='Jail',charUid=<eval <REF1>>,charAcct=<eval <REF1.tag0.idAcct>>,x=<eval <p.x>>,y=<eval <p.y>>,z=<eval <p.z>>,char2Uid=<eval <uid>>,char2Acct=<eval <tag0.idAcct>>,message='<DB.ESCAPEDATA <f_timestring <argv0>>: <argv2>>'"
endif

//*****************************************************************************
// rp_updateGrade
//*****************************************************************************
//gera a nota media do <DEFAULT> a partir do DB
[FUNCTION rp_updateGrade]
if (<DB.connected>)
    DB.QUERY "SELECT round(avg(grade)) AS grade FROM rpGrade WHERE charUid=<eval <uid>>"
    
    if (<DB.ROW.NUMROWS> > 0)
        CTAG.rp_grade=<DB.ROW.0.grade>
    else
        CTAG.rp_grade=0
    endif
endif
skill_init

//*****************************************************************************
// rp_getGmGrade(gmDbId)
//*****************************************************************************
//pega a nota dada pelo GM para o <DEFAULT>
[FUNCTION rp_getGmGrade]
if (<DB.connected>)
    DB.QUERY "SELECT grade FROM rpGrade WHERE charUid=<eval <uid>> AND gmAcct=<eval <argv0>>"
    
    if (<DB.ROW.NUMROWS> > 0)
        TRY CTAG.rp_<argv0>=<DB.ROW.0.grade>
    else
        TRY CTAG.rp_<argv0>=0
    endif
endif

//*****************************************************************************
// rp_setGrade(nota)
//*****************************************************************************
//muda a nota de RP do <DEFAULT> dada pelo GM <SRC>
[FUNCTION rp_setGrade]

if <argv0> > 10
    LOCAL.n = 10
elif <argv0> < 0
    LOCAL.n = 0
else
    LOCAL.n = <argv0>
endif

    LOCAL.n *= 2
    LOCAL.n -= 10
    
serv.log [RP] <account> (<uid>) recebeu nota: <eval <LOCAL.n>> do <src.account> 

if (<DB.connected>)
    LOCAL.q = "timestamp=NOW(), charUid=<eval <uid>>, charAcct=<eval <tag0.idAcct>>, gmUid=<eval <src>>, gmAcct=<eval <src.tag0.idAcct>>, grade=<eval <LOCAL.n>>"
    DB.EXECUTE "INSERT INTO rpGrade SET <LOCAL.q> ON DUPLICATE KEY UPDATE <LOCAL.q>"
endif

rp_updateGrade
rp_getGmGrade <src.tag0.idAcct>

//*****************************************************************************
// virtue_bt
//*****************************************************************************
//Acionado quando usa o Virtue Button
[FUNCTION virtue_bt]
ficha_pc <uid>

//*****************************************************************************
// fichastat
//*****************************************************************************
//Ficha de um char
[FUNCTION fichastat]
if (!<argo>)
    sysmessageyellow Selecione o jogador:
    targetf fichastat
else
    ficha_pc <argo.uid>
endif

//*****************************************************************************
// ficha_pc
//*****************************************************************************
//Ficha do seu proprio char
[FUNCTION ficha_pc]
ctag.ficha_uid=<argn>
ctag.ficha_page=1
SDIALOG d_ficha_pc

//*****************************************************************************
// ficha_pc_stats
//*****************************************************************************
//Elabora texto de estatísticas do char do DIALOG d_ficha_pc
[FUNCTION ficha_pc_stats]
obj=<argn>
local.s = <DEF.BFONT_DCYAN>Idade real: <f_timestring <obj.f_age>><DEF.BR>
local.s .= Idade na MyT: <f_timestring <eval <obj.f_age>*minutos_por_tick*10>><DEF.BR>
//count pack cash
LOCAL.copper=<obj.rescount i_coin_copper>
LOCAL.silver=<obj.rescount i_coin_silver>
LOCAL.gold=<obj.rescount i_gold>

//Check bank
LOCAL.copper += <obj.findlayer(layer_bankbox).rescount i_coin_copper>
LOCAL.silver += <obj.findlayer(layer_bankbox).rescount i_coin_silver>
LOCAL.gold += <obj.findlayer(layer_bankbox).rescount i_gold>

LOCAL.m=<EVAL <LOCAL.copper>+(<LOCAL.silver>*10)+(<LOCAL.gold>*100)>

local.s .= <DEF.BFONT_DYELLOW>Dinheiro: <STRTRESURE <local.m>><DEF.BR>
local.s .= Peso no banco: <eval <obj.findlayer.layer_bankbox.weight>/10>/<eval <obj.findlayer.layer_bankbox.tag.OVERRIDE.MAXWEIGHT>/10><DEF.BR>
//local.s .= <DEF.BFONT_DPURPLE>Maximo de skill permitido: <fval <obj.TAG.skill_maxSkillValue>><DEF.BR>
//local.s .= Soma das skills: <fval <obj.skilltotal>> (max: <fval <obj.TAG.skill_maxSkillSumValue>>)<DEF.BR>
local.s .= <DEF.BFONT_RED>Pontos de alma: <obj.dtag.pontosdealma><DEF.BR>
local.s .= Desmaios: <obj.dtag.morte_desmaios>/<dDEF.max_desmaios><DEF.BR>
local.s .= <DEF.BFONT_DCYAN>Alimentacao: <obj.comida_msg>
return <local.s>

//*****************************************************************************
[FUNCTION ficharp]
//*****************************************************************************

dialogclose d_ficharp   //both dialogs use the same uid
dialogclose d_profile
ctag.ficharp_task=FICHARP_TASK_SHOW

//Summon target para GMs ou abre o dialog d_ficharp para jogadores
if <IsGM>
    ctag.ficharp_page=FICHARP_PAGE_QUEST
    if <argn>
        ctag.ficharp_UID=<argn>
    elif <argo>
        ctag.ficharp_UID=<argo>
    else
        sysmessageyellow Que jogador?
        targetf ficharp
        return
    endif
else
    ctag.ficharp_UID=<uid>
    ctag.ficharp_page=FICHARP_PAGE_FACE
endif

DIALOG d_ficharp   


//*****************************************************************************
[FUNCTION ficharp_atualize]
//*****************************************************************************
//Atualiza ou envia dados do formulário de ficharp para a função correta
serv.log Atualizando ficha: <DEF.FICHARP_<eval <ctag0.ficharp_page>>_HEADER> (<eval <ctag0.ficharp_page>>). <src.tag.name> -> <uid.<ctag0.ficharp_uid>.tag.name>
IF (<UID>==<ctag0.ficharp_UID>) || (<IsGM>)
    DOSWITCH <DEF.<ctag.ficharp_page>>
        local.0
        CALL ficharp_update_face
        CALL ficharp_update_body
        CALL ficharp_update_marks
        CALL ficharp_update_psycho
        CALL ficharp_update_nature
        CALL ficharp_update_race
        CALL ficharp_update_class
        CALL ficharp_update_quest
        CALL ficharp_update_past
        CALL ficharp_update_earlys
        CALL ficharp_update_memo
        CALL ficharp_update_critics
    ENDDO
ENDIF

//*****************************************************************************
[FUNCTION ficharp_update_face]
//*****************************************************************************
//Envia dados do formulário de rosto do fichaRP para o banco de dados.
DB.QUERY="SELECT uid FROM background WHERE uid=<dctag.ficharp_uid> AND kind=1;"
if (!<DB.ROW.NUMROWS>)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=1, value='<argtxt[0]>';"
else
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>' WHERE uid=<dctag.ficharp_uid> AND kind=1;"
endif
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_explode_body]
//*****************************************************************************
//Explode os dados do body vindos da DB
local.height=<argv0>
local.weight=<argv1>
local.agility=<argv2>

//*****************************************************************************
[FUNCTION ficharp_update_body]
//*****************************************************************************
//Envia dados do formulário de corpo do fichaRP para o banco de dados.
for i 0 4
    if (<argchk[<local.i>]>)
        local.height=<local.i>
    endif
endfor
for i 5 13
    if (<argchk[<local.i>]>)
        local.weight=<eval <local.i>-5>
    endif
endfor
for i 14 20
    if (<argchk[<local.i>]>)
        local.agility=<eval <local.i>-14>
    endif
endfor
local.value=<local.height>,<local.weight>,<local.agility>
DB.QUERY="SELECT uid FROM background WHERE uid=<dctag.ficharp_uid> AND kind=2;"
if (!<DB.ROW.NUMROWS>)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=2, value='<local.value>';"
else
    DB.EXECUTE="UPDATE background SET value='<local.value>' WHERE uid=<dctag.ficharp_uid> AND kind=2;"
endif
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_marks]
//*****************************************************************************
//Envia dados do formulário de marcas visíveis do fichaRP para o banco de dados.
DB.QUERY="SELECT uid FROM background WHERE uid=<dctag.ficharp_uid> AND kind=3;"
if (!<DB.ROW.NUMROWS>)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=3, value='<argtxt[0]>';"
else
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>' WHERE uid=<dctag.ficharp_uid> AND kind=3;"
endif
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_psycho]
//*****************************************************************************
//Envia dados do formulário de perfil psicológico do fichaRP para o banco de dados.
DB.QUERY="SELECT uid FROM background WHERE uid=<dctag.ficharp_uid> AND kind=4;"
if (!<DB.ROW.NUMROWS>)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=4, value='<argtxt[0]>';"
else
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>' WHERE uid=<dctag.ficharp_uid> AND kind=4;"
endif
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_nature]
//*****************************************************************************
//Envia dados do formulário de natureza e comportamento do fichaRP para o banco de dados.
DB.QUERY="SELECT uid FROM background WHERE uid=<dctag.ficharp_uid> AND kind=5;"
if (!<DB.ROW.NUMROWS>)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=5, value='<argtxt[0]>';"
else
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>' WHERE uid=<dctag.ficharp_uid> AND kind=5;"
endif
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_race]
//*****************************************************************************
//Envia dados do formulário de estereótipo de raça do fichaRP para o banco de dados.
DB.QUERY="SELECT uid FROM background WHERE uid=<dctag.ficharp_uid> AND kind=6;"
if (!<DB.ROW.NUMROWS>)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=6, value='<argtxt[0]>';"
else
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>' WHERE uid=<dctag.ficharp_uid> AND kind=6;"
endif
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_class]
//*****************************************************************************
//Envia dados do formulário de estereótipo de classe do fichaRP para o banco de dados.
DB.QUERY="SELECT uid FROM background WHERE uid=<dctag.ficharp_uid> AND kind=7;"
if (!<DB.ROW.NUMROWS>)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=7, value='<argtxt[0]>';"
else
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>' WHERE uid=<dctag.ficharp_uid> AND kind=7;"
endif
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_quest]
//*****************************************************************************
//Envia dados do formulário de busca pessoal do fichaRP para o banco de dados.
if (<ctag0.ficharp_task>==FICHARP_TASK_ADD)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=8, value='<argtxt[0]>';"
else
    DB.QUERY="SELECT timestamp FROM background  WHERE uid=<dctag.ficharp_uid> AND kind=8 ORDER BY timestamp ASC LIMIT <dctag0.ficharp_entry>,1;"
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>', timestamp=NOW() WHERE uid=<dctag.ficharp_uid> AND kind=8 AND timestamp='<DB.ROW.0.timestamp>';"
endif
ctag.ficharp_task=FICHARP_TASK_SHOW
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_past]
//*****************************************************************************
//Envia dados do formulário de passado distante do fichaRP para o banco de dados.
if (<ctag0.ficharp_task>==FICHARP_TASK_ADD)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=9, value='<argtxt[0]>';"
else
    DB.QUERY="SELECT timestamp FROM background  WHERE uid=<dctag.ficharp_uid> AND kind=9 ORDER BY timestamp ASC LIMIT <dctag0.ficharp_entry>,1;"
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>', timestamp=NOW() WHERE uid=<dctag.ficharp_uid> AND kind=9 AND timestamp='<DB.ROW.0.timestamp>';"
endif
ctag.ficharp_task=FICHARP_TASK_SHOW
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_earlys]
//*****************************************************************************
//Envia dados do formulário de passado recente do fichaRP para o banco de dados.
if (<ctag0.ficharp_task>==FICHARP_TASK_ADD)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=10, value='<argtxt[0]>';"
else
    DB.QUERY="SELECT timestamp FROM background  WHERE uid=<dctag.ficharp_uid> AND kind=10 ORDER BY timestamp ASC LIMIT <dctag0.ficharp_entry>,1;"
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>', timestamp=NOW() WHERE uid=<dctag.ficharp_uid> AND kind=10 AND timestamp='<DB.ROW.0.timestamp>';"
endif
ctag.ficharp_task=FICHARP_TASK_SHOW
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_memo]
//*****************************************************************************
//Envia dados do formulário de anotações e memórias do fichaRP para o banco de dados.
if (<ctag0.ficharp_task>==FICHARP_TASK_ADD)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=11, value='<argtxt[0]>';"
else
    DB.QUERY="SELECT timestamp FROM background  WHERE uid=<dctag.ficharp_uid> AND kind=11 ORDER BY timestamp ASC LIMIT <dctag0.ficharp_entry>,1;"
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>', timestamp=NOW() WHERE uid=<dctag.ficharp_uid> AND kind=11 AND timestamp='<DB.ROW.0.timestamp>';"
endif
ctag.ficharp_task=FICHARP_TASK_SHOW
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_update_critics]
//*****************************************************************************
//Envia dados do formulário de críticas do fichaRP para o banco de dados.
serv.log [FICHARP] <src.tag.name> mandando uma critica pada <uid.<dctag.ficharp_uid>.tag.name>: '<argtxt[0]>'
if (<ctag0.ficharp_task>==FICHARP_TASK_ADD)
    DB.EXECUTE="INSERT INTO background SET uid=<dctag.ficharp_uid>, kind=12, value='<argtxt[0]>';"
else
    DB.QUERY="SELECT timestamp FROM background  WHERE uid=<dctag.ficharp_uid> AND kind=12 ORDER BY timestamp ASC LIMIT <dctag0.ficharp_entry>,1;"
    DB.EXECUTE="UPDATE background SET value='<argtxt[0]>', timestamp=NOW() WHERE uid=<dctag.ficharp_uid> AND kind=12 AND timestamp='<DB.ROW.0.timestamp>';"
endif
ctag.ficharp_task=FICHARP_TASK_SHOW
dialogclose d_ficharp
DIALOG d_ficharp
obj=<ctag0.ficharp_uid>
if (<obj.IsOnline>)
    obj.sysmessagegreen Voce recebeu uma nova critica ao seu RP!
    obj.sysmessagegreen Utilize o comando '.ficharp' ou o botao de profile de seu paperdoll para le-la.
    obj.psfx 1460
endif

//*****************************************************************************
[FUNCTION ficharp_deleteEntry]
//*****************************************************************************
//Deleta uma entrada do firchaRP para uma página que tenha lista e paginação.
DB.QUERY="SELECT timestamp FROM background  WHERE uid=<dctag.ficharp_uid> AND kind=<eval <DEF.<ctag0.ficharp_page>>> ORDER BY timestamp ASC LIMIT <dctag0.ficharp_entry>,1;"
DB.EXECUTE="DELETE FROM background WHERE uid=<dctag.ficharp_uid> AND kind=<eval <DEF.<ctag0.ficharp_page>>> AND timestamp='<DB.ROW.0.timestamp>';"
ctag.ficharp_task=FICHARP_TASK_SHOW
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
[FUNCTION ficharp_BuildProfile]
//*****************************************************************************
//Monta o HTML de profile para o char de UID <ctag0.ficharp_uid>
obj=<ctag0.ficharp_uid>
local.can=<rec_CanSee <obj>>
DB.QUERY="SELECT value FROM background WHERE uid=<dctag0.ficharp_uid> AND kind=2";
if (<eval strlen(0<DB.ROW.0.value>)> > 1)
    args=<DB.ROW.0.value>
    local.body=<DEF.BFONT_BLACK><DEF.FICHARP_BODY_0_<eval <argv0>>>, <DEF.FICHARP_BODY_1_<eval <argv1>>> e <DEF.FICHARP_BODY_2_<eval <argv2>>><DEF.BR>
else
    local.body=<DEF.BFONT_LRED>Usuario nao definiu corpo.
endif
if (<local.can>)
    DB.QUERY="SELECT value FROM background WHERE uid=<dctag0.ficharp_uid> AND kind=1";
    if (<eval strlen(0<DB.ROW.0.value>)> > 4)
        local.face=<DEF.BFONT_BLACK><DB.ROW.0.value>
    else
        local.face=<DEF.BFONT_LRED>Usuario nao definiu rosto.
    endif
    local.t=<DEF.BFONT_SIZE4><DEF.BFONT_GREEN>Rosto:<DEF.BFONT_SIZE3><DEF.BR><local.face><DEF.BR>
    
    local.t .= <DEF.BR><DEF.BFONT_SIZE4><DEF.BFONT_GREEN>Corpo:<DEF.BR><DEF.BFONT_SIZE3><local.body>
    
    DB.QUERY="SELECT value FROM background WHERE uid=<dctag0.ficharp_uid> AND kind=3";
    if (<DB.ROW.NUMROWS>)
        local.t .= <DEF.BR><DEF.BFONT_SIZE4><DEF.BFONT_GREEN>Marcas visiveis:<DEF.BR><DEF.BFONT_SIZE3><DEF.BFONT_BLACK><DB.ROW.0.value><DEF.BR>
    endif
else
    local.t = <DEF.BR><DEF.BFONT_SIZE4><DEF.BFONT_GREEN>Corpo:<DEF.BR><DEF.BFONT_SIZE3><local.body>
endif
return <local.t>
    
    
//*****************************************************************************
[FUNCTION ficharp_renderbuttons]
//*****************************************************************************
//Desenha os botões de menu direito do dialog d_ficharp
dorigin 15 40
dtext +15 - 044 Aparencia
button - *25 2103 2104 1 0 1
dtext +15 -5 48 Rosto
button - *20 2103 2104 1 0 2
dtext +15 -5 48 Corpo
button - *20 2103 2104 1 0 3
dtext +15 -5 48 Marcas pessoais
dtext +15 *30 044 Personalidade
button - *25 2103 2104 1 0 4
dtext +15 -5 48 Manias e peculiaridades
button - *20 2103 2104 1 0 5
dtext +15 -5 48 Natureza
button - *20 2103 2104 1 0 6
dtext +15 -5 48 Comportamento racial
button - *20 2103 2104 1 0 7
dtext +15 -5 48 Estereotipo profissional
button - *20 2103 2104 1 0 8
dtext +15 -5 48 Busca pessoal
dtext +15 *30 044 Historia
button - *25 2103 2104 1 0 9
dtext +15 -5 48 Passado distante
button - *20 2103 2104 1 0 10
dtext +15 -5 48 Passado recente
button - *20 2103 2104 1 0 11
dtext +15 -5 48 Memorias
dtext +15 *30 044 Avaliacao
button - *25 2103 2104 1 0 12
dtext +15 -5 48 Criticas


//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************
[TYPEDEF t_jail_stone]
on=@dclick
src.clearctags list_
local.t=<f_timestring <src.findid.i_mry_GM_jail.more1>>
src.sysmessagegreen Faltam <local.t> para que voce seja solto.
src.timerf 3 sysmessageyellow Voce deve permanecer online para que o tempo conte.
src.list_new Tempo de prisao restante: <local.t>. Prisoes que sua conta ja sofreu:
src.list_setAction clearctags list_
src.list_setActionBack clearctags list_
src.list_newDbCol 25,timestamp,Data
src.list_newDbCol 150,message,Tempo: Motivo
src.list_loadFromDb SELECT DATE_FORMAT(timestamp,'%d/%m/%Y %H:%i') AS timestamp, message FROM eventsLog WHERE char2Acct=<src.dtag0.idAcct> AND eventType='Jail' ORDER BY timestamp DESC;
src.list_show 1
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_mry_GM_jail
//*****************************************************************************
[ITEMDEF i_mry_GM_jail]
id=i_memory
name=Preso por GM
type=t_eq_script
layer=30

on=@create
attr=0c010

on=@timer
timer=1
if (<cont.isonline>)
    more1 -= 1
    if (<more1> <= 0)
        cont.forgive
        cont.go=<morep>
        cont.tag.ficha_jail_time=5
        cont.update
        cont.sysmessagegreen Certo, voce cumpriu sua pena. Cuidado com a sua RePresentacao.
        remove
    endif 
endif
return 1

//*****************************************************************************
//*****************************************************************************
// DIALOGs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// d_prender
//*****************************************************************************

[DIALOG d_prender]
100,75
page 0
resizepic 0 0 9270 403 173
dhtmlgump 16 15 371 34 0 0 <DEF.CENTER><DEF.BFONT_LRED>Prender <uid.<ctag.admin_player>.tag.name> [<uid.<ctag.admin_player>.account>]<DEF.DIV_E>
dtext 20 57 53 Prender durante
gumppic 127 53 2444
dtext 196 57 53 horas e
gumppic 245 53 2444
dtext 313 57 53 minutos
dtext 21 81 53 Motivo:
resizepic 72 84 9300 310 48
dtextentry 134 54 45 20 0 0 <dctag0.admin_jail_h>
dtextentry 255 54 45 20 0 1 0
dtextentry 78 89 294 43 0 2
button 272 136 247 248 1 0 1
button 69 136 241 242 1 0 0

[DIALOG d_prender text]

[DIALOG d_prender button]
ON=0
// Cancel
if (!<IsEmpty <ctag0.admin_task>>)//Veio do .admin/.ficha?
    DIALOG d_sphereadmin
endif

ON=1
// Okay
obj=<ctag.admin_player>
local.t=<eval ((<argtxt[0]>*60)+<argtxt[1]>)*60>
if (<restest 1 i_mry_GM_jail>)
    obj.sysmessagered Infracao detectada. Prisao aumentada em <f_timestring <local.t>>.
    obj.findid.i_mry_gm_jail.more1=<obj.findid.i_mry_gm_jail.more1>+<local.t>
    return 1
endif

if (<DB.connected>)
    DB.EXECUTE "INSERT INTO eventsLog SET timestamp=NOW(), eventType='Jail', char2Uid=<eval <obj.uid>>,char2Acct=<eval <obj.tag.idAcct>>,x=<eval <obj.p.x>>,y=<eval <obj.p.y>>,z=<eval <obj.p.z>>,charUid=<eval <src.uid>>,charAcct=<eval <src.tag0.idAcct>>,message='<DB.ESCAPEDATA <f_timestring <local.t>>: <argtxt[2]>>'"
endif

obj.sysmessagered Preso por <f_timestring <local.t>>.
obj.sysmessagered Motivo: <argtxt[2]>

sysmessagered Preso por <f_timestring <local.t>>.
sysmessagered Motivo: <argtxt[2]>

serv.newitem i_mry_gm_jail
new.cont=<obj>
new.morep=<obj.p>
new.more1=<LOCAL.t>
new.timer=1
obj.tag.prisao_total=<eval <obj.tag0.prisao_total>+<local.t>>
obj.go 5275,1164

if (!<IsEmpty <ctag0.admin_task>>)//Veio do .admin/.ficha?
    DIALOG d_sphereadmin
endif

//*****************************************************************************
// d_ficha_pc
//*****************************************************************************
[DIALOG d_ficha_pc]
//Ficha auxiliar de jogador
100,75
page 0
obj=<ctag.ficha_uid>
resizepic 50 31 2620 500 400
checkertrans 55 38 490 385
gumppic 0 0 10400
gumppic 0 160 10401
gumppic 0 356 10402
gumppic 518 0 10410
gumppic 518 160 10411
gumppic 518 356 10412
gumppic 225 45 2501
dtext 231 45 26 Mystical Tales Shard
resizepic 92 87 2620 416 310
dhtmlgump 105 100 391 52 0 0 <DEF.CENTER><DEF.BFONT_SIZE7><DEF.BFONT_DYELLOW><obj.tag.name> - <obj.tag.raca> - <obj.tag.classe><DEF.CENTERE><DEF.BFONTE>
if (<ctag0.ficha_page>==1)
    dhtmlgump 113 155 377 134 1 1 <ficha_pc_stats <obj>>
    dorigin 115 280
    button - *20 2224 2224 1 0 1
    dtext +25 -4 62 Emotes
    button - *20 2224 2224 1 0 2
    dtext +25 -4 62 Estrategia de batalha
    button - *20 2224 2224 1 0 3
    dtext +25 -4 62 Comandar animais
    button - *20 2224 2224 1 0 4
    dtext +25 -4 62 Reconhecer uma pessoa
    dorigin 315 280
    button - *20 2224 2224 1 0 5
    dtext +25 -4 62 Gerenciar moveis
    button - *20 2224 2224 1 0 6
    dtext +25 -4 62 Arremessar objeto
    button - *20 2224 2224 1 0 7
    dtext +25 -4 62 Tirar objeto de uma pilha
elif (<ctag0.ficha_page>==10)
    dorigin 115 100
    local.i=1
    local.j=14
    WHILE (!<IsEmpty <DEF.f_emote<dlocal.i>>>)
        if (<local.i>==<local.j>)
            dorigin 215 100
        endif
        button - *20 2224 2224 1 0 <eval 100+<dlocal.i>>
        dtext +25 -4 62 <DEF.f_emote<dlocal.i>>
        local.i += 1
    END
endif    

[DIALOG d_ficha_pc text]

[DIALOG d_ficha_pc button]
ON=1
// Emotes
ctag.ficha_page=10
SDIALOG d_ficha_pc

ON=2
// .combate
combate

ON=3
// .pastor
pastor

ON=4
// .r
r

ON=5
// .Moveis
moveis

ON=6
// .arrecessar
arremessar

ON=7
// .unstack
unstack

ON=100 199
obj=<ctag.ficha_uid>
local.n=<eval <argn>-100>
try obj.<DEF.f_emote<dlocal.n>>


// Created 23/2/2010 20:10:00, with Gump Studio.
// Exported with with SphereExporter ver 1.1.
// Script for 0.56/Revisions
//*****************************************************************************
[DIALOG d_macroer]
//*****************************************************************************
// Comando .macroer para prender macroers.
100,75
page 0
ref1=<ctag.macroer>
resizepic 50 31 2620 500 400
checkertrans 55 38 490 385
gumppic 0 0 10400
gumppic -1 157 10401
gumppic 0 356 10402
gumppic 518 0 10410
gumppic 518 160 10411
gumppic 518 356 10412
gumppic 225 45 2501
dtext 231 45 26 Mystical Tales Shard
resizepic 92 87 2620 416 310
dtext 207 94 48 Prender e penalizar por macro
dhtmlgump 115 118 370 64 2 1 <macroer_Resume>
dtext 117 190 166 Prender por
resizepic 196 187 9350 40 25
dtextentry 206 190 25 20 0 0 <dctag.macroer_hours>
dtext 239 190 166 horas
resizepic 279 187 9350 40 25
dtextentry 288 189 25 20 0 1 <dctag.macroer_minutes>
dtext 322 189 166 minutos
dtext 117 220 166 Penalidade de skills
resizepic 239 218 9350 49 25
dtextentry 243 221 36 20 0 2 <fval <ctag.macroer_penalty>>
dtext 118 247 166 (-10.0 eh igual a -100, mas eh diferente de -10)
dorigin 119 253
IF (!<IsEmpty <cTAG.macroer_skill_0>>)
    local.i=0
    WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
        button - *20 1209 1210 1 0 <eval 10+<local.i>>
        dtext +20 -3 2100 <serv.skill.<cTAG.macroer_skill_<dlocal.i>>.name> (<REF1.<cTAG.macroer_skill_<dlocal.i>>>)
        local.i += 1
    ENDWHILE
ENDIF
IF (<IsEmpty <cTAG.macroer_skill_4>>)
    button +20 *20 2224 2223 1 0 2
    dtext +40 -3 32 Adicionar skill
ENDIF
button 439 397 247 248 1 0 1//prender
button 96 397 241 242 1 0 0//cancelar

[DIALOG d_macroer text]


[DIALOG d_macroer button]
ON=0
// Fechar/cancelar
ctag.macroer_hours=
ctag.macroer_minutes=
ctag.macroer_penalty=
ctag.macroer=
IF (!<IsEmpty <cTAG.macroer_skill_0>>)
    local.i=0
    WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
        TRY cTAG.macroer_skill_<dlocal.i>=
        local.i += 1
    ENDWHILE
ENDIF
sysmessagered Miou. Deixa o meliante ai.
if (!<IsEmpty <ctag0.admin_task>>)//Veio do .admin/.ficha?
    DIALOG d_sphereadmin
endif


ON=1
// Prender agora
obj=<ctag.macroer>
local.time=<eval ((<ARGTXT[0]>*60)+<ARGTXT[1]>)*60>
local.s=":"
IF (!<IsEmpty <cTAG.macroer_skill_0>>)
    local.i=0
    WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
        local.s .= " <serv.skill.<cTAG.macroer_skill_<dlocal.i>>.name>;"
        TRY obj.<dcTAG0.macroer_skill_<dlocal.i>> = <eval <obj.<dcTAG0.macroer_skill_<dlocal.i>>>+<ARGTXT[2]>>
        TRY cTAG.macroer_skill_<dlocal.i>=
        local.i += 1
    ENDWHILE
ENDIF
IF <DB.CONNECTED>
    DB.EXECUTE "INSERT INTO eventsLog SET timestamp=NOW(), eventType='Jail',char2Uid=<eval <obj>>,char2Acct=<eval <obj.tag.idAcct>>,x=<eval <obj.p.x>>,y=<eval <obj.p.y>>,z=<eval <obj.p.z>>,charUid=<eval <uid>>,charAcct=<eval <tag0.idAcct>>,message='<DB.ESCAPEDATA <f_timestring <local.time>>: Macroando<local.s> longe do PC ou fora de contexto RP.>'"
ENDIF

obj.sysmessagered Preso por <f_timestring <local.time>>
obj.sysmessagered Motivo: Macroando <local.s> fora do PC ou fora de contexto RP.

serv.newitem i_mry_gm_jail
new.cont=<obj>
new.morep=<obj.p>
new.more1=<LOCAL.time>
new.timer=1
obj.tag.prisao_total=<eval <obj.tag0.prisao_total>+<local.time>>
obj.go 5275,1164
obj.account.tag.macro_count=<eval <obj.account.tag0.macro_count>+1>

sysmessageyellow DONE! MUAHUAUHAUHAUHAUHAUH!!!
clearctags macroer_

if (!<IsEmpty <ctag0.admin_task>>)//Veio do .admin/.ficha?
    DIALOG d_sphereadmin
endif


ON=2
// Adicionar skill
ctag.macroer_hours=<ARGTXT[0]>
ctag.macroer_minutes=<ARGTXT[1]>
ctag.macroer_penalty=<ARGTXT[2]>
macroer_skill

ON=10 14
// Remove uma skill
ctag.macroer_hours=<ARGTXT[0]>
ctag.macroer_minutes=<ARGTXT[1]>
ctag.macroer_penalty=<ARGTXT[2]>
macroer_DeleteSkill <eval <argn>-10>

[FUNCTION admin_suspendPages]
obj=<cTAG0.admin_player>
if (<argn> < 1)
    obj.tag.noPagesTil=
else
    obj.tag.noPagesTil=<eval <serv.time>+(<argn>*60*10)>
endif
DIALOG d_sphereadmin

[FUNCTION admin_removeChar]
if (<argn>==<ctag0.chalenge>)
    trysrv uid.<ctag.admin_player>.remove 1
else
    sysmessagered Numero incorreto.
    DIALOG d_sphereadmin
endif

[DEFNAME IM]
IM_RECIVE       1
IM_SEND         2

[FUNCTION InstantMessage]
//Abre o diálogo de mensagem instantanea para player <argn>
if (!<argv>)
    return
endif
clearctags IM_
ctag.IM_to=<argn>
ctag.IM_task=IM_SEND
DIALOGCLOSE d_messenger
DIALOG d_messenger

[DIALOG d_messenger]
75,75
page 0
resizepic 0 0 9270 491 238
IF (<ctag0.IM_TASK>==IM_SEND)
    dhtmlgump 17 16 451 22 0 0 <DEF.BFONT_LRED><DEF.CENTER>Sistema de mensagens online: <uid.<ctag.IM_to>.tag.name><DEF.DIV_E>
    dtext 21 42 52 Mensagem:
    resizepic 93 44 9350 378 64
    dtextentry 98 49 367 55 0 0
    dtext 58 112 52 Link:
    dtext 27 129 52 (opcional)
    resizepic 92 114 9350 378 49
    dtextentry 97 119 367 40 0 1 
    IF (<IsGM>)
        checkbox 97 167 210 211 0 0
        dtext 124 167 4 Permitir responder mensagem
    ENDIF
    button 412 200 247 248 1 0 1
    button 14 200 241 242 1 0 0
    page 2
ELSE
    dhtmlgump 17 16 451 22 0 0 <DEF.BFONT_LRED><DEF.CENTER>Sistema de mensagens online: <uid.<ctag.IM_Sender>.tag.name><DEF.DIV_E>
    dhtmlgump 19 43 448 72 7 1 <ctag.IM_Text>
    if (!<IsEmpty <ctag.IM_link>>)
        dhtmlgump 99 125 372 47 1 0 <ctag.IM_link>
        button 25 126 2224 2224 1 0 2
        dtext 47 122 52 Visitar
    endif
    button 451 206 4017 4018 1 0 0
    if (<ctag0.IM_CanReply>) || (<IsGM>)
        button 183 191 2445 2445 1 0 3
        dtext 209 192 52 Responder
    endif
ENDIF

[DIALOG d_messenger text]

[DIALOG d_messenger button]
ON=0
clearctags IM_
if (!<IsEmpty <ctag0.admin_task>>)//Veio do .admin/.ficha?
    DIALOG d_sphereadmin
endif

ON=1
//Send
obj=<ctag0.IM_to>
obj.ctag.IM_Sender=<uid>
obj.ctag.IM_Text=<argtxt[0]>
if (<eval strlen(<argtxt[1]>)> > 7 )
    obj.ctag.IM_Link=<argtxt[1]>
endif
obj.ctag.IM_CanReply=<eval <argchk[0]>>
obj.ctag.IM_task=IM_RECIVE
trysrc <obj> src.DIALOG d_messenger
if (!<IsEmpty <ctag0.admin_task>>)//Veio do .admin/.ficha?
    DIALOG d_sphereadmin
endif

ON=2
//Open Link
if (!<IsEmpty <ctag.IM_link>>)
    if (STRMATCH(http*,<ctag.IM_link>))
        local.link=<STRSUB 7 <eval strlen(<ctag.IM_link>)> <ctag.IM_link>>
    else
        local.link=<ctag.IM_link>
    endif
    WEBLINK=<local.link>
else
    sysmessagered Link invalido. Expirou.
endif
DIALOG d_messenger

ON=3
//Repty
InstantMessage <ctag.IM_sender>

//*****************************************************************************
// Created 8/4/2010 13:11:48, with Gump Studio.
// Exported with with SphereExporter ver 1.1.
// Script for 0.56/Revisions
//*****************************************************************************
[DIALOG d_ficharp]
//*****************************************************************************
//Ficha de dados RP do personagem.
75,75
page 0
obj=<ctag0.ficharp_UID>
resizepic 0 0 9270 640 460
dhtmlgump 13 15 608 21 0 0 <DEF.CENTER><DEF.BFONT_DCYAN>[<obj.account>] -> <obj.tag.name> -> <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_HEADER><DEF.CENTERE>
ficharp_renderbuttons

IF (<ctag.ficharp_page>==FICHARP_PAGE_FACE) || (<ctag.ficharp_page>==FICHARP_PAGE_MARKS) || (<ctag.ficharp_page>==FICHARP_PAGE_PSYCHO) || (<ctag.ficharp_page>==FICHARP_PAGE_NATURE) || (<ctag.ficharp_page>==FICHARP_PAGE_RACE) || (<ctag.ficharp_page>==FICHARP_PAGE_CLASS)
    DB.QUERY="SELECT (TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND) < NOW() as can, DATE_FORMAT(TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND,'%d/%m/%Y %H:%i') as ETA, value, DATE_FORMAT(timestamp,'%d/%m/%Y %H:%i') as date from background WHERE uid=<dctag0.ficharp_uid> AND kind=<eval <DEF.<ctag0.ficharp_page>>>;"
    resizepic 166 42 9270 454 123
    dhtmlgump 176 52 431 100 3 0 <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_INFO>
    resizepic 166 172 9270 454 120
    resizepic 178 184 9350 431 97
    local.size=<EVAL (STRLEN(<DB.ROW.0.VALUE>)) > 1>
    if ( (0<DB.ROW.0.can>) || (!<DB.ROW.NUMROWS>) ) && (<ctag.ficharp_uid>==<uid>)
        button 590 425 4029 4030 1 0 100
        dtextentry 181 188 423 90 0 0 <QVAL <local.size>?<DB.ROW.0.VALUE>:>
        dtext 200 300 048 Editavel somente a cada <f_timestring <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>>
    else
        dhtmlgump 181 188 423 90 0 0 <QVAL <local.size>?<DB.ROW.0.VALUE>:>
        if (<ctag.ficharp_uid>==<uid>)//player vendo
            dtext 200 300 021 Editavel somente em <DB.ROW.0.ETA>
        else//GM vendo.
            dtext 200 300 048 Ultimo edit em <DB.ROW.0.date>
        endif
    endif
    //button 170 425 4020 248 1 0 1
    
ELIF (<ctag.ficharp_page>==FICHARP_PAGE_BODY)
    DB.QUERY="SELECT (TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND) < NOW() as can, DATE_FORMAT(TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND,'%d/%m/%Y %H:%i') as ETA, value, DATE_FORMAT(timestamp,'%d/%m/%Y %H:%i') as date from background WHERE uid=<dctag0.ficharp_uid> AND kind=2;"
    local.can=<eval ( (0<DB.ROW.0.can>) || (!<DB.ROW.NUMROWS>) ) && (<ctag0.ficharp_uid>==<uid>)>
    if (!<DB.ROW.NUMROWS>)
        call ficharp_explode_body 2,2,4//Tipo fisico padrao
    else
        call ficharp_explode_body <DB.ROW.0.value>
    endif
    resizepic 166 42 9270 454 380
    checkertrans 179 54 428 354
    resizepic 186 62 9270 414 103
    dhtmlgump 196 72 391 80 3 0 <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_INFO>
    dorigin 200 150
    dtext +15 *20 042 Estatura
    local.i=0
    local.j=0
    Group 0
    WHILE (!<IsEmpty <DEF.FICHARP_BODY_0_<dlocal.i>>>)
        dtext +20 *20 2100 <DEF.FICHARP_BODY_0_<dlocal.i>>
        if (<local.can>)
            radio - - 210 211 <QVAL <local.height>==<local.i>?1:0> <dlocal.j>
            local.j += 1
        else
            gumppic - - <QVAL <local.height>==<local.i>?211:210>
        endif
        local.i += 1
    ENDDO
    dorigin 320 150
    dtext +15 *20 042 Tipo fisico
    local.i=0
    Group 1
    WHILE (!<IsEmpty <DEF.FICHARP_BODY_1_<dlocal.i>>>)
        dtext +20 *20 2100 <DEF.FICHARP_BODY_1_<dlocal.i>>
        if (<local.can>)
            radio - - 210 211 <QVAL <local.weight>==<local.i>?1:0> <dlocal.j>
            local.j += 1
        else
            gumppic - - <QVAL <local.weight>==<local.i>?211:210>
        endif
        local.i += 1
    ENDDO
    dorigin 440 150
    dtext +15 *20 042 Movimentos
    local.i=0
    Group 2
    WHILE (!<IsEmpty <DEF.FICHARP_BODY_2_<dlocal.i>>>)
        dtext +20 *20 2100 <DEF.FICHARP_BODY_2_<dlocal.i>>
        if (<local.can>)
            radio - - 210 211 <QVAL <local.agility>==<local.i>?1:0> <dlocal.j>
            local.j += 1
        else
            gumppic - - <QVAL <local.agility>==<local.i>?211:210>
        endif
        local.i += 1
    ENDDO

    if (<local.can>)
        button 590 425 4029 4030 1 0 100
        dtext 200 380 048 Editavel somente a cada <f_timestring <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>>
    else
        if (<IsGM>)
            dtext 200 380 048 Ultimo edit em <DB.ROW.0.date>
        else
            dtext 200 380 021 Editavel somente em <DB.ROW.0.ETA>
        endif
    endif

ELIF (<ctag0.ficharp_page>!=FICHARP_PAGE_CRITICS)
    if (<ctag0.ficharp_task>==FICHARP_TASK_SHOW)
    //Mostrar a lista e páginas
        DB.QUERY="SELECT count(kind) AS cnt FROM background WHERE kind=<eval <DEF.<ctag0.ficharp_page>>> AND uid=<dctag.ficharp_uid>;"
        if (<DB.ROW.0.cnt>)//Só monta se houvferem valores
           local.pages=<eval (<DB.ROW.0.cnt>/4)+1>
           local.page=<eval <ctag0.ficharp_pagen>+1>
           local.start=<eval <ctag0.ficharp_pagen>*4>
           DB.QUERY="SELECT (TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND) < NOW() as can, value, DATE_FORMAT(timestamp,'%d/%m/%Y %H:%i') as date FROM background WHERE uid=<dctag0.ficharp_uid> AND kind=<eval <DEF.<ctag0.ficharp_page>>> ORDER BY timestamp ASC LIMIT <dlocal.start>,<eval <dlocal.start>+4>;"
           resizepic 166 42 9270 454 380
           checkertrans 179 54 428 354
           dorigin 185 -20
           FOR i 0 <eval <DB.ROW.NUMROWS>-1>
               dhtmlgump - *80 400 80 0 1 <DEF.BFONT_YELLOW>     <DB.ROW.<dlocal.i>.date><DEF.BFONT_BLUE> - <DEF.BFONT_WHITE><DB.ROW.<dlocal.i>.value>
               button +1 +1 4011 4012 1 0 <eval 1000+(<local.i>+(4*<ctag0.ficharp_pagen>))>
           ENDFOR
           if (<local.page>!=1)//Botão de voltar página
               button 170 425 4014 4015 1 0 102
           endif
           if (<local.page>!=<local.pages>)//Botão de avançar página
               button 590 425 4005 4006 1 0 103
           endif
           dtext 355 390 0C2 Pagina <dlocal.page>/<dlocal.pages>           

       else//Página vazia. Descreva seu funcionamento
           resizepic 166 42 9270 454 123
           dhtmlgump 176 52 431 100 3 0 <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_INFO>
       endif
       
       DB.QUERY="SELECT (TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND) < NOW() as can FROM background WHERE uid=<dctag0.ficharp_uid> AND kind=<eval <DEF.<ctag0.ficharp_page>>> ORDER BY timestamp DESC LIMIT 1;"
       if ( (0<DB.ROW.0.CAN>) || (!<DB.ROW.NUMROWS>) ) && (<ctag0.ficharp_uid>==<uid>)//Decide se pode postar alguma coisa nova.
            button 375 425 4011 4012 1 0 101
       endif

    elif  (<ctag0.ficharp_task>==FICHARP_TASK_ADD)
    //Adicionar uma entrada nova.
        resizepic 166 42 9270 454 123
        dhtmlgump 176 52 431 100 3 0 <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_INFO>
        resizepic 166 172 9270 454 120
        resizepic 178 184 9350 431 97
        button 590 425 4029 4030 1 0 100
        textentry 181 188 423 90 0 0 <QVAL <local.size>?<DB.ROW.0.VALUE>:>
    else//Editando uma entrada existente.
        DB.QUERY="SELECT (TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND) < NOW() as can, value, DATE_FORMAT(TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND,'%d/%m/%Y %H:%i') as ETA FROM background WHERE uid=<dctag0.ficharp_uid> AND kind=<eval <DEF.<ctag0.ficharp_page>>> ORDER BY timestamp ASC LIMIT <dctag0.ficharp_entry>,1;"
        resizepic 166 42 9270 454 123
        dhtmlgump 176 52 431 100 3 0 <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_INFO>
        resizepic 166 172 9270 454 120
        resizepic 178 184 9350 431 97
        local.size=<EVAL (STRLEN(<DB.ROW.0.VALUE>)) > 1>
        if (0<DB.ROW.0.can>) || (!<DB.ROW.NUMROWS>)
            button 590 425 4029 4030 1 0 100
            button 170 425 4017 4018 1 0 104
            dtextentry 181 188 423 90 0 0 <QVAL <local.size>?<DB.ROW.0.VALUE>:>
            dtext 200 300 048 Editavel somente a cada <f_timestring <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>>
        else
            dhtmlgump 181 188 423 90 0 0 <QVAL <local.size>?<DB.ROW.0.VALUE>:>
            dtext 200 300 021 Editavel somente em <DB.ROW.0.ETA>
        endif
    endif
ELSE//Página de críticas
    if (<ctag0.ficharp_task>==FICHARP_TASK_SHOW)
    //Mostrar a lista e páginas
        DB.QUERY="SELECT count(kind) AS cnt FROM background WHERE kind=<eval <DEF.<ctag0.ficharp_page>>> AND uid=<dctag.ficharp_uid>;"
        if (<DB.ROW.0.cnt>)//Só monta se houvferem valores
           local.pages=<eval (<DB.ROW.0.cnt>/4)+1>
           local.page=<eval <ctag0.ficharp_pagen>+1>
           local.start=<eval <ctag0.ficharp_pagen>*4>
           DB.QUERY="SELECT (TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND) < NOW() as can, value, DATE_FORMAT(timestamp,'%d/%m/%Y %H:%i') as date FROM background WHERE uid=<dctag0.ficharp_uid> AND kind=<eval <DEF.<ctag0.ficharp_page>>> ORDER BY timestamp ASC LIMIT <dlocal.start>,<eval <dlocal.start>+4>;"
           resizepic 166 42 9270 454 380
           checkertrans 179 54 428 354
           dorigin 185 -20
           FOR i 0 <eval <DB.ROW.NUMROWS>-1>
               dhtmlgump - *80 400 80 0 1 <DEF.BFONT_YELLOW>     <DB.ROW.<dlocal.i>.date><DEF.BFONT_BLUE> - <DEF.BFONT_WHITE><DB.ROW.<dlocal.i>.value>
               button +1 +1 4011 4012 1 0 <eval 1000+(<local.i>+(4*<ctag0.ficharp_pagen>))>
           ENDFOR
           if (<local.page>!=1)//Botão de voltar página
               button 170 425 4014 4015 1 0 102
           endif
           if (<local.page>!=<local.pages>)//Botão de avançar página
               button 590 425 4005 4006 1 0 103
           endif
           dtext 355 390 0C2 Pagina <dlocal.page>/<dlocal.pages>           

       elif (<IsGM>)//Página vazia. Descreva seu funcionamento
           resizepic 166 42 9270 454 123
           dhtmlgump 176 52 431 100 1 1 <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_INFO>
       else //Player vendo página vazia
           resizepic 166 42 9270 454 123
           dhtmlgump 176 52 431 100 1 0 GMs usarao esta area para comentar sobre o andamento da sua representacao quando necessario.
       endif
       
       if (<IsGM>)//Decide se pode postar alguma coisa nova.
            button 375 425 4011 4012 1 0 101
       endif

    elif  (<ctag0.ficharp_task>==FICHARP_TASK_ADD)
    //Adicionar uma entrada nova.
        resizepic 166 42 9270 454 123
        dhtmlgump 176 52 431 100 3 0 <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_INFO>
        resizepic 166 172 9270 454 120
        resizepic 178 184 9350 431 97
        button 590 425 4029 4030 1 0 100
        textentry 181 188 423 90 0 0 <QVAL <local.size>?<DB.ROW.0.VALUE>:>
    else//Editando uma entrada existente.
        DB.QUERY="SELECT (TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND) < NOW() as can, value, DATE_FORMAT(TIMESTAMP +INTERVAL <EVAL <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_COOLDOWN>> SECOND,'%d/%m/%Y %H:%i') as ETA FROM background WHERE uid=<dctag0.ficharp_uid> AND kind=<eval <DEF.<ctag0.ficharp_page>>> ORDER BY timestamp ASC LIMIT <dctag0.ficharp_entry>,1;"
        resizepic 166 42 9270 454 123
        dhtmlgump 176 52 431 100 3 0 <DEF.FICHARP_<eval <DEF.<ctag0.ficharp_page>>>_INFO>
        resizepic 166 172 9270 454 120
        resizepic 178 184 9350 431 97
        local.size=<EVAL (STRLEN(<DB.ROW.0.VALUE>)) > 1>
        if (<IsGM>)
            button 590 425 4029 4030 1 0 100
            button 170 425 4017 4018 1 0 104
            dtextentry 181 188 423 90 0 0 <QVAL <local.size>?<DB.ROW.0.VALUE>:>
        else
            dhtmlgump 181 188 423 90 0 0 <QVAL <local.size>?<DB.ROW.0.VALUE>:>
        endif
    endif
ENDIF
//*****************************************************************************
[DIALOG d_ficharp text]

//*****************************************************************************
[DIALOG d_ficharp button]
ON=0

ON=1 15
ctag.ficharp_pagen=0
DOSWITCH <argn>
    return
    ctag.ficharp_page=FICHARP_PAGE_FACE
    ctag.ficharp_page=FICHARP_PAGE_BODY
    ctag.ficharp_page=FICHARP_PAGE_MARKS
    ctag.ficharp_page=FICHARP_PAGE_PSYCHO
    ctag.ficharp_page=FICHARP_PAGE_NATURE
    ctag.ficharp_page=FICHARP_PAGE_RACE
    ctag.ficharp_page=FICHARP_PAGE_CLASS
    ctag.ficharp_page=FICHARP_PAGE_QUEST
    ctag.ficharp_page=FICHARP_PAGE_PAST
    ctag.ficharp_page=FICHARP_PAGE_EARLYS
    ctag.ficharp_page=FICHARP_PAGE_MEMO
    ctag.ficharp_page=FICHARP_PAGE_CRITICS
ENDDO
ctag.ficharp_task=FICHARP_TASK_SHOW
dialogclose d_ficharp
DIALOG d_ficharp

ON=100
// Manda atualizar a ficha pelo <ARGTXT[0]>
CALL ficharp_atualize

ON=101
// Add comentario
ctag.ficharp_task=FICHARP_TASK_ADD
dialogclose d_ficharp
DIALOG d_ficharp

ON=102
//Previous
ctag0.ficharp_pagen -= 1
dialogclose d_ficharp
DIALOG d_ficharp

ON=103
//Next
ctag0.ficharp_pagen += 1
dialogclose d_ficharp
DIALOG d_ficharp

ON=104
//Delete entry
prompt_new Deseja realmente apagar este bloco do seu background?
prompt_setAction ficharp_deleteEntry
prompt_setActionBack DIALOG d_ficharp
ynprompt_show   

ON=1000 9999
ctag.ficharp_entry=<eval <argn>-1000>
ctag.ficharp_task=FICHARP_TASK_EDIT
dialogclose d_ficharp
DIALOG d_ficharp

//*****************************************************************************
// Created 9/4/2010 13:55:16, with Gump Studio.
// Exported with with SphereExporter ver 1.1.
// Script for 0.56/Revisions
[DIALOG d_profile]
//*****************************************************************************
//Dialog do botão 'profile' dos chars.
100,100
page 0
resizepic 0 0 9380 265 296
dhtmlgump 0 5 265 20 0 0 <DEF.CENTER><DEF.BFONT_SIZE4><DEF.BFONT_DRED>Perfil fisico<DEF.CENTERE>
dhtmlgump 27 37 213 223 0 1 <ficharp_BuildProfile>
if (<ctag0.ficharp_uid>==<uid>) || (<IsGM>)
    button 230 275 2511 2510 1 0 2
elif (!<IsGM>) && (<rec_canSee <ctag0.ficharp_uid>>) && (<canseelos <ctag0.ficharp_uid>>)
    button 22 277 2224 2224 1 0 1
    dtext 45 273 642 Reconhecer
endif

[DIALOG d_profile text]
Reconhecer

[DIALOG d_profile button]
ON=1
// Reconhecer

ON=2
// Ficharp
ficharp <ctag0.ficharp_uid>

[EOF]

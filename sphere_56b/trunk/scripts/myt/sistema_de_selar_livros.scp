/ Script downloaded from Emulator Resource
// Website - celeron.twilightmanor.net
// Contact - celeron@twilightmanor.net
// Author - Rudenid
//Book Copying script
//V2.0
//By Rudenid.
//Use Pen and Ink to copy a blank book.
//Use paper seals to seal a not-so-blank book.
//I put the seals on scribe NPCs myself, but feel free to distribute them however you'd like
//EDITADO POR MIM. :D Silver huahauaha

[DIALOG d_sealbook]
0,0
page 0
gumppic 150 130 2202
gumppic 0 0 2204
text 205 175 0 1
text 215 270 0 2
text 355 180 0 3
text 390 210 0 4
text 390 235 0 5
text 405 305 0 6
button 355 205 2474 2473 1 0 1
button 355 230 2474 2473 1 0 2

[DIALOG d_sealbook TEXT]
Nothing
<NAME>
<AUTHOR>
Selar este livro?
Sim
Nao
<PAGES> Pages

[DIALOG d_sealbook BUTTON]

ONBUTTON=1

IF !(<TYPE> == t_book)
SRC.SYSMESSAGE Isso nao e um livro.
RETURN 1
ENDIF

IF (<MORE1> & 080000000)
SRC.SYSMESSAGE Esse livro ja esta selado.
RETURN 1
ENDIF

IF (<ATTR> & attr_move_never)
SRC.SYSMESSAGE Voce nao pode selar isto.
RETURN 1
ENDIF

IF (<SRC.UID> != <TOPOBJ.UID>)
SRC.SYSMESSAGE O livro deve estar em sua mochila para ser selado.
RETURN 1
ENDIF

IF (<SRC.RESCOUNT i_seal_paper> < <PAGES>)
SRC.SYSMESSAGE Esse livro tem <PAGES> paginas, e no entanto voce apenas tem <SRC.RESCOUNT i_seal_paper> selos. Voce precisa de um por pagina.
RETURN 1
ENDIF

VAR.REQINSCRIPTION=<eval <PAGES> * 10.0>
IF (<eval <VAR.REQINSCRIPTION>> > 100.0)
VAR.REQINSCRIPTION=100.0
ENDIF
VAR.ALWAYSSCRIBE=<eval <VAR.REQINSCRIPTION> * 4>
VAR.ALWAYSSCRIPT=<eval <VAR.ALWAYSSCRIBE> / <SRC.INSCRIPTION>>
VAR.CHANCESCRIBE=rand(<eval <VAR.ALWAYSSCRIBE>>)

IF (<eval <VAR.REQINSCRIPTION>> > <SRC.INSCRIPTION>)
SRC.SYSMESSAGE Voce nao tem conhecimento para selar um livro de <PAGES> paginas.
RETURN 1
ENDIF

IF (<eval <VAR.CHANCESCRIBE>> > <SRC.INSCRIPTION>)
SRC.SYSMESSAGE Voce falhou em selar o livro.
ELSE
SRC.SFX 79
SRC.CONSUME <PAGES> i_seal_paper
SRC.SYSMESSAGE Voce sela o livro.
MORE1=080000000
ENDIF

VAR.CHANCESCRIBE=
VAR.ALWAYSSCRIBE=
VAR.REQINSCRIPTION=

[ITEMDEF 0fbf]
//pen and ink
DEFNAME=i_pen_and_ink
RESOURCES=1 i_FEATHER, 1 i_INK_POT
WEIGHT=2
CATEGORY=MyT - Items by Professions
SUBSECTION=Artists
DESCRIPTION=Pen & Ink (W)
DUPELIST=0fc0

ON=@DCLICK
TARGET Escolha um livro de onde copiar.
RETURN 1

ON=@TARGON_ITEM
IF <LINK.SERIAL>==0
IF !(<SRC.TARG.TYPE>==t_book)
SRC.SYSMESSAGE Isso nao e um livro.
RETURN 1
ENDIF
//IF (<SRC.TARG.ATTR>&010)
//SRC.SYSMESSAGE ///////
//RETURN 1
//ENDIF
LINK=<SRC.TARG.SERIAL>
TARGET Escolha um livro de onde copiar.
RETURN 1
ELSE
IF !(<SRC.TARG.TYPE>==t_book)
SRC.SYSMESSAGE Isto nao e um livro.
LINK=0
RETURN 1
ENDIF
IF (<SRC.TARG.ATTR>&010)
SRC.SYSMESSAGE Voce nao pode mover isto.
LINK=0
RETURN 1
ENDIF
IF (<SRC.TARG.MORE1>&080000000)
SRC.SYSMESSAGE Esse livro esta selado e nada pode ser escrito nele.
LINK=0
RETURN 1
ENDIF
IF !(<SRC.TARG.CONT>==<SRC.FINDLAYER(21).SERIAL>)
SRC.SYSMESSAGE Este livro deve estar em sua mochila.
LINK=0
RETURN 1
ENDIF
IF (<SRC.TARG.PAGES> > 0)
SRC.SYSMESSAGE Esse livro ja foi escrito.
LINK=0
RETURN 1
ENDIF

IF (rand(110.0) < <SRC.INSCRIPTION>)
SRC.NEWITEM <SRC.TARG.ID>
SRC.ACT.ATTR=0
SRC.ACT.MORE1=0
SRC.ACT.NAME=<LINK.NAME>
SRC.ACT.AUTHOR=<LINK.AUTHOR>
SRC.ACT.LINK=<LINK.UID>
SRC.ACT.MOREX=0
SRC.COPYBOOK
SRC.ACT.LINK=
SRC.ACT.DISPID=<SRC.TARG.DISPID>
SRC.ACT.COLOR=<SRC.TARG.COLOR>
SRC.SFX 0247
SRC.SYSMESSAGE Voce copiou o livro com sucesso.
SRC.ACT.BOUNCE
SRC.TARG.REMOVE
LINK=0
ELSE
SRC.SYSMESSAGE Voce falhou em copiar o livro.
LINK=0
ENDIF

IF (<SRC.INSCRIPTION> < 100.0)

IF (<SRC.INSCRIPTION> < 30.0)
IF (rand(3)=1)
SRC.INSCRIPTION=<SRC.INSCRIPTION>+{ 1 20 2 5 3 2 4 1 }
ENDIF
ELSEIF (<SRC.INSCRIPTION> < 50.0)
IF (rand(75)=1)
SRC.INSCRIPTION=<SRC.INSCRIPTION>+{ 1 20 2 5 3 2 4 1 }
ENDIF
ELSEIF (<SRC.INSCRIPTION> < 80.0)
IF (rand(100)=1)
SRC.INSCRIPTION=<SRC.INSCRIPTION>+{ 1 20 2 5 3 2 4 1 }
ENDIF
ELSE
IF (rand(200)=1)
SRC.INSCRIPTION=<SRC.INSCRIPTION>+{ 1 20 2 5 3 2 4 1 }
ENDIF
ENDIF

ENDIF

IF (<SRC.INSCRIPTION> > 100.0)
SRC.INSCRIPTION=100.0
ENDIF

RETURN 1

[FUNCTION copybook]
IF <ACT.MOREX> > <ACT.LINK.PAGES>
ACT.MOREX=0
RETURN 1
ENDIF
TRYP 0 ACT.BODY<ACT.MOREX>=<ACT.LINK.BODY<ACT.MOREX>>
ACT.MOREX=<ACT.MOREX>+1
COPYBOOK

[ITEMDEF 0fc0]
//pen and ink
DUPEITEM=0fbf
CATEGORY=MyT - Items by Professions
SUBSECTION=Artists
DESCRIPTION=Pen & Ink (N)

ON=@DCLICK
TARGET Escolha um livro de onde copiar.
RETURN 1

ON=@TARGON_ITEM
IF <LINK.SERIAL>==0
IF !(<SRC.TARG.TYPE>==t_book)
SRC.SYSMESSAGE Isso nao e um livro.
RETURN 1
ENDIF
//IF (<SRC.TARG.ATTR>&010)
//SRC.SYSMESSAGE You cannot copy a locked down book
//RETURN 1
//ENDIF
LINK=<SRC.TARG.SERIAL>
TARGET EScolha um livro para copiar.
RETURN 1
ELSE
IF !(<SRC.TARG.TYPE>==t_book)
SRC.SYSMESSAGE Isso nao e um livro.
LINK=0
RETURN 1
ENDIF
IF (<SRC.TARG.ATTR>&010)
SRC.SYSMESSAGE Voce nao pode mover isto.
LINK=0
RETURN 1
ENDIF
IF (<SRC.TARG.MORE1>&080000000)
SRC.SYSMESSAGE Esse livro esta selado e nao pode ser escrito.
LINK=0
RETURN 1
ENDIF
IF !(<SRC.TARG.CONT>==<SRC.FINDLAYER(21).SERIAL>)
SRC.SYSMESSAGE O livro deve estar em sua mochila.
LINK=0
RETURN 1
ENDIF
IF (<SRC.TARG.PAGES> > 0)
SRC.SYSMESSAGE Esse livro ja foi escrito.
LINK=0
RETURN 1
ENDIF

IF (rand(110.0) < <SRC.INSCRIPTION>)
SRC.NEWITEM <SRC.TARG.ID>
SRC.ACT.ATTR=0
SRC.ACT.MORE1=0
SRC.ACT.NAME=<LINK.NAME>
SRC.ACT.AUTHOR=<LINK.AUTHOR>
SRC.ACT.LINK=<LINK.UID>
SRC.ACT.MOREX=0
SRC.COPYBOOK
SRC.ACT.LINK=
SRC.ACT.DISPID=<SRC.TARG.DISPID>
SRC.ACT.COLOR=<SRC.TARG.COLOR>
SRC.SFX 0247
SRC.SYSMESSAGE Voce copiou o livro com sucesso.
SRC.ACT.BOUNCE
SRC.TARG.REMOVE
LINK=0
ELSE
SRC.SYSMESSAGE Voce falhou em copiar o livro.
LINK=0
ENDIF

IF (<SRC.INSCRIPTION> < 100.0)

IF (<SRC.INSCRIPTION> < 30.0)
IF (rand(3)=1)
SRC.INSCRIPTION=<SRC.INSCRIPTION>+{ 1 20 2 5 3 2 4 1 }
ENDIF
ELSEIF (<SRC.INSCRIPTION> < 50.0)
IF (rand(75)=1)
SRC.INSCRIPTION=<SRC.INSCRIPTION>+{ 1 20 2 5 3 2 4 1 }
ENDIF
ELSEIF (<SRC.INSCRIPTION> < 80.0)
IF (rand(100)=1)
SRC.INSCRIPTION=<SRC.INSCRIPTION>+{ 1 20 2 5 3 2 4 1 }
ENDIF
ELSE
IF (rand(200)=1)
SRC.INSCRIPTION=<SRC.INSCRIPTION>+{ 1 20 2 5 3 2 4 1 }
ENDIF
ENDIF

ENDIF

IF (<SRC.INSCRIPTION> > 100.0)
SRC.INSCRIPTION=100.0
ENDIF

RETURN 1

[ITEMDEF i_seal_paper]
ID=i_bedroll_open
TYPE=t_normal
NAME=paper seal
RESOURCES=2 i_hides_cut
SKILLMAKE=TAILORING 70.0, t_sewing_kit

ON=@DClick
TARGET Escolha o livro a ser selado.
RETURN 1

ON=@Targon_Item
IF !(<SRC.TARG.TYPE> == t_book)
SRC.SYSMESSAGE Isso nao é um livro.
RETURN 1
ENDIF

IF (<SRC.TARG.MORE1> & 080000000)
SRC.SYSMESSAGE Esse livro ja esta selado.
RETURN 1
ENDIF

IF (<SRC.TARG.ATTR> & attr_move_never)
SRC.SYSMESSAGE Voce nao pode selar isso.
RETURN 1
ENDIF

IF (<SRC.UID> != <SRC.TARG.TOPOBJ.UID>)
SRC.SYSMESSAGE O livro deve estar em sua mochila para ser selado.
RETURN 1
ENDIF

IF (<SRC.RESCOUNT i_seal_paper> < <SRC.TARG.PAGES>)
SRC.SYSMESSAGE Esse livro tem <SRC.TARG.PAGES> paginas, ae voce apenas tem<SRC.RESCOUNT i_seal_paper> selos. Voce precisa de um por pagina.
RETURN 1
ENDIF

SRC.TARG.DIALOG d_sealbook

RETURN 1

[FUNCTION desselar_livro]
more1 ^= 080000000

[FUNCTION selar_livro]
more1 |= 080000000


[FUNCTION f_book_start]
if (<ISEMPTY <tag.name>>)
 LOCAL.name=Livro
 tag.name=Livro
else
 LOCAL.name=<tag.name>
endif
if (<ISEMPTY <tag.author>>)
 LOCAL.author=Desconhecido
 tag.author=Deconhecido
else
 LOCAL.author=<tag.author>
endif

[TYPEDEF t_book]
on=@ClientToolTip
if (<more2>)
 return 0
endif
call f_book_start
src.addcliloc 1042971,<LOCAL.name>
src.addcliloc 1070722,Por: <LOCAL.author>
return 1

on=@dclick
if (<more2>)
 return 0
endif
dialogclose d_book
src.cTAG.book=<UID>
src.ctag.book.page=1
src.DIALOG d_book
return 1

[DIALOG d_book]
150,150
page 0
obj=<src.ctag.book>
gumppic 0 0 510

//Pagina 1
if (<src.ctag.book.page> == 1)
 dtext 82 19 9 Titulo
 dtext 86 102 9 Autor
 if (<src.ctag0.book.edit>==1)
  dtextentry 41 40 143 22 5 0 <obj.tag.name>
  dtextentry 41 127 143 22 5 1 <obj.tag.author>
 else
  dtext 41 40 0 <obj.tag.name>
  dtext 41 127 0 <obj.tag.author>
 endif
 if ((<src.isgm>) || (<obj.tag0.owner>==<src.uid>) || (!<obj.tag0.owner>)) && (!<src.ctag0.book.edit>)
  button 54 161 2445 2445 1 0 5
  if (!<obj.tag0.owner>)
   dtext 80 162 92  ser dono
  elif (<obj.more1>&080000000)
   dtext 90 162 368  editar
  else
   dtext 80 162 38  nao editar
  endif
 endif
else
 if (<src.ctag0.book.edit>==<src.ctag0.book.page>)
  dtextentry 40 41 143 155 5 0 <obj.tag.page.<src.dctag0.book.page>>
 else
  dhtmlgump 40 38 143 159 0 0 <obj.tag.page.<src.dctag0.book.page>>
 endif
endif
dtext 100 200 894 <src.dctag.book.page>

//Pagina 2
if (<src.ctag0.book.edit>==<src.ctag0.book.page>+1)
 dtextentry 232 40 143 156 3 0 <obj.tag.page.<eval <src.ctag0.book.page>+1>>
else
 dhtmlgump 232 40 146 156 0 0 <obj.tag.page.<eval <src.ctag0.book.page>+1>>
endif
dtext 300 200 894 <eval <src.ctag.book.page>+1>

//Folhas
if (<src.ctag.book.page> != 1) && (!<src.ctag0.book.edit>)
 button 0 0 511 511 1 0 1	//folha -
endif
if (<src.ctag.book.page> < 62) && (!<src.ctag0.book.edit>)
 button 356 0 512 512 1 0 2	//folha +
endif

if (!(<obj.more1>&080000000) || (<src.ISGM>)) && (!<src.ctag0.book.edit>)
 tilepic 19 200 4032
 button 49 205 1209 1210 1 0 3	//edit 1
 tilepic 353 200 4032
 button 355 205 1209 1210 1 0 4	//edit 2
endif

[DIALOG d_book text]
HtmlGump id.0
Textentry id.0
HtmlGump id.1
Textentry id.1

[DIALOG d_book button]
ON=0
if (<src.ctag0.book.edit>)
 obj=<src.ctag0.book>
 //Estamos editando o cabeçalho aqui...
 if (<src.ctag0.book.edit>==1)
  obj.tag.name=<argtxt[0]>
  obj.tag.author=<argtxt[1]>
  obj.updatex
 else
  obj.tag.page.<src.dctag0.book.edit>=<argtxt[0]>
 endif
 src.ctag0.book.edit
 dialog d_book
 return 1
endif

ON=1
// Folha -
src.ctag.book.page -= 2
dialog d_book
return 1

ON=2
// Folha +
src.ctag.book.page += 2
dialog d_book
return 1

ON=3
// Edit 1
src.ctag.book.edit=<src.ctag0.book.page>
dialog d_book
return 1

ON=4
// Edit 2
src.ctag.book.edit=<eval <src.ctag0.book.page>+1>
dialog d_book
return 1

ON=5
// Ownership
obj=<src.ctag.book>
if (!<obj.tag.owner>)
 obj.tag.owner=<src.UID>
elif (<obj.more1>&080000000)
 obj.more1 ^= 080000000
else
 obj.more1 |= 080000000
endif
dialog d_book
return 1

[ITEMDEF i_carta]
ID=0e34
NAME=carta
TYPE=t_book_scroll

CATEGORY=MyT - Sistemas
SUBSECTION=Livros
DESCRIPTION=Carta

on=@create
more1=0		//Não selada
more2=1		//Editável

[TYPEDEF t_book_scroll]
on=@dclick
src.ctag.scroll=<UID>
src.DIALOG d_scroll
return 1

//Triggers gambiarra para fizar em loot de NPCs. Se usar attr 010 ele some quando o char morre.
on=@DropOn_Item
if (<tag0.nomove>)
 timerf 86400,remove
endif

on=@DropOn_Char
if (<tag0.nomove>)
 timerf 86400,remove
endif

on=@DropOn_Self
if (<tag0.nomove>)
 timerf 86400,remove
endif


[DIALOG d_scroll]
150,150
page 0
obj=<src.ctag0.scroll>
resizepic 0 0 9380 307 316
if (!<src.ctag0.scroll.edit>) && (<obj.link> == 04fffffff)
 dhtmlgump 32 35 253 245 0 1 <DEF.BFONT_BLACK><QVAL !<ISEMPTY <obj.tag.text>> ? <obj.tag.text> :>
ELIF (<obj.link.baseid>==i_trig_msg_gen)
 dhtmlgump 32 35 253 245 0 1 <DEF.BFONT_BLACK><QVAL !<ISEMPTY <obj.link.tag.text>> ? <obj.link.tag.text> :>
else
 dtextentry 32 35 234 244 0 1 <QVAL !<ISEMPTY <obj.tag.text>> ? <obj.tag.text> :>
endif
if ((<src.isgm>) || (<obj.tag0.owner>==<src.uid>) || (!<obj.tag0.owner>)) && (!<src.ctag0.scroll.edit>) && (<obj.link>==04fffffff)
 button 27 292 2445 2445 1 0 1
 if (!<obj.tag0.owner>)
  dtext 55 293 92 ser dono
 else
  dtext 62 293 368 editar
 endif
endif


[DIALOG d_scroll button]
ON=0
if (<src.ctag0.scroll.edit>)
 uid.<src.ctag.scroll>.tag.text=<argtxt[1]>
 src.ctag.scroll.edit
 src.DIALOG d_scroll
endif

ON=1
obj=<src.ctag0.scroll>
if (!<obj.tag0.owner>)
 obj.tag.owner=<src.UID>
 src.DIALOG d_scroll
else
 src.ctag.scroll.edit=1
 src.DIALOG d_scroll
endif

[ITEMDEF 0fbd]
//brown open book facing SE
NAME=Livro
DEFNAME=i_book_open
RESOURCES=1 i_BOOK_LG_PUB
WEIGHT=2
TYPE=T_BOOK
CATEGORY=MyT - Sistemas
SUBSECTION=Livros
DESCRIPTION=Livro marrom (aberto)
DUPELIST=0fbe

[ITEMDEF 0fbe]
//brown open book facing SW
DUPEITEM=0fbd

[ITEMDEF 0fef]
//brown book facing NE
NAME=Livro
DEFNAME=i_book_lg_pub
RESOURCES=1 I_CANVAS, 10 I_PAPER, 1 I_THREAD, 1 I_HIDE
WEIGHT=2
TYPE=T_BOOK
CATEGORY=MyT - Sistemas
SUBSECTION=Livros
DESCRIPTION=Livro (marrom)

[ITEMDEF 0ff0]
//light tan book facing NE
DEFNAME=i_book_sm_pub
RESOURCES=1 I_CANVAS, 10 I_PAPER, 1 I_THREAD, 1 I_HIDE
WEIGHT=2
TYPE=T_BOOK
CATEGORY=MyT - Sistemas
SUBSECTION=Livros
DESCRIPTION=Livro (bege)

[ITEMDEF 0ff1]
//dark brown book facing NE
NAME=Livro
DEFNAME=i_book_sm
RESOURCES=1 I_CANVAS, 10 I_PAPER, 1 I_THREAD, 1 I_HIDE
WEIGHT=2
CATEGORY=MyT - Sistemas
SUBSECTION=Livros
DESCRIPTION=Livro (marrom escuro)

[ITEMDEF 0ff2]
//purple book facing NE
NAME=Livro
DEFNAME=i_book_lg
RESOURCES=1 I_CANVAS, 10 I_PAPER, 1 I_THREAD, 1 I_HIDE
WEIGHT=2
TYPE=T_BOOK
CATEGORY=MyT - Sistemas
SUBSECTION=Livros
DESCRIPTION=Livro (roxo)

[ITEMDEF 0ff3]
//small open book facing SE
NAME=Livro
DEFNAME=i_book_sm_open
RESOURCES=1 I_CANVAS, 10 I_PAPER, 1 I_THREAD, 1 I_HIDE
WEIGHT=2
TYPE=T_BOOK
CATEGORY=MyT - Sistemas
SUBSECTION=Livros
DESCRIPTION=Livro aberto (marrom)

[ITEMDEF 0ff4]
//large open book facing SE
NAME=Livro
DEFNAME=i_book_lg_open
RESOURCES=1 I_CANVAS, 10 I_PAPER, 1 I_THREAD, 1 I_HIDE
WEIGHT=2
TYPE=T_BOOK
CATEGORY=MyT - Sistemas
SUBSECTION=Livros
DESCRIPTION=Livro aberto (grande)

[EOF]
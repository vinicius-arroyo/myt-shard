//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE CHAVEIRO por
//  Jean Gabin
//  Galthar
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

// Neste script você encontrará:
// - Fechaduras para serem instaladas em portas, baús e caixas com fechadura
// - cadeados para aplicações diversas
// - Chaves que funcionam com o script de armadilha
// - sistema de cópia de chave integrado ao tinker
// - Chave-mestra para GM (abre qualquer coisa sem o priv de GM)

//TODO:
//
//TAGS usadas:
//tag.fechadura         nas portas/containers com o tipo da trava
//tag.name              nome da chave
//tag.dono              dono da porta/container
//tag.owner             dono da porta/container
//tag.user              UID de quem está usando (tranca com código)
//tag.code              código com números de 1 a 8 (tranca com código)
//tag.entry             código que está no painel no momento (tranca com código)
//tag.chars             Quantos caracteres tem a senha (tranca com código)
//memory.tag.code       codigo a ser instalado na fechadura

//CTAGs:
//tranca_uid            menu da fechadura: uid da porta/container
//tranca_typ            menu da fechadura: tipo da tranca instalada
//ctag.lockc_mode       Modo do gump de tranca com código.SET ou TRY
//ctag.lockc_UID        UID da porta/container com código

//ATTRibutos
//item.attr_stolen      permite item ser aberto com lockpick
//item.attr_owned       publico, trava sozinho apos determinado tempo
//*****************************************************************************

[PLEVEL 2]
fechadura

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME KEY_LOCK]
FECHADURA_CADEADO   1
FECHADURA_SIMPLES   2
FECHADURA_NORMAL    3
FECHADURA_COMPLEXA  4
FECHADURA_CODE_3    5
FECHADURA_CODE_4    6
FECHADURA_CODE_5    7
FECHADURA_CODE_6    8
FECHADURA_QUEBRADA_CADEADO  9

LOCK_MIN_SKILL_00 00
LOCK_MIN_SKILL_01 00
LOCK_MIN_SKILL_02 200
LOCK_MIN_SKILL_03 400
LOCK_MIN_SKILL_04 500
LOCK_MIN_SKILL_05 300
LOCK_MIN_SKILL_06 500
LOCK_MIN_SKILL_07 600
LOCK_MIN_SKILL_08 700


LOCK_NAME_0 Nenhuma
LOCK_NAME_00 Nenhuma
LOCK_NAME_01 Cadeado
LOCK_NAME_02 Fechadura Simples
LOCK_NAME_03 Fechadura Normal
LOCK_NAME_04 Fechadura Complexa
LOCK_NAME_05 Tranca com codigo de 3 caracteres
LOCK_NAME_06 Tranca com codigo de 4 caracteres
LOCK_NAME_07 Tranca com codigo de 5 caracteres
LOCK_NAME_08 Tranca com codigo de 6 caracteres
LOCK_NAME_09 Cadeado Quebrado

//DEFNAMES para sistema de fechaduras com código
LOCKC_SET   1
LOCKC_TRY   2
LOCKC_ID_A  5547
LOCKC_ID_B  5549
LOCKC_ID_C  5551
LOCKC_ID_D  5553
LOCKC_ID_E  5555
LOCKC_ID_F  5557
LOCKC_ID_G  5577

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// fechadura
//*****************************************************************************
[FUNCTION fechadura]
if !<argo>
    src.sysmessageyellow Selecione porta/container:
    targetf fechadura
    return 1
elif (<argo.type>!=t_door) && (<argo.type>!=t_door_locked) && (<argo.type>!=t_container) && (<argo.type>!=t_container_locked)
    src.sysmessageyellow Apenas portas ou containeres.
    return 1
endif

src.cTAG.tranca_uid=<argo>
src.cTAG.tranca_typ=<argo.tag.fechadura>
menu m_gm_tranca

//*****************************************************************************
// CHECK_TARG_OWNER_PRESENSE
//*****************************************************************************
[FUNCTION CHECK_TARG_OWNER_PRESENSE]
REF1=<targ>
forplayers 3
 if (<isonline>)
  if (<uid>==<REF1.tag0.dono>) || (<uid>==<REF1.tag0.owner>)
   return 1
  elif (<ref1.link.type>==t_multi_custom)
   if (<ref1.link.CustomH_IsOwner <uid>>) || (<ref1.link.CustomH_FriendCanLock <uid>>)
    return 1
   endif
  endif
 endif
end
return 0

//*****************************************************************************
// f_makekey
//*****************************************************************************
//Faz uma chave para o container/porta do tipo <argv0>. Também usado no sistema de aluguel e artesão.
//Retonra a UID da chave (para efeitos bounce, p= e etc...)
[FUNCTION f_makekey]
//cria a chave apropriada para a tranca
doswitch (<argv0>)
    return 0
    serv.newitem=i_key_rusty
    serv.newitem=i_key_copper
    serv.newitem=i_key_iron
    serv.newitem=i_key_gold
    serv.newitem=i_key_code_rusty
    serv.newitem=i_key_code_copper
    serv.newitem=i_key_code_iron
    serv.newitem=i_key_code_gold    
enddo
new.more1=<R01000,0ffff> //codigo
new.attr |= attr_newbie
bounce <new>
return <new>

//*****************************************************************************
// key_addname
//*****************************************************************************
[FUNCTION key_addname]
src.targ.tag.name <args>
src.targ.update

//*****************************************************************************
// cont_uid
//*****************************************************************************
[FUNCTION cont_uid]
if (<more2>==50)  // acaba com o loop eterno
return 1
elif (0<FINDCONT(<more2>).UID>==<src.targ.link>)
return 1
ENDIF
more2=<more2>+1
cont_uid
RETURN 1

//*****************************************************************************
[FUNCTION lockc_OpenSet]
//*****************************************************************************
//Abre o dialog de tranca com código para setar novo código
ctag.lockc_mode=LOCKC_SET
ctag.lockc_UID=<argn>
DIALOGCLOSE d_code_lock
DIALOG d_code_lock

//*****************************************************************************
[FUNCTION lockc_OpenTry]
//*****************************************************************************
//Abre o dialog de tranca com código para tentar abrir
ctag.lockc_mode=LOCKC_TRY
ctag.lockc_UID=<argn>
DIALOGCLOSE d_code_lock
DIALOG d_code_lock

//*****************************************************************************
[FUNCTION lockc_use]
//*****************************************************************************
//@DCLICK de fechadura com código. <ARGN> é a UID da porta/container
obj=<argn>

//Se for porta trancada e estiver aberta, simplesmente feche.
if (<obj.type>==t_door_locked) && (<obj.timer> > 0)
    obj.timerd 1
    return 1
endif

//Testa pra ver se alguem esta usando
if (<obj.tag0.user>)
    ref1=<obj.tag.user>
    if (<ref1.IsOnline>) && (<ref1>!=<src>)
        if (<ref1.distance> < 2)
            src.sysmessagered <src.rec_fGetName <ref1>> esta mexendo com <obj.name> no momento.
            return 1
        endif
    endif
endif
obj.tag.user=<src>

//Precisa fazer o código ainda?
if (<IsEmpty <obj.tag.code>>)
    src.lockc_OpenSet <obj>
else
    src.lockc_OpenTry <obj>
endif
return 1

//*****************************************************************************
[FUNCTION lockc_AskChangePass]
//*****************************************************************************
//Uso da chave numa fechadura com tranca de código. Dialog para perguntar a ação.
ctag.lockc_UID=<argn>
obj=<argn>
prompt_new O que deseja fazer?<DEF.BR>(D)estrancar, (R)emover fechadura, (M)mudar codigo:
prompt_setAction lockc_AnsChangePass
prompt_setActionBack=
prompt_show

//*****************************************************************************
[FUNCTION lockc_AnsChangePass]
//*****************************************************************************
//Seletor de ação de lockc_AskChangePass
obj=<ctag.lockc_UID>
if (<obj.distance> > 1)
    sysmessagered Muito longe...
    return 1
elif (STRMATCH(<args>,D)) || (STRMATCH(<args>,T))
    if (<obj.type>==t_door_locked)
        obj.type=t_door
    elif (<obj.type>==t_container_locked)
        obj.type=t_container
    endif
    sfx 03E5
    sysmessageyellow Destrancou.
elif (STRMATCH(<args>,M))
    lockc_ChangeCode
elif (STRMATCH(<args>,R))
    prompt_new Deseja mesmo remover a fechadura de codigo? Ela sera inutilizada e voce tera que instalar outra se quiser ter uma fechadura de codigo aqui.
    prompt_setAction lockc_RemoveLock
    prompt_setActionBack=
    prompt_show
endif

//*****************************************************************************
[FUNCTION lockc_RemoveLock]
//*****************************************************************************
//Player remove a tranca de código para sempre.
obj=<ctag.lockc_UID>
if (<obj.distance> > 1)
    sysmessagered Muito longe...
    return 1
elif (<obj.type>==t_door_locked)
    obj.type=t_door
elif (<obj.type>==t_container_locked)
    obj.type=t_container
endif
sfx 1451
obj.tag.fechadura=
obj.tag.more1=
obj.tag.chars=
obj.tag.code=
src.sysmessagegreen Voce removeu a fechadura de codigo.

//*****************************************************************************
[FUNCTION lockc_html1]
//*****************************************************************************
//Monta o 1º HTML do diálogo de tranca com código
IF (<ctag0.lockc_mode>==LOCKC_SET)
    local.s = <DEF.CENTER><DEF.BFONT_SIZE7><DEF.BFONT_DYELLOW>Insira uma nova senha<DEF.CENTERE><DEF.BFONT_YELLOW><DEF.BFONT_SIZE3>Apos inserir uma nova senha, clique OKAY. Nunca se esqueca da sua senha. Se voce esquecer a senha talvez voce nunca mais possa abrir esta tranca.
ELSE
    local.s = <DEF.CENTER><DEF.BFONT_SIZE7><DEF.BFONT_DYELLOW>Qual eh a senha?<DEF.CENTERE>
ENDIF
return <local.s>

//*****************************************************************************
[FUNCTION lockc_html2]
//*****************************************************************************
//Monta o 1º HTML do diálogo de tranca com código
obj=<ctag.lockc_UID>
IF (<ctag0.lockc_mode>==LOCKC_SET)
    local.s = <DEF.CENTER><DEF.BFONT_SIZE3><DEF.BFONT_YELLOW>Esta tranca tem <eval <obj.tag.chars>@7> possiveis combinacoes diferentes.
ELSE
    local.s = <DEF.CENTER><DEF.BFONT_YELLOW><DEF.BFONT_SIZE3>Entre com a senha utilizando os botoes abaixo e entao clique OKAY.<DEF.CENTERE>
ENDIF
return <local.s>

//*****************************************************************************
[FUNCTION lockc_DrawEntry]
//*****************************************************************************
//Monta parte do gump com os caracteres já escolhidos
obj=<ctag.lockc_UID>
local.step=<eval 390/<obj.tag.chars>>
dorigin <eval (215/(<obj.tag.chars>-1))-<local.step>>,70
FOR i 0 <eval <obj.tag0.chars>-1>
    if (<local.i> >= <eval STRLEN(<obj.tag.entry>)>) || (<ISEMPTY <obj.tag.entry>>)
        gumppic *<local.step> 70 5546
    else
        gumppic *<local.step> 70 <DEF.LOCKC_ID_<STRSUB <local.i> 1 <obj.tag.entry>>>
    ENDIF
ENDFOR

//*****************************************************************************
[FUNCTION lockc_AddChar]
//*****************************************************************************
//Adiciona um caractere de A a G para o painel da fechadura com código
obj=<ctag.lockc_UID>
if (<obj.distance> > 1)
    sysmessagered Muito longe...
    return 1
elif (<eval STRLEN(<obj.tag.entry>)> < <obj.tag.chars>)
    if (<IsEmpty <obj.tag.entry>>)
        obj.tag.entry=<args>
    else
        obj.tag.entry .= <args>
    endif
endif
IF (!<IsGM>)
    obj.sfx 05b9
ENDIF
DIALOGCLOSE d_code_lock
DIALOG d_code_lock

//*****************************************************************************
[FUNCTION lockc_ShowPassword]
//*****************************************************************************
//Mostra para um GM o código da tranca.
obj=<ctag.lockc_UID>
obj.tag.entry=<obj.tag.code>
DIALOGCLOSE d_code_lock
DIALOG d_code_lock

//*****************************************************************************
[FUNCTION lockc_ClearEntry]
//*****************************************************************************
//Limpa os caracteres selecionados para a tranca com código.
obj=<ctag.lockc_UID>
obj.tag.entry
DIALOGCLOSE d_code_lock
DIALOG d_code_lock

//*****************************************************************************
[FUNCTION lockc_ChangeCode]
//*****************************************************************************
//Prompt de mudança de código da tranca. Se sim, limpa o código
obj=<ctag.lockc_UID>
if (!<argn>)
    prompt_new Deseja mesmo limpar a senha desta tranca? Nao faca isso se nao deseja danar outro jogador!
    prompt_setAction lockc_ChangeCode 1
    prompt_setActionBack lockc_OpenTry <obj>
    ynprompt_show
else
    if (<obj.distance> > 1)
        sysmessagered Muito longe...
        return 1
    endif    
    obj.tag.entry=
    obj.tag.code=
    lockc_OpenSet <obj>
endif

//*****************************************************************************
[FUNCTION lockc_SetCode]
//*****************************************************************************
//Seta um novo código da tranca
obj=<ctag.lockc_UID>
if (<obj.distance> > 1)
    sysmessagered Muito longe...
    return 1
elif (<eval STRLEN(<obj.tag.entry>)>==<obj.tag.chars>)
    obj.tag.code=<obj.tag.entry>
    obj.tag.entry=
    lockc_OpenTry <obj>
else
    sysmessagered Voce deve usar todos os caracteres diponiveis para criar um codigo
    lockc_OpenSet <obj>
endif

//*****************************************************************************
[FUNCTION lockc_TryCode]
//*****************************************************************************
//Checa se <obj.tag.entry>==<obj.tag.code> para abrir ou não a tranca.
obj=<ctag.lockc_UID>
if (<obj.distance> > 1)
    sysmessagered Muito longe...
    return 1
ELIF (<eval STRLEN(<obj.tag.entry>)>!=<obj.tag.chars>)
    sysmessagered Voce deve usar todos os caracteres diponiveis do codigo
    lockc_OpenTry <obj>
ELIF (STRMATCH(<obj.tag.entry>,<obj.tag.code>))
    obj.tag.entry=
    if (<obj.type>==t_container) || (<obj.type>==t_container_locked)
        obj.type=t_container
        newitem i_code_locker
        new.p=<p>
        new.link=<obj>
        new.more1=<uid>
        new.timer=5
        src.dclick <obj>
    elif (<obj.type>==t_door) || (<obj.type>==t_door_locked)
        obj.timerd 1
    endif
    src.sysmessagegreen Codigo correto!
    obj.sfx 05BA
ELSE
    obj.tag.entry=
    lockc_OpenTry <obj>
    sysmessagered Codigo incorreto.
    obj.sfx 05BB
ENDIF

//*****************************************************************************
[FUNCTION lockc_clear]
//*****************************************************************************
//Limpa todas as vars de uma tranca com código para economizar espaço e memoria
UID.<ctag.lockc_UID>.tag.entry
UID.<ctag.lockc_UID>.tag.user
ctag.lockc_mode
ctag.lockc_UID

//*****************************************************************************
[FUNCTION lockc_close]
//*****************************************************************************
//fecha o diálogo de tranca com código. Tira todas as vars menos o código que já foi digitado.
UID.<ctag.lockc_UID>.tag.user
ctag.lockc_mode
ctag.lockc_UID


//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_container_locked
//*****************************************************************************
[TYPEDEF t_container_locked]
on=@DROPON_SELF
src.sysmessageyellow Nao pode colocar nada dentro sem destrancar.
return <eval !<src.IsGM>>

ON=@Dclick
DOSWITCH <tag0.fechadura>
    local.0
    src.sysmessageyellow Trancada com cadeado...
    src.sysmessageyellow Trancada...
    src.sysmessageyellow Trancada...
    src.sysmessageyellow Trancada...
    src.lockc_use <uid>
    src.lockc_use <uid>
    src.lockc_use <uid>
    src.lockc_use <uid>
ENDDO
return <eval !<src.isgm>>   //Evita behavior HC de abrir se tiver chave.

//*****************************************************************************
// t_door_locked
//*****************************************************************************
[TYPEDEF t_door_locked]
ON=@Dclick
DOSWITCH <tag0.fechadura>
    src.sysmessageyellow Trancada...
    src.sysmessageyellow Trancada com cadeado...
    src.sysmessageyellow Trancada...
    src.sysmessageyellow Trancada...
    src.sysmessageyellow Trancada...
    src.lockc_use <uid>
    src.lockc_use <uid>
    src.lockc_use <uid>
    src.lockc_use <uid>
ENDDO
return <eval !<src.isgm>>   //Evita behavior HC de abrir se tiver chave.


//*****************************************************************************
// t_fechadura
//*****************************************************************************
//more1=tipo
//more2=codigo
[TYPEDEF t_fechadura]

ON=@CLIENTTOOLTIP
f_sendTooltip <name>,<def.LOCK_NAME_<more1>>
return 1

ON=@DCLICK
if (<topobj> != <src>)
    src.sysmessagered Coloque na bolsa antes de instalar.
    return 1
elif <more1> > 8
    src.sysmessagered Esta quebrado...
    return 1
endif

src.sysmessagegreen Selecione a porta ou o container para instalar
target
return 1


ON=@targon_char
src.sysmessagered Nao eh possivel instalar <def.LOCK_NAME_<more1>> neste local.
return 1

ON=@targon_item
if (<src.targ.distance> > 1) && (!<src.IsGM>)
    src.sysmessageyellow Chegue mais perto.
    return 1
elif (<src.targ.type>==t_door) || (<src.targ.type>==t_container) || (<src.targ.type>==t_door_locked) || (<src.targ.type>==t_container_locked)
    if (<src.targ.baseid>==i_door_secret_stone_1) || (<src.targ.baseid>==i_door_secret_stone_2) || (<src.targ.baseid>==i_door_secret_stone_3) || (<src.targ.baseid>==i_door_secret_stone_4) || (<src.targ.baseid>==i_door_secret_stone_4_O) || (<src.targ.baseid>==i_door_secret_wood_1) || (<src.targ.baseid>==i_door_secret_wood_2)
        src.sysmessagered Nao se coloca tranca em porta secreta.
        return 1
    elif (!<src.targ.tag0.dono>) && (!<src.targ.tag0.owner>) && (!<src.IsGM>) && (!<src.targ.tag0.fixture>)
        src.sysmessagered Este eh um bem publico. Nao pode ser trancado.
        return 1
    elif (!<src.CHECK_TARG_OWNER_PRESENSE>) && !<src.isgm>
        src.sysmessagered Voce so deve instalar na presenca do dono ou responsavel.
        return 1
    else
        if <more1>==<def.FECHADURA_CADEADO> 
            if (<src.targ.type>==t_container) && ( (<src.targ.TAG0.LOCKTYPE> < 9) || (<src.targ.TAG0.LOCKTYPE> > 50) )
                src.sysmessagered Nao e possivel trancar isto com cadeado.
                return 1
            endif
            src.emoteyellow tranca com cadeado
            ref1=<src.f_makekey 1>
            if (<more2>)//Este cadeado tem código.
                ref1.more1=<more2>
            else
                src.targ.more1=<ref1.more1>
            endif
            src.targ.more1=<ref1.more1>
            if (<src.targ.type>==t_door)
                src.targ.type=t_door_locked
            elif (<src.targ.type>==t_container)
                src.targ.type=t_container_locked
            endif
            src.targ.tag.fechadura=<more1>
            
        else
            if (<src.targ.type>==t_container) && ( (<src.targ.TAG0.LOCKTYPE> < 9) || (<src.targ.TAG0.LOCKTYPE> > 50) )
                src.sysmessagered Nao e possivel instalar uma fechadura neste local.
                return 1
            endif
            src.emoteyellow comeca a instalar fechadura
            src.anim 020
            sfx 1449
            serv.newitem=i_mry_install_fechadura
            new.more1=<more1>       //tipo
            new.tag.code=<more2>
            new.morep=<src.p>       //marca o local do src. se ele se mover para o script
            new.more2=0
            new.link=<src.targ>     //onde sera instalada
            new.tag.fechadura=<DEFNAME>
            new.timer=1
            new.equip            
        endif
    endif

    src.update
    remove
else
    src.sysmessagered Nao e possivel instalar.
endif
return 1

//*****************************************************************************
// t_key
//*****************************************************************************
[TYPEDEF t_key]
ON=@CLIENTTOOLTIP
if (STRMATCH(<tag.name>,))
    return 0
else
    f_sendTooltip <name>, <tag.name>
endif
return 1

ON=@DCLICK
if (<topobj> != <src>)
    src.sysmessageyellow Coloque a chave na bolsa antes de usa-la.
    return 1
endif
src.sysmessageyellow Onde deseja usar a chave?
target
return 1

ON=@TARGON_ITEM
if (<src.targ>==<UID>)
    src.sysmessageyellow Digite nome para esta chave:
    src.promptconsole key_addname 
    return 1
elif (<link.type>==t_ship) || (<link.type>==t_multi)
    return 0
elif (<src.targ.distance> > 1)
    src.sysmessageyellow Chegue mais perto.
    target 
    return 1
elif <tdata3>!=<src.targ.tag0.fechadura>
    src.sysmessagered Esta chave nao entra!
    return 1
elif <more1>!=<src.targ.more1>
    src.sysmessagered Esta chave nao serve!
    return 1
else
    if <tdata3>==1 //cadeado?
        src.emotered abre cadeado
        sfx 1451
        src.targ.more1=     // tira o codigo da porta. sphere nao aceita mais a chave.
        serv.newitem=i_cadeado
        new.more2=<more1>
        new.bounce
        src.targ.tag.fechadura=
        
        if (<src.targ.type>==t_door_locked)
            src.targ.type=t_door
        elif (<src.targ.type>==t_container_locked)
            src.targ.type=t_container
        endif        
        remove
        return 1
    elif (<tdata3> > 4) && (<tdata3> < 9)   //Chave de código?
        if (<src.targ.type>==t_door_locked) || (<src.targ.type>==t_container_locked)
            src.lockc_AskChangePass <src.targ>
            return 1
        endif
    endif
endif

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_cadeado
//*****************************************************************************
[ITEMDEF i_cadeado]  
ID=0dcf
NAME=Cadeado
RESOURCES=i_springs, i_gears, i_barra_metal
SKILLMAKE=30.0 Tinkering
TYPE=t_fechadura
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Cadeado

ON=@CREATE
color=03cf
more1=1 // no 9 o cadeado está quebrado e inutilizável 

//*****************************************************************************
//Item: i_fechadura_complexa
//*****************************************************************************
//Rev: 1 Ts: 2010-07-27 15:05:43
[ITEMDEF i_fechadura_complexa]
ID=3706
NAME=fechadura complexa
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=fechadura complexa
WEIGHT=1.0
VALUE=378
TYPE=t_fechadura
RESOURCES=3 i_springs, 1 i_axel_and_gears
SKILLMAKE=Tinkering 925

ON=@CREATE
COLOR=975
//Criacao automatica
more1 4

//*****************************************************************************
//Item: i_fechadura_normal
//*****************************************************************************
//Rev: 1 Ts: 2010-07-27 15:05:59
[ITEMDEF i_fechadura_normal]
ID=3706
NAME=fechadura normal
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=fechadura normal
WEIGHT=1.0
VALUE=162
TYPE=t_fechadura
RESOURCES=3 i_springs, 1 i_axel_and_gears
SKILLMAKE=Tinkering 750

ON=@CREATE
COLOR=975
//Criacao automatica
more1 3

//*****************************************************************************
//Item: i_fechadura_simples
//*****************************************************************************
//Rev: 1 Ts: 2010-07-27 15:06:18
[ITEMDEF i_fechadura_simples]
ID=3706
NAME=fechadura simples
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=fechadura simples
WEIGHT=1.0
VALUE=75
TYPE=t_fechadura
RESOURCES=1 i_springs, 2 i_gears
SKILLMAKE=Tinkering 575

ON=@CREATE
COLOR=975
//Criacao automatica
more1 02


//*****************************************************************************
// i_fechadura_code_3
//*****************************************************************************
[ITEMDEF i_fechadura_code_3]  
ID=3706
NAME=fechadura de 3 codigos
RESOURCES=3 i_axel_and_gears, i_springs, i_gears
SKILLMAKE=60.0 Tinkering
TYPE=t_fechadura
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=fechadura de 3 codigos

ON=@CREATE
color=03cf
more1=5

//*****************************************************************************
// i_fechadura_code_4
//*****************************************************************************
[ITEMDEF i_fechadura_code_4]
ID=3706
NAME=fechadura de 4 codigos
RESOURCES=4 i_axel_and_gears, i_springs, i_gears
SKILLMAKE=70.0 Tinkering
TYPE=t_fechadura
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=fechadura de 4 codigos

ON=@CREATE
color=03cf
more1=6

//*****************************************************************************
// i_fechadura_code_5
//*****************************************************************************
[ITEMDEF i_fechadura_code_5]
ID=3706
NAME=fechadura de 5 codigos
RESOURCES=5 i_axel_and_gears, i_springs, i_gears
SKILLMAKE=80.0 Tinkering
TYPE=t_fechadura
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=fechadura de 5 codigos

ON=@CREATE
color=03cf
more1=7

//*****************************************************************************
// i_fechadura_code_6
//*****************************************************************************
[ITEMDEF i_fechadura_code_6]
ID=3706
NAME=fechadura de 6 codigos
RESOURCES=6 i_axel_and_gears, i_springs, i_gears
SKILLMAKE=90.0 Tinkering
TYPE=t_fechadura
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=fechadura de 8 codigos

ON=@CREATE
color=03cf
more1=8

//*****************************************************************************
// i_key_rusty
//*****************************************************************************
[ITEMDEF 01013]
DEFNAME=i_key_rusty
NAME=Chave de cadeado
RESOURCES=1 i_INGOT_RUSTY
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave de cadeado
TDATA3=1

//*****************************************************************************
// i_key_copper
//*****************************************************************************
[ITEMDEF 0100e]
DEFNAME=i_key_copper
NAME=Chave simples
RESOURCES=3 i_ingot_iron
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave simples
SKILLMAKE=TINKERING 43.0,t_tinker_tools
TDATA3=2

//*****************************************************************************
// i_key_iron
//*****************************************************************************
[ITEMDEF 01010]
DEFNAME=i_key_iron
NAME=Chave normal
RESOURCES=3 i_ingot_iron
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave normal
SKILLMAKE=TINKERING 30.0,t_tinker_tools
TDATA3=3

//*****************************************************************************
// i_key_gold
//*****************************************************************************
[ITEMDEF 0100f]
DEFNAME=i_key_gold
NAME=Chave complexa
RESOURCES=2 i_ingot_iron, 1 i_ingot_gold
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave complexa
SKILLMAKE=TINKERING 60.0,t_tinker_tools
TDATA3=4

//*****************************************************************************
// i_key_code_rusty
//*****************************************************************************
[ITEMDEF i_key_code_rusty]
ID=01013
NAME=Chave de codigo
RESOURCES=1 i_INGOT_RUSTY
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave-codigo 3 chars
TDATA3=5

//*****************************************************************************
// i_key_code_copper
//*****************************************************************************
[ITEMDEF i_key_code_copper]
ID=0100e
NAME=Chave de codigo
RESOURCES=3 i_ingot_iron
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave-codigo 4 chars
SKILLMAKE=TINKERING 43.0,t_tinker_tools
TDATA3=6

//*****************************************************************************
// i_key_code_iron
//*****************************************************************************
[ITEMDEF i_key_code_iron]
ID=01010
NAME=Chave de codigo
RESOURCES=3 i_ingot_iron
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave-codigo 5 chars
SKILLMAKE=TINKERING 30.0,t_tinker_tools
TDATA3=7

//*****************************************************************************
// i_key_code_gold
//*****************************************************************************
[ITEMDEF i_key_code_gold]
ID=0100f
NAME=Chave de codigo
RESOURCES=2 i_ingot_iron, 1 i_ingot_gold
WEIGHT=1
TYPE=T_KEY
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave-codigo 6 chars
SKILLMAKE=TINKERING 60.0,t_tinker_tools
TDATA3=8

//*****************************************************************************
// i_key_ring
//*****************************************************************************
[ITEMDEF 01011]
DEFNAME=i_key_ring
NAME=Chaveiro
TYPE=T_KEYRING
TDATA2=1
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chaveiro
RESOURCES=1 i_ingot_iron
SKILLMAKE=TINKERING 30.0,t_tinker_tools

ON=@DCLICK
    SRC.MESSAGE Arraste e solte as chaves no chaveiro para guarda-las.
return 1

//*****************************************************************************
// i_key_ring_1
//*****************************************************************************
[ITEMDEF 01769]
DEFNAME=i_key_ring_1
NAME=Chaveiro com chaves
TYPE=T_KEYRING
TDATA2=1
RESOURCES=1 i_ingot_iron
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chaveiro com chaves
FLIP=0
//DUPELIST=0176a,0176b

on=@dclick
if (<topobj.uid> != <src.uid>)
    src.sysmessageyellow Coloque o chaveiro com chaves na bolsa antes de usa-lo.
return 1
endif

on=@targon_item
if (<link.type>==t_ship) || (<link.type>==t_multi)
elif (<src.targ.distance> > 1)
   src.sysmessageyellow Chegue mais perto.
   target 
return 1
elif (strmatch(<src.targ.tag.fechadura>,cadeado))
  more2=0
  cont_uid
  if (0<FINDCONT(<more2>).more1>==<src.targ.more1>)
    src.newitem=i_cadeado
    src.act.bounce
    src.act.more1=<src.targ.more1>   // linka o novo cadeado a chave
    src.targ.more1=         // tira o link da porta
    if (<src.targ.type>==t_door_locked)
      src.targ.type=t_door
    elif (<src.targ.type>==t_container_locked)
      src.targ.type=t_container
    endif
    src.targ.tag.fechadura=
  return 1
  endif
endif


[ITEMDEF 0176a]
//key ring
DUPEITEM=01769

[ITEMDEF 0176b]
//key ring
DUPEITEM=01769

//*****************************************************************************
// i_key_magic
//*****************************************************************************
[ITEMDEF 01012]
DEFNAME=i_key_magic
NAME=Chave mestra
WEIGHT=1
TYPE=T_script
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave mestra
SKILLMAKE=TINKERING 80.0, MAGERY 50.0,t_tinker_tools
RESOURCES=5 i_ingot_gold, 5 i_ingot_silver, 5 i_ingot_copper

ON=@Create
    ATTR=attr_magic|attr_newbie

on=@dclick
if (<topobj.uid> != <src.uid>)
    src.sysmessageyellow Coloque a chave na bolsa antes de usa-la.
return 1
endif

on=@targon_item
if (<link.type>==t_ship) || (<link.type>==t_multi)
elif (<src.targ.distance> > 1)
   src.sysmessageyellow Chegue mais perto.
   target 
return 1
elif (<src.targ.type> == t_door_locked)
    src.targ.type> == t_door
    src.sysmessagegreen Voce destrancou a porta.
return 1
elif (<src.targ.type> == t_door)
    src.targ.type> == t_door_locked
    src.sysmessagegreen Voce trancou a porta.
return 1
elif (<src.targ.type> == t_container_locked)
    src.targ.type> == t_container
    src.sysmessagegreen Voce destrancou o container.
elif (<src.targ.type> == t_container)
    if (<src.targ.baseid>==i_basket_bushel) || (<src.targ.baseid>==i_pouch) || (<src.targ.baseid>==i_basket_2) || (<src.targ.baseid>==i_backpack) || (<src.targ.baseid>==i_bag) || (<src.targ.baseid>==i_barrel_open) || (<src.targ.baseid>==i_basket_round) || (<src.targ.baseid>==i_basket_square) || (<src.targ.baseid>==i_tub_empty) || (<src.targ.baseid>==i_keg_small) || (<src.targ.baseid>==i_basin)
        src.sysmessagered Nao e possivel trancar isto.
        return 1
    else
        src.targ.type> == t_container_locked
        src.sysmessagegreen Voce trancou o container.
    return 1
    endif
return 1
endif



//*****************************************************************************
// i_mry_install_fechadura
//*****************************************************************************
[ITEMDEF i_mry_install_fechadura]
ID=i_memory
NAME=Instalando Fechadura
TYPE=t_eq_script
LAYER=layer_special

ON=@CREATE
ATTR=attr_newbie

ON=@TIMER
if !(<cont.p.x> == <morex>) || !(<cont.p.y> == <morey>)
    cont.sysmessagered Voce falhou ao instalar a fechadura.
    if (!<IsEmpty <tag.fechadura>>)
        serv.newitem=<tag.fechadura>
        cont.bounce <new>
    endif
    remove
    return 1
elif <cont.isgm>
    local.nothing=
elif (<more2>>=20) && ( (<more1>==<DEF.FECHADURA_SIMPLES>) || (<more1>==<DEF.FECHADURA_CODE_3>) || (<more1>==<DEF.FECHADURA_CODE_4>) ) // se é simples
    if (!<belltest <cont.tinkering>,325>) && (<R0,4>) //1/5 de chance de sempre conseguir
     morep=0
     timer=1
     return 1
    endif
elif (<more2>>=30) && ( (<more1>==<DEF.FECHADURA_NORMAL>) ||  (<more1>==<DEF.FECHADURA_CODE_5>) )  // se é normal
    if (!<belltest <cont.tinkering>,550>) && (<R0,7>) //1/8 de chance de sempre conseguir
     morep=0
     timer=1
     return 1
    endif
elif (<more2>>=40) && ( (<more1>==<DEF.FECHADURA_COMPLEXA>) ||  (<more1>==<DEF.FECHADURA_CODE_6>) )// se é complexa
    if (!<belltest <cont.tinkering>,825>) && (<R0,11>) //1/12 de chance de sempre conseguir
     morep=0
     timer=1
     return 1
    endif
else
    dorand 6
      cont.emoteyellow instalando fechadura
      cont.anim 020
      sfx 1449
    enddo
    timer=1
    more2 += 1
    return 1
endif
link.tag.fechadura=<more1>

//Se for uma tranca de código, coloque a quantidade de caracteres corretos
DOSWITCH <more1>
    local.0
    local.1
    local.2
    local.3
    local.4
    link.tag.chars=3
    link.tag.chars=4
    link.tag.chars=5
    link.tag.chars=6
ENDDO
obj=<cont.f_makekey <more1>>
link.more1=<obj.more1>
cont.emotegreen terminou de instalar
cont.Skill_Gain Skill_Tinkering
remove
return 1

//*****************************************************************************
[ITEMDEF i_code_locker]
//*****************************************************************************
//Item que tranca um baú com code lock se o char sair do lugar.
NAME=tranca de código
ID=i_worldgem_bit
TYPE=t_script

ON=@CREATE
ATTR=attr_invis|attr_move_never
COLOR=021d

ON=@TIMER
ref1=<more1>
if (<ref1.IsPlayer>)
    if (<ref1.IsOnline>)
        if (<ref1.p.x>==<p.x>) && (<ref1.p.y>==<p.y>)
            TIMER=3
            return 1
        endif
    endif
endif
link.type=t_container_locked
link.update     //Para fechar o container na tela dos clients
remove
return 1

//*****************************************************************************
//*****************************************************************************
// MENUs
//*****************************************************************************
//*****************************************************************************

[MENU m_gm_tranca]
(MyT] Fechaduras [MyT)
on=0 Tipo de fechadura: <def.LOCK_NAME_<ctag0.tranca_typ>>
ctag0.tranca_typ += 1
if (<ctag.tranca_typ> > 8)
    ctag.tranca_typ=0
endif
uid.<cTAG.tranca_uid>.tag.fechadura=<ctag.tranca_typ>
sysmessagegreen A fechadura foi alterada para <def.LOCK_NAME_<ctag.tranca_typ>>
menu m_gm_tranca

on=0 Auto-Rearmavel: <QVAL <UID.<cTAG.tranca_uid>.attr>&attr_owned ? Sim : Nao>
IF (<UID.<cTAG.tranca_uid>.attr>&attr_owned)
    UID.<cTAG.tranca_uid>.attr=<UID.<cTAG.tranca_uid>.attr>&~attr_owned
else
    UID.<cTAG.tranca_uid>.attr=<UID.<cTAG.tranca_uid>.attr>|attr_owned
endif
menu m_gm_tranca

on=0 Lockpick permitido: <QVAL <UID.<cTAG.tranca_uid>.attr>&attr_stolen ? Sim : Nao>
IF (<UID.<cTAG.tranca_uid>.attr>&attr_stolen)
    UID.<cTAG.tranca_uid>.attr=<UID.<cTAG.tranca_uid>.attr>&~attr_stolen
else
    UID.<cTAG.tranca_uid>.attr=<UID.<cTAG.tranca_uid>.attr>|attr_stolen
endif
menu m_gm_tranca

on=0 Copiar chave (codigo: <UID.<cTAG.tranca_uid>.more1>)
OBJ=<f_makekey <cTAG.tranca_typ>>
if (<OBJ>)
    OBJ.more1=<uid.<cTAG.tranca_uid>.more1>
    sysmessageyellow Chave copiada com codigo <OBJ.more1>
else
    sysmessageyellow Nao tem uma tranca!
endif
menu m_gm_tranca

on=0 Gerar chave (CUIDADO! Troca o codigo)
LOCAL.key=<f_makekey <cTAG.tranca_typ>>
if (<LOCAL.key>)
    sysmessageyellow Codigo trocado para <UID.<LOCAL.key>.more1>.
else
    sysmessageyellow Nao tem uma tranca!
endif
uid.<cTAG.tranca_uid>.more1=<uid.<LOCAL.key>.more1>
menu m_gm_tranca

ON=@Cancel
IF (<ctag.tranca_typ> > 4)
    obj=<cTAG.tranca_uid>
    DOSWITCH <eval <ctag.tranca_typ>-5>
        obj.tag.chars=3     //Tranca com 3 caracteres
        obj.tag.chars=4     //Tranca com 4 caracteres
        obj.tag.chars=5     //Tranca com 5 caracteres
        obj.tag.chars=6     //Tranca com 6 caracteres
    ENDIF
    if (<IsEmpty <obj.tag.code>>)
        lockc_OpenSet <obj>
    elif (<eval STRLEN(<obj.tag.code>)>!=<obj.tag.chars>)
        lockc_OpenSet <obj>
    endif
ENDIF
ctag.tranca_typ=
cTAG.tranca_uid=

//*****************************************************************************
//*****************************************************************************
// DIALOGs
//*****************************************************************************
//*****************************************************************************

// Created 12/2/2010 02:13:12, with Gump Studio.
// Exported with with SphereExporter ver 1.1.
// Script for 0.56/Revisions

//*****************************************************************************
[DIALOG d_code_lock]
//*****************************************************************************
//Tranca com código
150,150
page 0
resizepic 0 0 5120 500 150
gumppic 5 5 5032
gumppic 485 133 5032
gumppic 5 133 5032
gumppic 485 5 5032
dhtmlgump 20 7 460 55 0 0 <lockc_html1>
resizepic 0 150 5120 500 150
dhtmlgump 20 167 460 23 0 0 <lockc_html2>
gumppic 5 155 5032
gumppic 485 283 5032
gumppic 5 283 5032
gumppic 485 155 5032
CALL lockc_DrawEntry
button 20 205 5547 5548 1 0 1
button 85 205 5549 5550 1 0 2
button 150 205 5551 5552 1 0 3
button 215 205 5553 5554 1 0 4
button 280 205 5555 5556 1 0 5
button 345 205 5557 5558 1 0 6
button 410 205 5577 5578 1 0 7
button 410 270 247 248 1 0 100
button 21 269 241 242 1 0 101
IF (<ctag0.lockc_mode>==LOCKC_SET)
    button 192 269 2445 2445 1 0 103
    dtext 206 270 47 Limpar codigo
ELIF (<IsGM>) && (STRMATCH(<obj.tag.entry>,<obj.tag.code>))
    button 192 269 2445 2445 1 0 104
    dtext 203 270 47 Trocar codigo
ELIF (<IsGM>)
    button 192 269 2445 2445 1 0 102
    dtext 203 270 47 Mostrar codigo
ENDIF

[DIALOG d_code_lock text]

[DIALOG d_code_lock button]
ON=0
// fechar
lockc_close

ON=1 7
//Adicionar
lockc_AddChar <CHR <eval <argn>+64>>

ON=100
IF (<ctag0.lockc_mode>==LOCKC_SET)
    lockc_SetCode
ELSE
    lockc_TryCode
ENDIF

ON=101
//Fechar
lockc_clear

ON=102
//ver senha
lockc_ShowPassword

ON=103
//limpar codigo
lockc_ClearEntry

ON=104
//Trocar o código
lockc_ChangeCode

[EOF]
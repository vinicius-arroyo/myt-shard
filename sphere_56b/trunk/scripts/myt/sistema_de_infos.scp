//*****************************************************************************
//*****************************************************************************
//
// SISTEMA INFORMACOES DE ITEMS
//
//  Galthar  
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:
//CTAG.iteminfo

[PLEVEL 0]
analisar

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME INFO_FUNCTIONS]
INFO_MAX=4

INFO_t_weapon_axe           = info_weapons
INFO_t_weapon_mace_smith    = info_weapons
INFO_t_weapon_mace_sharp    = info_weapons
INFO_t_weapon_sword         = info_weapons
INFO_t_weapon_fence         = info_weapons
INFO_t_weapon_bow           = info_weapons
INFO_t_weapon_mace_staff    = info_weapons
INFO_t_weapon_mace_crook    = info_weapons
INFO_t_weapon_mace_pick     = info_weapons
INFO_t_weapon_arrow         = info_weapons
INFO_t_weapon_bolt          = info_weapons
INFO_t_weapon_xbow          = info_weapons
//INFO_t_string               = info_weapons
INFO_t_weapon_mace_smith    = info_weapons
INFO_t_weapon_mace_staff    = info_weapons
INFO_t_weapon_mace_crook    = info_weapons
INFO_t_weapon_mace_pick     = info_weapons

INFO_t_armor                = info_armors
INFO_t_armor_leather        = info_armors
INFO_t_shield               = info_armors

INFO_t_potion               = info_potions

INFO_t_food                 = info_food
INFO_t_fish                 = info_food
INFO_t_fruit                = info_food
INFO_t_food_raw             = info_food
INFO_t_meat_raw             = info_food

INFOTIP_t_potion_empty      = Garrafa Alquimica<br>Usada para armazenar pocoes, a garrafa eh feita de vidro e eh de facil manuseio. Algumas pocoes permitem que o frasco seja reutilizado.
INFOTIP_t_espiriteira       = Suporte utilizado para aquecimento dos compostos em seus respectivos recipientes. Suporta destiladores, baloes e misturadores, mas so pode ser colocado um instrumento por vez sobre a mesma. Eh composto por uma vela e um suporte.
INFOTIP_t_almofariz         = Almofariz<br>Esse pequeno instrumento faz parte da vida de qualquer alquimista. Serve para amassar os reagentes para obter o extrato na sua forma mais simples. O extrato pode ser usado diretamente na manufatura de pocoes fracas ou utilizado em baloes e destiladores.
INFOTIP_t_tubo              = Tubo de Ensaio<br>Os tubos sao utilizados para armazenamento dos extratos, borras e demais solucoes em sua forma pura. Eh um recipiente de carater intermediario para manter os composto antes da formacao da pocao.
INFOTIP_t_tubo_cheio        = Tubo de Ensaio<br>Os tubos sao utilizados para armazenamento dos extratos, borras e demais solucoes em sua forma pura. Eh um recipiente de carater intermediario para manter os composto antes da formacao da pocao.
INFOTIP_t_destilador        = Destilador<br>O destilador eh um recipiente de vidro utilizado para formacao de destilados. Primeiramente tenha em maos um extrato, coloque-o no destilador e em seguida na espititeria sobre a vela. Espere aquecer mas nao deixe muito. Quando o destilado estiver pronto coloque-o em um tubo de ensaio. Com o uso do destilador eh possivel se obter pocoes de concentracao mediana.
INFOTIP_t_balao             = Balao<br>O balao eh um recipiente de vidro utilizado para formacao de borras. Primeiramente tenha em maos um extrato, coloque-o no balao e em seguida na espititeria sobre a vela. Quando a borra estiver pronta coloque-a em um tubo de ensaio. Com o uso do balao eh possivel se obter pocoes de concentracao forte.
INFOTIP_t_misturador        = Misturador<br>O misturador eh um recipiente de vidro utilizado para formacao de misturas a partir de borras e destilados. Deve-se misturar duas pocoes feitas a partir de borras ou destilados e levar a espiriteira. O resultado sera uma pocao concentrada com efeitos que dependem da combinacao utilizada.
INFOTIP_t_musical           = Instrumento musical.
//INFOTIP_t_carpentry         =
INFOTIP_t_normal             = Cadeira<br>Usada para sentar
INFOTIP_t_sextant           = Sextante<br>Instrumento matematico usado para medir angulos , usado para indicar coordenadas com auxilio das estrelas e sol (longitude e latitude).
INFOTIP_t_table             = Mesa<br>Usada para suporte de vasos livros, ferramentas, instrumentos alquimicos e etc.
INFOTIP_t_fish_pole         = Vara de Pesca<br>Usada para pesca em rios ou mares. Requer o uso de isca.
INFOTIP_t_shaft             = Varetas<br>Trata-se de um pequeno cabo de madeira usado na producao de flechas.
INFOTIP_t_tinker_tools      = Instrumento de Artesao<br>Jogo de ferramentas requerido no trabalho do artesao para a criacao de espitiferias, frascos de vidro, canecas, garrafas, relogios, fechaduraras, armaduras de aneis e etc.
INFOTIP_t_carpentry_tool    = Ferramenta de Carpinteiro<br>Utilizada para contruir moveis e outros produtos que tem como materia-prima a madeira.
//INFOTIP_t_cooking_tool      =
INFOTIP_t_bancada           = Bancada<br>Usada de suporte para carpinteiros e artesoes na manufatura de certos produtos.
INFOTIP_t_light_out         =
INFOTIP_t_oleo              = Oleo<br>Usado em lamparinas, para curtir couro, para acender pequenas fogueiras ou ate mesmo fazer os outros escorregarem.
INFOTIP_t_book              = Livro<br>Objeto normalmente retangular com folhas anexas a ele , onde podem ser escritos receitas , pensamentos e ate monografias.
INFOTIP_t_sand              = Areia<br>Umas das materias primas do artesao, a areia eh usada na criacao de recipientes de vidro.
//INFOTIP_t_preitem_bowcraft  =
//INFOTIP_t_preitem_carpentry =
INFOTIP_t_arame             = Arame<br>Usado comumente por ferreiros, artesaos e carpinteiros o arame eh uma liga fina de fio de ferro que eh comumente usada na producao de outros produtos como barbante de ferro, pregos e etc.
//INFOTIP_t_luz_oleo          =
//INFOTIP_t_container         =
//INFOTIP_t_tailoring         =
//INFOTIP_t_thread            =
//INFOTIP_t_yarn              =
//INFOTIP_t_jewelry           =
INFOTIP_t_bandagem          = Ataduras<br>Produto obtido por tailoring. Pedaco de pano usado para criacao de tochas e para o estancamento de feridas.
INFOTIP_t_log               = Toras<br>Obtido pelo uso de lumberjacking em arvores, a tora eh a materia prima usada por artesaos e carpinteiros.
INFOTIP_t_ore               = Minerio<br>Obtido por mining. Sao rochas impuras que contem em seu interior materias preciosos que pelo meio do derretimento geram a materia prima dos ferreiros.
//INFOTIP_t_ingot             =
INFOTIP_t_seed              = Sementes<br>Sao obtidos durante a colheita de frutos ou vegetais. Podem ser plantadas em solo desde que o mesmo seja adequado para isso.
INFOTIP_t_leather           = Couro<br>Obtido de animais. Sua extracao eh feita atraves do corte. O couro cru pode ser trabalhado com uso de oleo de uma fogueira para que se torne couro tratado.
INFOTIP_t_loom              = Aparelho de Tecer
INFOTIP_t_feather           = Pena<br>Obtido comumente de aves atraves do corte. Usada na produção de flechas , cocares  e etc.
INFOTIP_t_wool              = La<br>Usada para producao de vestimentas, a la crua pode ser obtida na tosa de ovelhas com um objeto cortante, como uma adaga.
//INFOTIP_t_fur               =
//INFOTIP_t_thread            =
//INFOTIP_t_yarn              =
INFOTIP_t_cotton            = Algodao<br>Pode ser obtido por pes de algodao. O algodao eh materia prima na fabricacao de tecidos, usados especialmente na confeccao de vestimentas.
//INFOTIP_t_hide              =
//INFOTIP_t_buy_raw           =
//INFOTIP_t_buy_misc          =
//INFOTIP_t_grain             =
INFOTIP_t_mring               = Aneis de metal<br>Utilizado para confeccao de armaduras malha e aneis.
INFOTIP_t_plate               = Placas de metal<br>Utilizado para confeccao de armaduras de placa.
INFOTIP_t_crafting_tool       = Ferramenta<br>Utilizada para confeccao de outros objetos.
INFOTIP_t_plaster             = Emplastro<br>Liquido viscoso para melhorar temporariamente o dano de armas e a resistencia de armaduras.
INFOTIP_t_wheting_stone       = Pedra para amolar<br>Pedra para afiar laminas de armas. Da bonus temporario no dano e dependendo da sua skill Arms Lore pode restituir de 5 a 50% dos hitpoints da arma.
INFOTIP_t_finding_powd        = Po de grafite<br>Pode ser jogado no ambiente para facilitar a procura de objetos ou pessoas escondidas.

[COMMENT]
INFO_t_wand )
INFO_t_reagente )
INFO_t_recipient )
INFO_t_clock )
INFO_t_termometro )
INFO_t_ar_projeto )
INFO_t_ar_livro )
INFO_t_fechadura )
INFO_t_clothing )
INFO_t_cloth )

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// analisar
//*****************************************************************************
[FUNCTION analisar]
sysmessageyellow O que gostaria de analisar?
targetf analisar_


//<item>,<flags>
//01=item de venda
//02=item de compra
[FUNCTION analisar_]

if <argo>
    REF1=<argo>
elif <argn>
    REF1=<argv0>
else
    REF1=0
endif

if !<REF1> || !<REF1.isitem>
    sysmessageyellow Nao eh possivel analisar isto. Selecione outro item.
    targetf analisar_
    return
endif

for R 1 <DEF.INFO_MAX>
    if <ctag0.iteminfo_<LOCAL.R>>==<REF1>
        sysmessageyellow Ja esta analisando este item.
        return
    elif !<LOCAL.found>
        if !<ctag0.iteminfo_<LOCAL.R>>
            LOCAL.found=<LOCAL.R>
        endif
    endif
endfor

if !<LOCAL.found>
    sysmessageyellow Muitas janelas abertas. Feche alguma.
    return
endif

TRY ctag.iteminfo_<LOCAL.found> = <REF1>
TRY ctag.iteminfo_flags<LOCAL.found> = <argv1>

analisar__ <LOCAL.found>,0

[FUNCTION analisar__]
dialog d_itemid_analize_<argv0> 0,<argv0>,<argv1>

//*****************************************************************************
// info_item
//*****************************************************************************
//retorna informacoes explicativas sobre o item
[FUNCTION info_item]
if !<isempty <DEF.INFO_<type>>>
    LOCAL.d=<<DEF.INFO_<type>>>
elif !<isempty <DEF.INFOTIP_<type>>>
    LOCAL.d=<DEF.INFOTIP_<type>>
else
    LOCAL.d=Nenhuma informacao especifica encontrada para <name>
endif

LOCAL.d .="<DEF.BR><DEF.bfont_dcyan>Preco Referencia: <DEF.bfont_white>" 

LOCAL.guildprice=<merchant_get_value <uid>>
if (<strpos 0 . <LOCAL.guildprice>> > 0) //ponto decimal
    LOCAL.d .=<LOCAL.guildprice>mc
elif (<LOCAL.guildprice> > 0)
    LOCAL.d .=<strtresure <DLOCAL.guildprice>>
else    
    LOCAL.d .= Nao definido
endif

return <LOCAL.d>

//*****************************************************************************
// info_craft
//*****************************************************************************
//retorna informacoes sobre a manufatura do item
[FUNCTION info_craft]

if <skillmake.1.val>==0
    return Nenhuma informacao encontrada para <name>
endif

LOCAL.t = <DEF.bfont_dcyan>Skill: <DEF.BR><DEF.bfont_white>  <fval <skillmake.1.val>>% <serv.skill.<skillmake.1.key>.name> 
//LOCAL.t = <DEF.bfont_dcyan>Skills: <DEF.bfont_white>
//LOCAL.c=<skillmake.count>
//for R 1 <LOCAL.c>
//    LOCAL.t .= <DEF.BR><fval <skillmake.<LOCAL.R>.val>>% <serv.skill.<skillmake.<LOCAL.R>.key>.name> 
//endfor


LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Materia Prima: <DEF.bfont_white>
LOCAL.c=<resources.count>
for R 1 <LOCAL.c>
    LOCAL.t .= <DEF.BR>  <resources.<LOCAL.R>.val> - <serv.itemdef.<resources.<LOCAL.R>.key>.name>
endfor

return <LOCAL.t>

//*****************************************************************************
// info_weapons
//*****************************************************************************
//retorna informacoes explicativas sobre armas
[FUNCTION info_weapons]

if (!<maxhits>)
    serv.log [INFO_WEAPONS] NO MAXHITS: <name> <type> <uid> <src>
    maxhits=1
endif

//gm ve tudo
if (<src.isgm>) || (<attr>&attr_identified)
    LOCAL.c=1
else
    LOCAL.c=0
endif

//definir nome de exibição
LOCAL.name=<name>
if (<LOCAL.c>) && !(<isempty <DEF.<baseid>_IDNAME>>)
    LOCAL.name=<DEF.<baseid>_IDNAME>
endif

LOCAL.t = <DEF.bfont_yellow><LOCAL.name> 

//sneek peak de gm?
if (<attr>&attr_identified)
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Identificado: <DEF.bfont_white>Sim
else
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Identificado: <DEF.bfont_white>Nao
endif

//mostra dano
LOCAL.t .= "<DEF.BR><DEF.bfont_dcyan>Dano:<DEF.bfont_white> "
LOCAL.min=<dam.lo>
LOCAL.max=<dam.hi>
LOCAL.dn=<eval (<dam.hi>+<dam.lo>)/2>
if (<LOCAL.dn><5)
    LOCAL.t .= Fraco
elif (<LOCAL.dn><10)
    LOCAL.t .= Baixo
elif (<LOCAL.dn><25)
    LOCAL.t .= Medio
elif (<LOCAL.dn><40)
    LOCAL.t .= Forte
else
    LOCAL.t .= Muito forte
endif

if (<LOCAL.c>)
    LOCAL.t .= " <eval <dam.lo>>-<eval <dam.hi>> +<eval <dmgfire>>FG +<eval <dmgcold>>GE +<eval <dmgpoison>>VE +<eval <dmgenergy>>EL"
endif

//mostra durabilidade
LOCAL.t .= "<DEF.br><DEF.bfont_dcyan>Qualidade:<DEF.bfont_white> "
LOCAL.min=<hits>
LOCAL.max=<maxhits>
LOCAL.d=<eval (<hits>*100)/<maxhits>>
if (<LOCAL.d><25)
    LOCAL.t .= Pessimo
elif (<LOCAL.d><50)
    LOCAL.t .= Degradado
elif (<LOCAL.d><75)
    LOCAL.t .= Bom
else
    LOCAL.t .= Perfeito
endif
if (<LOCAL.c>)
    LOCAL.t .= " (<eval <LOCAL.min>>/<eval <LOCAL.max>>)"
endif

LOCAL.s = <eval <speed>*(<src.dex>+100)> 
LOCAL.t .= <DEF.br><DEF.bfont_dcyan>Swing: <DEF.bfont_white><fval 150000/<LOCAL.s>>s

if (<LOCAL.c>)
    FLOAT.sw=<fval 150000/<LOCAL.s>>
    FLOAT.ss=<eval <LOCAL.dn>*10>
    LOCAL.t .= <DEF.br><DEF.bfont_dcyan>DPS: <DEF.bfont_white><fval <FLOAT.ss>/<FLOAT.sw>>
endif

//forca necessaria
LOCAL.t .= "<DEF.br><DEF.bfont_dcyan>Forca para uso: <DEF.bfont_white><eval <reqstr>>"

//duas maos?
if (<TWOHANDS>)
    LOCAL.t .= " (Duas Maos)"
endif

//Range
if (<LOCAL.c>)
    if (0<STREAT <serv.itemdef.<defname>.range>>)
        LOCAL.t .= "<DEF.BR><DEF.bfont_dcyan>Alcance: <DEF.bfont_white><STRARG <serv.itemdef.<defname>.range>> a <STREAT <serv.itemdef.<defname>.range>>"
    else
        LOCAL.t .= "<DEF.BR><DEF.bfont_dcyan>Alcance: <DEF.bfont_white><serv.itemdef.<defname>.range>"
    endif
endif

LOCAL.t .= <DEF.br><DEF.bfont_dcyan>Skill: <DEF.bfont_white><serv.skill.<skill>.name> 
LOCAL.t .= <DEF.br><DEF.bfont_dcyan>Peso: <DEF.bfont_white><eval <weight>/10> stones

//mostrar atributos
if <LOCAL.c>
    if (<attr>&attr_cursed)
        LOCAL.t .= <DEF.br><DEF.bfont_lpurple>amaldiçoado
    endif
    if (<attr>&attr_cursed2)
        LOCAL.t .= <DEF.br><DEF.bfont_lpurple>maldito
    endif
    if (<attr>&attr_blessed)
        LOCAL.t .= <DEF.br><DEF.bfont_dcyan>abençoado
    endif
    if (<attr>&attr_blessed2)
        LOCAL.t .= <DEF.br><DEF.bfont_dcyan>sagrado
    endif

    if (<morex>==s_poison)
        LOCAL.t .= <DEF.br><DEF.bfont_dcyan>Envenenado
    elif (<attr>&attr_magic)
        if (<morex>) && (<morex>!=s_poison)
            LOCAL.t .= <DEF.br><DEF.bfont_dcyan>Magia:<DEF.bfont_white> <serv.spell.<more2>.name>
        else
            LOCAL.t .= <DEF.br><DEF.bfont_dcyan>Mágico
        endif
    endif
       
endif

//definir TOOLTIP se houver
if (<LOCAL.c>) && !(<isempty <DEF.<baseid>_IDTIP>>)
  LOCAL.t .= <DEF.br><DEF.bfont_cyan><DEF.<baseid>_IDTIP>
endif

return <LOCAL.t>

//*****************************************************************************
// info_armors
//*****************************************************************************
//retorna informacoes explicativas sobre armaduras
[FUNCTION info_armors]
//gm ve tudo
if (<src.isgm>) || (<attr>&attr_identified)
    LOCAL.c=1
else
    LOCAL.c=0
endif

//definir nome de exibição
LOCAL.name=<name>
if (<LOCAL.c>) && !(<isempty <DEF.<baseid>_IDNAME>>)
  LOCAL.name=<DEF.<baseid>_IDNAME>
endif

LOCAL.t = <DEF.bfont_yellow><LOCAL.name> 

//sneek peak de gm?
if !(<attr>&attr_identified) && <src.isgm>
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Identificado: <DEF.bfont_white>Sim
else
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Identificado: <DEF.bfont_white>Nao
endif


//mostra protecao
LOCAL.t .= "<DEF.BR><DEF.bfont_dcyan>Protecao:<DEF.bfont_white> "
LOCAL.min=<armor.lo>
LOCAL.max=<armor.hi>
LOCAL.d=<eval (<armor.hi>+<armor.lo>)/2>
if (<LOCAL.d><5)
    LOCAL.t .= Fraca
elif (<LOCAL.d><10)
    LOCAL.t .= Pouca
elif (<LOCAL.d><25)
    LOCAL.t .= Media
elif (<LOCAL.d><40)
    LOCAL.t .= Forte
else
    LOCAL.t .= Muito forte
endif
if (<LOCAL.c>)
    LOCAL.t .= " <eval <armor.lo>>-<eval <armor.hi>><info_armors_resmod <tag.resmod>>"
endif


//mostra qualidade
LOCAL.t .= "<DEF.BR><DEF.bfont_dcyan>Qualidade:<DEF.bfont_white> "
LOCAL.min=<hits>
LOCAL.max=<maxhits>
LOCAL.d=<eval (<hits>*100)/<maxhits>>
if (<LOCAL.d><25)
    LOCAL.t .= Pessimo
elif (<LOCAL.d><50)
    LOCAL.t .= Degradado
elif (<LOCAL.d><75)
    LOCAL.t .= Bom
else
    LOCAL.t .= Perfeito
endif
if (<LOCAL.c>)
    LOCAL.t .= " (<eval <LOCAL.min>>/<eval <LOCAL.max>>)"
endif

//forca necessaria
LOCAL.t .= "<DEF.BR><DEF.bfont_dcyan>Forca para uso:<DEF.bfont_white> <eval <reqstr>>"
LOCAL.t .= "<DEF.br><DEF.bfont_dcyan>Peso: <DEF.bfont_white><eval <weight>/10> stones"

//mostrar atributos
if <LOCAL.c>
  
    if (<attr>&attr_cursed)
        LOCAL.t .= <DEF.br><DEF.bfont_lpurple>amaldiçoado
    endif
    if (<attr>&attr_cursed2)
        LOCAL.t .= <DEF.br><DEF.bfont_lpurple>maldito
    endif
    if (<attr>&attr_blessed)
        LOCAL.t .= <DEF.br><DEF.bfont_dcyan>abençoado
    endif
    if (<attr>&attr_blessed2)
        LOCAL.t .= <DEF.br><DEF.bfont_dcyan>sagrado
    endif


    if (<attr>&attr_magic)

//        if (<morex>) && (<morex>!=s_poison)
//            LOCAL.t .= <DEF.bfont_dcyan>Magia:<DEF.bfont_white> <serv.spell.<more2>.name><DEF.br>
//        else
            LOCAL.t .= <DEF.br><DEF.bfont_dcyan>Mágico
//        endif
    endif

endif

//definir TOOLTIP se houver
if (<LOCAL.c>) && !(<isempty <DEF.<baseid>_IDTIP>>)
    LOCAL.t .= <DEF.br><DEF.bfont_cyan><DEF.<baseid>_IDTIP>
endif

return <LOCAL.t>

//*****************************************************************************
// info_armors_resmod
//*****************************************************************************
//facilita captura dos valores de resmod q estao em tag
[FUNCTION info_armors_resmod]

LOCAL.tm = <DEF.bfont_green>
LOCAL.x = 0

if <argv0>
    LOCAL.x=1
    LOCAL.tm .= " +<eval <argv0>>FG"
endif
if <argv1>
    LOCAL.x=1    
    LOCAL.tm .= " +<eval <argv1>>GE"
endif
if <argv2>
    LOCAL.x=1
    LOCAL.tm .= " +<eval <argv2>>VE"
endif
if <argv3>
    LOCAL.x=1
    LOCAL.tm .= " +<eval <argv3>>EL"
endif

if <LOCAL.x>
    return <LOCAL.tm>
endif

//return "+<eval <argv0>>FG +<eval <argv1>>GE +<eval <argv2>>VE +<eval <argv3>>EL"

//*****************************************************************************
// info_potions
//*****************************************************************************
//retorna informacoes explicativas sobre pocoes
[FUNCTION info_potions]

//gm ve tudo
if (<src.isgm>) || (<attr>&attr_identified)
    LOCAL.c=1
else
    LOCAL.c=0
endif

//definir nome de exibição
LOCAL.name=<name>
if (<LOCAL.c>) && !(<isempty <DEF.<baseid>_IDNAME>>)
    LOCAL.name=<DEF.<baseid>_IDNAME>
endif

LOCAL.t = <DEF.bfont_yellow><LOCAL.name> 

//sneek peak de gm?
if !(<attr>&attr_identified) && <src.isgm>
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Identificado: <DEF.bfont_white>Sim
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Intensidade: <DEF.bfont_white><f_potionStr>
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Efeito: <DEF.BR><DEF.bfont_white>
    if !(<isempty <DEF.<baseid>_IDTIP>>)
        LOCAL.t .= <DEF.<baseid>_IDTIP>
    else
        LOCAL.t .= Desconhecido
    endif

else
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Identificado: <DEF.bfont_white>Nao
    LOCAL.t .= <DEF.BR>Identifique para saber mais sobre esta pocao.
endif

return <LOCAL.t>


//*****************************************************************************
// info_food
//*****************************************************************************
//retorna informacoes explicativas sobre alimentos
[FUNCTION info_food]

//gm ve tudo
if (<src.isgm>) || (<attr>&attr_identified)
    LOCAL.c=1
else
    LOCAL.c=0
endif

LOCAL.t = <DEF.bfont_yellow><name> 

if <type>==t_fruit
    LOCAL.t .= " [ fruta ]" 
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Satisfacao Alimentar: <DEF.bfont_white>3% a 8% por fruta
elif <type>==t_fish
    LOCAL.t .= " [ peixe ]" 
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Satisfacao Alimentar: <DEF.bfont_white>2% a 10% por pedaco
elif <type>==t_food_raw
    LOCAL.t .= " [ cru ]" 
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Satisfacao Alimentar: <DEF.bfont_white>Nao pode ser ingerido
elif <type>==t_meat_raw
    LOCAL.t .= " [ carne crua ]" 
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Satisfacao Alimentar: <DEF.bfont_white>2% a 10% por pedaco (somente ORCs)
endif

if <LOCAL.c>
    if <morey>==s_poison
        LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Envenenado:<DEF.bfont_white>Sim
    elif <morey>
        LOCAL.t .= <DEF.br><DEF.bfont_dcyan>Magia:<DEF.bfont_white> <serv.spell.<morey>.name>
    endif        
endif

if (<amount>==1) && <morex>
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Porcoes: <DEF.bfont_white><eval <morex>>
else
    LOCAL.t .= <DEF.BR><DEF.bfont_dcyan>Quantidade: <DEF.bfont_white><eval <amount>>
endif

return <LOCAL.t>


//*****************************************************************************
// info_dialog_create <slot>,<page>
//*****************************************************************************
//cria gump com dados do slot e pagina selecionados
[FUNCTION info_dialog_create]
LOCAL.o = <eval 10+(<argv0>*7)>
//serv.log [ITEMINFO]-Dialog Slot:<argv0> 
resizepic 0 0 2620 360 250
//checkertrans 5 7 348 234
button 87 30 2224 2224 1 0 <eval 1+<LOCAL.o>>
dtext 111 26 166 Geral
button 87 50 2224 2224 1 0 <eval 2+<LOCAL.o>>
dtext 111 46 166 Manufatura
button 87 70 2224 2224 1 0 <eval 6+<LOCAL.o>>
dtext 111 66 166 Fechar

obj = <ctag0.iteminfo_<argv0>>
LOCAL.flags=<ctag0.iteminfo_flags<argv0>>

//se estiver alocado na lista de compra/venda
if <LOCAL.flags>

    //compra?
    if <LOCAL.flags>&2
        button 180 30 2224 2224 1 0 <eval 3+<LOCAL.o>>
        dtext 204 26 166 Comprar

        if <src.has_merchant_priv>
            button 180 50 2224 2224 1 0 <eval 4+<LOCAL.o>>
            dtext 204 46 166 Devolver
            button 180 70 2224 2224 1 0 <eval 5+<LOCAL.o>>
            dtext 204 66 166 Retirar
        endif
    //venda?
    elif <LOCAL.flags>&1
        button 180 30 2224 2224 1 0 <eval 3+<LOCAL.o>>
        dtext 204 26 166 Vender
    endif
endif

if (!<argv1>) || (<argv1>==1)
    LOCAL.title=Geral:
    LOCAL.text=<obj.info_item>
elif <argv1>==2
    LOCAL.title=Manufatura:
    LOCAL.text=<obj.info_craft>
endif

dtext 7 8 2301 <LOCAL.title>
tilepichue 7 30 <OBJ.id> <eval <OBJ.color>>
dhtmlgump 10 100 345 143 0 1 <LOCAL.text>

//*****************************************************************************
// info_dialog_button <button_id>
//*****************************************************************************
//processa botao dos gumps e extrai slot e pagina para trabalhar
[FUNCTION info_dialog_button]
//remove offset de seguranca
argn -= 10

//calcula slot/pagina a partir do botao
LOCAL.slot=<eval <argn>/7>
LOCAL.pg=<eval <argn>-(<LOCAL.slot>*7)>

//serv.log [ITEMINFO]-Click Slot:<dLOCAL.slot> Pagina:<dLOCAL.pg> Botao:<argn>
//fechar?
if <LOCAL.pg> == 6
    TRY ctag.iteminfo_<LOCAL.slot>=
//retirar //devolver //comprar/vender
elif (<LOCAL.pg>==5) || (<LOCAL.pg>==4) || (<LOCAL.pg>==3)
    obj = <ctag0.iteminfo_<LOCAL.slot>>
    TRY ctag.iteminfo_<LOCAL.slot>=
    obj.vendor_info_callback <eval <LOCAL.pg>-2>
//trocar pagina    
else
    analisar__ <LOCAL.slot>,<LOCAL.pg>
endif

//*****************************************************************************
//*****************************************************************************
// DIALOGs
//*****************************************************************************
//*****************************************************************************

//FORAM CRIADOS 4 (INFO_MAX) DIALOGS IDENTICOS PARA PERMITIR O PLAYER MANTER ABERTO
//QUANTOS QUISER E MOVIMENTAR SEM PERDER A POSICAO DE CADA QUE FICA SALVA
//NO CLIENT

//*****************************************************************************
// d_itemid_analize_x <slot>,<page>
//*****************************************************************************
[DIALOG d_itemid_analize_01]
30,50
page 0
info_dialog_create <args>

[DIALOG d_itemid_analize_01 text]

[DIALOG d_itemid_analize_01 button]
ON=0
info_dialog_button 23           //10+slot*7+6

ON=10 1000
info_dialog_button <argn>

[DIALOG d_itemid_analize_02]
400,50
page 0
info_dialog_create <args>

[DIALOG d_itemid_analize_02 text]

[DIALOG d_itemid_analize_02 button]
ON=0
info_dialog_button 30           //10+slot*7+6

ON=10 1000
info_dialog_button <argn>

[DIALOG d_itemid_analize_03]
30,320
page 0
info_dialog_create <args>

[DIALOG d_itemid_analize_03 text]

[DIALOG d_itemid_analize_03 button]
ON=0
info_dialog_button 37           //10+slot*7+6

ON=10 1000
info_dialog_button <argn>

[DIALOG d_itemid_analize_04]
400,320
page 0
info_dialog_create <args>

[DIALOG d_itemid_analize_04 text]

[DIALOG d_itemid_analize_04 button]
ON=0
info_dialog_button 44           //10+slot*7+6

ON=10 1000
info_dialog_button <argn>

[EOF]

//***********************************************************************
//***********************************************************************
//
//              Mystical Tales Shard
//                Skills de bardo
//           Criado por: Galthar, o Errante
//        Modificado por: Pyrilin
//                        Miss 2018-05-15
//
//***********************************************************************
//***********************************************************************
//ToDo:
//     - Melhorar gerenciamento de textos;
//     - Criar uma forma mais ágil de trocar as canções;
//     - Criar spellbook para Bardo mais dinâmico;
//
[PLEVEL 0]
BARDO

//***********************************************************************
//***********************************************************************
//                  DEFNAMES
//***********************************************************************
//***********************************************************************

[DEFNAME bard_skills]
SND_DRUM_BATTLE     02E7
SND_DRUM_DRAMA      02E8
SND_DRUM_PEACE      02E9
SND_DRUM            02EA
SND_DRUM_BAD        02EB

SND_HARP_BATTLE     0391
SND_HARP_DRAMA      0392
SND_HARP_PEACE      0393
SND_HARP            045
SND_HARP_BAD        046

SND_LUTE_BATTLE     0403
SND_LUTE_DRAMA      040B
SND_LUTE_PEACE      0418
SND_LUTE            04C
SND_LUTE_BAD        04D

SND_TAMBOURINE_BATTLE   04B5
SND_TAMBOURINE_DRAMA    04B6
SND_TAMBOURINE_PEACE    04B7
SND_TAMBOURINE          052
SND_TAMBOURINE_BAD      053

BARD_ACTION_PLAY    1
BARD_ACTION_SONG    2
BARD_ACTION_TEXT    3

BARD_PLAY_BATTLE    Melodia de Batalha
BARD_PLAY_DRAMA     Melodia Dramatica
BARD_PLAY_PEACE     Melodia de Paz
BARD_PLAY_HAPPY     Melodia Alegre

BARD_P00_NAME=Um Segundo de Atencao
BARD_P00_MANA=10
BARD_P00_DIF=10.0
BARD_P00_SKILL=5.0
BARD_P00_SKILLGAIN=1
BARD_P00_DESC=" Emite um som chamando a atencao da plateia.<DEF.BR> Talvez alguem conforte sua carencia."
BARD_P00_FUNC=BARDSONG_ATENTO
BARD_P00_FAIL=Voce nao chamou atencao, tente pendurar uma melancia na cabeca ou sei la...

BARD_P01_NAME=Galhofa
BARD_P01_MANA=5
BARD_P01_DIF=10.0
BARD_P01_SKILL=5.0
BARD_P01_SKILLGAIN=1
BARD_P01_DESC=" Voce tem um pessimo senso de humor, mas seu carisma talvez arranque umas risadas mesmo com as piores piadas."
BARD_P01_FUNC=BARDSONG_RISO
BARD_P01_FAIL=A sua piada foi tao ruim que doeu em voce.

BARD_P02_NAME=Notas Cortantes
BARD_P02_MANA=10
BARD_P02_DIF=20.0
BARD_P02_SKILL=10.0
BARD_P02_SKILLGAIN=1
BARD_P02_DESC=" Voce ainda toca terrivelmente, mas suas notas horriveis podem produzir uma rajada sonica direcionada a um inimigo."
BARD_P02_FUNC=BARDSONG_PIERCING_NOTES
BARD_P02_FAIL=Suas notas nao foram fortes o suficiente.

BARD_P03_NAME=Encorajar
BARD_P03_MANA=10
BARD_P03_DIF=30.0
BARD_P03_SKILL=20.0
BARD_P03_SKILLGAIN=2 //quanto menor o numero, maior a chance de chamar o Skill_Gain
BARD_P03_DESC=" Encoraja um grupo de aventureiros dando a ele bonus de ataque e defesa. <DEF.BR> Continue tocando para aumentar o bonus."
BARD_P03_FUNC=BARDSONG_VALOR
BARD_P03_FAIL=Sua cancao nao foi muito encorajadora.

BARD_P04_NAME=Adormecer
BARD_P04_MANA=15
BARD_P04_DIF=40.0
BARD_P04_SKILL=30.0
BARD_P04_SKILLGAIN=1
BARD_P04_DESC=" Entoa uma cancao de ninar para deixar um grupo de inimigos sonolento aplicando penalidades de destreza. <DEF.BR> Talvez alguns deles ate durmam."
BARD_P04_FUNC=BARDSONG_LULABY
BARD_P04_FAIL=Ninguem ficou com sono.

BARD_P05_NAME=Ventriloquismo
BARD_P05_MANA=10
BARD_P05_DIF=50.0
BARD_P05_SKILL=40.0
BARD_P05_SKILLGAIN=1
BARD_P05_DESC=" Voce toca mal seu instrumento como forma gerar uma dissonancia acustica no ambiente.<DEF.BR> O alvo entao recita o seu texto memorizado para recitar.<DEF.BR> Um mestre da cancao sempre tem muitas cartas na bolsa."
BARD_P05_FUNC=BARDSONG_VENTRILOCO
BARD_P05_FAIL=Voce nao conseguiu criar dissonancias acusticas no ambiente.

BARD_P06_NAME=A Grande Marcha
BARD_P06_MANA=6
BARD_P06_DIF=55.0
BARD_P06_SKILL=50.0
BARD_P06_SKILLGAIN=10
BARD_P06_DESC=" Concede maior velocidade e disposicao para os aliados a sua volta."
BARD_P06_FUNC=BARDSONG_RUN
BARD_P06_FAIL=Sua marcha nao foi muito motivadora.

BARD_P07_NAME=Encantar
BARD_P07_MANA=20
BARD_P07_DIF=60.0
BARD_P07_SKILL=50.0
BARD_P07_SKILLGAIN=1
BARD_P07_DESC=" Entoa uma bela cancao, tao bela que pode prender a atencao de um grupo de inimigos para que eles fiquem estaticos."
BARD_P07_FUNC=BARDSONG_CHARM
BARD_P07_FAIL=Sua cancao nao foi encantadora o suficiente para prender a atencao de ninguem.

BARD_P08_NAME=Cancao Revigorante
BARD_P08_MANA=6
BARD_P08_DIF=60.0
BARD_P08_SKILL=55.0
BARD_P08_SKILLGAIN=5
BARD_P08_DESC=" Toca uma cancao revigorante que lentamente recupera a vitalidade de todos a sua volta."
BARD_P08_FUNC=BARDSONG_REGEN
BARD_P08_FAIL=Sua cancao nao foi muito revigorante.

BARD_P09_NAME=Cancao Aterrorizante
BARD_P09_MANA=10
BARD_P09_DIF=70.0
BARD_P09_SKILL=60.0
BARD_P09_SKILLGAIN=1
BARD_P09_DESC=" Toca uma cancao terrivel, com notas horriveis, gerando medo em quem falhar a um teste de inteligencia contra o Bardo. <DEF.BR> A vitima ficara impossibilitada de atacar, usar pericias ou magias. NPCs fugirao tambem. <DEF.BR> O custo de mana eh para cada personagem que se tentar aplicar o efeito."
BARD_P09_FUNC=BARDSONG_SCARE
BARD_P09_FAIL=Sua cancao nao foi tao perturbadora assim.


//***********************************************************************
//***********************************************************************
//                  TYPEDEFs
//***********************************************************************
//***********************************************************************

//***********************************************************************
// t_musical
//***********************************************************************
[TYPEDEF t_musical]
ON=@DCLICK
//Checks de CAN
IF (<topobj>!=<src>)
    IF (<cont>)
        src.sysmessageyellow Nao da para usar <name> aonde esta. Voce deve ter ele em sua mochila.
        return 1
    endif
else
    IF <src.findid.i_meditation>
        src.sysmessageyellow Voce esta recuperando inspiracao...
        return 1
    ELIF (<src.action> != -1) || (<timer> > 0)
        src.sysmessageyellow Aguarde ate estar desocupado para fazer isso.
        return 1
    endif
endif

    IF (<HITS>>0)
        HITS=(<HITS>-1)
        update
        return 0
    else
        src.sysmessagered O instrumento precisa de reparos.
        return 1
    endif

ON=@ClientToolTip
//bughunt
if (!<maxhits>)
 SERV.LOG [CRAFTMENU] Item criado sem maxhits! <name>::<defname>
 if (!<TDATA4>)
  SERV.LOG [CRAFTMENU] Item criado sem TDATA4! <name>::<defname>
  maxhits=100
  hits=100
 else
  maxhits=<TDATA4>
  hits=<TDATA4>
 endif
endif

//bugtrap
if (<TDATA4>)
 local.max=<TDATA4>
else
 local.max=<maxhits>
endif

IF (<attr>&01)//Identificado
 local.t=Estado: <EVAL <HITS>>/<EVAL <MAXHITS>><DEF.BR>
 local.t .= Qualidade:
 DOSWITCH <eval (<maxhits>*6)/<local.max>>
  local.t .= " Pessima"
  local.t .= " Duvidosa"
  local.t .= " Media"
  local.t .= " Boa"
  local.t .= " Excelente"
  local.t .= " Excelente"
  local.t .= " Excelente"
 ENDDO
ELSE
 local.t="Estado: "
 DOSWITCH <eval (<hits>*5)/<maxhits>>
  local.t .= " Quebrando-se"
  local.t .= " Deteriorado"
  local.t .= " Boas condicoes"
  local.t .= " Semi-novo"
  local.t .= " Novo em folha"
  local.t .= " Novo em folha"
 ENDDO
 local.t .= "<DEF.BR>Qualidade: "
 DOSWITCH <eval (<maxhits>*3)/<local.max>>
  local.t .= " Duvidosa"
  local.t .= " Media"
  local.t .= " Boa"
  local.t .= " Boa"
 ENDDO
ENDIF
src.f_sendTooltipData <name>
src.f_sendTooltip <name>,<local.t>
return 1

//desocupa
ON=@TIMER
timer=-1
return 1

//***********************************************************************
//***********************************************************************
//                  SKILLS
//***********************************************************************
//***********************************************************************

//***********************************************************************
// PROVOCATION
//***********************************************************************
[SKILL 22]
//DEFNAME=SKILL_PROVOCATION
KEY=Provocation
TITLE=Bard
PROMPT_MSG=@036 Quem voce gostaria de provocar?
DELAY=10.0,1.0
STAT_STR=20
STAT_INT=60
STAT_DEX=70
BONUS_STR=0
BONUS_DEX=90
BONUS_INT=10
BONUS_STATS=5
//ADV_RATE=2.5,50.0,200.0

ON=@Fail
    SRC.SYSMESSAGERED Sua musica falhou em provocar raiva o suficiente.
    //Diminuir chance de ser atacado.
    IF (<SRC.PROVOCATION> < <R600>)
        return 0
    ELSE
        RETURN 1
    ENDIF

ON=@Abort
    SRC.SYSMESSAGEYELLOW Sua musica falhou em provocar raiva o suficiente.
    //Diminuir chance de ser atacado.
    IF (<SRC.PROVOCATION> < <R600>)
        return 0
    ELSE
        RETURN 1
    ENDIF

//***********************************************************************
// ENTICEMENT
//***********************************************************************
[SKILL 15]
//DEFNAME=Skill_Enticement
KEY=Enticement
TITLE=Bard
PROMPT_MSG=Quem voce gostaria de incitar?
DELAY=10.0,1.0
STAT_STR=15
STAT_INT=70
STAT_DEX=50
BONUS_STR=0
BONUS_DEX=50
BONUS_INT=50
BONUS_STATS=5
//ADV_RATE=2.5,50.0,200.0

ON=@Fail
    SRC.SYSMESSAGEYELLOW Voce falhou.

ON=@Abort
    SRC.SYSMESSAGEYELLOW Voce nao termina a musica.

//***********************************************************************
// PEACEMAKING
//***********************************************************************
[SKILL 9]
//DEFNAME=SKILL_PEACEMAKING
KEY=Peacemaking
TITLE=Bard
PROMPT_MSG=
DELAY=2.0
STAT_STR=0
STAT_INT=70
STAT_DEX=70
BONUS_STR=0
BONUS_DEX=50
BONUS_INT=50
BONUS_STATS=5
//ADV_RATE=2.5,50.0,200.0

ON=@Fail
    SRC.SYSMESSAGEYELLOW Voce falha ao tentar acalmar.

ON=@Abort
    SRC.SYSMESSAGEYELLOW Voce nao termina a musica.

//***********************************************************************
// MUSICIANSHIP
//***********************************************************************
[SKILL 29]
//DEFNAME=Skill_Musicianship
KEY=Musicianship
TITLE=Bard
PROMPT_MSG=
DELAY=2.0
STAT_STR=20
STAT_INT=50
STAT_DEX=70
BONUS_STR=0
BONUS_DEX=80
BONUS_INT=20
BONUS_STATS=10
//ADV_RATE=2.5,50.0,200.0

ON=@START
//default delay between retries
src.act.timer 5
//just play or sing?
findlayer.layer_hand1.unequip
findlayer.layer_hand2.unequip
IF (<tag0.bard.action>==BARD_ACTION_PLAY)
    emoteyellow tocando <act.name>
    IF <BELLTEST <musicianship>,<R100,250>>
        INSTRUMENT <tag.bard.item>
    ELSE
        INSTRUMENT BAD
        src.act.timer 8
        sysmessageyellow Voce tocou muito mal.
    ENDIF
ELIF (<tag0.bard.action>==BARD_ACTION_SONG)
    TRY <DEF.BARD_P<hval <tag0.bard.item>>_FUNC>
ELSE
    INSTRUMENT
    emoteyellow tocando <act.name>
ENDIF
action=-1
return 1

ON=@SUCCESS
    return 1

ON=@FAIL
    return 1

ON=@ABORT
    return 1

//***********************************************************************
//***********************************************************************
//                 FUNÇÕES
//***********************************************************************
//***********************************************************************

//***********************************************************************
//                 BARDO
//***********************************************************************
[FUNCTION BARDO]
SDIALOG d_book_bard 1

//***********************************************************************
//                 INSTRUMENT
//***********************************************************************
[FUNCTION INSTRUMENT]
//Faz o som do instrumeto que o jogador possui de acordo com o argumento <ARGS>
// ARGS=[BATTLE | DRAMA | PEACE | BAD | <nada>]

//Qual é o intrumento?
IF (<src.act.type> == t_musical) && (<src.act.topobj> == <src.uid>)
    obj=<src.act.uid>
ELSE
    obj=<src.findtype.t_musical>
ENDIF

// Determinar o tipo dos intrumento
IF (<obj.baseid>==i_lute)
    LOCAL.ins=LUTE
ELIF (<obj.baseid>==i_lyre)
    LOCAL.ins=HARP
ELIF (<obj.baseid>==i_tambourine)
    LOCAL.ins=TAMBOURINE
ELIF (<obj.baseid>==i_drum)
    LOCAL.ins=DRUM
ELSE
    RETURN 0
ENDIF

//Que tipo de som?
IF (<ISEMPTY <args>>) || (<ISEMPTY <DEF.SND_<LOCAL.INS>_<ARGS>>>)
    SFX <DEF.SND_<LOCAL.INS>>
ELSE
    SFX <DEF.SND_<LOCAL.INS>_<ARGS>>
ENDIF
RETURN 1

//***********************************************************************
//                 BARDSONG_TEST
//***********************************************************************
//Testa musicianship, gasta mana e retorna 0 ou 1.
//Deduz qual canção bardica testar pelo tag.bard.item
[FUNCTION BARDSONG_TEST]
//GMs sempre se dão bem.
IF (<IsGM>)
    return 1
ENDIF

//Testa se tem mana
LOCAL.mana=<DEF.BARD_P<hval <tag0.bard.item>>_MANA>
IF <MANA> < <LOCAL.mana>
    sysmessagered Voce nao tem mana o suficiente.
    return 0
ENDIF
MANA -= <LOCAL.mana>

//Testa a skill
IF !(<R<DEF.BARD_P<hval <tag0.bard.item>>_SKILLGAIN>>)
    SKILL_GAIN Skill_Musicianship
endif


IF (!<BELLTEST <MUSICIANSHIP>,<DEF.BARD_P<hval <tag0.bard.item>>_DIF>>)

    IF (!<IsEmpty <DEF.BARD_P<hval <tag0.bard.item>>_FAIL>>)
        sysmessagered <DEF.BARD_P<hval <tag0.bard.item>>_FAIL>
    ELSE
        sysmessagered Voce falhou.
    ENDIF
    return 0
ENDIF
return 1

//***********************************************************************
//***********************************************************************
//                 FUNÇÕES DE PODER
//***********************************************************************
//***********************************************************************

//***********************************************************************
//                 Encorajar
//***********************************************************************

[FUNCTION BARDSONG_VALOR]
IF !(<INSTRUMENT BATTLE>)
sysmessagered O Instrumento precisa estar na sua mochila.
return 1
endif
emoteyellow Entoa uma cancao encorajadora
LOCAL.timer=<f_rangeValue 8,2,<musicianship>>
TIMERf <LOCAL.timer>, _BARDSONG_VALOR

[FUNCTION _BARDSONG_VALOR]
IF (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 return 1
endif
LOCAL.r=<BETWEEN 1,18,<musicianship>,90.0>
LOCAL.moddam=<BETWEEN 0,4,<musicianship>,120.0>
LOCAL.modar=<BETWEEN 0,4,<musicianship>,120.0>
LOCAL.timer=<BETWEEN 30,400,<musicianship>,120.0>
//sysmessageyellow r:<dLOCAL.r> moddam:<dLOCAL.moddam> modar:<dLOCAL.modar>
OBJ=<UID>
FORCHARS <LOCAL.r>
 IF (!<IsMyEnemy <OBJ>>) && ((<IsMyMaster <OBJ>>) || (<IsPlayer>)) && !(<RESTEST 4 i_mry_COMBAT_MODS>)
  EFFECT_BARDSONG_VALOR <LOCAL.timer>,<LOCAL.modar>,<LOCAL.moddam>
 ENDIF
ENDFOR

[FUNCTION EFFECT_BARDSONG_VALOR]
//EFFECT_BARDSON_VALOR <time>,<MODAR>,<MODDAM>
serv.newitem i_mry_COMBAT_MODS
new.more1=<argv1>
new.more2=<argv2>
equip <new>
new.timer=<argv0>
message=@02, *encorajad<sex o/a>* 
sysmessagegreen Voce se sente mais corajos<sex o/a>!

[ITEMDEF i_mry_COMBAT_MODS]
ID=i_lyre
NAME=Comat Mod (Bardsong)
TYPE=t_eq_script
LAYER=layer_special
WEIGHT=0

on=@equip
cont.modar += <more1>
cont.tag0.moddam += <more2>

on=@timer
cont.modar -= <more1>
cont.tag0.moddam -= <more2>
remove
return 1


//***********************************************************************
//                 SONOLÊNCIA
//***********************************************************************

[FUNCTION BARDSONG_LULABY]
IF !(<INSTRUMENT PEACE>)
 sysmessagered O Instrumento precisa estar na sua mochila.
 return 1
endif
emoteyellow Entoa uma cancao de ninar
TIMERF <BETWEEN2 4,10,<musicianship>,100.0>,,_BARDSONG_LULABY

[FUNCTION _BARDSONG_LULABY]
IF (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 RETURN 1
endif
LOCAL.r=<BETWEEN 1,18,<musicianship>,90.0>
OBJ=<UID>
FORCHARS <LOCAL.r>
if (<belltest <obj.musicianship>, 110.0>)
    local.f = 4
elif (<belltest <obj.musicianship>, 80.0>)
    local.f = 3
elif (<belltest <obj.musicianship>, 50.0>)
    local.f = 2
else
    local.f = 1
endif
if (<belltest <obj.musicianship>, 90.0>)
    local.s = 1
endif

 IF ((<IsMyEnemy <OBJ>>) || (!<IsPlayer>)) && (!<IsMyMaster <OBJ>>) && (<CanSeeLos <OBJ>>) && (<UID> != <obj>) //|| (!<IsPlayer>))
 EFFECT_BARDSONG_LULABY <OBJ> <local.f> <local.s>
 ENDIF
ENDFOR

[FUNCTION EFFECT_BARDSONG_LULABY]
obj=<argn1>
obj.say Eu sou obj
say Eu sou ME
IF ((<R<INT>> > <obj.int>) || (<BELLTEST <obj.Musicianship>,120.0>)) && (<R15>)
 emotered cai sono
 f_faint
 timerF <BETWEEN 10,30,<obj.Musicianship>,1000>,f_WakeUp
 stun <BETWEEN 10,30,<obj.Musicianship>,1000>
else
 F_SONOLENTO <argn1> <argn2> <argn3>
ENDIF

[FUNCTION f_sonolento]
OBJ=<argn1>
emotered Sonolent<sex o/a>
if <restest i_mry_music_sonolencia>
    obj.sysmessageyellow O alvo ja sofre dos efeitos da sonolencia.
    return 1
else
    serv.newitem i_fx_curse
    new.attr = attr_static|attr_decay
    new.timer = 2
    new.p = <p>
    local.time=<BETWEEN 30,70,<obj.Musicianship>,1000>
    local.dex=<eval -<BETWEEN 5,20,<obj.Musicianship>,1000>>
    trysrc <uid> src.Buff_DEX <local.dex>,<local.time>
    STAM=<minimum <dex>,<stam>>

    newitem i_mry_music_sonolencia
    new.link <uid>
    new.more1 <eval <local.time>>
    new.more2 <argn2>
    new.moreX <argn3>
    trysrc <uid> src.new.equip
endif

[ITEMDEF i_mry_music_sonolencia]
ID=02082
name=Sonolencia
type=t_eq_script
layer=layer_special
TEVENTS=e_BuffStats

ON=@Create
COLOR=05d
ATTR=attr_cursed2
  
on=@equip
if (<morex> == 1)
link.speedmode=2
link.sysmessageorange Voce sente suas pernas cansadas
timer=1
cont.sysmessageorange Voce se sente extremamente cansado, como se nao dormisse a dias.
timerf 15, cont.sysmessageorange A cancao reverbera em sua mente.

on=@unequip
cont.sysmessagegreen O sono dissipa-se.

on=@timer
if (<more1><=0)
 remove
 link.speedmode=0
endif
more1 -= 1

if (<link.stam> > 10)
 link.stam -= <more2>
endif
timerd 15
return 1

//***********************************************************************
//                 ENCANTAR
//***********************************************************************
[FUNCTION BARDSONG_CHARM]
IF !(<INSTRUMENT PEACE>)
 sysmessagered O Instrumento precisa estar na sua mochila.
 return 1
endif
emoteyellow Entoa uma cancao encantadora
TIMERF <BETWEEN2 3,6,<musicianship>,100.0>,_BARDSONG_CHARM

[FUNCTION _BARDSONG_CHARM]
IF (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 RETURN 1
ENDIF
LOCAL.r=<BETWEEN 1,18,<musicianship>,90.0>
OBJ=<UID>
FORCHARS <LOCAL.r>
 IF ((<IsMyEnemy <OBJ>>) || (!<IsPlayer>)) && (!<IsMyMaster <OBJ>>) && (<CanSeeLos <OBJ>>) && (<UID> != <obj>)
  EFFECT_BARDSONG_CHARM
 ENDIF
ENDFOR

[FUNCTION EFFECT_BARDSONG_CHARM]
if <tag0.recemEncantado>
    sysmessagegreen Voce resiste ao encantamento, pois foi encatado recentemente
    obj.sysmessagered A musica nao surte efeito em alvos encantados recentemente
else
    if (<BELLTEST <obj.Musicianship>,60.0>) && (<R<INT>> <= <obj.int>)
        emotered encantado
        stun <BETWEEN 2,20,<obj.Musicianship>,200.0>
        tag.recemEncantado=1
        timerf 40, tag.recemEncantado=
    endif
endif


//***********************************************************************
//                 NOTAS CORTANTES
//***********************************************************************
[FUNCTION BARDSONG_PIERCING_NOTES]
SYSMESSAGEYELLOW Para onde voce quer direcionar as notas cortantes?
targetf _BARDSONG_PIERCING_NOTES

[FUNCTION _BARDSONG_PIERCING_NOTES]
if (!<argo>)
 sysmessagered Voce deve escolher um alvo.
 RETURN 0
ELIF (!<ARGO.ISCHAR>)
 sysmessagered Seu alvo deve ser uma criatura.
 RETURN 0
ELIF (<ARGO>==<UID>)
 emoteyellow toca notas terriveis
 INSTRUMENT BAD
 sysmessagered Nao funciona apontar o som para voce <SEX mesmo/mesma>.
 RETURN 0
ELIF (!<CanSeeLos <ARGO>>)
 sysmessagered Nao consegue ver o alvo.
 RETURN 0
endif
INSTRUMENT BAD
emoteyellow toca notas terriveis
IF (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 return 1
ENDIF
argo.effect 0 i_fx_ebolt 6 6 0
LOCAL.DAM=<R7,<eval <musicianship>/60>>
argo.DAMAGE <LOCAL.DAM> dam_god <UID>
argo.sysmessagered uma melodia aflige seus timpanos!!!


//***********************************************************************
//                 MEDO
//***********************************************************************
[FUNCTION BARDSONG_SCARE]
IF !(<INSTRUMENT DRAMA>)
 sysmessagered O Instrumento precisa estar na sua mochila.
 return 1
endif
emoteyellow entoa uma cancao de fantasmagorica
TIMERF <BETWEEN2 2,6,<musicianship>,200.0>,_BARDSONG_SCARE

[FUNCTION _BARDSONG_SCARE]
OBJ=<UID>
LOCAL.r=<BETWEEN 1,18,<musicianship>,120.0>
FORCHARS <LOCAL.r>
FORCHARS 8
    IF ((<IsMyEnemy <OBJ>>) && (!<IsMyMaster <OBJ>>) && (<CanSeeLos <OBJ>>) && (<UID> != <obj>) && (<OBJ.distance> < 14) //(!<IsPlayer>))
  EFFECT_BARDSONG_SCARE
 ENDIF
ENDFOR

[FUNCTION EFFECT_BARDSONG_SCARE]
if <tag0.recemApavorado>
    sysmessagegreen Voce resiste ao apavoramento, pois foi apavorado recentemente
    obj.sysmessagered A musica nao surte efeito em alvos apavorados recentemente
else
    if (<OBJ.BARDSONG_TEST>) && (<R<INT>> <= <obj.int>)
        emotered Apavorado
        if (<flags>&statf_war)
            flags &= ~statf_war
        endif
        events +e_scared
        timerf <BETWEEN 10,40,<obj.musicianship>,200.0>,events,-e_scared
        tag.recemApavorado=1
        timerf 40, tag.recemApavorado=
    endif
endif


//***********************************************************************
//                 CANCAO DA CURA
//***********************************************************************
[FUNCTION BARDSONG_REGEN]
IF !(<INSTRUMENT PEACE>)
 sysmessagered O Instrumento precisa estar na sua mochila.
 return 1
endif
emoteyellow Entoa uma cancao revigorante
skill_Inuse skill_Musicianship
events +e_no_attack
events +e_no_dclick
cTAG.DISTURB
TIMERF 3,_BARDSONG_REGEN

[FUNCTION _BARDSONG_REGEN]
findlayer.layer_hand1.unequip
findlayer.layer_hand2.unequip
IF !(<INSTRUMENT PEACE>)
 sysmessagered Voce nao tem um instrumento
 return 1
endif
IF (<f_isDisturbed>) || (!<IsOnline>) || (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 sysmessagered Voce foi perturbado
 emotered Para de cantar
 skill_Inuse 0
 events -e_no_attack
 events -e_no_dclick
 return 1
ENDIF

OBJ=<UID>
LOCAL.r=<BETWEEN 1,18,<musicianship>,120.0>
FORCHARS <LOCAL.r>
if (<belltest <obj.musicianship>, 110.0>)
    local.f = 4
elif (<belltest <obj.musicianship>, 90.0>)
    local.f = 3
elif (<belltest <obj.musicianship>, 75.0>)
    local.f = 2
else
    local.f = 1
endif
 IF (!<IsMyEnemy <OBJ>>) && ((<IsMyMaster <OBJ>>) || (<IsPlayer>)) && (!<RESTEST i_mry_music_regen>)
  EFFECT_BARDSONG_REGEN <local.f>
 ENDIF
ENDFOR

TIMERF 3,_BARDSONG_REGEN 

[FUNCTION EFFECT_BARDSONG_REGEN]
newitem i_mry_music_regen
new.link <obj.uid>
new.more1 3
new.more2 <argn1>
trysrc <uid> src.new.equip

[ITEMDEF i_mry_music_regen]
ID=0209C
name=Cancao da Cura
type=t_eq_script
layer=layer_special
TEVENTS=e_BuffStats

ON=@Create
COLOR=05d
ATTR=attr_blessed2
  
on=@equip
timer=1
cont.sysmessageyellow a cancao esta curando suas feridas...

on=@timer
if (<more1>>=4)
    more1=3
endif
if (<more1><=0) || (<cont.distance <link>> > 8)
 remove
endif

more1 -= 1
if (<cont.hitpoints> < <cont.maxhits>)
    cont.hitpoints += <more2>
endif
timerd 5
return 1

//***********************************************************************
//                 A Grande Marcha
//***********************************************************************
[FUNCTION BARDSONG_RUN]
IF !(<INSTRUMENT BATTLE>)
 sysmessagered O Instrumento precisa estar na sua mochila.
 return 1
endif
emoteyellow Entoa uma marcha motivante
skill_Inuse skill_Musicianship
events +e_no_attack
events +e_no_dclick
cTAG.DISTURB
TIMERF 3,_BARDSONG_RUN

[FUNCTION _BARDSONG_RUN]
findlayer.layer_hand1.unequip
findlayer.layer_hand2.unequip
IF !(<INSTRUMENT BATTLE>)
 sysmessagered Voce nao tem um instrumento
 return 1
endif
IF (<f_isDisturbed>) || (!<IsOnline>) || (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 sysmessagered Voce foi perturbado
 emotered Para de cantar
 skill_Inuse 0
 events -e_no_attack
 events -e_no_dclick
 return 1
ENDIF

OBJ=<UID>
LOCAL.r=<BETWEEN 1,18,<musicianship>,120.0>
FORCHARS <LOCAL.r>
if (<belltest <obj.musicianship>, 110.0>)
    local.f = 4
elif (<belltest <obj.musicianship>, 90.0>)
    local.f = 3
elif (<belltest <obj.musicianship>, 75.0>)
    local.f = 2
else
    local.f = 1
endif
 IF (!<IsMyEnemy <OBJ>>) && ((<IsMyMaster <OBJ>>) || (<IsPlayer>)) && (!<RESTEST i_mry_music_regen>)
  EFFECT_BARDSONG_RUN <local.f>
 ENDIF
ENDFOR

TIMERF 3,_BARDSONG_RUN 

[FUNCTION EFFECT_BARDSONG_RUN]
newitem i_mry_music_run
new.link <obj.uid>
new.more1 3
new.more2 <argn1>
trysrc <uid> src.new.equip

[ITEMDEF i_mry_music_run]
ID=02080
name=A Grande Marcha
type=t_eq_script
layer=layer_special
TEVENTS=e_BuffStats

ON=@Create
COLOR=05d
ATTR=attr_blessed2
  
on=@equip
cont.speedmode=1
timer=1
cont.sysmessageyellow a cancao lhe empolga e deixa mais rapido!

on=@timer
if (<more1><=0) || (<cont.distance <link>> > 10)
 remove
 cont.speedmode=0
endif
more1 -= 1
if (<cont.stam> < <cont.maxstam>)
    cont.stam += <more2>
endif
timerd 5
return 1

//***********************************************************************
//                 GALHOFA
//***********************************************************************
[FUNCTION BARDSONG_RISO]
IF !(<INSTRUMENT HAPPY>)
 sysmessagered O Instrumento precisa estar na sua mochila.
 return 1
endif
emoteyellow Entoa uma cancao de humor
TIMERF <BETWEEN2 3,6,<musicianship>,100.0>,_BARDSONG_RISO

[FUNCTION _BARDSONG_RISO]
IF (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 RETURN 1
ENDIF
LOCAL.r=<BETWEEN 1,18,<musicianship>,70.0>
OBJ=<UID>
FORCHARS <LOCAL.r>
    IF (<CanSeeLos <OBJ>>) && (!<TAG.vendor_type>) && (!<IsTEvent.e_bank>) && (<UID> != <obj>)
        newitem i_mry_music_riso
        new.link <obj.uid>
        new.more1 (<eval <obj.musicianship>/100)>)
        new.more2 <argn1>
        trysrc <uid> src.new.equip
    ENDIF
ENDFOR

[ITEMDEF i_mry_music_riso]
ID=0209e
name=Galhofa
type=t_eq_script
layer=layer_special

ON=@Create
COLOR=05d
ATTR=attr_blessed2
  
on=@equip
timer=1
cont.sysmessageyellow Bate uma vontade incontrolável de rir!!!

on=@timer
if (<more1>>=11)
    more1=10
endif
if (<more1><=0) || (<cont.distance <link>> > 12)
    cont.say ai ai...
    remove
endif

more1 -= 1
dorand 7
    cont.rir 
    cont.rir
    cont.rir
    cont.say Rairirairararaaaa!!!
    cont.anim 32
    cont.tossir
    cont.say mas essa foi de... hahaha...
enddo

timerd 2
return 1

//***********************************************************************
//                 ATENÇÃO
//***********************************************************************
[FUNCTION BARDSONG_ATENTO]
INSTRUMENT BAD
emoteyellow Emite um som estranho que chama atencao
TIMERF <BETWEEN2 3,6,<musicianship>,100.0>,_BARDSONG_ATENTO

[FUNCTION _BARDSONG_ATENTO]
IF (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 RETURN 1
ENDIF
LOCAL.r=<BETWEEN 1,18,<musicianship>,90.0>
OBJ=<UID>
FORCHARS <LOCAL.r>
    IF (<CanSeeLos <OBJ>>) && (STRMATCH(<tag.vendor_type>,) && (!<IsTEvent.e_bank>) && (<UID> != <obj>)
        EFFECT_BARDSONG_ATENTO
    ENDIF
ENDFOR

[FUNCTION EFFECT_BARDSONG_ATENTO]
if <tag0.recemAtraido>
    sysmessagegreen Voce resiste em olhar para o bardo
    obj.sysmessagered A musica nao surte efeito em alvos atraidos recentemente
else
    if (<BELLTEST <obj.Musicianship>,20.0>) && (<R<INT>> <= <obj.int>)
        emotered olha para <obj.name>
        face <obj.uid>
        tag.recemAtraido=1
        timerf 40, tag.recemAtraido=
    endif
endif

//***********************************************************************
//                 VENTRILOQUISMO
//***********************************************************************
[FUNCTION BARDSONG_VENTRILOCO]
SYSMESSAGEYELLOW Quem voce quer dublar?
targetf _BARDSONG_VENTRILOCO

[FUNCTION _BARDSONG_VENTRILOCO]
if (!<argo>)
    sysmessagered Voce deve escolher um alvo.
    RETURN 0
ELIF (!<ARGO.ISCHAR>)||(<ARGO.NPC>==brain_animal)
    sysmessagered Seu alvo deve ser uma criatura pensante.
    RETURN 0
ELIF (<ARGO.ISGM>)
    sysmessagered Seu alvo deve ser uma criatura limitada como voce.
    RETURN 0
ELIF (<ARGO>==<UID>)
    emoteyellow toca notas terriveis
    INSTRUMENT BAD
    sysmessagered Nao funciona dublar a si <SEX mesmo/mesma>.
    RETURN 0
ELIF (!<CanSeeLos <ARGO>>)
    sysmessagered Nao consegue ver o alvo.
    RETURN 0
endif

INSTRUMENT BAD
emoteyellow tocando <act.name>
IF (!<BARDSONG_TEST>) || (<findid.i_meditation>)
 return 1
 ELSE
    newitem i_mry_music_ventriloco
    new.link <src.uid>
    new.more1 <BETWEEN 1,20,<src.musicianship>,120.0>
    argo.equip <new>
ENDIF

[ITEMDEF i_mry_music_ventriloco]
ID=020ab
name=Ventriloco
type=t_eq_script
layer=layer_special

ON=@Create
COLOR=05d
ATTR=attr_cursed2

on=@equip
cont.sysmessagreen Voce sente uma leve coceira na garganta..
timer=1

on=@timer
if (<more1><=0)
    cont.sysmessageblue Voce sente um alivio na garganta...
    remove
elif (<more1>>=20)
    more1=10
endif

if (STRMATCH(<tag.ventriloco>,)
    tag.ventriloco <link.tag.recitar>
    more1 -= 1
    timer 2
    return 1
else
    cont.say @037 <tag.ventriloco>
    cont.sysmessagered voce fala sem pensar...
    tag.ventriloco=
    link.tag.recitar=
    more1 -= 1
    timer 2
endif

return 1
    
//***********************************************************************
//***********************************************************************
//                  ITEMDEFS
//***********************************************************************
//***********************************************************************

[ITEMDEF i_reparador_instrumental]
ID=0E7A
NAME=Reparador Instrumental
TYPE=t_script
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Bard
DESCRIPTION=Reparador Instrumental
RESOURCES=1 i_barbante_algodao, 1 i_madeiraenvergada
SKILLMAKE=Carpentry 30.0

on=@create
COLOR=094

on=@DCLICK
    src.sysmessagegreen Qual instrumento deseja reparar?
    targetf f_reparador_instrumental <uid>
    return 1

[FUNCTION f_reparador_instrumental]
    if (<argo.type> != t_musical)
        src.sysmessagegreen Isso nao e um instrumento
        return 1
    elif(<argo.hits> >= <argo.tdata4>)
        src.sysmessagegreen O instrumento nao precisa de reparos.
        return 1
    else
        src.emotered Reparando o instrumento <argo.name>
        src.anim 6
        src.sfx 04A
        argo.hits = <argo.tdata4>
        uid.<argv0>.remove
        return 0
    endif


[ITEMDEF 0e9c]
DEFNAME=i_drum
NAME=tambor
TYPE=T_MUSICAL
TDATA1=
TDATA2=
TDATA4=450
WEIGHT=4
CATEGORY=MyT - Items by Professions
SUBSECTION=Bard
DESCRIPTION=Tambor
RESOURCES=1 i_madeiraenvergada, 1 i_couro_cru, 5 i_pregos
SKILLMAKE=Carpentry 45.0

[ITEMDEF 0e9d]
//Tambourine
DEFNAME=i_tambourine
NAME=pandeiro
TYPE=T_MUSICAL
TDATA1=
TDATA2=
TDATA4=300
RESOURCES=1 i_madeiraenvergada, 1 i_arame_ferro, 5 i_pregos
SKILLMAKE=Carpentry 55.0
WEIGHT=3
CATEGORY=MyT - Items by Professions
SUBSECTION=Bard
DESCRIPTION=Pandeiro

[ITEMDEF 0e9e]
//tambourine (tassle)
DEFNAME=i_tambourine_tas
NAME=meia-lua
RESOURCES=1 i_madeiraenvergada, 1 i_arame_ferro, 5 i_pregos, 1 i_bandage
SKILLMAKE=Carpentry 55.0
TYPE=T_MUSICAL
TDATA1=
TDATA2=
TDATA4=170
WEIGHT=2
CATEGORY=MyT - Items by Professions
SUBSECTION=Bard
DESCRIPTION=Meia-lua

[ITEMDEF 0eb1]
//standing harp
DEFNAME=i_HARP_LARGE
NAME=arpa
TYPE=T_MUSICAL
TDATA1=
TDATA2=
TDATA4=3000
WEIGHT=20
CATEGORY=MyT - Items by Professions
SUBSECTION=Bard
DESCRIPTION=Arpa
RESOURCES=2 i_madeiraenvergada, 3 i_arame_ferro, 5 i_pregos
SKILLMAKE=Carpentry 80.0

[ITEMDEF 0eb2]
//Lap Harp
DEFNAME=i_lyre
NAME=lira
TYPE=T_MUSICAL
TDATA1=
TDATA2=
TDATA4=1000
WEIGHT=6
CATEGORY=MyT - Items by Professions
SUBSECTION=Bard
DESCRIPTION=Lira
RESOURCES=1 i_madeiraenvergada, 3 i_arame_ferro, 5 i_pregos
SKILLMAKE=Carpentry 75.0

[ITEMDEF 0eb3]
//Lute
DEFNAME=i_lute
NAME=alaude
RESOURCES=1 i_madeiraenvergada, 3 i_arame_ferro, 5 i_pregos
SKILLMAKE=Carpentry 60.0
TYPE=T_MUSICAL
TDATA1=
TDATA2=
TDATA4=700
WEIGHT=5
CATEGORY=MyT - Items by Professions
SUBSECTION=Bard
DESCRIPTION=Alaude
DUPELIST=0eb4

[ITEMDEF 0eb4]
//lute
DUPEITEM=0eb3

//******************************************************************************
//******************************************************************************
//                  GUMP
//******************************************************************************
//******************************************************************************

[DIALOG d_book_bard]
250,150
page 0
gumppic 0 0 500
tilepic 71 5 3762
tilepic 275 8 3763
page 1
dtext 65 40 0 Mystical Tales
dtext 91 62 0 Shard
dtext 60 96 67 Livro de cancoes
dtext 103 123 67 do
dtext 91 156 67 Bardo
dtext 221 33 4 Ao tocar:
DOSWITCH <tag0.bard.action>
 dtext 282 33 2100 Nada
 dtext 282 33 2100 Entoar melodia
 dtext 282 33 2100 Cancao bardica
 dtext 282 33 2100 Recitar texto
ENDDO
dtext 222 54 2100 <f_book_bard_action_item>
button 227 79 2472 2473 1 0 11
dtext 256 83 2100 Entoar melodia
button 227 109 2472 2473 1 0 12
dtext 256 113 2100 Cancao bardica
button 227 138 2472 2473 1 0 13
dtext 256 142 2100 Recitar texto
button 235 190 2103 2104 1 0 14
tilepic 216 183 4084
dtext 261 185 32 Gerenciar textos
page 2
dtext 65 40 0 Mystical Tales
dtext 91 62 0 Shard
dtext 60 96 67 Livro de cancoes
dtext 103 123 67 do
dtext 91 156 67 Bardo
button 227 79 2472 2473 1 0 21
dtext 256 83 2100 Batalha
button 227 109 2472 2473 1 0 22
dtext 256 113 2100 Drama
button 227 138 2472 2473 1 0 23
dtext 256 142 2100 Paz
button 227 167 2472 2473 1 0 24
dtext 256 171 2100 Alegre
dtext 248 40 4 Slecione a melodia
page 3
dtext 57 38 4 Cancao bardica
dtext 39 58 2100 <DEF.BARD_P<hval <tag0.bard.page>>_NAME>
dtext 39 91 4 Mana:
dtext 80 91 2100 <EVAL <DEF.BARD_P<hval <tag0.bard.page>>_MANA>>
dtext 39 111 4 Dificuldade:
dtext 117 111 2100 <DEF.BARD_P<hval <tag0.bard.page>>_DIF>
dhtmlgump 226 69 161 136 0 1 <DEF.BFONT_DPURPLE><DEF.BARD_P<hval <tag0.bard.page>>_DESC>
dtext 266 43 67 Descricao
button 67 169 2472 2473 1 0 33
dtext 96 173 2100 Selecionar
IF (<tag0.bard.page>)
 button 0 0 501 501 1 0 31
ENDIF
button 355 0 502 502 1 0 32
page 4
dtext 65 40 0 Mystical Tales
dtext 91 62 0 Shard
dtext 60 96 67 Livro de cancoes
dtext 103 123 67 do
dtext 91 156 67 Bardo
button 227 60 2472 2473 1 0 41
dtext 256 64 2100 (Texto 1)
dtext 250 40 4 Selecione o texto
page 5
dtext 53 41 32 Gerenciar textos
button 223 64 1150 1151 1 0 60
dtext 256 64 2100 (Texto 1)
dtext 255 40 4 Remover texto
dtext 63 130 0 Adicionar texto
dtext 59 147 0 de livro ou carta
button 94 168 2472 2473 1 0 51

[DIALOG d_book_bard text]
Mystical Tales
Shard
Livro de cancoes
do
Bardo
Ao tocar:
Entoar melodia
(nome da coisa)
Entoar melodia
Cancao bardica
Recitar texto
Gerenciar textos
Mystical Tales
Shard
Livro de cancoes
do
Bardo
Batalha
Drama
Paz
Alegre
Slecione a melodia
Cancao bardica
(nome)
Mana:
(mana)
Dificuldade:
(dif)
(descricao HTML)
Descricao
Selecionar
Mystical Tales
Shard
Livro de cancoes
do
Bardo
(Texto 1)
Selecione o texto
Gerenciar textos
(Texto 1)
Remover texto
Adicionar texto
de livro ou carta

[DIALOG d_book_bard button]
ON=11
// select: melodia
DIALOG d_book_bard 2

ON=12
// select: cancao
IF (<musicianship><50)
    sysmessagered Nada encontrado...
    DIALOG d_book_bard 1
ELSE
    DIALOG d_book_bard 3

ON=13
// select: recitar
say @26 <tag.recitar>
bardo

ON=14
// Gerenciar textos
target selecione um texto
f_select_text
bardo

ON=21
// Entoar batalha
tag.bard.action=BARD_ACTION_PLAY
tag.bard.item=BATTLE
bardo

ON=22
// Entoar drama
tag.bard.action=BARD_ACTION_PLAY
tag.bard.item=DRAMA
bardo

ON=23
// Entoar paz
tag.bard.action=BARD_ACTION_PLAY
tag.bard.item=PEACE
bardo

ON=24
// Entoar alegre
tag.bard.action=BARD_ACTION_PLAY
tag.bard.item=HAPPY
bardo

ON=33
// Select song
tag.bard.action=BARD_ACTION_SONG
tag.bard.item=<tag0.bard.page>
sysmessagegreen Cancao bardica: <DEF.BARD_P<hval <tag0.bard.page>>_NAME>
DIALOG d_book_bard 1

ON=31
// song page -
f_book_bard_page_previous

ON=32
// Song page +
f_book_bard_page_next

ON=41
// Copy of Entoar batalha

ON=60
// remover texto 0

ON=51
// Adicionar texto


[FUNCTION f_book_bard_action_item]
IF (<tag0.bard.action>==BARD_ACTION_PLAY)
 RETURN <DEF.BARD_PLAY_<tag.bard.item>>
ELIF (<tag0.bard.action>==BARD_ACTION_SONG)
 RETURN <DEF.BARD_P<hval <tag0.bard.item>>_NAME>
ENDIF

[FUNCTION f_book_bard_page_next]
LOCAL.page=<hval <tag0.bard.page>>
WHILE (!<IsEmpty <DEF.BARD_P<hval <LOCAL.page>+1>_NAME>>)
 LOCAL.page += 1
 IF (!<IsEmpty <DEF.BARD_P<LOCAL.page>_NAME>>)
  IF (<musicianship> > <DEF.BARD_P<LOCAL.page>_SKILL>)
   tag.bard.page=<LOCAL.page>
   DIALOG d_book_bard 3
   RETURN 1
   ELSE
       sysmessagered Nada mais encontrado...
       DIALOG d_book_bard 3
       RETURN 1
  ENDIF
 ENDIF
ENDWHILE
tag.bard.page=<LOCAL.page>
DIALOG d_book_bard 3

[FUNCTION f_book_bard_page_previous]
LOCAL.page=<hval <tag0.bard.page>>
WHILE (<LOCAL.page>)
 LOCAL.page -= 1
 IF (<LOCAL.page>)
  IF (<musicianship> > <DEF.BARD_P<LOCAL.page>_SKILL>)
   tag.bard.page=<LOCAL.page>
   DIALOG d_book_bard 3
   RETURN 1
  ENDIF
 ENDIF
ENDWHILE
IF (!<LOCAL.page>)
 tag.bard.page
else
 tag.bard.page=<LOCAL.page>
endif
DIALOG d_book_bard 3

[FUNCTION f_select_text]

sysmessageyellow Selecione um texto breve...
targetf _text_selected

[FUNCTION _text_selected]

if !(<argo.type>==t_book_scroll)
    src.sysmessagered Isto nao e um texto breve!!!
    return 1
    else
      if (<argo.topobj> != <src>) //se está fora da mochila
          src.sysmessagered Voce precisa ter o texto na mochila para recita-lo!!!
      elif (<argo.tag0.owner>==<src.uid>) //se está dentro da mochila e você é o dono
          src.tag.recitar = <argo.tag.text>
          src.sysmessageyellow Agora, voce vai recitar o seguinte trecho:
          sysmessageblue <src.tag.recitar>

[EOF]

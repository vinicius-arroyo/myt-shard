//*****************************************************************************
//*****************************************************************************
//
// SKILL HEALING 
//
//	Finnegan
//	UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:

// Bandit (AKA Banditation)
// - Healer deve estar ao lado do ferido
// - Ferido deve estar ferido...
// - Ferido nao pode estar sendo curado por outro healer
//SE FERIDO ESTIVER VIVO:
// - Healer cura de 1 a 7 hits por segundo dependendo do skill, durante 2 a 12 
//		segundos.
//SE FERIDO ESTIVER DESMAIADO:
// - Healer cura de 1 a 4 hits por segundo dependendo do skill, durante 10 a 20 
//		segundos.
// - Ambos healer e ferido devem permanacer parados
// - Ambos nao podem estar em warmode
// - Ambos nao podem receber ataque algum

// Possiveis alteracoes futuras:
//   Healer nao poder estar segurando nada (escudo, arma, lanterna...)

// Atributos da memory
// MORE1  - hitpoints que poderao ser curados
// MORE2  - dificuldade de uso da skill healing
// MOREX  - indicador de falha (1-agressao)
// MOREY  - se a bandagem esta sendo aplicada na propria pessoa

//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_bandagem
//*****************************************************************************
[TYPEDEF t_bandagem]
ON=@DCLICK
if ( <src.uid> != <topobj.uid> )
	src.sysmessageyellow A bandagem precisa estar na sua mochila.
else
	src.sysmessageyellow Que paciente deseja tratar?
	target 
endif
return 1

ON=@TARGON_ITEM
if (<src.targ.type>==t_corpse) && (<src.targ.link.isplayer>)
	src.f_healing <src.targ.link>
else
	src.sysmessageyellow Utilize a bandagem em pessoas.
endif
return 1
	
ON=@TARGON_CHAR
if (<src.targ.isplayer>)
	src.f_healing <src.targ>
else
	src.sysmessageyellow Utilize a bandagem em pessoas.
endif
return 1

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// e_bandagem
//*****************************************************************************
[EVENTS e_bandagem]
ON=@GETHIT
	IF ( 0<TAG.BANDMEM> )
		UID.<TAG.BANDMEM>.MOREX=1
	ELSE
		EVENTS -e_bandagem
	ENDIF

ON=@ITEMDCLICK
	if (<findid.i_mry_bandagem>)
		SYSMESSAGERED Voce nao pode fazer outra coisa durante o tratamento.
		return 1
	else
		events -e_bandagem
	endif
	
[FUNCTION f_healing]
obj=<argv0>
if !(<obj.flags>&statf_dead) && ( <distance <obj>> > 1 )
		sysmessagered Paciente esta muito longe.
		return 1
elif ( <obj.hits> >= <obj.str> )
		sysmessagered O paciente nao precisa de tratamento.
		return 1
ELIF ( <obj.isevent.e_mortalStrike>)
		sysmessagered O paciente recebeu um ataque mortal!
		obj.sysmessagered Voce não pode ser curado! Recebeu um ataque mortal!
		return 1
elif ( <obj.restest 1 i_mry_bandagem> )
		sysmessagered Este paciente ja esta sendo tratado.
		return 1
elif ( !<restest 1 i_bandage> )
		sysmessagered A bandagem parece nao estar mais na sua mochila.
		return 1
endif

consume 1 i_bandage
serv.newitem i_mry_bandagem
if (<obj.flags>&statf_dead) 
	new.more1=<r10,<eval 10+(<healing>/100)>> //10 a 20 segundos
	new.more2=<eval 1+(<healing>/300)>	//1 a 4 hits de healing
else
	new.more1=<r2,<eval 2+(<healing>/100)>> //2 a 12 segundos
	new.more2=<eval 2+(<healing>/120)>	//2 a 10 hits de healing
	if ( <uid> == <obj> )
			new.morey=1
	else
			new.morey=0
	endif
endif
new.morex=0
new.link=<uid>
new.timer=1
new.cont=<obj>
sysmessagegreen Voce comeca a tratar os ferimentos.
events +e_bandagem
tag.bandmem=<new.uid>
tag.bandx=<src.p.x>
tag.bandy=<src.p.y>

if ( !<new.morey> )
		obj.sysmessagegreen Seus ferimentos estao sendo tratados.
		obj.events +e_bandagem
		obj.tag.bandmem=<new.uid>
		obj.tag.bandx=<obj.p.x>
		obj.tag.bandy=<obj.p.y>
endif

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_bandage
//*****************************************************************************
[ITEMDEF 0e21]
DEFNAME=i_bandage
NAME=bandage%ns/m
RESOURCES=i_cloth
TYPE=t_bandagem
WEIGHT=.1
DUPELIST=0ee9
CATEGORY=MyT - Items by Professions
SUBSECTION=Healer
DESCRIPTION=Clean Bandages

//*****************************************************************************
// i_mry_bandagem
//*****************************************************************************
//link: curador
//cont: char sendo curado
[ITEMDEF i_mry_bandagem]
NAME=Com bandagens
TYPE=t_eq_script
LAYER=layer_special

ON=@CREATE
    ATTR=attr_decay

ON=@TIMER
	if (<link.p.x>!=<link.tag.bandx>) || (<link.p.y>!=<link.tag.bandy>) || ( !(<cont.flags>&statf_dead) && ( (<cont.p.x>!=<cont.tag.bandx>) || (<cont.p.y>!=<cont.tag.bandy>) ) )
		if ( !<morey> )
			cont.sysmessagered Deve-se permanecer parado durante o tratamento.
		endif
		link.sysmessagered Deve-se permanecer parado durante o tratamento.
	elif ( <morex>==1 )
		if ( !<morey> )
			link.sysmessagered Um ataque interrompeu o tratamento.
		endif
		cont.sysmessagered Um ataque interrompeu o tratamento.
	elif ( (<link.flags> & statf_war) && (!<morey>)  )
		link.sysmessagered Voce precisa relaxar para tratar o paciente.
	elif ( <cont.flags> & statf_war )
		if ( !<morey> )
			link.sysmessagered O paciente esta muito agitado.
		endif
		cont.sysmessagered Voce esta muito agitado.
	elif ( ((<link.flags> & statf_stone) || (<link.flags> & statf_dead)) && !<morey> )
		cont.sysmessagered Quem estava lhe tratando perdeu a consciencia.
	elif (<cont.flags>&statf_dead) && (!<cont.findid.i_mry_desmaio>)
		link.sysmessagered Este corpo nao pode retornar a vida
	elif ( <cont.hits> < <cont.maxhits> )
		link.skill_gain skill_healing

		dorand 9
			link.anim 16
			sfx 04f
			link.anim 19
			sfx 04f
			link.emoteyellow aplicando curativos
			link.anim 16
			sfx 04f
			sfx 04f
			link.anim 19
		enddo

		cont.hits=<cont.hits>+<r1,<more2>>
		if (<cont.hits>>=<cont.maxhits>)
			cont.hits=<cont.maxhits>
		else
			more1=<more1>-1
			if ( <more1> )
				timer=1
				return 1
			endif
		endif
  endif

	if (!<more1>)
	
		//se alvo estiver envenenado e passar em um teste de healing
		if (<cont.flags>&statf_poisoned)
			if (<R600,1000> < <link.healing>)
				//cura
				cont.spelleffect s_cure,<link.healing>
				if ( !<morey> )
					link.sysmessagegreen Voce retirou parte do veneno do paciente.
				endif
			else
				if ( !<morey> )
					link.sysmessagegreen Voce falhou au tentar lidar com o veneno.
				endif
			endif
		else
			if ( !<morey> )
				link.sysmessagegreen Voce termina de tratar o paciente.
			endif
			cont.sysmessagegreen Voce nao esta mais sendo tratado.		
		endif
		
		//remove desmaio
		cont.findid.i_mry_desmaio.more1 = 0  
		cont.findid.i_mry_desmaio.timerd = 1
	endif
	
	link.events -e_bandagem
	link.tag.bandmem=
	cont.events -e_bandagem
	cont.tag.bandmem=
	
	//nem sempre devolve bandagem suja
	if ( <r1,10><5 )
		serv.newitem i_bandage_bloody
		new.name bandagem suja
		new.cont=<link>
	endif

	remove
return 1

[EOF]
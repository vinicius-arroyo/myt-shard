//////////////////////////////////////////////////////////////////
//			Mystical tales Shard			//
//		    Events de NPCs ligados a AI			//
//			Galthar 07/2007				//
//								//
//								//
//////////////////////////////////////////////////////////////////

//Definições de poderes de monstros
[DEFNAME  MONSTERPOW]
MONSTERPOW_MANADRAIN_FAR	01
MONSTERPOW_MANADRAIN		02
MONSTERPOW_MEDUSA		04
MONSTERPOW_CHILL		08
MONSTERPOW_LIFEDRAIN		010

//Definições de santuários de NPCs (deixa eles mais fortes)
[DEFNAME  SANCTUARY]
SANCTUARY_UNDEAD		01
SANCTUARY_DEAMON		02
SANCTUARY_EXTRAPLANAR		04

[EVENTS e_PvM]
//CAHRGE - NPCs que podem correr, correm contra o player.
on=@NPCActFight
if (<CAN>&MT_RUN)
 if (<magery> == 0) && (<distance <src.uid>> > 2) && (<NPCCanCharge>)
  f_NPC_AI_Charge 1
 elif (<magery> == 0) && (<distance <src.uid>> < 2) && (<tag0.charge>==1)
  f_NPC_AI_Charge 2
 endif
endif
SETMEMORY <DEF.memory_fight>,<SRC.UID>

//////////////////////////////
//	PODERES DE MONTROS (a distância)
//////////////////////////////
IF (<NPCCanPower>) && (<tag0.MONSTERPOW>)
//Mana Drain Far
//Drena mana de todos os players da tela
 if (<tag0.MONSTERPOW>&<DEF.MONSTERPOW_MANADRAIN_FAR>) && (RAND(<eval <INT>*250>) < <FOCUS>)
  f_NPC_AI_PowerDelay 3
  f_NPC_POW_Manadrain_Far
 endif
ENDIF//////////////FIM PODERES DE MONSTROS


on=@Hit
SETMEMORY <DEF.memory_iaggressor>,<SRC.UID>
IF (<CAN>&MT_USEHANDS)
 //Tenho poção de stam? Preciso usar? Posso usar?
 if (<restest 1 i_bottle_red>) && (<stam> < <eval <maxstam>/3>) && (<NPC>!=brain_undead) && (RAND(<INT>) > 25)
  LOCAL.timer=<eval {1 3}>
  timerf <LOCAL.timer>,f_NPC_AI_Use,i_bottle_red,1
 endif
ENDIF

//////////////////////////////
//	PODERES DE MONTROS (de perto)
//////////////////////////////
IF (<NPCCanPower>) && (<tag0.MONSTERPOW>)
//Chill
//Efeito de clima frio. Talvez congele.
 if (<tag0.MONSTERPOW>&<DEF.MONSTERPOW_LIFEDRAIN>) && (RAND(<eval <INT>*110>) < <FOCUS>)
  f_NPC_AI_PowerDelay 4
  hits=<hits>+{{0 <argn1>} <argn1>}
  emotered suga
  src.sysmessageorange <name> esta sugando sua vida.
 elif (<tag0.MONSTERPOW>&<DEF.MONSTERPOW_CHILL>) && (RAND(<eval <INT>*100>) < <FOCUS>)
  f_NPC_AI_PowerDelay 6
  src.f_NPC_POW_Chill <Focus>
 elif (<tag0.MONSTERPOW>&<DEF.MONSTERPOW_MANADRAIN>) && (RAND(<eval <INT>*90>) < <FOCUS>)
  f_NPC_AI_PowerDelay 6
  //FAZER///////FAZER////////FAZER////////FAZER////////FAZER//////
 endif
ENDIF//////////////FIM PODERES DE MONSTROS



on=@GetHit
SETMEMORY <DEF.memory_harmedby>,<SRC.UID>
IF (<CAN>&MT_USEHANDS)
 //Tenho poção de heal? Preciso usar? Posso usar?
 if (<restest 1 i_bottle_yellow>) && (<hits> < <eval <maxhits>/3>) && (<NPC>!=brain_undead) && (RAND(<INT>) > 25)
  LOCAL.timer=<eval {1 3}>
  timerf <LOCAL.timer>,f_NPC_AI_Use,i_bottle_yellow,1
 //Tenho poção de cure? Preciso usar? Posso usar?
 elif (<restest 1 i_bottle_orange>) && (<ispoisoned>) && (<NPC>!=brain_undead) && (RAND(<INT>) > 25)
  LOCAL.timer=<eval {1 3}>
  timerf <LOCAL.timer>,f_NPC_AI_Use,i_bottle_orange,1
 //Tenho poção de str? Preciso usar? Posso usar?
 elif (<restest 1 i_bottle_white>) && (<src.str> > <str>) && (RAND(<INT>) > 25)
  LOCAL.timer=<eval {1 3}>
  timerf <LOCAL.timer>,f_NPC_AI_Use,i_bottle_white,1
 endif
ENDIF


[FUNCTION f_NPC_AI_Use]
findid.<argv0>.use
if (<argv1>)
 consume 1 <argv0>
endif

[FUNCTION f_NPC_AI_Charge]
if (<argn>==1)
 moddex=<moddex>+100
 tag.HasCharge=1
 tag.Charge=1
 LOCAL.timer=<eval {30 60}>
 timerf <LOCAL.timer>,f_NPC_AI_Charge
elif (<argn>==2)
 moddex=<moddex>-100
 tag.charge=0
else
 tag.HasCharge=0
 if (<tag0.charge>)
  tag.charge=0
  moddex=<moddex>-100
 endif
endif


[FUNCTION NPCCanCharge]
if (<tag0.HasCharge>==1)
 return 0
else
 return 1
endif

[FUNCTION MONSTER_POWER]
tag0.MONSTERPOW=<eval <tag0.MONSTERPOW>|<argv0>>

[FUNCTION f_NPC_AI_PowerDelay]
if (<argn>)
 tag0.HasPower=1
 timerf <argn>,f_NPC_AI_PowerDelay
else
 tag0.HasPower
endif

[FUNCTION NPCCanPower]
if (<tag0.HasPower>==1)
 return 0
else
 return 1
endif

[FUNCTION f_NPC_POW_Manadrain_Far]
LOCAL.mana=<eval {5 <f_rangeValue 0,25,<Focus>>}>
LOCAL.range=<f_rangeValue 6,16,<Focus>>
OBJ=<UID>
LOCAL.did=0
FORPLAYERS 6
 IF (!<IsGM>)
  IF (<mana> > <LOCAL.mana>)
   mana=<mana>-<LOCAL.mana>
  ELSE
   mana=0
  ENDIF
  effect 3 i_fx_sparkle_2 0 16 0 01f
  OBJ.mana=<obj.mana>+<LOCAL.mana>
  cry
  sysmessagered Sua energia esta sendo sugada!
  LOCAL.did=1
 ENDIF
ENDFOR
IF (<LOCAL.did>)
 effect 3 i_fx_heal_effect 0 16 0
 emoteyellow suga
 sfx 0209
ENDIF


[FUNCTION f_NPC_POW_Chill]
LOCAL.skill=<eval {0 <argv0>}>
IF (<LOCAL.SKILL> <= 600)
 sysmessagegreen Voce esta sentindo frio.
 emotegreen frio
 LOCAL.losestam=<eval {1 2}>
ELIF (<LOCAL.skill> <= 950)
 sysmessagegreen VOCE ESTA QUASE CONGELANDO DE FRIO!
 emotegreen muito frio
 LOCAL.losestam=<eval {2 4}>
ELSE
 local.timer=<eval {5 <f_rangeValue 5,25,<local.skill>>}>
 emotered congelou
 stonned <local.timer>
 sysmessagegreen Voce congelou de tanto frio!
 RETURN 1
ENDIF
IF (<stam> > 3) && (<stam> > <LOCAL.losestam>)
 stam=<stam>-<LOCAL.losestam>
ENDIF
damage <LOCAL.losestam> dam_cold
RETURN 0

[EVENTS e_Undead]
on=@NPCLookAtChar
if (<src.restest 1 i_spl_death_mask>)
	return 1
endif


on=@NPCActFight
if (<src.restest 1 i_spl_death_mask>)
	return 1
endif

ON=@EnvironChange
IF (<region.tag0.sanctuary>==SANCTUARY_UNDEAD)
 //Estou em casa!
 IF (!<tag0.sanctuary>)
  //Faça-me forte!
  tag.lighthits=<maxhits>
  tag.lightmana=<maxmana>
  tag.lightstam=<maxstam>
  maxhits = (<maxhits>*<R15,30>)/10
  maxstam = (<maxstam>*<R15,50>)/10
  maxmana = (<maxmana>*<R15,35>)/10
  aid
  tag.sanctuary 1
 ENDIF  
ELIF (<var0.thour> > 5) && (<var0.thour> < 19) && !(<flags>&statf_indoors) && !(<REGION.FLAGS>&region_flag_underground)
 //Está claro aqui. Isso vai doer.
 IF (<eval <maxhits>+<maxmana>+<maxstam>> < 250) && (!<tag0.lighthits>)
  //sou fraquinho. Me mate.
  findlayer.21.remove
  suicide
 ELIF (!<tag0.lighthits>)
  //sou fortinho, penalize-me
  tag.lighthits=<maxhits>
  tag.lightmana=<maxmana>
  tag.lightstam=<maxstam>
  maxhits = (<maxhits>*10)/<R15,30>
  maxstam = (<maxstam>*10)/<R15,50>
  maxmana = (<maxmana>*10)/<R15,35>
  flags |= statf_conjured
  aid
 ENDIF
ELSE
 //Não estou em casa, mas ainda estou confortável
 IF (<tag0.lighthits>)
  maxhits=<tag0.lighthits>
  maxmana=<tag0.lightmana>
  maxstam=<tag0.lightstam>
  tag.lighthits=
  tag.lightmana=
  tag.lightstam=
  tag.sanctuary=
  flags ^= statf_conjured
 ENDIF
ENDIF

on=@SPELLEFFECT
if (<argn1>==4)		//Heal
 DAMAGESPELL s_harm,DAM_GOD,<argn2>,<src.UID>
 return 1
ELIF (<argn1>==17)	//bless
 IF (<eval {0 <src.int>}+20> > <int>)
  sfx <eval {028 1 0206 1 0207 1}>
  remove
  RETURN 1
 ELSE
  DAMAGESPELL s_Bless,dam_god,<src.tactics>,<src.uid>
  DORAND 4
   emotered zonzo
   emotered assustado
  ENDDO
 ENDIF
ENDIF

[EVENTS e_NPC_immune]
on=@GetHit
//ARGN2 TIPO
IF (<argn2>&<TAG0.IMMUNE>)
 DORAND 2
  src.sysmessageorange <name> parece ser imune a este tipo de ataque.
  emotered ileso
 ENDDO
 return 1
endif

[EVENTS e_NPC_aggravate]
on=@GetHit
//ARGN2 TIPO
IF (<argn2>&<TAG0.AGGRAVATE>)
 DORAND 2
  act.sysmessageorange <name> parece muito assustado com este tipo de ataque.
  emotered dor
 ENDDO
endif

[EVENTS e_reaper]
on=@gethit
IF (<hits> < <eval <str>/4>) && (!<tag0.meta>)
 tag.meta=1
 IF (!<eval {0 3}>)
  serv.newnpc c_reaper_redux
 elif (!<eval {0 2}>)
  serv.newnpc c_treefellow
 else
  return 2
 endif
 new.p=<p>
 new.update
 new.effect 3 i_fx_sparkle 0 32 0 
 new.sfx 01e7
 new.emotered metamorfa
 new.hits=(<new.hits>/4)
 new.attack <src.uid>
 src.attack <new.uid>
 remove
 return 1
ENDIF

[EVENTS e_NPC_regendam]
on=@GetHit
//ARGN2 TIPO
IF (<argn2>&<TAG0.REGENDAM>)
 DORAND 2
  act.sysmessageorange <name> parece melhor agora do que antes do ataque.
  emotered aliviado
 ENDDO
endif

[FUNCTION NPC_IMMUNE]
tag0.immune <eval <argv0>>
EVENTS +e_NPC_immune

[FUNCTION NPC_AGGRAVATE]
tag0.aggravate <eval <argv0>>
EVENTS +e_NPC_aggravate

[FUNCTION NPC_REGENDAM]
tag0.regendam <eval <argv0>>
EVENTS +e_NPC_regendam

[EVENTS e_changeling]
//NPC que se transforma no player que avistar
on=@NPCActFight
if (!<src.npc>)
 NPC_CHANGELING <src.uid>
endif

on=@Death
FOR layer 0 25
 findlayer.<local.layer>.remove
ENDFOR
body=<obody>

[FUNCTION NPC_CHANGELING]
obj=<argv0>
obody=<body>
body=<obj.body>
str=<obj.str>
hits=<obj.str>
dex=<obj.dex>
int=<obj.int>
mana=<obj.int>
color=<obj.color>
for layer 1 24
 findlayer.<local.layer>.remove
 local.item=<obj.findlayer.<local.layer>.baseid>
 local.color=<obj.findlayer.<local.layer>.color>
 local.mor=<obj.findlayer.<local.layer>.more1>
 local.mor2=<obj.findlayer.<local.layer>.more2>
 serv.newitem=<local.item>
 new.attr=010
 new.color=<local.color>
 new.more1=<local.mor>
 new.more2=<local.mor2>
 new.cont=<uid>
ENDFOR
DORAND 5
 say=@026 So pode haver um de mim!
 say=@026 Ah! Impostor! Morra!
 say=@026 Ola, mim! Morra!
 say=@026 Conheco alguem identico a voce...
 say=@026 Como se atreve a me imitar! So pode haver um!
enddo
FOR skill 0 54
 <local.skill>=<src.<local.skill>>
ENDFOR
if (<magery> > 500)
 MODINT=60
 EVENTS +e_mage_phaser
endif
attack <src.uid>


[EVENTS e_caster_warrior]
on=@GetHit
if !(RAND(11))
 if (<magery>==0)
  magery=<tag0.Omagery>
 else
  tag.Omagery=<magery>
  magery=0
 endif
endif

[EVENTS e_spl_planta_carnivora]
on=@death
FORITEMS 3
 IF (<baseid>==i_forbiden)
  remove
 ENDIF
ENDFOR

[EVENTS e_paroxysmus]
on=@SpellCast
if (<NPC_CanCast>)
 OBJ=<MEMORYFINDTYPE.memory_war_targ.link>
 if (!<obj.cansee>)
  return 1
 endif
 local.meat=<eval {01e88 1 01e89 1 01e90 1 01e91 1 03d69 1 01ce0 1 01ce8 1 01d9f 1 01dad 1}>
 local.dam=<eval {5 15}>
 OBJ.effect 0 <local.meat> 10 16 0
 obj.damage <local.dam> dam_physical <uid>
 sfx 0232
 serv.newitem <local.meat>
 new.p=<obj.p>
 new.attr=012
 new.timer 5
 anim 5
 SPELL_DELAY <eval {6 32}>
endif
return 1

[FUNCTION f_restoreMana]
mana=<int>


/////////////////////////////////////////////////////////
//////////Kork, o Ciclope GM
/////////////////////////////////////////////////////////

[ITEMDEF i_stone_hurl]
ID=i_rock_plain_x
TYPE=t_script
NAME=pedras
FLIP=0

on=@create
attr |= attr_newbie

on=@DClick
target
return 1

ON=@TARGON_ITEM
target
return 1

ON=@TARGON_GROUND
target
return 1

ON=@TARGON_CHAR
if (<src.NPC_CanCast>)
 src.anim 12
 src.targ.effect 0 i_rock_7 6 0 0
 src.targ.damage <eval {5 12}> dam_physical <src.uid>
 src.spell_delay 7
endif
target
return 1

[EVENTS e_kork]
on=@death
if (!<isplayer>)
 return 0
endif
flags=<flags>|(statf_dead|statf_invisible|statf_invul|statf_insubstantial)
flags=<flags>&~statf_war

if (<tag0.comcapuz>)
	capuz
endif
serv.newitem i_corpse
new.amount=<body>
new.morep=<body>,0,<dir>
new.link=<uid>
new.more2=<uid>
new.color=<color>
new.p=<p>
for layer 1 24
	obj=<findlayer.<local.layer>>
	if (<obj>)
		obj.cont=<new>
		obj.layer=<local.layer>
	endif
endfor
new.timer=10
new.update
new.emotered desmaiou
tag.corpse=<new>

//body de morto
body=<eval c_man+2>
serv.newitem i_deathshroud
new.cont=<uid>
update

forclients
	update
endfor
sysmessageyellow 30 segundos de molho.
timerf 30,f_returnToLife
timerf 30,hits,5
timerf 30,stam,1
return 1

/////////////////////////////////////////////////////////
//////////Reis mortos de Luyaran
/////////////////////////////////////////////////////////
[EVENTS e_king_undead]
on=@HITTRY
dorand 8
 SAY=@026 Liberte-me!
 SAY=@026 Nao quero mais machucar meu povo!
 SAY=@026 Fuja! Nao quero machuca-lo, fuja!
enddo
SFX=<hval {0482 0486}>


[FUNCTION f_king_give_axe]
wipeObject i_machado_luna
serv.newitem i_machado_luna
src.bounce <new.uid>

[FUNCTION f_king_give_crescent]
wipeObject i_crescente_mond
serv.newitem i_crescente_mond
src.bounce <new.uid>

[FUNCTION f_king_give_pike]
wipeObject i_pique_atila
serv.newitem i_pique_atila
src.bounce <new.uid>


/////////////////////////////////////////////////////////
//////////Criaturas que somem se não for noite
/////////////////////////////////////////////////////////
[EVENTS e_haunt]
on=@environchange
if (<var.thour> > 5) && (<var.thour> < 20)
 f_haut_vanish
endif
return 1

[FUNCTION f_haut_vanish]
serv.newitem i_fx_sparkle_2
new.p=<p>
new.attr=012
new.timerd 13
new.sfx 05C7
remove

/////////////////////////////////////////////////////////
//////////Guardas maus
/////////////////////////////////////////////////////////
[EVENTS e_guard_bad]
ON=@NPCSEENEWPLAYER
IF (!<src.IsGM>) 
 IF (<ISEMPTY <region.tag.faction.master>>)
  dorand 4
   say=@02c Como chegou aqui?
   say=@02c Intruso! Soem o alarme!
   say=@02c Intruso! Intruso!
   say=@02c Ah! Um intruso! Morra!
  enddo
 else
  dorand 4
   say=@02c <region.tag.faction.master> deve ser protegido!
   say=@02c Intruso! Soem o alarme! Avisem <region.tag.faction.master>!
   say=@02c Intruso, <region.tag.faction.master>! Intruso!
   say=@02c Ah! Um intruso! Morra por <region.tag.faction.master>!
  enddo
endif

on=@NPCActFight
IF (!<src.IsGM>) && (!<tag0.taunt>)
 IF (<ISEMPTY <region.tag.faction.master>>)
  dorand 4
   say=@02c Nao sei como achou este lugar, mas morrera aqui!
   say=@02c Intruso imundo!
   say=@02c Conheca minha amiga morte, seu intrometido!
   say=@02c O mestre nao gosta de companhia por aqui!
  enddo
 else
  dorand 5
   say=@02c Nao sei como achou este lugar, mas <region.tag.faction.master> mandou matar intrusos!
   say=@02c Intruso imundo! <region.tag.faction.master> já sabe que voce esta aqui!
   say=@02c <region.tag.faction.master> comanda por aqui. E voce sera exterminado
   say=@02c <region.tag.faction.master> nao gosta de companhia por aqui!
  enddo
endif
tag.taunt=1
timerf <eval <R15>+10>,tag.taunt,0

[FUNCTION f_npc_faction_color]
IF (<region.tag0.faction.color>)
 serv.newitem i_cape
 new.color=<region.tag0.faction.color>
 new.name = capa da guarda
 if (!<ISEMPTY <region.tag.faction.master>>)
  new.name=<new.name> de <region.tag.faction.master>
 endif
 equip <new.uid>
ENDIF

/////////////////////////////////////////////////////////
//////////events de casting para NPCs
/////////////////////////////////////////////////////////
[FUNCTION NPC_CASTER_GARGYL]
events +e_cast_gargyl
tag.mag=4
tag.spl_terra <eval {1 2}>
tag.spl_fogo <eval {2 3}>
tag.spl_morte <eval {1 3}>

[EVENTS e_cast_gargyl]
//Magias de gargula
on=@SpellCast
if 1//(<NPC_CanCast>)// && (<argn3>==1)
 //OBJ=<MEMORYFINDTYPE.memory_war_targ.link>
 DORAND <eval <magery>/50>
  argn1=s_magic_arrow		//Magery 5.0
  argn1=s_magic_arrow
  argn1=s_magic_arrow
  argn1=s_magic_arrow
  argn1=s_stalagmite
  argn1=s_stalagmite		//Magery 30.0
  argn1=s_stalagmite
  argn1=s_firedarts
  argn1=s_firedarts
  argn1=s_firedarts		//Magery 50.0
  argn1=s_fireball
  argn1=s_fireball
  argn1=s_entangle
  argn1=s_entangle
  argn1=s_stalagmiteCell	//Magery 75.0
  argn1=s_stalagmiteCell
  argn1=s_heatWeapon
  argn1=s_fear
  argn1=s_CelaVeneno
  argn1=s_DeathConfusion	//Magery 100.0
  argn1=s_magic_arrow
  argn1=s_stalagmite
  argn1=s_firedarts
  argn1=s_fireball
  argn1=s_entangle		//Magery 125.0
  argn1=s_stalagmiteCell
  argn1=s_heatWeapon
  argn1=s_fear
  argn1=s_CelaVeneno
  argn1=s_DeathConfusion	//Magery 150.0
  argn1=s_fireball
  argn1=s_fireball
  argn1=s_fireball
  argn1=s_entangle
  argn1=s_entangle		//Magery 175.0
  argn1=s_stalagmiteCell
  argn1=s_heatWeapon
  argn1=s_fear
  argn1=s_CelaVeneno
  argn1=s_DeathConfusion	//Magery 200.0
 ENDDO
 serv.log [NPC CASTER] <serv.resources.<argn1>.name>, <argn2>
endif
return 0

on=@spellselect
return 0
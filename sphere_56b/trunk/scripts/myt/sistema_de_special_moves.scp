//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE MOVIMENTOS ESPECIAIS por
//  Galthar
//  UsMarine (vinicius.arroyo at gmail.com)
//  Alterado por: Pyrilin
//
//*****************************************************************************
//*****************************************************************************

//TAGS usadas:

//tag.specialmove   Movimento especial selecionado
//tag.specialmove_active Movimento especial ativo

//*****************************************************************************

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME sm_wpn] ////DispID Decimal separados por ';' das de todas as armas (e sua versão Flip) que tem o Special Move
SM_1_WPN=;9922;9932;5120;5121;3938;3939;5180;5181;5039;5040;9917;9927;3907;3908;3937;3936;11554;11566;3934;3935;2314;16491;5118;5119; //IgnoreAR
SM_2_WPN=;2302;16498;3719;3720;5124;5125;18622;18623;3911;3912;18608;18609;3778;3779;18606;18607;5184;5185;3909;3910;2316;16499;9914;9924;18628;18629;11552;11564;5039;5040;5114;5115;3780;3781; //Bleed attack
SM_3_WPN=;3919;3920;11556;11568;3932;3933;5112;5113;18616;18617;9920;9930;18634;18635;5178;5179;18626;18627;3721;3722;3911;3912;18608;18609;2315;16500;3936;3937;5182;5183;2303;16487;5049;5050 //Concussion blow
SM_4_WPN=;5126;5127;5178;5179;3713;3714;9916;9926;5091;5092;11569;11556;10150;10225;5176;5177;11560;11572;10146;10221 //crushing blow
SM_5_WPN=;11572;11560;11558;11570;16492;2308;5124;5125;3714;3713;3932;3933;2307;16494;3570;3571;3572;3573;5110;3907;3908;3717;3718;9934;9935; //Disarm
SM_6_WPN=;9920;9930;3570;3571;3572;3573;3917;3918;9917;9927;5043;5044;5116;5117;3719;3720; //Dismount
SM_7_WPN=;9923;9933;9919;9929;3721;3722;5186;5187;9921;9931;3915;3916;5118;5119;3717;3718;5045;5046;10153; //Double strike
SM_8_WPN=;11565;11553;5110;5111;3921;3922;5120;5121;9918;9928;9919:9929; //Infecting
SM_9_WPN=;3909;3910;3919;9916;9926;9921;9931;5180;5181;9915;9925;5041;5042;5122;5123;2314;16491; //mortal strike
SM_10_WPN=;2305;16492;5116;5117;9922;9932;9923;9933; //Moving shot
SM_11_WPN=;5041;5042;9918;9928;3917;3918;9925;9915;9914;9924;5045;5046;5049;5050;3938;3939;3568;3569;5112;5113;4020;4021;5091;5092;2302;16498; //Paralyzing blow
SM_12_WPN=;5043;5044;5122;5123;3780;3781;11553;11565;5184;5185;3921;3922;5186;5187; //Shadow strike
SM_13_WPN=;11559;11571;3568;3569;5114;5115;5182;5183;9934;9935;5176;5177;10157;10232;2312;16501;3915;3916; //Whirlwind attack
SM_14_WPN=;10146;10221; //Riding Swipe
SM_15_WPN=;10150;10225;10148;10223;10151;10226; //Frenzied Whirlwind
SM_16_WPN=;10158;10233;11557;11569; //Block
SM_17_WPN=;10151;10226;10232;10157;10158;10233; //Defence Mastery
SM_18_WPN=;10152;10227; //Nerve Strike
SM_19_WPN=;10155;10230; //Talon Strike
SM_20_WPN=;10152;10227;10153;10228;11554;11566;10147;10222; //Feint
SM_21_WPN=;18638;18639;10155;10230;10159;10234;18636;18637;10147;10222; //Dual Weild
SM_22_WPN=;10149;10224; //Double Shot
SM_23_WPN=;10149;10224;10159;10234; //Armor Pierce
SM_24_WPN=;11558;11570;11559;11571;11561;11573;11555;11567; //Bladeweave
SM_25_WPN=;11550;11562; //Force Arrow
SM_26_WPN=;11551;11563; //Lighting Arrow
SM_27_WPN=;11552;11564;11551;11563; //Psychic Attack
SM_28_WPN=;11550;11562; //Sepent Arrow (usa o mesmo SM que Infecting)
SM_29_WPN=;11557;11569; //Force of Nature
SM_30_WPN=;2305;16492; //Infused Throw (SM nao implementado)
SM_31_WPN=;2303;16487; //Mystic Arc (SM nao implementado)

[DEFNAME special_moves]
SMO1_SKILL      600         //IgnoreAR
SMO2_SKILL      750         //Bleed attack
SMO3_SKILL      600         //Concussion blow
SMO4_SKILL      300         //crushing blow
SMO5_SKILL      800         //Disarm
SMO6_SKILL      600         //Dismount
SMO7_SKILL      750         //Double strike
SMO8_SKILL      600         //Infecting
SMO9_SKILL      700         //mortal strike
SMO10_SKILL     750         //Movig shot
SMO11_SKILL     750         //Paralyzing blow
SMO12_SKILL     500         //Shadow strike
SMO13_SKILL     800         //Whirlwind attack
SMO14_SKILL     650         //Riding Swipe
SMO15_SKILL     850         //Frenzied Whirlwind
SMO16_SKILL     800         //Block
SMO17_SKILL     800         //Defence Mastery
SMO18_SKILL     750         //Nerve Strike
SMO19_SKILL     750         //Talon Strike
SMO20_SKILL     750         //Feint
SMO21_SKILL     700         //Dual Weild
SMO22_SKILL     800         //Double Shot
SMO23_SKILL     600         //Armor Pierce
SMO24_SKILL     760         //Bladeweave
SMO25_SKILL     760         //Force Arrow
SMO26_SKILL     800         //Lighting Arrow
SMO27_SKILL     600         //Psychic Attack
SMO28_SKILL     600         //Sepent Arrow
SMO29_SKILL     800         //Force of Nature

SMO0_NAME       Nenhum
SMO1_NAME       Armor Ignore
SMO2_NAME       Bleed Attack
SMO3_NAME       Concussion Blow
SMO4_NAME       Crushing Blow
SMO5_NAME       Disarm
SMO6_NAME       Dismount
SMO7_NAME       Double Strike
SMO8_NAME       Infecting
SMO9_NAME       Mortal Strike
SMO10_NAME      Movig Shot
SMO11_NAME      Paralyzing Blow
SMO12_NAME      Shadow Strike
SMO13_NAME      Whirlwind Attack
SMO14_NAME      Riding Swipe
SMO15_NAME      Frenzied Whirldwind
SMO16_NAME      Block
SMO17_NAME      Defence Mastery
SMO18_NAME      Nerve Strike
SMO19_NAME      Talon Strike
SMO20_NAME      Feint
SMO21_NAME      Dual Weild
SMO22_NAME      Double Shot
SMO23_NAME      Armor Pierce
SMO24_NAME      Bladeweave
SMO25_NAME      Force Arrow
SMO26_NAME      Lighting Arrow
SMO27_NAME      Psychic Attack
SMO28_NAME  	Serpent Arrow
SMO29_NAME      Force of Nature
                    
SMO1_MANA       10          //IgnoreAR
SMO2_MANA       20          //Bleed attack
SMO3_MANA       30          //Concussion blow
SMO4_MANA       17          //crushing blow
SMO5_MANA       30          //Disarm
SMO6_MANA       10          //Dismount
SMO7_MANA       15          //Double strike
SMO8_MANA       17          //Infecting
SMO9_MANA       20          //mortal strike
SMO10_MANA      15          //Movig shoot
SMO11_MANA      20          //Paralyzing blow
SMO12_MANA      30          //Shadow strike
SMO13_MANA      20          //Whirlwind attack
SMO14_MANA      20          //Riding Swipe
SMO15_MANA      30          //Frenzied Whirlwind
SMO16_MANA      20          //Block
SMO17_MANA      30          //Defence Master
SMO18_MANA      25          //Nerve Strike
SMO19_MANA      10          //Talon Strike
SMO20_MANA      15          //Feint
SMO21_MANA      10          //Dual Weild
SMO22_MANA      20          //Double Shot
SMO23_MANA      10          //Armor Pierce
SMO24_MANA      19          //Bladeweave
SMO25_MANA      30          //Force Arrow
SMO26_MANA      30          //Lighting Arrow
SMO27_MANA      10          //Psichic Attack
SMO28_MANA      17          //Serpent Arrow
SMO29_MANA      30          //Force of Nature

SMO0_FUNC       eval 0              //Sem bônus algum
SMO1_FUNC       f_sm_IgnoreAR       //IgnoreAR
SMO2_FUNC       f_sm_bleed          //Bleed attack
SMO3_FUNC       f_sm_concussionBlow //Concussion blow
SMO4_FUNC       f_sm_crushingBlow   //crushing blow
SMO5_FUNC       f_sm_disarm         //Disarm
SMO6_FUNC       f_sm_dismount       //Dismount
SMO7_FUNC       f_sm_doubleStrike   //Double strike
SMO8_FUNC       f_sm_infecting      //Infecting
SMO9_FUNC       f_sm_mortalStrike   //mortal strike
SMO10_FUNC      f_sm_movingShot     //Movig shoot
SMO11_FUNC      f_sm_paralyzingBlow //Paralyzing blow
SMO12_FUNC      f_sm_shadowStrike   //Shadow strike
SMO13_FUNC      f_sm_whirlwindAttack//Whirlwind attack
SMO14_FUNC      f_sm_RidingSwipe    //Riding Swipe
SMO15_FUNC      f_sm_FenziWhirl     //Frenzied Whirlwind
SMO16_FUNC      f_sm_block          //Block
SMO17_FUNC      f_sm_DefMastery     //Defence Mastery
SMO18_FUNC      f_sm_NerveStrike    //Nerve Strike
SMO19_FUNC      f_sm_TalonStrike    //Talon Strike
SMO20_FUNC      f_sm_Feint          //Feint
SMO21_FUNC      f_sm_doubleStrike   //Dual Weild
SMO22_FUNC      f_sm_double_shot    //Double Shot
SMO23_FUNC      f_sm_IgnoreAR       //Armor Pierce
SMO24_FUNC      f_sm_Bladeweave     //Bladeweave
SMO25_FUNC      f_sm_ForceArrow     //Force Arrow
SMO26_FUNC      f_sm_LightArrow     //Lighting Arrow
SMO27_FUNC      f_sm_PsyAttack      //Psychic Attack
SMO28_FUNC      f_sm_infecting      //Serpent Arrow
SMO29_FUNC      f_sm_ForceNature    //Force of Nature

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs - Suporte ao sistema
//*****************************************************************************
//*****************************************************************************

//===================================================================================================
//================================[Verificar skills, copiado do script de combate]===================
//===================================================================================================
[FUNCTION cmb_getActSkill]
if (<action> == SKILL_WRESTLING)
    return <wrestling>
elif (<action> == SKILL_FENCING)
    return <fencing>
elif (<action> == SKILL_SWORDSMANSHIP)
    return <swordsmanship>
elif (<action> == SKILL_MACEFIGHTING)
    return <macefighting>
elif (<action> == SKILL_ARCHERY)
    return <archery>
endif
return 0


//*****************************************************************************
// f_trySpecialMove
//*****************************************************************************
//Determina se usa ou não o movimento especial
//Consome mana
//Se não pode usar, retorna 0
//Se pode usar, retorna o valor do movimento especial.
//say SMO<eval <tag0.specialmove>>_SKILL
[FUNCTION f_trySpecialMove]
IF !<tag0.specialmove> || !<BELLTEST <Tactics>, <DEF.SMO<eval <tag0.specialmove>>_SKILL>>
 return 0
ELIF <cmb_getactskill> < 900  //se skill de combate for menor que 900, ignora a ação
 return 0
ELIF (<f_consumeMana <DEF.SMO<eval <tag0.specialmove>>_MANA>>)
// sysmessagegreen Movimento especial: <DEF.SMO<eval <tag0.specialmove>>_NAME>!
 tag.specialmove_active=<tag.specialmove>
 return <tag0.specialmove>
ENDIF
return 0

//*****************************************************************************
// f_consumeMana
//*****************************************************************************
//Tenta consumir <argn> mana. Se tiver o suficiente retorna 1.
//Se não tiver, não consome e retorna 0.
[FUNCTION f_consumeMana]
IF (<IsGM>)
 return 1
ELIF (<mana> >= <argn>)
 mana -= <argn>
 return 1
ENDIF
return 0

//*****************************************************************************
// item_reequip <item>
//*****************************************************************************
[FUNCTION item_reequip]

OBJ=<argn>
REF1=<OBJ.tag0.owner>
if <OBJ> && <REF1>
    OBJ.attr &= ~attr_move_never
    REF1.equip <OBJ>
    REF1.emoteyellow pega <obj.name> do chao
//else
//    OBJ.remove
endif

//*****************************************************************************
// f_clrSpecialMove
//*****************************************************************************
//Cancela o special move.
[FUNCTION f_clrSpecialMove]
IF (<IsPlayer>)
 SENDPACKET (0bf 00 05 00 021)
ENDIF
tag.specialmove=0
tag.specialmove_active=

//*****************************************************************************
// f_smIsTrue
//*****************************************************************************
//Verifica se a arma utilizada tem o SM chamado.
[FUNCTION f_smIsTrue]
local.list=<DEF.SM_<eval <argn>>_WPN>
If !(STRMATCH(*;<weapon.dispiddec>;*, <local.list>))
    f_clrSpecialMove
	return 0
else
	return 1
endif


//*****************************************************************************
//*****************************************************************************
// FUNCTIONs - Movimentos Especiais
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// f_sm_ignoreAR
//*****************************************************************************
//Ignora a armadura do oponente
[FUNCTION f_sm_ignoreAR]
if (<f_smIsTrue 1>) || (<brain> != 0)
	emotered inbloqueavel
	sfx 02b2
	return 0
endif
//O resto é feito no e_combat

//*****************************************************************************
// f_sm_bleed
//*****************************************************************************
//Faz SRC sangrar 6 segndos perdendo primeiro 6 hits, depois 5...
[FUNCTION f_sm_bleed]
if (<f_smIsTrue 2>) || (<brain> != 0)
	IF (<src.BLOODCOLOR>==0ffff)
	 sysmessageyellow Seu oponente nao tem sangue...
	 return 0
	endif
	serv.newitem i_mry_sm_bleed
	new.more1=6
	new.link=<UID>
	src.equip <new>
	return 0
endif

[ITEMDEF i_mry_sm_bleed]
ID=0122A
TYPE=t_eq_script
name=Bleed Attack
layer=LAYER_SPECIAL

on=@equip
if (<src.restest 2 <baseid>>)
 remove
else
cont.emotered sangrando
timerd = 5
endif
return 0

on=@timer
if (!<more1>)
 remove
else
dorand 3
 cont.emotered sangrando
enddo
 cont.damage <more1> dam_god <link>
 cont.sfx 05ab
 more1 -= 1
 timerd 60
endif
return 1

//*****************************************************************************
// f_sm_concussionBlow
//*****************************************************************************
//Aumenta o dano do ataque na diferensa dos hits e mana de SRC (alvo)
//Nunca maior que 25
[FUNCTION f_sm_concussionBlow]
if (<f_smIsTrue 3>) || (<brain> != 0)
	LOCAL.dam=<QVAL <src.hits> > <src.mana> ? <eval <src.hits>-<src.mana>> : <eval <src.mana>-<src.hits>>>
	//Não sei pq QVAL não funcionou aqui. Nem evalutando.
	IF <LOCAL.dam> > 25
	 LOCAL.dam=25
	ENDIF
	sfx 04f4
	src.emotered abalado
	return <LOCAL.dam>
endif

//*****************************************************************************
// f_sm_crushingBlow
//*****************************************************************************
//Aumenta o dano em até 27 hits dependendo do tactics
[FUNCTION f_sm_crushingBlow]
if (<f_smIsTrue 4>) || (<brain> != 0)
	emotered bate forte
	sfx 051C
	return <eval <tactics>/45>
endif

//*****************************************************************************
// f_sm_disarm
//*****************************************************************************
//Desarma SRC e põe a culpa em DEFAULT
[FUNCTION f_sm_disarm]
//say <weapon.layer>
if (<f_smIsTrue 5>) || (<weapon.dispiddec> = 0) || (<brain> != 0)
	OBJ=<src.weapon>
	if (!<obj>)
	 sysmessageyellow Voce nao pode desarmar seu oponente.
	 return 0
	endif

	if <src.npc>
		OBJ.attr |= attr_move_never | attr_decay
		OBJ.timer = 100
		OBJ.tag.owner = <src>
		src.events +e_no_equip_weapon
		src.timerf 7, events -e_no_equip_weapon
	else
		OBJ.timer = 600
		OBJ.attr |= attr_decay
		OBJ.tag.owner = 
		src.events +e_no_equip_weapon
		src.timerf <BETWEEN2 1,7,<src.TACTICS>,1000>, events -e_no_equip_weapon //<BETWEEN2 7,1,<src.TACTICS>,1000>
		endif    

	OBJ.movenear <src> 1

	sfx 033
	src.emotered desarmado
endif
	
[EVENTS e_no_equip_weapon]
on=@ItemEquip
if (<act.IsWeapon>)
    sysmessagered Voce nao pode se armar ainda.
return 1 
endif


//*****************************************************************************
// f_sm_dismount
//*****************************************************************************
//Desmonta o SRC e põe a culpa em DEFAULT
[FUNCTION f_sm_dismount]
if (<f_smIsTrue 6>) || (<brain> != 0)
	if <src.findlayer.layer_horse>
	 OBJ=<src.findlayer.layer_horse.more2>
	 src.emotered cai
	 src.dismount
	 src.ctag0.nomount = <eval (<serv.time> + 200)>
	 LOCAL.dam=<R5,15>
	 OBJ.DAMAGE <local.dam> dam_god <UID>   //ferir o cavalo
	 return <LOCAL.dam> 
	endif
	sysmessageyellow Seu alvo nao esta montado...
	return 0
endif

//*****************************************************************************
// f_sm_doubleStrike
//*****************************************************************************
//Repete o dano depois de 1 segundo
[FUNCTION f_sm_doubleStrike]
if (<f_smIsTrue 7>) || (<brain> != 0)
	emotered ataque duplo
	//local.dam=<R<weapon.dam>>
	//serv.log [Ataque Duplo] D: <weapon.dam.lo>,<weapon.dam.hi> (<dlocal.dam>) - AR: <src.armor> - TOTAL: <muldiv <local.dam>,<src.armor>,100>
	//local.dam=<muldiv <local.dam>,<src.armor>,100>
	//src.timerf 1 damage,<local.dam>,dam_god,<uid>
	timerf 1,bark,2
	RETURN 0
endif

//*****************************************************************************
// f_sm_infecting //Sepent Arrow 
//*****************************************************************************
//Caso a arma esteja envenenada, envenena com skill 100.0
[FUNCTION f_sm_infecting]
if (<f_smIsTrue 8>) || (<f_smIsTrue 28>) || (<brain> != 0)
	if (<weapon.more2>!=014) && !(<weapon.dmgpoison>)
	 sysmessageyellow Sua arma nao esta envenenada...
	 return 0
	else
	 src.veneno 100.0
	endif
	sfx 0199
	return 0
endif

//*****************************************************************************
// f_sm_mortalStrike
//*****************************************************************************
//Previde o alvo de poder ser curado por certo tempo.

[FUNCTION f_sm_mortalStrike]
if (<f_smIsTrue 9>) || (<brain> != 0)
	SFX 051C
	LOCAL.time=<f_rangeValue 5,15 <tactics>>
	serv.newitem i_mry_sm_mortalStrike
	src.equip <new>
	new.timer <LOCAL.time>
	return <eval <LOCAL.time>/2>    //Aumento o dano de 2 a 7
endif

[ITEMDEF i_mry_sm_mortalStrike]
ID=0122A
TYPE=t_eq_script
name=Mortal Strike
layer=LAYER_SPECIAL

on=@equip
if (<src.restest 2 <baseid>>)
 remove
endif
cont.events +e_mortalStrike
cont.sysmessagered Voce recebeu um ataque mortal!
cont.emotered mortalmente ferido
return 0

on=@timer
cont.events -e_mortalStrike
cont.sysmessageblue Seu ferimento mortal nao atrapalha mais sua cura.
remove
return 1

//Evita poder ser curado por quaisquer meios
[EVENTS e_mortalStrike]
ON=@SPELLEFFECT
if (<argn1>==4) || (<argn1>==11)
 sysmessagered Voce não pode ser curado! Recebeu um ataque mortal!
 return 1
endif

ON=@DCLICK
if (<NPC>)
 if (<hits> < <maxhits>)
  src.sysmessagered Voce nao pode montar nele enquanto ele estiver ferido!
  return 1
 else
  events -e_mortalStrike
 endif
endif
//Testa no skill_healing se pode ou não ser tratado com bandagens.

//*****************************************************************************
// f_sm_movingShoot
//*****************************************************************************
//Permite andar atirando por 10 a 15 segundos.
[FUNCTION f_sm_movingShoot]
if (<f_smIsTrue 10>) || (<brain> != 0)
	sfx 0fA
	serv.newitem i_mry_sm_moving_shoot
	equip <new>
	new.timer <R10,15>
	return 0
endif

[ITEMDEF i_mry_sm_moving_shot]
ID=i_arrow_x
TYPE=t_eq_script
name=Moving Shot
layer=LAYER_SPECIAL
weight=0

on=@equip
if (<src.restest 2 <baseid>>)
 remove
endif
cont.flags |= statf_archercanmove
return 0

on=@timer
cont.flags &= ~statf_archercanmove
cont.sysmessagered Voce nao pode atirar em movimento.
remove
return 1

//*****************************************************************************
// f_sm_paralyzingBlow
//*****************************************************************************
//Paraliza SRC por 2 a 3 segundos e põe a culpa em DEFAULT
[FUNCTION f_sm_paralyzingBlow]
if (<f_smIsTrue 11>)|| (<weapon.dispiddec> = 0) || (<brain> != 0)
	sfx 0525
	src.stun <R2,3>
	src.emotered paralizado
	return 0
endif

//*****************************************************************************
// f_sm_shadowStrike
//*****************************************************************************
//Deixa o atacante invis
[FUNCTION f_sm_shadowStrike]
if (<f_smIsTrue 12>) || (<brain> != 0)
	sfx 051b
	flags &= ~statf_war //Tira de warmode

	//SCP de hiding.
	//act=0
	//ctag.disturb=0
	serv.newitem i_hiding
	new.cont=<uid>
endif


//*****************************************************************************
// f_sm_double_shot
//*****************************************************************************
//Atira 2 flechas e acerta ambas
[FUNCTION f_sm_double_shot]
if (<f_smIsTrue 22>) || (<brain> != 0)
	if <weapon.TAG0.OVERRIDE.AMMOTYPE>
		REF1=<findid.<weapon.TAG.OVERRIDE.AMMOTYPE>>
	else
		REF1=<findid.<weapon.TDATA3>>
	endif

	if <REF1>
		emotered atira duas vezes
		REF1.decrement
	else
		sysmessagered Nao tem mais flechas para tiro duplo
		//f_clrSpecialMove
		TAG.specialmove_active=
	endif

	return 0
endif

//*****************************************************************************
// f_sm_ForceArrow
//*****************************************************************************
//Aumenta o dano de uma flecha em até 25 dependendo do tactics
[FUNCTION f_sm_ForceArrow]
if (<f_smIsTrue 25>) || (<brain> != 0)
	emotered atira mais forte
	sfx 02b1
	return <eval <tactics>/46>
	return 0
endif

//*****************************************************************************
// f_sm_whirlwindAttack
//*****************************************************************************
[FUNCTION f_sm_whirlwindAttack]
//Dá dano em todos os chars (amigos ou não) em um raio de 1 tile
if (<f_smIsTrue 13>)
	sfx 051f
	emotered gira freneticamente
	obj=<uid>
	forchars 1
	 if (<uid>!=<obj>)
	  timerf 1 damage,<R<obj.weapon.dam>>,dam_god,<obj.uid>
	 endif
	end
endif

//*****************************************************************************
// f_sm_DefMastery
//*****************************************************************************
//Dá bonus de AR mas diminui dano da arma temporariamente. Não pode trocar de arma
[FUNCTION f_sm_DefMastery]
if (<f_smIsTrue 17>) || (<brain> != 0)
	//Primeira chamada, aplicar
	if !(0<argn>)
	 //Verifica se não há duplicidade do efeito
	 if (<tag0.sm_DefMastery>)
	  sysmessagered Esta habilidade ja esta sendo usada
	  return 0
	 else
	  tag.sm_DefMastery=1
	 endif
	 sfx 0521
	 emotered apura a defesa
	 //calcula e salva bonus de ar
	 tag.sm_bonus=<eval <tactics>/25>
	 modar += <tag.sm_bonus>        //0 a 40 de ar a + 
	 //calcula e salva penalidade de dano
	 tag.sm_onus=<eval <tactics>/100>
	 weapon.modar -= <tag.sm_onus>     //0 a 10 de penalidade de dano
	 //salva se a arma era cursed antes
	 tag.sm_cursed=<eval <weapon.attr>&0100>
	 weapon.attr |= 0100
	 //salva a arma
	 tag.sm_weapon=<weapon>
	 //calcula tempo
	 local.timer=<between 1,15,<tactics>,1000>
	 timerf <local.timer> f_sm_DefMastery,1
	 return 1
	endif
	//Segunda chamada. Retirar efeito.
	IF (0<uid.<tag.sm_weapon>.instances>)//Se a arma ainda existe
	 obj=<tag0.sm_weapon>
	 obj.modar += <tag.sm_onus>
	 tag.sm_onus=
	 if !(<tag0.sm_cursed>)//Se a arma NÃO era cursed originalmente
	  obj.attr &= ~0100
	  tag.sm_cursed=
	 endif
	ENDIF
	tag.sm_weapon=
	modar -= <tag.sm_bonus>
	tag.sm_bonus=
	tag.sm_DefMastery=
	sysmessagered O efeito de Defence Mastery passou
endif

//*****************************************************************************
// f_sm_Bladeweave
//*****************************************************************************
//Efeito imprevisível mas eficaz
[FUNCTION f_sm_Bladeweave]
if (<f_smIsTrue 24>) || (<brain> != 0)
	DORAND <eval <tactics>/100>                            //Onze coisas imprevisíveis
	 local.ret <R3,5>                                      //Bônus de 3 a 5 de dano
	 local.ret <R5,10>                                     //Bônus de 5 a 10 de dano
	 local.ret <R10,15>                                    //Bônus de 7 a 15 de dano
	 local.extra <R3,15>                                   //Dano imbloqueavel de 3 a 15
	 begin                                                 //Paralizado por 5 segundos
	  src.emotered medo
	  src.stun 5
	 end
	 begin                                                 //Ataque duplo
	  tag.specialmove_active=7
	  f_sm_doubleStrike
	 end
	 local.extra <R7,22>                                   //Dano imbloqueavel de 7 a 22
	 begin
	  src.events +e_scared
	  src.emotered apavorado
	  src.timerf 7,events -e_scared
	 end                                                   //Não luta, nem usa skill alguma por 7 segundos
	 local.ret <R15,20>                                    //Bônus de 15 a 20 no dano
	 f_sm_DefMastery                                       //Aumenta defesa e diminui dano temporariamente
	 f_sm_Bleed                                            //Sangra por 6 segundos perdendo 6! HP.
	 begin
	  sysmessageblue Ataque super critico! TADAO!
	  gritar
	  DORAND 2                      //Ataque crítico! 3,3% de chance e só para GMs em Tactics!
	   local.extra 50                               //Dano imbloqueavel 50
	   local.ret <src.hits>                         //Pois é... Morreu
	   call f_sm_TADAO                              //Okay, eu sei. É apelação total. Mas eu gosto =]
	  end
	 end
	end
	if 0<local.ret>
	 sfx 0520
	 sysmessageorange Bonus de ataque: <dlocal.ret> hits!
	 return <local.ret>
	endif
	if 0<local.extra>
	 timerf 1 sfx,0520
	 timerf 1 emotered,ataque extra
	 src.timerf 1 damage,<local.extra>,dam_god,<uid>
	endif
endif

[FUNCTION f_sm_TADAO]
//Função super apelativa para batalha
tag.specialmove_active=15                                                //dano duplo
timerf 2 f_sm_DefMastery                                                 //Aumenta defesa e diminui dano
f_sm_whirlwindAttack                                                     //Acerta tudo ao redor
serv.b=@021 [<tag.name> eh mundialmente famoso pelo seu ataque TADAO!]   //Todo mundo fica sabendo
timerf 7,sfx,0543                                                        //Sonzinho cretino pra ele ficar feliz

//*****************************************************************************
// f_sm_Feint
//*****************************************************************************
//Anula a armadura do oponente por 8 segundos
[FUNCTION f_sm_Feint]
if (<f_smIsTrue 20>) || (<brain> != 0)
	src.emotered vulderavel
	src.modar=-<src.ar>
	src.timerf 8 src.modar,<src.ar>
endif
	

//*****************************************************************************
// f_sm_PsyAttack
//*****************************************************************************
//Diminui a mana do oponente e dá leve bonus de dano
[FUNCTION f_sm_PsyAttack]
if (<f_smIsTrue 27>) || (<brain> != 0)
	src.mana /= 2
	src.emotered desconcentrado
	return <R5,12>
endif

//*****************************************************************************
// f_sm_Block
//*****************************************************************************
//Aumenta a AR por curto periodo de tmpo
[FUNCTION f_sm_Block]
if (<f_smIsTrue 16>) || (<brain> != 0)
	if !(0<argn>)
	 sfx 0520
	 tag.sm_block_bonus=<BETWEEN 10,30,<TACTICS>,1000>
	 modar += <tag.sm_block_bonus>
	 timerf 12 f_sm_Block,1
	else
	 modar -= <tag.sm_block_bonus>
	 tag.sm_block_bonus=
	endif
endif

//*****************************************************************************
// f_sm_TalonStrike
//*****************************************************************************
//Sangra por 6 segundos perdendo 24! hits ao total
[FUNCTION f_sm_TalonStrike]
if (<f_smIsTrue 19>) || (<brain> != 0)
	serv.newitem i_mry_sm_bleed
	new.more1=6
	new.link=<UID>
	src.equip <new>
	return <R3,5>
endif

//*****************************************************************************
// f_sm_FenziWhirl
//*****************************************************************************
//Dano em todos os chars a 1 tile de distância + 4! de dano over time
[FUNCTION f_sm_FenziWhirl]
if (<f_smIsTrue 15>) || (<brain> != 0)
	obj=<uid>
	forchars 1
	 if (<uid>!=<obj>)
	  timerf 1 damage,<R<obj.weapon.dam>>,dam_god,<obj.uid>
	  serv.newitem i_mry_sm_bleed
	  new.more1=3
	  new.link=<OBJ>
	  equip <new>
	 endif
	end
endif

//*****************************************************************************
// f_sm_ForceNature
//*****************************************************************************
//50 de dano poison
[FUNCTION f_sm_ForceNature]
if (<f_smIsTrue 29>) || (<brain> != 0)
	src.timerf 1 damage,27,dam_poison,<uid>
	sfx 05BE
endif

//*****************************************************************************
// f_sm_LightArrow
//*****************************************************************************
//Dano em corrente para quem está a x tiles de distância
[FUNCTION f_sm_LightArrow]
if (<f_smIsTrue 26>) || (<brain> != 0)
	if !(0<argn>)
	 src.f_sm_LightArrow <BETWEEN 10,20,<tactics>,1000>
	 src.effect,1,1,1,1
	 timerf 1,sfx,<eval <DEF.SND_THUNDER>>
	 return 0
	endif
	obj=<UID>
	forchars <argn>
	 if (<uid>!=<obj>)
	  timerf 1 damage,<R15,30>,dam_energy,<obj.uid>
	  timerf 1 effect,1,1,1,1
	 endif
	end
endif

//*****************************************************************************
// f_sm_NerveStrike
//*****************************************************************************
//Dano extra e rápida paralizia
[FUNCTION f_sm_NerveStrike]
if (<f_smIsTrue 18>) || (<brain> != 0)
	sfx 0525
	src.stun <R1,2>
	src.emotered paralizado
	return <R7,12>
endif

//*****************************************************************************
// f_sm_RidingSwipe
//*****************************************************************************
//Desmonta o SRC e põe a culpa em DEFAULT
[FUNCTION f_sm_dismount]
if (<f_smIsTrue 14>) || (<brain> != 0)
	if <src.findlayer.layer_horse>
	 OBJ=<src.findlayer.layer_horse.more2>
	 src.emotered cai
	 src.dismount
	 src.ctag0.nomount = <eval (<serv.time> + 100)>
	 LOCAL.dam=<R15,30>
	 OBJ.DAMAGE <local.dam> dam_god <UID>   //ferir o cavalo
	 return <LOCAL.dam> 
	endif
	sysmessageyellow Seu alvo nao esta montado...
	return 0
endif

[EOF]

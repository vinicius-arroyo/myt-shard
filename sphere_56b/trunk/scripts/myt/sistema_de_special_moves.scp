//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE MOVIMENTOS ESPECIAIS por
//  Galthar
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TAGS usadas:

//tag.specialmove   Movimento especial selecionado
//tag.specialmove_active Movimento especial ativo

//*****************************************************************************

//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************
[DEFNAME special_moves]
SMO1_SKILL      600         //IgnoreAR
SMO2_SKILL      750         //Bleed attack
SMO3_SKILL      600         //Concussion blow
SMO4_SKILL      300         //crushing blow
SMO5_SKILL      800         //Disarm
SMO6_SKILL      600         //Dismount
SMO7_SKILL      750         //Double strike
SMO8_SKILL      600         //Infecting
SMO9_SKILL      700         //mortal strike
SMO10_SKILL     750         //Movig shot
SMO11_SKILL     750         //Paralyzing blow
SMO12_SKILL     500         //Shadow strike
SMO13_SKILL     800         //Whirlwind attack
SMO14_SKILL     650         //Riding Swipe
SMO15_SKILL     850         //Frenzied Whirlwind
SMO16_SKILL     800         //Block
SMO17_SKILL     800         //Defence Mastery
SMO18_SKILL     750         //Nerve Strike
SMO19_SKILL     750         //Talon Strike
SMO20_SKILL     750         //Feint
SMO21_SKILL     700         //Dual Weild
SMO22_SKILL     800         //Double Shot
SMO23_SKILL     600         //Armor Pierce
SMO24_SKILL     760         //Bladeweave
SMO25_SKILL     760         //Force Arrow
SMO26_SKILL     800         //Lighting Arrow
SMO27_SKILL     600         //Psychic Attack
SMO28_SKILL     600         //Sepent Arrow
SMO29_SKILL     800         //Force of Nature

SMO0_NAME       Nenhum
SMO1_NAME       Armor Ignore
SMO2_NAME       Bleed Attack
SMO3_NAME       Concussion Blow
SMO4_NAME       Crushing Blow
SMO5_NAME       Disarm
SMO6_NAME       Dismount
SMO7_NAME       Double Strike
SMO8_NAME       Infecting
SMO9_NAME       Mortal Strike
SMO10_NAME      Movig Shot
SMO11_NAME      Paralyzing Blow
SMO12_NAME      Shadow Strike
SMO13_NAME      Whirlwind Attack
SMO14_NAME      Riding Swipe
SMO15_NAME      Frenzied Whirldwind
SMO16_NAME      Block
SMO17_NAME      Defence Mastery
SMO18_NAME      Nerve Strike
SMO19_NAME      Talon Strike
SMO20_NAME      Feint
SMO21_NAME      Dual Weild
SMO22_NAME      Double Shot
SMO23_NAME      Armor Pierce
SMO24_NAME      Bladeweave
SMO25_NAME      Force Arrow
SMO26_NAME      Lighting Arrow
SMO27_NAME      Psychic Attack
SMO28_NAME  Serpent Arrow
SMO29_NAME      Force of Nature
                    
SMO1_MANA       10          //IgnoreAR
SMO2_MANA       20          //Bleed attack
SMO3_MANA       30          //Concussion blow
SMO4_MANA       17          //crushing blow
SMO5_MANA       30          //Disarm
SMO6_MANA       10          //Dismount
SMO7_MANA       15          //Double strike
SMO8_MANA       17          //Infecting
SMO9_MANA       20          //mortal strike
SMO10_MANA      15          //Movig shoot
SMO11_MANA      20          //Paralyzing blow
SMO12_MANA      30          //Shadow strike
SMO13_MANA      20          //Whirlwind attack
SMO14_MANA      20          //Riding Swipe
SMO15_MANA      30          //Frenzied Whirlwind
SMO16_MANA      20          //Block
SMO17_MANA      30          //Defence Master
SMO18_MANA      25          //Nerve Strike
SMO19_MANA      10          //Talon Strike
SMO20_MANA      15          //Feint
SMO21_MANA      10          //Dual Weild
SMO22_MANA      20          //Double Shot
SMO23_MANA      10          //Armor Pierce
SMO24_MANA      19          //Bladeweave
SMO25_MANA      30          //Force Arrow
SMO26_MANA      30          //Lighting Arrow
SMO27_MANA      10          //Psichic Attack
SMO28_MANA      17          //Serpent Arrow
SMO29_MANA      30          //Force of Nature

SMO0_FUNC       eval 0              //Sem bônus algum
SMO1_FUNC       f_sm_IgnoreAR       //IgnoreAR
SMO2_FUNC       f_sm_bleed          //Bleed attack
SMO3_FUNC       f_sm_concussionBlow //Concussion blow
SMO4_FUNC       f_sm_crushingBlow   //crushing blow
SMO5_FUNC       f_sm_disarm         //Disarm
SMO6_FUNC       f_sm_dismount       //Dismount
SMO7_FUNC       f_sm_doubleStrike   //Double strike
SMO8_FUNC       f_sm_infecting      //Infecting
SMO9_FUNC       f_sm_mortalStrike   //mortal strike
SMO10_FUNC      f_sm_movingShot     //Movig shoot
SMO11_FUNC      f_sm_paralyzingBlow //Paralyzing blow
SMO12_FUNC      f_sm_shadowStrike   //Shadow strike
SMO13_FUNC      f_sm_whirlwindAttack//Whirlwind attack
SMO14_FUNC      f_sm_RidingSwipe    //Riding Swipe
SMO15_FUNC      f_sm_FenziWhirl     //Frenzied Whirlwind
SMO16_FUNC      f_sm_block          //Block
SMO17_FUNC      f_sm_DefMastery     //Defence Mastery
SMO18_FUNC      f_sm_NerveStrike    //Nerve Strike
SMO19_FUNC      f_sm_TalonStrike    //Talon Strike
SMO20_FUNC      f_sm_Feint          //Feint
SMO21_FUNC      f_sm_doubleStrike   //Dual Weild
SMO22_FUNC      f_sm_double_shot    //Double Shot
SMO23_FUNC      f_sm_IgnoreAR       //Armor Pierce
SMO24_FUNC      f_sm_Bladeweave     //Bladeweave
SMO25_FUNC      f_sm_ForceArrow     //Force Arrow
SMO26_FUNC      f_sm_LightArrow     //Lighting Arrow
SMO27_FUNC      f_sm_PsyAttack      //Psychic Attack
SMO28_FUNC      f_sm_infecting      //Serpent Arrow
SMO29_FUNC      f_sm_ForceNature    //Force of Nature

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs - Suporte ao sistema
//*****************************************************************************
//*****************************************************************************

//===================================================================================================
//================================[Verificar skills, copiado do script de combate]===================
//===================================================================================================
[FUNCTION cmb_getActSkill]
if (<action> == SKILL_WRESTLING)
    return <wrestling>
elif (<action> == SKILL_FENCING)
    return <fencing>
elif (<action> == SKILL_SWORDSMANSHIP)
    return <swordsmanship>
elif (<action> == SKILL_MACEFIGHTING)
    return <macefighting>
elif (<action> == SKILL_ARCHERY)
    return <archery>
endif
return 0


//*****************************************************************************
// f_trySpecialMove
//*****************************************************************************
//Determina se usa ou não o movimento especial
//Consome mana
//Se não pode usar, retorna 0
//Se pode usar, retorna o valor do movimento especial.
//say SMO<eval <tag0.specialmove>>_SKILL
[FUNCTION f_trySpecialMove]
IF !<tag0.specialmove> || !<BELLTEST <Tactics>, <DEF.SMO<eval <tag0.specialmove>>_SKILL>>
 return 0
ELIF <cmb_getactskill> < 900  //se skill de combate for menor que 900, ignora a ação
 return 0
ELIF (<f_consumeMana <DEF.SMO<eval <tag0.specialmove>>_MANA>>)
// sysmessagegreen Movimento especial: <DEF.SMO<eval <tag0.specialmove>>_NAME>!
 tag.specialmove_active=<tag.specialmove>
 return <tag0.specialmove>
ENDIF
return 0

//*****************************************************************************
// f_consumeMana
//*****************************************************************************
//Tenta consumir <argn> mana. Se tiver o suficiente retorna 1.
//Se não tiver, não consome e retorna 0.
[FUNCTION f_consumeMana]
IF (<IsGM>)
 return 1
ELIF (<mana> >= <argn>)
 mana -= <argn>
 return 1
ENDIF
return 0

//*****************************************************************************
// item_reequip <item>
//*****************************************************************************
[FUNCTION item_reequip]

OBJ=<argn>
REF1=<OBJ.tag0.owner>
if <OBJ> && <REF1>
    OBJ.attr &= ~attr_move_never
    REF1.equip <OBJ>
    REF1.emoteyellow pega <obj.name> do chao
//else
//    OBJ.remove
endif

//*****************************************************************************
// f_clrSpecialMove
//*****************************************************************************
//Cancela o special move.
[FUNCTION f_clrSpecialMove]
IF (<IsPlayer>)
 SENDPACKET (0bf 00 05 00 021)
ENDIF
tag.specialmove=0
tag.specialmove_active=

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs - Movimentos Especiais
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// f_sm_ignoreAR
//*****************************************************************************
//Ignora a armadura do oponente
[FUNCTION f_sm_ignoreAR]
emotered inbloqueavel
sfx 02b2
return 0
//O resto é feito no e_combat

//*****************************************************************************
// f_sm_bleed
//*****************************************************************************
//Faz SRC sangrar 6 segndos perdendo primeiro 6 hits, depois 5...
[FUNCTION f_sm_bleed]
IF (<src.BLOODCOLOR>==0ffff)
 sysmessageyellow Seu oponente nao tem sangue...
 return 0
endif
serv.newitem i_mry_sm_bleed
new.more1=6
new.link=<UID>
src.equip <new>
return 0

[ITEMDEF i_mry_sm_bleed]
ID=0122A
TYPE=t_eq_script
name=Bleed Attack
layer=LAYER_SPECIAL

on=@equip
if (<src.restest 2 <baseid>>)
 remove
else
 timerd = 1
endif
return 1

on=@timer
if (!<more1>)
 remove
else
 cont.emotered sangrando
 cont.damage <more1> dam_god <link>
 cont.sfx 05ab
 more1 -= 1
 timerd {8 12}
endif
return 1

//*****************************************************************************
// f_sm_concussionBlow
//*****************************************************************************
//Aumenta o dano do ataque na diferensa dos hits e mana de SRC (alvo)
//Nunca maior que 30
[FUNCTION f_sm_concussionBlow]
LOCAL.dam=<QVAL <src.hits> > <src.mana> ? <eval <src.hits>-<src.mana>> : <eval <src.mana>-<src.hits>>>
//Não sei pq QVAL não funcionou aqui. Nem evalutando.
IF <LOCAL.dam> > 30
 LOCAL.dam=30
ENDIF
sfx 04f4
src.emotered abalado
return <LOCAL.dam>

//*****************************************************************************
// f_sm_crushingBlow
//*****************************************************************************
//Aumenta o dano em até 30 hits dependendo do tactics
[FUNCTION f_sm_crushingBlow]
emotered bate forte
sfx 051C
return <eval <tactics>/33>

//*****************************************************************************
// f_sm_disarm
//*****************************************************************************
//Desarma SRC e põe a culpa em DEFAULT
[FUNCTION f_sm_disarm]
//say <weapon.layer>
OBJ=<src.weapon>
if (!<obj>)
 sysmessageyellow Voce nao pode desarmar seu oponente.
 return 0
endif

if <src.npc>
    OBJ.attr |= attr_move_never
    OBJ.timer = -1
    OBJ.tag.owner = <src>
    timerf 5 item_reequip <obj>
else
    OBJ.timer=600
    OBJ.attr |= attr_decay
    OBJ.tag.owner = 
endif    

OBJ.movenear <src> 1

sfx 033
src.emotered desarmado
return 0

//*****************************************************************************
// f_sm_dismount
//*****************************************************************************
//Desmonta o SRC e põe a culpa em DEFAULT
[FUNCTION f_sm_dismount]
if <src.findlayer.layer_horse>
 OBJ=<src.findlayer.layer_horse.more2>
 src.emotered cai
 src.dismount
 LOCAL.dam=<R5,10>
 OBJ.DAMAGE <local.dam> dam_god <UID>   //ferir o cavalo
 return <LOCAL.dam> //Aumenta o dano de 5 a 10
endif
sysmessageyellow Seu alvo nao esta montado...
return 0

//*****************************************************************************
// f_sm_doubleStrike
//*****************************************************************************
//Repete o dano depois de 1 segundo
[FUNCTION f_sm_doubleStrike]
emotered ataque duplo
//local.dam=<R<weapon.dam>>
//serv.log [Ataque Duplo] D: <weapon.dam.lo>,<weapon.dam.hi> (<dlocal.dam>) - AR: <src.armor> - TOTAL: <muldiv <local.dam>,<src.armor>,100>
//local.dam=<muldiv <local.dam>,<src.armor>,100>
//src.timerf 1 damage,<local.dam>,dam_god,<uid>
timerf 1,bark,2
RETURN 0

//*****************************************************************************
// f_sm_infecting
//*****************************************************************************
//Caso a arma esteja envenenada, envenena com skill 100.0
[FUNCTION f_sm_infecting]
IF (<weapon.more2>!=014) && !(<weapon.dmgpoison>)
 sysmessageyellow Sua arma nao esta envenenada...
 return 0
else
 src.veneno 2000
endif
sfx 0199
return 0

//*****************************************************************************
// f_sm_mortalStrike
//*****************************************************************************
//Previde o alvo de poder ser curado por certo tempo.
[FUNCTION f_sm_mortalStrike]
SFX 051C
LOCAL.time=<f_rangeValue 5,15 <tactics>>
serv.newitem i_mry_sm_mortalStrike
src.equip <new>
new.timer <LOCAL.time>
return <eval <LOCAL.time>/2>    //Aumento o dano de 2 a 7

[ITEMDEF i_mry_sm_mortalStrike]
ID=0122A
TYPE=t_eq_script
name=Mortal Strike
layer=LAYER_SPECIAL

on=@equip
if (<src.restest 2 <baseid>>)
 remove
endif
cont.events +e_mortalStrike
cont.sysmessagered Voce recebeu um ataque mortal!
cont.emotered mortalmente ferido
return 0

on=@timer
cont.events -e_mortalStrike
cont.sysmessageblue Seu ferimento mortal nao atrapalha mais sua cura.
remove
return 1

//Evita poder ser curado por quaisquer meios
[EVENTS e_mortalStrike]
ON=@SPELLEFFECT
if (<argn1>==4) || (<argn1>==11)
 sysmessagered Voce não pode ser curado! Recebeu um ataque mortal!
 return 1
endif

ON=@DCLICK
if (<NPC>)
 if (<hits> < <maxhits>)
  src.sysmessagered Voce nao pode montar nele enquanto ele estiver ferido!
  return 1
 else
  events -e_mortalStrike
 endif
endif
//Testa no skill_healing se pode ou não ser tratado com bandagens.

//*****************************************************************************
// f_sm_movingShoot
//*****************************************************************************
//Permite andar atirando por 10 a 15 segundos.
[FUNCTION f_sm_movingShoot]
sfx 0fA
serv.newitem i_mry_sm_moving_shoot
equip <new>
new.timer <R10,15>
return 0

[ITEMDEF i_mry_sm_moving_shot]
ID=i_arrow_x
TYPE=t_eq_script
name=Moving Shot
layer=LAYER_SPECIAL

on=@equip
if (<src.restest 2 <baseid>>)
 remove
endif
cont.flags |= statf_archercanmove
return 0

on=@timer
cont.flags &= ~statf_archercanmove
cont.sysmessagered Voce nao pode atirar em movimento.
remove
return 1

//*****************************************************************************
// f_sm_paralyzingBlow
//*****************************************************************************
//Paraliza SRC por 3 a 7 segundos e põe a culpa em DEFAULT
[FUNCTION f_sm_paralyzingBlow]
sfx 0525
src.stun <R3,7>
src.emotered paralizado
return 0

//*****************************************************************************
// f_sm_shadowStrike
//*****************************************************************************
//Deixa o atacante invis
[FUNCTION f_sm_shadowStrike]
sfx 051b
flags &= ~statf_war //Tira de warmode

//SCP de hiding.
act=0
ctag.disturb=0
serv.newitem i_hiding
new.cont=<uid>


//*****************************************************************************
// f_sm_double_shot
//*****************************************************************************
//Atira 2 flechas e acerta ambas
[FUNCTION f_sm_double_shot]
if <weapon.TAG0.OVERRIDE.AMMOTYPE>
    REF1=<findid.<weapon.TAG.OVERRIDE.AMMOTYPE>>
else
    REF1=<findid.<weapon.TDATA3>>
endif

if <REF1>
    emotered atira duas vezes
    REF1.decrement
else
    sysmessagered Nao tem mais flechas para tiro duplo
    //f_clrSpecialMove
    TAG.specialmove_active=
endif

return 0

//*****************************************************************************
// f_sm_ForceArrow
//*****************************************************************************
//Aumenta o dano de uma flecha em até 30 dependendo do tactics
[FUNCTION f_sm_ForceArrow]
emotered atira mais forte
sfx 02b1
return <eval <tactics>/33>
return 0

//*****************************************************************************
// f_sm_whirlwindAttack
//*****************************************************************************
[FUNCTION f_sm_whirlwindAttack]
//Dá dano em todos os chars (amigos ou não) em um raio de 1 tile
sfx 051f
emotered gira freneticamente
obj=<uid>
forchars 1
 if (<uid>!=<obj>)
  timerf 1 damage,<R<obj.weapon.dam>>,dam_god,<obj.uid>
 endif
end

//*****************************************************************************
// f_sm_DefMastery
//*****************************************************************************
[FUNCTION f_sm_DefMastery]
//Dá bonus de AR mas diminui dano da arma temporariamente. Não pode trocar de arma

//Primeira chamada, aplicar
if !(0<argn>)
 //Verifica se não há duplicidade do efeito
 if (<tag0.sm_DefMastery>)
  sysmessagered Esta habilidade ja esta sendo usada
  return 0
 else
  tag.sm_DefMastery=1
 endif
 sfx 0521
 emotered apura a defesa
 //calcula e salva bonus de ar
 tag.sm_bonus=<eval <tactics>/29>
 modar += <tag.sm_bonus>        //0 a 33 de ar a + 
 //calcula e salva penalidade de dano
 tag.sm_onus=<eval <tactics>/100>
 weapon.modar -= <tag.sm_onus>     //0 a 10 de penalidade de dano
 //salva se a arma era cursed antes
 tag.sm_cursed=<eval <weapon.attr>&0100>
 weapon.attr |= 0100
 //salva a arma
 tag.sm_weapon=<weapon>
 //calcula tempo
 local.timer=<between 1,15,<tactics>,1000>
 timerf <local.timer> f_sm_DefMastery,1
 return 1
endif

//Segunda chamada. Retirar efeito.
IF (0<uid.<tag.sm_weapon>.instances>)//Se a arma ainda existe
 obj=<tag0.sm_weapon>
 obj.modar += <tag.sm_onus>
 tag.sm_onus=
 if !(<tag0.sm_cursed>)//Se a arma NÃO era cursed originalmente
  obj.attr &= ~0100
  tag.sm_cursed=
 endif
ENDIF
tag.sm_weapon=
modar -= <tag.sm_bonus>
tag.sm_bonus=
tag.sm_DefMastery=
sysmessagered O efeito de Defence Mastery passou

//*****************************************************************************
// f_sm_Bladeweave
//*****************************************************************************
[FUNCTION f_sm_Bladeweave]
//Efeito imprevisível mas eficaz
DORAND <eval <tactics>/100>                            //Onze coisas imprevisíveis
 local.ret <R3,5>                                      //Bônus de 3 a 5 de dano
 local.ret <R5,10>                                     //Bônus de 5 a 10 de dano
 local.ret <R10,15>                                    //Bônus de 7 a 15 de dano
 local.extra <R3,15>                                   //Dano imbloqueavel de 3 a 15
 begin                                                 //Paralizado por 5 segundos
  src.emotered medo
  src.stun 5
 end
 begin                                                 //Ataque duplo
  tag.specialmove_active=7
  f_sm_doubleStrike
 end
 local.extra <R7,22>                                   //Dano imbloqueavel de 7 a 22
 begin
  src.events +e_scared
  src.emotered apavorado
  src.timerf 7,events,-e_scared
 end                                                   //Não luta, nem usa skill alguma por 7 segundos
 local.ret <R15,20>                                    //Bônus de 15 a 20 no dano
 f_sm_DefMastery                                       //Aumenta defesa e diminui dano temporariamente
 f_sm_Bleed                                            //Sangra por 6 segundos perdendo 6! HP.
 begin
  sysmessageblue Ataque super critico! TADAO!
  gritar
  DORAND 2                      //Ataque crítico! 3,3% de chance e só para GMs em Tactics!
   local.extra 50                               //Dano imbloqueavel 50
   local.ret <src.hits>                         //Pois é... Morreu
   call f_sm_TADAO                              //Okay, eu sei. É apelação total. Mas eu gosto =]
  end
 end
end
if 0<local.ret>
 sfx 0520
 sysmessageorange Bonus de ataque: <dlocal.ret> hits!
 return <local.ret>
endif
if 0<local.extra>
 timerf 1 sfx,0520
 timerf 1 emotered,ataque extra
 src.timerf 1 damage,<local.extra>,dam_god,<uid>
endif

[FUNCTION f_sm_TADAO]
//Função super apelativa para batalha
tag.specialmove_active=15                                                //dano duplo
timerf 2 f_sm_DefMastery                                                 //Aumenta defesa e diminui dano
f_sm_whirlwindAttack                                                     //Acerta tudo ao redor
serv.b=@021 [<tag.name> eh mundialmente famoso pelo seu ataque TADAO!]   //Todo mundo fica sabendo
timerf 7,sfx,0543                                                        //Sonzinho cretino pra ele ficar feliz

//*****************************************************************************
// f_sm_Feint
//*****************************************************************************
[FUNCTION f_sm_Feint]
//Anula a armadura do oponente por 7 segundos
src.emotered vulderavel
src.modar=-<src.ar>
src.timerf 7 src.modar,<src.ar>

//*****************************************************************************
// f_sm_PsyAttack
//*****************************************************************************
[FUNCTION f_sm_PsyAttack]
//Diminui a mana do oponente e dá leve bonus de dano
src.mana /= 2
src.emotered desconcentrado
return <R3,8>

//*****************************************************************************
// f_sm_Block
//*****************************************************************************
[FUNCTION f_sm_Block]
//Aumenta a AR por curto periodo de tmpo
if !(0<argn>)
 sfx 0520
 tag.sm_block_bonus=<BETWEEN 10,30,<TACTICS>,1000>
 modar += <tag.sm_block_bonus>
 timerf 7 f_sm_Block,1
else
 modar -= <tag.sm_block_bonus>
 tag.sm_block_bonus=
endif

//*****************************************************************************
// f_sm_TalonStrike
//*****************************************************************************
//Sangra por 3 segundos perdendo 3! hits ao total
[FUNCTION f_sm_TalonStrike]
serv.newitem i_mry_sm_bleed
new.more1=3
new.link=<UID>
src.equip <new>
return <R1,10>

//*****************************************************************************
// f_sm_FenziWhirl
//*****************************************************************************
[FUNCTION f_sm_FenziWhirl]
//Dano em todos os chars a 1 tile de distância + 4! de dano over time
obj=<uid>
forchars 1
 if (<uid>!=<obj>)
  timerf 1 damage,<R<obj.weapon.dam>>,dam_god,<obj.uid>
  serv.newitem i_mry_sm_bleed
  new.more1=3
  new.link=<OBJ>
  equip <new>
 endif
end

//*****************************************************************************
// f_sm_ForceNature
//*****************************************************************************
[FUNCTION f_sm_ForceNature]
//50 de dano poison
src.timerf 1 damage,50,dam_poison,<uid>
sfx 05BE

//*****************************************************************************
// f_sm_LightArrow
//*****************************************************************************
[FUNCTION f_sm_LightArrow]
//Dano em corrente para quem está a x tiles de distância
if !(0<argn>)
 src.f_sm_LightArrow <BETWEEN 1,4,<tactics>,1000>
 timerf 1,sfx,<eval <DEF.SND_THUNDER>>
 return 0
endif
obj=<UID>
forchars <argn>
 if (<uid>!=<obj>)
  timerf 1 damage,<R5,10>,dam_energy,<obj.uid>
  timerf 1 effect,1,1,1,1
 endif
end

//*****************************************************************************
// f_sm_NerveStrike
//*****************************************************************************
[FUNCTION f_sm_NerveStrike]
//Dano extra e rápida paralizia
sfx 0525
src.stun <R2,5>
src.emotered paralizado
return <R7,12>

//*****************************************************************************
// f_sm_RidingSwipe
//*****************************************************************************
[FUNCTION f_sm_RidingSwipe]
//Se você está a pé, desmonta oponente e dá Mortal Strike no cavalo.
//Se você esta motnado, Aumenta dano e da Stun
if !(<src.flags>&statf_onhorse)
 sysmessageyellow Seu alvo nao esta montado...
 return 0
endif
if <findlayer.layer_horse>
 OBJ=<src.findlayer.layer_horse.more2>
 src.emotered cai
 src.dismount
 LOCAL.dam=<R5,10>
 OBJ.DAMAGE <local.dam> dam_god <UID>   //ferir o cavalo
 OBJ.events +e_mortalStrike
 OBJ.timerf,60,events,-e_mortalStrike
 return <LOCAL.dam> //Aumenta o dano de 5 a 10
endif
src.stun <R2,5>
src.emotered paralizado
return <R7,12>

[EOF]

//////////////////////////////////////////////////////////////
////////////// Skill Lockpicking	      ///////////////
/////////////	Mystical Tales Shard	     ///////////////
////////////    Jean Gabin		    ///////////////
///////////	Galthar, o Errante (update)///////////////
/////////////////////////////////////////////////////////

// Neste script você encontrará:
// - Skill Lockpick


// Id`s:
// - Lockpick = 014fb - sfx 577
// - kit lockpick = 014fd


// - lockpicking 100 = erro 1 para 1 em todas e complexa 2 para 1 - timer 40


////////////////////////////////////////////////////////////
/////////////////////  Lockpick  //////////////////////////
//////////////////////////////////////////////////////////

[ITEMDEF 014fc]
//lockpick
DUPEITEM=014fb

[ITEMDEF 014fb]
DEFNAME=i_lockpick
NAME=Chave micha
TYPE=T_LOCKPICK
FLIP=1
RESOURCES=1 i_ingot_iron
WEIGHT=.5
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Chave micha
DUPELIST=014fc
SKILLMAKE=TINKERING 48.5,t_tinker_tools

on=@dclick
IF (<src.restest 1 i_mry_lockpick_fechadura>)
 serv.log <src.tag.name> [<src.account.name>] tentando usar exploit de Lockpicking
 src.findid.i_mry_lockpick_fechadura.morex=0
 return 1
endif
if (<topobj.uid> != <src.uid>)
	src.sysmessageyellow Coloque a micha na bolsa antes de usa-la.
return 1
else
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1
endif

ON=@targon_char
   src.sysmessagered Use isto em uma fechadura ou cadeado.
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1

ON=@targon_item
if (<src.targ.distance> > 1)
   src.sysmessageyellow Chegue mais perto.
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1
elif (<src.targ.type>==t_trap)
   src.targ.use
return 1
elif (<src.targ.type>==t_door) || (<src.targ.type>==t_container)
   src.sysmessagered isto nao esta trancado.
return 1
elif !(<src.targ.attr>&attr_stolen)
   src.sysmessagered Esta fechadura eh absurdamente complexa para sua habilidade.
return 1
elif (<src.targ.type>==t_door_locked) || (<src.targ.type>==t_container_locked)
	if (strmatch(<src.targ.tag.fechadura>,complexa))
		src.sysmessagered Nao e possivel tentar abrir esta fechadura com esta Chave micha. Use um kit de chave micha.
	return 1
	elif (<src.lockpicking> < 30.0) && (strmatch(<src.targ.tag.fechadura>,simples))
		src.sysmessagered Voce olha para a fechadura e percebe que ela e muito complexa para suas habilidades.
   	return 1
	elif (<src.lockpicking> < 40.0) && (strmatch(<src.targ.tag.fechadura>,normal))
		src.sysmessagered Voce olha para a fechadura e percebe que ela e muito complexa para suas habilidades.
   	return 1
	elif (strmatch(<src.targ.tag.fechadura>,simples)) || (strmatch(<src.targ.tag.fechadura>,normal)) || (strmatch(<src.targ.tag.fechadura>,cadeado))
		src.emotered tentando abrir tranca
		src.anim 020
		sfx 577
		src.newitem=i_mry_lockpick_fechadura
		//src.act.more1=<more1>		// tipo de fechadura a ser instalada: 1=simples, 2=normal, 3=complexa
		src.act.morep=<src.p>		// marca o local do src. se ele se mover para o script
		src.act.more2=0			// timer da memory
		src.act.link=<src.targ.uid>	// linka a memory a porta
		src.act.timer=1
		src.act.equip
		src.update
		//remove
	endif

else
   src.sysmessageyellow Nao ha fechadura ou cadeado neste local.
endif
return 1

///////////////////////////////////////////////////////

[ITEMDEF 014fe]
//lockpicks
DUPEITEM=014fd

[ITEMDEF 014fd]
DEFNAME=i_lockpick_set
NAME=Kit de chaves micha
TYPE=T_LOCKPICK
FLIP=1
RESOURCES=3 i_LOCKPICK
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Chaveiro
DESCRIPTION=Kit de chaves micha
DUPELIST=014fe

on=@dclick
IF (<src.restest 1 i_mry_lockpick_fechadura>)
 serv.log <src.tag.name> [<src.account.name>] tentando usar exploit de Lockpicking
 src.findid.i_mry_lockpick_fechadura.morex=0
 return 1
endif
if (<topobj.uid> != <src.uid>)
	src.sysmessageyellow Coloque a micha na bolsa antes de usa-la.
return 1
else
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1
endif

ON=@targon_char
   src.sysmessagered Use isto em uma fechadura ou cadeado.
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1

ON=@targon_item
if (<src.targ.distance> > 1)
   src.sysmessageyellow Chegue mais perto.
   target Selecione a porta ou o container com fechadura ou cadeado.
return 1
elif (<src.targ.type>==t_trap)
   src.targ.use
return 1
elif (<src.targ.type>==t_door) || (<src.targ.type>==t_container)
   src.sysmessageyellow isto nao esta trancado.
return 1
elif !(<src.targ.attr>&attr_stolen)
   src.sysmessagered Esta fechadura eh muito absurdamente complexa para sua habilidade.
return 1
elif (<src.targ.type>==t_door_locked) || (<src.targ.type>==t_container_locked)
   if (<src.lockpicking> < 50.0)
	src.sysmessagered Voce olha para a fechadura e percebe que ela e muito complexa para suas habilidades.
   return 1
   else
	src.emotered tentando abrir tranca
	src.anim 020
	sfx 577
	src.newitem=i_mry_lockpick_fechadura
	src.act.morep=<src.p>		// marca o local do src. se ele se mover para o script
	src.act.more2=0			// timer da memory
	src.act.link=<src.targ.uid>	// linka a memory a porta
	src.act.timer=1
	src.act.equip
	src.update
	//remove
   endif
else
   src.sysmessageyellow Nao ha fechadura ou cadeado neste local.
endif
return 1

////////////////////////////////////////////////////////////////
/////////////  Memory arrombando fechadura  ////////////////////
////////////////////////////////////////////////////////////////

[ITEMDEF i_mry_lockpick_fechadura]
id=i_memory
NAME=Instalando Fechadura
TYPE=T_eq_script
WEIGHT=0

ON=@Create
	ATTR=010
	more1=0  	// vai fazer a operação que resultará na probabilidade de acerto
	more2=0  	// counter

ON=@TIMER
if (<cont.p.x> != <morex>) || (<cont.p.y> != <morey>) || (<cont.flags>&0c095242e)
 cont.sysmessagered Voce interrompeu a tentativa de abrir a fechadura ou cadeado.
 remove
 return 1
endif

if (<more2>&04)
 cont.anim 020
endif
if (<more2>&08)
 sfx 577
endif
if (<more2>&010) && !(<more2>&02)
 dorand 36
  cont.emotered mechendo na fechadura
  cont.emotered gira a micha
  cont.emotered forca para a esquerda
  cont.emotered forca para cima
  cont.emotered olha para os lados
  cont.emotered forca para baixo
  cont.emotered forca para a direita
  cont.emotered tenso
  cont.emotered olha para os lados
  cont.emotered gira a micha
  cont.tossir
  cont.espirrar
 enddo
endif

timer=1
more2=<more2>+1

if (<more2> == 1)
 cont.sysmessageyellow Voce introduz a micha na fechadura com cuidado.
elif (<more2> == 10)
 cont.sysmessageyellow Voce gira lentamente a micha procurando a fenda da tranca.
elif (<more2> == 20)
 cont.sysmessageyellow Voce encosta a micha na tranca e comeca a fazer pressao para tentar destravar.
elif (<more2> == 30)
 cont.sysmessageyellow Voce posiciona a micha na fenda oposta para forcar a trava a soltar.
elif (<more2> == 40)
 cont.sysmessageyellow Voce comeca a cutucar a fenda do fundo para soltar a trava principal
elif (<more2> == 50)
 cont.sysmessageyellow Voce tenta soltar a trava secundaria enquanto mantem a trava principal aberta.
elif (<more2> == 60)
 cont.sysmessageyellow Voce gira a micha para destravar a trava oposta enquanto mantem a trava principal e secundaria abertas.
endif

if (strmatch(<link.tag.fechadura>,cadeado)) && (<more2>>=10)
 dorand <eval (101 + (-((<cont.lockpicking>)/(10))))/(5) >
  f_lp_open_cad
  f_lp_fail_simple
 enddo
elif (strmatch(<link.tag.fechadura>,simples)) && (<more2>>=20)
  dorand <eval (131 + (-((<cont.lockpicking>)/(10))))/(4) >
   f_lp_open_lock
   f_lp_fail_simple
   f_lp_fail_simple
  enddo
elif (strmatch(<link.tag.fechadura>,normal)) && (<more2>>=40)
  dorand <eval (141 + (-((<cont.lockpicking>)/(10))))/(3) >
   f_lp_open_lock
   f_lp_fail_simple
   f_lp_fail_simple
   f_lp_fail_simple
  enddo
elif (strmatch(<link.tag.fechadura>,complexa)) && (<more2>>=60)
  dorand <eval (151 + (-((<cont.lockpicking>)/(10))))/(2) >
   f_lp_open_lock
   f_lp_fail_complex
   f_lp_fail_complex
   f_lp_fail_complex
   f_lp_fail_complex
   f_lp_fail_complex
  enddo
endif
return 1


////////////////////////////////////////////////////////////

[FUNCTION f_lp_open_cad]
   cont.message *conseguiu abrir o cadeado*
   serv.newitem=i_cadeado
   new.more2=1   	// quebra o cadeado, tornando-o inutilizável
   new.more1=1
   cont.bounce <new>
   f_unlock <link>		// troca o type, testa se precisa retrancar, põe o timer pra trancar.
   cont.skill_gain Skill_Lockpicking
   sfx 511
   remove

[FUNCTION f_lp_open_lock]
cont.message *conseguiu abrir a fechadura*
f_unlock <link.uid>
sfx 511
cont.skill_gain Skill_Lockpicking
remove


[FUNCTION f_lp_fail_simple]
  IF (<R<cont.lockpicking>> < 6.0)
   cont.consume 1 i_lockpick
   cont.sysmessagered A micha escapou se partiu na fechadura.
  ELSE
   cont.sysmessagered A micha escapou da fechadura. Tente novamente
  endif
  cont.skill_gain Skill_Lockpicking
  remove

[FUNCTION f_lp_fail_complex]
  IF (<R<cont.lockpicking>> < 2.5)
   cont.consume 1 i_lockpick_set
   cont.sysmessagered Uma das michas escapou e se partiu na fechadura. Seu kit esta arruinado.
  ELIF (!<R2>)
   cont.sysmessagered Uma das michas escapou da fechadura e acertou a sua mao.
   cont.cry
   cont.emotered se machuca
  ELIF (!<R1>)
   cont.sysmessagered Uma das michas escapou da fechadura prendendo a porta.
  ELSE
   cont.sysmessagered Uma das michas escapou da fechadura.
  endif
  cont.skill_gain Skill_Lockpicking
  remove

[FUNCTION f_unlock]
//Destranca a porta/container e retranca de for attr_owned
obj=<argn>
if (<obj.attr>&attr_owned)		//É público. Precisa retrancar.
 serv.newitem i_lockpick_reset
 new.tag.type=<obj.type>
 new.tag.fechadura=<obj.tag.fechadura>
 new.link=<obj.uid>
 new.timer=60*35		//35 minutos pra retrancar ou até um GM dar dclick na Worldgem Bit
 new.p=<obj.p>
 new.update
endif
if (<obj.type>==t_door_locked)
 obj.type=t_door
elif (<obj.type>==t_container_locked)
 obj.type=t_container
endif
obj.tag.fechadura
obj.update

[ITEMDEF i_lockpick_reset]
NAME=reset de tranca
ID=i_worldgem_bit
TYPE=t_script

on=@create
attr=092
color=015

on=@timer
if !(STRMATCH(<tag.fechadura>,))	//Tinha fechadura?
 link.tag.fechadura=<tag.fechadura>	//Repor fechadura
endif
link.type=<tag.type>
//link.emotered tranca
link.update
remove
return 1

on=@dclick
src.sysmessageyellow <link.name> trancado(a).
timerd 1
return 1

[EOF]
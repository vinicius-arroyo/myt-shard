//*************************************************************
//			SPELLS NECROMANTE
//
//			GALTHAR 2007
//
//*************************************************************

[DEFNAME undead_cast]
dark_gift		01
death_harm		02
death_bolt		03
death_curse		04
infestation		05
fear_paralyze		06
death_poison_field	07
in_putrefus		08
summon_undead		09
death_confusion		10

//***********************************************************
//		SPELL HARM
//***********************************************************

[Spell 12]
DEFNAME=s_harm
NAME=Ferir
SOUND=snd_SPELL_HARM
RUNES=AM
CAST_TIME=25
RESOURCES=i_reag_nightshade,i_reag_spider_silk
RUNE_ITEM=i_rune_HARM
SCROLL_ITEM=i_scroll_HARM
FLAGS=SPELLFLAG_TARG_CHAR | SPELLFLAG_DIR_ANIM | SPELLFLAG_HARM | SPELLFLAG_FX_TARG | SPELLFLAG_RESIST | spellflag_scripted
EFFECT_ID=i_fx_curse
EFFECT=5,18
DURATION=0.0
MANAUSE=8
SKILLREQ=MAGERY 20.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_harm,spl_morte>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
effect 2 i_fx_curse 0 16 0
DAMAGESPELL s_harm, dam_poison, <src.cTAG.cSkill>, <src.uid>
return 0

//***********************************************************
//		SPELL POISON FIELD
//***********************************************************
[Spell 39]
DEFNAME=s_poison_field
NAME=Poison Field
SOUND=snd_SPELL_POISON_FIELD
RUNES=0
CAST_TIME=62
//RESOURCES=i_reag_black_pearl,i_reag_nightshade,i_reag_spider_silk
RUNE_ITEM=i_rune_POISON_FIELD
//SCROLL_ITEM=i_scroll_POISON_FIELD
FLAGS=SPELLFLAG_TARG_XYZ | SPELLFLAG_HARM | SPELLFLAG_FX_TARG | SPELLFLAG_FIELD| SPELLFLAG_RESIST
EFFECT_ID=i_fx_curse
EFFECT=50,500
DURATION=5.0,60.0
MANAUSE=24
SKILLREQ=MAGERY 50.0
INTERRUPT=100.0,100.0

//***********************************************************
//		SPELL DARK GIFT
//***********************************************************

[Spell 250]
DEFNAME=s_darkGift
NAME=Presente negro
SOUND=01E1
RUNES=0
CAST_TIME=22
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted | SPELLFLAG_RESIST
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=9
SKILLREQ=MAGERY 35.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_darkGift,spl_morte>

on=@Fail
f_consumeMana <ctag.cMana>

on=@Effect
IF (<src.distance <uid>> > 1)
 src.sysmessageyellow Muito longe...
 return 1
ENDIF
effect 2 i_fx_curse 0 16 0 016a
if (<belltest <src.cTAG.cSkill>, 120.0>)
 DAMAGE <hits> dam_poison <src.uid>
 return 1
elif (<belltest <src.cTAG.cSkill>, 95.0>)
 veneno <src.cTAG.cSkill>
endif
DAMAGESPELL s_darkGift, dam_poison, <src.cTAG.cSkill>, <src.uid>
src.f_consumeMana <src.ctag.cMana>
return 0

//***********************************************************
//		SPELL Death Bolt
//***********************************************************
[Spell 251]
DEFNAME=s_deathBolt
NAME=Dardo da morte
SOUND=020D
RUNES=0
CAST_TIME=36
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted | spellflag_dir_anim
EFFECT_ID=0
EFFECT=10,18
//DURATION=30.0
MANAUSE=11
SKILLREQ=MAGERY 45.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_deathBolt,spl_morte>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <src.ctag.cMana>

on=@Effect
effect 0 i_fx_fireball_small 6 16 1 0317
DAMAGESPELL s_deathBolt, dam_poison, <src.cTAG.cSkill>, <src.uid>
return 0

//***********************************************************
//		SPELL Death Curse
//***********************************************************
[Spell 252]
DEFNAME=s_deathCurse
NAME=Maldicao da morte
SOUND=02f8
RUNES=0
CAST_TIME=42
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted | SPELLFLAG_RESIST
EFFECT_ID=0
EFFECT=15,60
//DURATION=30.0
MANAUSE=21
SKILLREQ=MAGERY 52.5
INTERRUPT=100.0,100.0

on=@select
return <trycast s_deathCurse,spl_morte>

on=@start
sfx 05c8

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
local.modstr=-<f_rangeValue 5,25,<src.ctag.cSkill>>
local.moddex=-<f_rangeValue 5,30,<src.ctag.cSkill>>
local.modint=-<f_rangeValue 5,10,<src.ctag.cSkill>>
local.time=<EFFECTSPELL s_deathCurse,<src.ctag.cSkill>>
f_polymorph c_zombie,<local.time>,<local.modstr>,<local.moddex>,<local.modint>
f_death_mask <local.time>
return 0


//***********************************************************
//		SPELL Infestation
//***********************************************************
[Spell 253]
DEFNAME=s_Infestation
NAME=Infestacao
SOUND=0
RUNES=0
CAST_TIME=35
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=spellflag_scripted
EFFECT_ID=0
EFFECT=5,45
//DURATION=30.0
MANAUSE=33
SKILLREQ=MAGERY 55.7
INTERRUPT=100.0,100.0

on=@select
return <trycast s_Infestation,spl_morte>

on=@start
sfx 05c8

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
FOR <eval <src.ctag.cSkill>/200>
 OBJ=<f_summon_creature c_rato_sepultura,<EFFECTSPELL s_Infestation, <ctag.cSkill>>>
 obj.movenear <uid> {1 2}
ENDFOR 
return 0


//***********************************************************
//		SPELL FEAR
//***********************************************************
[Spell 254]
DEFNAME=s_Fear
NAME=Medo
SOUND=01E1
RUNES=0
CAST_TIME=22
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_scripted | spellflag_resist
EFFECT_ID=0
EFFECT=3,8
//DURATION=30.0
MANAUSE=9
SKILLREQ=MAGERY 67.3
INTERRUPT=100.0,100.0

on=@select
return <trycast s_Fear,spl_morte>

on=@Fail
f_consumeMana <ctag.cMana>

on=@Effect
 src.sfx 0103
 OBJ=<SRC.UID>
 src.emotered gargalha
 FORCHARS <eval (<magery>/166)+2>
  //Só ataca quem pode tomar magia, não for morto vivo, não for o caster e for inimigo do caster.
  if !(<flags>&statf_nomagic) && (<npc>!=brain_undead) && (<UID>!=<OBJ.UID>) && (<ISMYENEMY <SRC.UID>>)
   stun <EFFECTSPELL s_Fear,<obj.ctag.cSkill>>
   emotered medo
  endif
 ENDFOR
return 0

//***********************************************************
//		SPELL POISON CELL
//***********************************************************
[Spell 255]
DEFNAME=s_CelaVeneno
NAME=Cela de veneno
SOUND=01E1
RUNES=0
CAST_TIME=82
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_scripted | spellflag_resist | spellflag_targ_char
EFFECT_ID=0
EFFECT=7,60
//DURATION=30.0
MANAUSE=12
SKILLREQ=MAGERY 77.3
INTERRUPT=100.0,100.0

on=@select
return <trycast s_CelaVeneno,spl_morte>

on=@Fail
f_consumeMana <ctag.cMana>

on=@Effect
f_cela_veneno <effectspell s_CelaVeneno,<src.cTAG.cSkill>>
return 0

//***********************************************************
//		SPELL Summon Undead
//***********************************************************
[Spell 256]
DEFNAME=s_SummonUndead
NAME=Conjurar morto-vivo
SOUND=0
RUNES=0
CAST_TIME=35
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=spellflag_scripted
EFFECT_ID=0
EFFECT=5,45
//DURATION=30.0
MANAUSE=33
SKILLREQ=MAGERY 75.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_SummonUndead,spl_morte>

on=@start
sfx 05c8

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
f_spl_summon_undead <src.cTag.cSkill>
return 0

//***********************************************************
//		SPELL Death Confusion
//***********************************************************
[Spell 257]
DEFNAME=s_DeathConfusion
NAME=Confusao mortal
SOUND=01E1
RUNES=0
CAST_TIME=82
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_scripted | spellflag_resist | spellflag_targ_char
EFFECT_ID=0
EFFECT=15,45
//DURATION=30.0
MANAUSE=12
SKILLREQ=MAGERY 77.3
INTERRUPT=100.0,100.0

on=@select
return <trycast s_DeathConfusion,spl_morte>

on=@Fail
f_consumeMana <ctag.cMana>

on=@Effect
EFFECT 3 i_fx_sparkle 0 16 0 026
EFFECT 3 i_fx_bless_effect 0 16 0 0a4
f_spl_confusion <EFFECTSPELL s_DeathConfusion,<src.cTAG.cSkill>>
return 0

//*************************************************************
//			EVENT
//*************************************************************


[EVENTS e_caster_undead]
on=@SpellCast
if (<NPC_CanCast>)
 OBJ=<MEMORYFINDTYPE.memory_war_targ.link>
 DORAND <eval <magery>/50>
  undead_cast 1, <obj.uid>	//Magery 5.0
  undead_cast 1, <obj.uid>
  undead_cast 1, <obj.uid>
  undead_cast 1, <obj.uid>
  undead_cast 2, <obj.uid>
  undead_cast 2, <obj.uid>	//Magery 30.0
  undead_cast 2, <obj.uid>
  undead_cast 3, <obj.uid>
  undead_cast 3, <obj.uid>
  undead_cast 3, <obj.uid>	//Magery 50.0
  undead_cast 4, <obj.uid>
  undead_cast 4, <obj.uid>
  undead_cast 5, <obj.uid>
  undead_cast 5, <obj.uid>
  undead_cast 6, <obj.uid>	//Magery 75.0
  undead_cast 6, <obj.uid>
  undead_cast 7, <obj.uid>
  undead_cast 8, <obj.uid>
  undead_cast 9, <obj.uid>
  undead_cast 10, <obj.uid>	//Magery 100.0
  undead_cast 1, <obj.uid>
  undead_cast 2, <obj.uid>
  undead_cast 3, <obj.uid>
  undead_cast 4, <obj.uid>
  undead_cast 5, <obj.uid>	//Magery 125.0
  undead_cast 6, <obj.uid>
  undead_cast 7, <obj.uid>
  undead_cast 8, <obj.uid>
  undead_cast 9, <obj.uid>
  undead_cast 10, <obj.uid>	//Magery 150.0
  undead_cast 4, <obj.uid>
  undead_cast 4, <obj.uid>
  undead_cast 4, <obj.uid>
  undead_cast 5, <obj.uid>
  undead_cast 5, <obj.uid>	//Magery 175.0
  undead_cast 6, <obj.uid>
  undead_cast 7, <obj.uid>
  undead_cast 8, <obj.uid>
  undead_cast 9, <obj.uid>
  undead_cast 10, <obj.uid>	//Magery 200.0
 ENDDO
endif
return 1




//*************************************************************
//			FUNCTION PRINCIPAL
//*************************************************************
[FUNCTION undead_cast]
//argv0		magia
//argv1		alvo
local.difcast=<eval (<argv0>*100)-{10 50}>
if (!<skilltest Magery <local.difcast>>)
 effect 3 i_fx_smoke_small 0 16 0
 sfx {04f 1 0227 1 0191 1}
 spell_delay {10 35}
 return 1
endif
OBJ=<argv1>
if (<argv0>==dark_gift) && (<mana> > 5)
 say=@026 mortis funi
 sfx 0fe
 OBJ.EFFECT 0 036e4 4 24 1 08ad
 LOCAL.DAM=<eval (<magery>/300) + {0 3} +1>
 OBJ.DAMAGE <LOCAL.DAM> dam_magic|dam_cold|dam_poison <UID>
 mana=<mana>-5
 spell_delay 17
elif (<argv0>==fear_paralyze) && (<mana> > 25)
 sfx 0103
 OBJ=<UID>
 emotered gargalha
 FORPLAYERS <eval (<magery>/166)+2>
  if (<skilltest MagicResistance <obj.magery>>)
   emotered resistiu
  else
   local.timer=<eval <obj.magery>/100>
   stun <local.timer>
   emotered medo
  endif
 ENDFOR
 mana=<mana>-25
 spell_delay 32
elif (<argv0>==infestation) && (<mana> > 20)
 say=@026 infestus
 FOR <eval <magery>/200>
  OBJ=<f_summon_creature c_rato_sepultura,15>
  obj.movenear <uid> {1 2}
 ENDFOR 
 mana=<mana>-15
 spell_delay 75
elif (<argv0>==death_bolt) && (<mana> > 15)
 say=@026 mortalia ominus
 sfx 015e
 OBJ.EFFECT 3 03789 4 16 1 08ad
 LOCAL.DAM=<eval (<magery>/150) + {0 5} +1>
 OBJ.DAMAGE <LOCAL.DAM> dam_magic|dam_cold|dam_poison <UID>
 mana=<mana>-15
 spell_delay 22
elif (<argv0>==death_harm) && (<mana> > 7)
 say=@026 injuris
 sfx 01fb
 OBJ.EFFECT 3 i_fx_curse 0 16 0
 LOCAL.DAM=<eval {0 (<magery>/250)} +6>
 OBJ.DAMAGE <LOCAL.DAM> dam_magic|dam_poison <UID>
 mana=<mana>-7
 spell_delay 33
elif (<argv0>==death_curse) && (<mana> > 7)
 say=@026 malektu
 OBJ.f_spl_curse <Magery>
 mana=<mana>-7
 spell_delay 33
elif (<argv0>==death_poison_field) && (<mana> > 15)
 say=@026 venomus carceris
 OBJ.f_cela_veneno <eval <Magery>/75>
 mana=<mana>-15
 spell_delay 100
elif (<argv0>==in_putrefus) && (<mana> > 20)
 say=@026 in putrefus et
 OBJ.f_spl_polymorph {c_zombie 6 c_skeleton 1}, <Magery>
 mana=<mana>-20
 spell_delay 37
 in_putrefus
elif (<argv0>==death_confusion) && (<mana> > 20)
 say=@026 conturbatis
 OBJ.f_spl_confusion <Magery>
 mana=<mana>-20
 spell_delay 5
 in_putrefus
elif (<argv0>==summon_undead) && (<mana> > 25)
 say=@026 Kal Xen
 f_spl_summon_undead <Magery>
 mana=<mana>-25
 spell_delay 35
endif



//*************************************************************
//			FUNCTIONS SPELLS
//*************************************************************

[FUNCTION f_spl_curse]
if (!<argn>)
 local.skill 500
 local.timer 30
else
 local.timer <eval <argn>/16>
 local.skill <argn>
endif
//modstats de -5 a -25
local.modstr=-<eval {5 (<local.skill>/40)}>
local.moddex=-<eval {5 (<local.skill>/40)}>
local.modint=-<eval {5 (<local.skill>/40)}>
serv.newitem i_spl_curse
new.timer <local.timer>
new.morex=<local.modstr>
new.morey=<local.moddex>
new.morez=<local.modint>
new.equip
new.timer=<local.timer>
effect 3 i_fx_curse 0 16 0
sfx 01fa



[FUNCTION f_spl_confusion]
serv.newitem i_spl_confusion
new.more2 = <eval <argn>/3>
equip <new>

[FUNCTION f_spl_summon_undead]
if (!0<argn>)
 local.skill={400 600}
else
 local.skill=<argn>
endif
local.undead={c_zombie 16 c_skeleton 10 c_skeleton_w_axe 8 c_skeleton_w_sword 8 c_grim_reaper 4 c_zumbi_superior 4 c_spectre 1}
local.rand =<eval {0 (<local.skill>/300)}>
FOR <local.rand>
 f_summon_creature <eval <local.undead>>,30
ENDFOR
return 1

[FUNCTION f_death_mask]
//simplesmente equipa um i_spl_death_mask
if (0<argv1>==0)
 local.timer 30
else
 local.timer <argv0>
endif
serv.newitem i_spl_death_mask
equip <new>
new.timer=<local.timer>
effect 3 i_fx_curse 0 16 0 017d
sfx 01fa



//*************************************************************
//			ITEMDEFS
//*************************************************************

[ITEMDEF i_spl_curse]
ID=i_rune_curse
name=curse
type=t_eq_script
layer=layer_special

on=@create
attr=02

on=@equip
cont.modstr=<cont.modstr>+<morex>
cont.moddex=<cont.moddex>+<morey>
cont.modint=<cont.modint>+<morez>

on=@timer
cont.modstr=<cont.modstr>-<morex>
cont.moddex=<cont.moddex>-<morey>
cont.modint=<cont.modint>-<morez>
remove
return 1


[ITEMDEF i_spl_confusion]
ID=i_rune_curse
name=insanity
type=t_eq_script
layer=layer_special

on=@create
attr=022
more2=5

on=@equip
cont.flags=<cont.flags>|0400000		//Hallu
timer 5
cont.sysmessagegreen Voce se sente muito estranho.

on=@timer
IF (<more2> >=0 )
 DORAND 5
  cont.msay louco
  cont.stun 3
  cont.msay louco
  cont.msay sentidos
  cont.f_vomita
 enddo
 more2=<more2>-1
 timer={2 4}
 cont.update
else
 cont.flags=<cont.flags>&~0400000	//Tira hallu
 cont.update
 cont.sysmessagegreen Voce se sente melhor
 remove
endif
return 1


[ITEMDEF i_spl_death_mask]
ID=i_rune_poison
name=death mask
type=t_eq_script
layer=layer_special

on=@equip
timer 1
cont.sysmessagegreen Voce se sente todo dormente, como um defunto.

on=@timer
cont.sysmessagegreen Voce volta ao seu estado natural de ser vivo.
remove
return 1

[FUNCTION ISMYENEMY]
//Retorna se <argn1> é meu inimigo ou não
obj=<argn1>
FORCHARMEMORYTYPE memory_war_targ
 IF (<LINK>==<obj.UID>)
  LOCAL.ret=1
 ENDIF
ENDFOR
RETURN <LOCAL.ret>

//***********************************************************
//		SPELL SUMMON UNDEAD
//***********************************************************
[FUNCTION f_spl_summon_undead]
IF (<argv0>)
 LOCAL.DIF=<EVAL <argv0>/100>
else
 LOCAL.DIF=<EVAL <magery>/100>
ENDIF
IF (<LOCAL.DIF> > 10)
 LOCAL.DIF=10
endif
LOCAL.DIF1=<eval {1 <LOCAL.DIF>}>
LOCAL.DIF=<eval {<LOCAL.DIF1> <LOCAL.DIF>}> //Isso aqui simula uma gausseana perneta
if (<LOCAL.DIF>==1)
 f_summon_creature c_zombie,60
elif (<LOCAL.DIF>==2)
 f_summon_creature c_zombie,28
 f_summon_creature c_zombie,33
elif (<LOCAL.DIF>==3)
 f_summon_creature c_skeleton,60
elif (<LOCAL.DIF>==4)
 f_summon_creature c_skeleton_w_axe,60
elif (<LOCAL.DIF>==5)
 f_summon_creature c_skeleton_w_sword,60
elif (<LOCAL.DIF>==6)
 f_summon_creature c_grim_reaper,30
elif (<LOCAL.DIF>==7)
 f_summon_creature c_zombie,28
 f_summon_creature c_skeleton_w_axe,33
elif (<LOCAL.DIF>==8)
 f_summon_creature c_grim_reaper,32
 f_summon_creature c_skeleton_w_axe,37
elif (<LOCAL.DIF>==9)
 f_summon_creature c_grim_reaper,25
 f_summon_creature c_zumbi_superior,29
 f_summon_creature c_spectre,35
elif (<LOCAL.DIF>==10)
 LOCAL.undead1={c_spectre 1 c_m_liche 1 c_Wailing_Banshee 1}
 LOCAL.undead2={c_m_liche_lord 1 c_zumbi_superior 1 c_grim_reaper 1}
 f_summon_creature <local.undead1>,45
 f_summon_creature <local.undead2>,47
endif

[EOF]
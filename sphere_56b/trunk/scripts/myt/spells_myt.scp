//*************************************************************
//*************************************************************
//
//		Spells especiais da MyT
//		     Galthar 2007
//
//*************************************************************
[DEFNAME d_magias]
spl_vulgar	00
spl_vida	01
spl_agua	02
spl_materia	03
spl_ar		04
spl_fogo	05
spl_morte	06
spl_terra	07
spl_energia	08

statf_nomagic=04201A //dead, invis, sleep, insubstatial, stone

//*************************************************************
//		FUNCTIONS GERAIS DE SPELL
//*************************************************************

[FUNCTION trycast]
//trycast <spell> <domain>
//Nos moldes novos, tenta castar <spell> no domínio <domain>
IF (<NPC>)
 return 1
ENDIF
LOCAL.mag=<tag0.mag>
if !(STRMATCH(<argv1>,))
 serv.log <ACCOUNT.NAME> castando <serv.resources.<argv0>.name> usando dominio <argv1>.
 //Penalidade por domínio oposto.
 DOSWITCH (<DEF.<argv1>>-1)
  LOCAL.mag=<LOCAL.mag>-(<tag0.spl_morte>+1)
  LOCAL.mag=<LOCAL.mag>-(<tag0.spl_fogo>+1)
  LOCAL.mag=<LOCAL.mag>-(<tag0.spl_energia>+1)
  LOCAL.mag=<LOCAL.mag>-(<tag0.spl_terra>+1)
  LOCAL.mag=<LOCAL.mag>-(<tag0.spl_agua>+1)
  LOCAL.mag=<LOCAL.mag>-(<tag0.spl_vida>+1)
  LOCAL.mag=<LOCAL.mag>-(<tag0.spl_ar>+1)
  LOCAL.mag=<LOCAL.mag>-(<tag0.spl_materia>+1)
 ENDDO
 //Bônus pelo domínio
 LOCAL.mags=(<LOCAL.mag>)+<tag0.<argv1>>
 LOCAL.magm=(<LOCAL.mag>)-<tag0.<argv1>> 
else
 LOCAL.mags=<LOCAL.mag>
 LOCAL.magm=<LOCAL.mag>
endif
LOCAL.magm=<f_manaPerMag <serv.resources.<argv0>.manause>, <LOCAL.magm>>
LOCAL.mags=<f_skillPerMag <magery>, <LOCAL.mags>>
IF (<mana> < <LOCAL.magm>)
 sysmessageyellow Voce nao tem mana o suficiente.
 return 1
endif
IF (<strarg <serv.resources.<argv0>.SKILLREQ>> > <LOCAL.mags>+75)
 sysmessageyellow Esta alem de sua habilidade...
 return 1
endif
ctag.cSkill=<LOCAL.mags>
ctag.cMana=<LOCAL.magm>
return 0


[FUNCTION skillchance]
// LOCAL.SKILL_CURR <argv0>
// LOCAL.SKILL_REQ <argv1>
//Obrigado, Taran! E feliz aniversário! (foi ontem...)
LOCAL.skill_diff <eval <argv1>-<argv0>>        // Notice the +- to deal with issues
LOCAL.skill_bell <eval randbell( <LOCAL.skill_diff>, 100 )>
if ( <LOCAL.skill_diff> < 0 )
    LOCAL.skill_bell <eval 1000 - <LOCAL.skill_bell>>
endif
return <eval <LOCAL.skill_bell>>

[FUNCTION belltest]
// <argv0> skill a testar
// <argv1> skill requerida
if (<eval {0 1000}> < <skillchance <argv0>,<argv1>>)
 RETURN 1
else
 RETURN 0
endif



[FUNCTION f_skillPerMag]
//Retorna %skill corrigido pelo tag.mag (ajustado no dialog d_magia)
//<argv0> = skill
//<argv1> = tag.mag
LOCAL.percent=<eval ((10*<argv1>)+50)>
LOCAL.skill=<eval ((<argv0>*<LOCAL.percent>)/100)>
RETURN <eval <LOCAL.skill>>

[FUNCTION f_manaPerMag]
//Retorna custo em mana corrigido pelo tag.mag (ajustado no dialog d_magia)
//<argv0> = mana
//<argv1> = tag.mag
IF (<argv1> <= 5)
 LOCAL.percent=<eval ((10*<argv1>)+50)>
ELSE
 LOCAL.percent=<eval (20*<argv1>)>
ENDIF
LOCAL.mana=<eval ((<argv0>*<LOCAL.percent>)/100)>
RETURN <eval <LOCAL.mana>>


//Testa de pode castar ou Previne castar magias por <argn> decimos de segundo
[FUNCTION spell_delay]
IF (<ARGN>)
 serv.newitem i_npc_spell_delay
 equip <new>
 new.timerd <argn>
ELIF (<findid.i_npc_spell_delay>)
 RETURN 1
else
 RETURN 0
endif
 

//Se NPCs podem castar
[FUNCTION NPC_CanCast]
if (<findid.i_npc_spell_delay>) || (<tag0.spell_delay>)
 RETURN 0
else
 RETURN 1
endif

//Se PCs podem castar
[FUNCTION CanCast]
RETURN <NPC_CanCast>

//*************************************************************
//		FUNCTIONS DE SPELLS GENERICAS
//*************************************************************

[FUNCTION f_summon_creature]
//Conjura uma criatura por um periodo de tempo às ordens do mago
//<argv0> = criatura
//<argv1> = tempo (segundos)
newnpc <argv0>
act.movenear 2 <UID>
obj <act.uid>
serv.newitem i_memory
new.color memory_ipet
new.more1 04
new.more2 <serv.time>
new.morep <p>
new.link <uid>
new.cont <obj>
serv.newitem i_mry_summon
new.more2 <argv1>
new.link <uid>
new.cont <obj>
new.timer 1
obj.flags=<obj.flags>|0c000000	//spawned e pet
obj.effect 3 i_fx_sparkle 0 16 0 0a1
obj.karma=<karma>
//Tirar o loot
if (<obj.findlayer.21>)
 obj.findlayer.21.empty
endif
sfx 0fb
return <obj.uid>

[FUNCTION f_conjure_creature]
//Conjura uma criatura por um periodo de tempo a solta pelo mundo sem mestre
//<argv0> = criatura
//<argv1> = tempo (segundos)
newnpc <argv0>
act.p <p>
obj <act.uid>
serv.newitem i_mry_summon
new.more2 <argv1>
new.link <uid>
new.cont <obj>
new.timer 1
obj.FLAGS=<obj.flags>|04000000	//spawned
obj.effect 3 i_fx_sparkle 0 16 0 022
sfx 0217
return <obj.uid>


[FUNCTION f_spl_polymorph]
//Polymorph undead. Tira stats
//<argv0> = body
//<argv1> = skill
if (0<argv1>==0)
 local.skill 500
 local.timer 30
else
 local.timer <eval <argv1>/16>
 local.skill <argv1>
endif
local.body=<argv0>
if (!<serv.resources.<local.body>>)
 local.body=c_zombie
else
 local.body=<argv0>
endif
// Já estou polymorphado? Se sim, aumente o tempo apenas.
if (<restest 1 i_spl_polymorph>)
 findid.i_spl_polymorph.timer=<findid.i_spl_polymorph.timer>+<local.timer>
 effect 3 i_fx_curse 0 16 0 017d
 sfx 01fa
 return 1
endif
//modstats de -5 a -25
local.modstr=-<eval {5 (<local.skill>/40)}>
local.moddex=-<eval {5 (<local.skill>/40)}>
local.modint=-<eval {5 (<local.skill>/40)}>
serv.newitem i_spl_polymorph
new.morex=<local.modstr>
new.morey=<local.moddex>
new.morez=<local.modint>
new.more1=<local.body>
equip <new.uid>
new.timer=<local.timer>
effect 3 i_fx_curse 0 16 0 017d
sfx 01fa

[FUNCTION f_polymorph]
//Polymorph genérico
//f_polymorph <body> <tempo> <modstr> <moddex> <modint>
if (0<argv1>==0)
 local.timer 30
else
 local.timer <argv1>
endif
local.body=<argv0>
if (!<serv.resources.<local.body>>)
 local.body=c_zombie
else
 local.body=<argv0>
endif
// Já estou polymorphado? Se sim, aumente o tempo apenas.
if (<restest 1 i_spl_polymorph>)
 findid.i_spl_polymorph.timer=<findid.i_spl_polymorph.timer>+<local.timer>
 effect 3 i_fx_curse 0 16 0 017d
 sfx 01fa
 return 1
endif
local.modstr=<argv2>
local.moddex=<argv3>
local.modint=<argv4>
serv.newitem i_spl_polymorph
new.morex=<local.modstr>
new.morey=<local.moddex>
new.morez=<local.modint>
new.more1=<local.body>
equip <new.UID>
new.timer=<local.timer>
effect 3 i_fx_curse 0 16 0 017d
sfx 01fa

[ITEMDEF i_spl_polymorph]
ID=i_rune_polymorph
name=polymorph
type=t_eq_script
layer=layer_special

on=@create
attr=022

on=@equip
cont.modstr=<cont.modstr>+<morex>
cont.moddex=<cont.moddex>+<morey>
cont.modint=<cont.modint>+<morez>
IF (<cont.findlayer.layer_hair.UID>)
 tag.hair=<cont.findlayer.layer_hair.ID>
 tag.haircolor=<cont.findlayer.layer_hair.color>
 tag.hairtimer=<cont.findlayer.layer_hair.timer>
endif
IF (<cont.findlayer.layer_beard.UID>)
 tag.beard=<cont.findlayer.layer_beard.ID>
 tag.beardcolor=<cont.findlayer.layer_hair.color>
 tag.beardtimer=<cont.findlayer.layer_hair.timer>
endif
color=<cont.color>
local.body <cont.body>
cont.body <more1>
more1=<local.body>
cont.update

on=@timer
cont.modstr=<eval <cont.modstr>-<morex>>
cont.moddex=<eval <cont.moddex>-<morey>>
cont.modint=<eval <cont.modint>-<morez>>
cont.color=<color>
cont.body=<more1>
IF (<tag0.hair>)
 serv.newitem <tag.hair>
 new.color=<tag.haircolor>
 new.timer=<tag.hairtimer>
ENDIF
IF (<tag0.beard>)
 serv.newitem <tag.beard>
 new.color=<tag.beardcolor>
 new.timer=<tag.beardtimer>
ENDIF
cont.update
cont.effect 3 i_fx_curse 0 16 0 017d
cont.sfx 01fa
remove
return 1

[FUNCTION f_field_sparce]
//Cria um campo de <item>s ao redor do SRC.
//f_field_sparce <item>, <ammount>, <raio>, <tempo>
if (strmatch(<argv0>,))
 LOCAL.item=i_balao
else
 LOCAL.item=<argv0>
endif
if (strmatch(<argv1>,))
 LOCAL.amt=5
else
 LOCAL.amt=<argv1>
endif
if (strmatch(<argv2>,))
 LOCAL.radius=5
else
 LOCAL.radius=<argv2>
endif
if (strmatch(<argv3>,))
 LOCAL.timer=<eval {10 20}>
else
 LOCAL.timer=<argv3>
endif
OBJ=<UID>
FOR ammount 1 <LOCAL.amt>
 serv.newitem <eval {<LOCAL.item>}>
 new.p=<OBJ.p.x>,<eval <OBJ.p.y>+1>, <OBJ.p.z>
 new.movenear <OBJ.uid> <eval {1 <LOCAL.radius>}>
 new.link=<obj.uid>
 new.timerf <LOCAL.timer>,remove
 new.update
ENDFOR

//Faz uma duplicata pet/summoned do DEFAULT.
[FUNCTION f_dupeChar]

//Copia o char
serv.newnpc <BaseID>
new.p=<p>
new.str=<str>
new.hits=<str>
new.dex=<dex>
new.int=<int>
new.mana=<int>
new.color=<color>
new.karma=<karma>
new.fame=<fame>
new.name=<name>
obj=<new.uid>

//Copia o equipamento
for layer 1 24
 local.item=<findlayer.<local.layer>.baseid>
 local.color=<findlayer.<local.layer>.color>
 local.mor=<findlayer.<local.layer>.more1>
 local.mor2=<findlayer.<local.layer>.more2>
 serv.newitem <local.item>
 new.color=<local.color>
 new.more1=<local.mor>
 new.more2=<local.mor2>
 new.cont=<obj>
ENDFOR
FORCHARLAYER layer_pack
 dupe
 new.cont <obj>
ENDFOR

//Copia as skills
FOR skill 0 54
 obj.<local.skill>=<src.<local.skill>>
ENDFOR
obj.poisoning 0 //Não morde.

//Melhora AI se mago
IF (<obj.magery> > 50.0)
 obj.modINT=<obj.modINT>+60
 obj.events +e_mage_phaser
 obj.events +e_caster_warrior
endif

//Põe events de briga
obj.events +e_PvM
obj.events +e_combat

//Seta como pet e summoned
serv.newitem i_memory
new.color memory_ipet
new.more1 04
new.more2 <serv.time>
new.morep <p>
new.link <uid>
new.cont <obj>
serv.newitem i_mry_summon
new.more2 <f_rangeValue 5,120,<magery>>
new.link <uid>
new.cont <obj>
new.timer 1
obj.effect 3 i_fx_sparkle 0 16 0 0a1
obj.flags=<obj.flags>|0c000000	//spawned e pet
sfx 0fb
return <obj.uid>


[FUNCTION f_domains]
//abre janela de dominio de magias
ctag.newdomains=<src.tag0.newdomains>
ctag.spl_materia=<tag0.spl_materia>
ctag.spl_agua=<tag0.spl_agua>
ctag.spl_vida=<tag0.spl_vida>
ctag.spl_terra=<tag0.spl_terra>
ctag.spl_energia=<tag0.spl_energia>
ctag.spl_fogo=<tag0.spl_fogo>
ctag.spl_morte=<tag0.spl_morte>
ctag.spl_ar=<tag0.spl_ar>
DIALOG d_mdomains

//*************************************************************
//		TYPEDEFS de magia
//*************************************************************
//Livro que contem Palavras do poder
//more1 SPL_* (que tipo de palavras vai conter? Vida? Morte? Materia?)
//more2 Quantas palavras vão ter? Não passar de 5 é uma boa.
[TYPEDEF t_book_WoP]
ON=@DClick
if (<topobj>!=<src.uid>)
 src.sysmessageyellow Nao da para ler o livro de onde ele esta.
 return 1 
elif (<src.magery> < 50.0)
 src.sysmessageyellow Voce nao entende nada do que esta escrito.
 return 1
endif
//Salvar posição
src.cTAG.cPX=<src.p.x>
src.cTAG.cPY=<src.p.y>
src.cTAG.cWoPB=<UID>
//Tempo que demora pra ler é de 15 a 90 segundos variando com magery
src.cTAG.cTimer=<f_rangeValue 90,15,<src.magery>>
//começa a ler
src.emoteyellow abre um livro
src.sfx 03e3
src.sysmessageblue Voce abre o livro de encantamentos e passa a ler.
src.timerf 1,f_read_WoPB
return 1

ON=@CLIENTTOOLTIP
DOSWITCH <MORE1>
 LOCAL.t=(magia vulgar)
 LOCAL.t=(vida)
 LOCAL.t=(agua)
 LOCAL.t=(materia)
 LOCAL.t=(ar)
 LOCAL.t=(fogo)
 LOCAL.t=(morte)
 LOCAL.t=(terra)
 LOCAL.t=(energia)
ENDDO
LOCAL.t .= <DEF.br>Encantamentos: <eval <more2>>
f_sendTooltipData <local.t>

[FUNCTION f_read_WoPB]
if (<cTAG.cPX>!=<p.x>) || (<cTAG.cPY>!=<p.y>) || (<FLAGS>&statf_cantcraft)
 sysmessagered Voce parou de ler o livro
 f_clearWoPB
 return 1
ENDIF
DORAND 6
 emoteyellow lendo
 sfx 055
ENDDO
cTAG.cTimer -= 1
if (!<cTAG.cTimer>)
 f_revealWoP
 f_clearWoPB
else
 timerf 1,f_read_WoPB 
endif

[FUNCTION f_clearWoPB]
src.cTAG.cPX=
src.cTAG.cPY=
src.cTAG.cWoPB=
src.cTAG.cTimer=

[FUNCTION f_revealWoP]
obj=<cTAG.cWoPB>
DOSWITCH <obj.more1>
 LOCAL.wop=<SPELL_WoP_CANTRIP>
 LOCAL.wop=<SPELL_WoP_VIDA>
 LOCAL.wop=<SPELL_WoP_AGUA>
 LOCAL.wop=<SPELL_WoP_MATERIA>
 LOCAL.wop=<SPELL_WoP_AR>
 LOCAL.wop=<SPELL_WoP_FOGO>
 LOCAL.wop=<SPELL_WoP_MORTE>
 LOCAL.wop=<SPELL_WoP_TERRA>
 LOCAL.wop=<SPELL_WoP_ENERGIA>
ENDDO
obj.more2 -= 1
obj.updatex
if (!<obj.more2>)
 obj.remove
endif
message <LOCAL.wop>
sysmessageblue Voce parou de ler e revelou uma palavra do poder!

[TYPEDEF e_focus]
on=@equiptest
if (<src.magery> < 50.0) && (!<src.IsGM>)
 src.sysmessagered Um campo magico evida que voce segure <name>.
 return 1
endif
src.dspeech +spk_mago

on=@unequip
src.dspeech -spk_mago

//*************************************************************
//		ITEMDEFs de magia
//*************************************************************


//------ WOP BOOKS --------

[ITEMDEF i_book_WoP_vulgar]
ID=3834
TYPE=t_book_WoP
NAME=Encantamentos (vulgar)

CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Magical Items
DESCRIPTION=Encantamentos (vulgar)

on=@create
color=036
more1=spl_vulgar
more2={2 5}


[ITEMDEF i_book_WoP_vida]
ID=i_spellbook_paladin
TYPE=t_book_WoP
NAME=Encantamentos (vida)

CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Magical Items
DESCRIPTION=Encantamentos (vida)

on=@create
color=0170
more1=spl_vida
more2={2 5}

[ITEMDEF i_book_WoP_morte]
ID=i_spellbook_necromancy
TYPE=t_book_WoP
NAME=Encantamentos (morte)

CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Magical Items
DESCRIPTION=Encantamentos (morte)

on=@create
color=0336
more1=spl_morte
more2={2 5}

[ITEMDEF i_book_WoP_terra]
ID=i_spellbook_bushido
TYPE=t_book_WoP
NAME=Encantamentos (terra)

CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Magical Items
DESCRIPTION=Encantamentos (terra)

on=@create
color=08c
more1=spl_terra
more2={2 5}

[ITEMDEF i_book_WoP_fogo]
ID=i_spellbook_arcanist
TYPE=t_book_WoP
NAME=Encantamentos (fogo)

CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Magical Items
DESCRIPTION=Encantamentos (fogo)

on=@create
color=026
more1=spl_fogo
more2={2 5}



//------ MEMORY DE MAGIAS --------

//CHARs com esta memory não podem castar.
[ITEMDEF i_npc_spell_delay]
ID=01810
NAME=spell delay
TYPE=t_eq_script
LAYER=LAYER_SPECIAL

on=@timer
remove
return 1

//Armadura mágica (memory)
[ITEMDEF i_spl_changeAR]
ID=i_rune_REACTIVE_ARMOR
TYPE=t_eq_script
LAYER=layer_special
NAME=changeAR

on=@equip
IF (<more2>)
 TIMER=10
ENDIF
src.resfire += <tag.resfire>
src.rescold += <tag.rescold>
src.respoison += <tag.respoison>
src.resenergy += <tag.resenergy>
src.modar += <more1>

on=@timer
IF (!<more2>) || (!<link.f_consumeMana <more2>>) || (<link.flags>&statf_dead) || (<cont.flags>&statf_dead)
 cont.resfire -= <tag.resfire>
 cont.rescold -= <tag.rescold>
 cont.respoison -= <tag.respoison>
 cont.resenergy -= <tag.resenergy>
 cont.modar -= <more1>
 remove
 return 1
ENDIF
timer 10
return 1

//NPCs com esta memory sumirão quano o timer expirar.
[ITEMDEF i_mry_summon]
ID=i_rune_summon_creature
name=summoned
type=t_eq_script
layer=layer_special

on=@create
attr=020

on=@timer
more2=<more2>-1
if (<more2> <= 0) || (!<link.uid>)
 serv.newitem i_fx_vortex_full
 new.attr=02
 new.timerd 16
 cont.sfx 020c
 new.color 0a1
 new.p=<cont.p>
 cont.remove
 remove
endif
timer=1
return 1

//*************************************************************
//			GUMPS DE SPELL
//*************************************************************

[DIALOG d_mdomains]
164,65
page 0
gumppic 7 0 30500
gumppic 196 37 1417 50
gumppic 308 77 1417 4
gumppic 348 189 1417 65
gumppic 308 301 1417 45
gumppic 196 341 1417 20
gumppic 88 301 1417 35
gumppic 46 189 1417 828
gumppic 88 77 1417 10
dtext 209 42 0 Materia
dtext 333 80 0 Agua
dtext 374 194 0 Vida
dtext 328 306 0 Terra
dtext 212 347 0 Energia
dtext 113 307 0 Fogo
dtext 65 198 0 Morte
dtext 120 83 0 Ar

//Tenho novos pontos de dominio extra?
if (<src.ctag0.newdomains>)
 gumppic 220 192 113
 gumppictiled 227 230 16 16 1803
 gumppic 219 230 1802
 gumppic 243 230 1804
 dtext 226 228 48 +<src.dctag.newdomains>

 //Quais posso escolher?
 if (!<src.ctag0.spl_energia>)
  button 212 116 4500 4500 1 0 2
 endif
 if (!<src.ctag0.spl_fogo>)
  button 278 139 4501 4501 1 0 3
 endif
 if (!<src.ctag0.spl_morte>)
  button 299 206 4502 4502 1 0 4
 endif
 if (!<src.ctag0.spl_ar>)
  button 278 268 4503 4503 1 0 5
 endif
 if (!<src.ctag0.spl_materia>)
  button 212 294 4504 4504 1 0 6
 endif
 if (!<src.ctag0.spl_agua>)
  button 143 268 4505 4505 1 0 7
 endif
 if (!<src.ctag0.spl_vida>)
  button 125 206 4506 4506 1 0 8
 endif
 if (!<src.ctag0.spl_terra>)
  button 148 139 4507 4507 1 0 9
 endif
else
 button 27 415 4014 4014 1 0 10
endif

//Quais eu já tenho?
dtext 230 67 32 <src.dctag.spl_materia>
dtext 343 107 32 <src.dctag.spl_agua>
dtext 382 220 32 <src.dctag.spl_vida> 
dtext 343 331 32 <src.dctag.spl_terra>
dtext 230 374 32 <src.dctag.spl_energia>
dtext 123 331 32 <src.dctag.spl_fogo>
dtext 80 220 32 <src.dctag.spl_morte>
dtext 123 107 32 <src.dctag.spl_ar>
gumppic 152 0 2440
dtext 182 1 47 Dominios da magia
button 88 390 2429 2427 1 0 1
button 322 392 2420 2418 1 0 0

[DIALOG d_mdomains text]
Materia
Agua
Vida
Terra
Energia
Fogo
Morte
Ar
+3
3
3
3
3
3
3
3
3
Dominios da magia

[DIALOG d_mdomains button]
ON=0
// Cancel
// 

ON=1
// Okay
// 
tag.newdomains=<ctag0.newdomains>
tag.spl_materia=<tag0.spl_materia>
tag.spl_agua=<ctag0.spl_agua>
tag.spl_vida=<ctag0.spl_vida>
tag.spl_terra=<ctag0.spl_terra>
tag.spl_energia=<ctag0.spl_energia>
tag.spl_fogo=<ctag0.spl_fogo>
tag.spl_morte=<ctag0.spl_morte>
tag.spl_ar=<ctag0.spl_ar>

ON=2
// +Materia
addDomain spl_materia
DIALOG d_mdomains

ON=3
// +agua
// 
addDomain spl_agua
DIALOG d_mdomains

ON=4
// +vida
// 
addDomain spl_vida
DIALOG d_mdomains

ON=5
// +terra
// 
addDomain spl_terra
DIALOG d_mdomains

ON=6
// +energia
// 
addDomain spl_energia
DIALOG d_mdomains

ON=7
// +fogo
// 
addDomain spl_fogo
DIALOG d_mdomains

ON=8
// +morte
// 
addDomain spl_morte
DIALOG d_mdomains

ON=9
// +ar
// 
addDomain spl_ar
DIALOG d_mdomains

ON=10
// para .magia
// 
mago

[FUNCTION addDomain]
try src.ctag.<args> = <eval <src.ctag0.<args>>+1>
src.ctag.newdomains -= 1

[PLEVEL 1]
magia
mago

[FUNCTION magia]
ctag.roll=(<tag0.mag>*24)+70
src.dialog d_magia
return 1

[FUNCTION mago]
magia

[DIALOG d_magia]
200, 150
noclose
nomove
PAGE 0
gumppic 0 0 1140
gumppic 61 88 1143
gumppic 136 41 2445
text 142 41 1101 0
text 65 69 455 1
text 263 69 455 2
button <eval <ctag.roll>> 94 2117 2118 0 0 0
button 45 95 2223 2223 1 0 1
button 328 95 2224 2224 1 0 2
button 137 118 2117 2118 1 0 3
button 137 139 2117 2118 1 0 4
button 137 161 2117 2118 1 0 5
text 159 114 1345 3
text 159 136 1345 4
text 158 156 1345 5
button 156 196 249 248 1 0 6
button 320 216 4005 4006 1 0 7

[DIALOG d_magia TEXT]
Foco de magia
Suave
Impactante
Impactante
Suave
Neutra

[DIALOG d_magia BUTTON]
on=1
if (<tag0.mag><=0)
 tag.mag=0
 DIALOG d_magia
 return 1
endif
tag.mag=<eval (<tag0.mag>+(-1))>
ctag.roll=<eval (<tag0.mag>*24)+70>
DIALOG d_magia
return 1

on=2
if (<tag0.mag>>=10)
 tag.mag=10
 DIALOG d_magia
 return 1
endif
tag.mag=<eval (<tag0.mag>+1)>
ctag.roll=<eval (<tag0.mag>*24)+70>
DIALOG d_magia
return 1

on=3
tag.mag=10
ctag.roll=(<tag0.mag>*24)+70
DIALOG d_magia
return 1

on=4
tag.mag=0
ctag.roll=(<tag0.mag>*24)+70
DIALOG d_magia
return 1

on=5
tag.mag=5
ctag.roll=(<tag0.mag>*24)+70
DIALOG d_magia
return 1

on=6
return 1

on=7
f_domains
[EOF]

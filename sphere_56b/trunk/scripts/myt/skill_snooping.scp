//*****************************************************************************
//*****************************************************************************
//
// SKILL SNOOPING e STEALING por
//
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//TODO:

//TAGS usadas:
//tag.light_lvl     //0=escuro, 30=claro
//tag.snoop_time    //quando podera usar snooping novamente
//tag.snoop_bag     //ultima mochila aberta
//tag.snoop_cnt     //quantas tentativas ja foram


//*****************************************************************************
//*****************************************************************************
// Types
//*****************************************************************************
//*****************************************************************************
[TYPEDEF t_i_phantom] //Item fantasma que fica depois que o item original for roubado
on=@Click
 I.consume
 return 1
on=@DClick
 I.consume
 return 1
on=@Equip
 I.consume
 return 1
on=@Pickup_Pack
 I.consume
 return 1


//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// sk_snooping
//*****************************************************************************
[FUNCTION sk_snooping]

if <serv.time> < <tag0.snoop_time>
    sysmessagered Aguarde antes de tentar novamente!
    return 1
elif <act.topobj.npc>    
    sysmessagered Nao pode furtar NPCs.
    return 1
endif

if <tag0.snoop_bag>!=<act>
    tag.snoop_bag=<act>
    tag.snoop_cnt=0
endif    

//so ganha skill 1x por bag a cada 30 segundos
if <serv.time> > <ctag0.snoop_<act>>
    skill_gain skill_snooping
    TRY ctag0.snoop_<act> = <eval <serv.time>+300>
endif

tag0.snoop_time=<eval <serv.time>+20> //1 por seg

if <belltest <snooping>,<SNOOP_CALC_DIF <act.topobj>>>
    sysmessagegreen Voce abriu a bolsa com sucesso!
    act.open
else
    sysmessagered Voce falhou ao tentar abrir a bolsa.
    emotered tenta abrir a bolsa
//    SNOOP_WARN <act.topobj>
    disturb
endif

if <tag.snoop_cnt> < 20
    tag.snoop_cnt += 1
endif

return 1




//*****************************************************************************
// sk_stealing
//*****************************************************************************
[FUNCTION sk_stealing]
sysmessagegreen Selecione o item que deseja furtar.
targetf sk_stealing_targ
return 1

//*****************************************************************************
// sk_stealing_targ
//*****************************************************************************
[FUNCTION sk_stealing_targ]

if !<argo>
    sysmessagered Cancelado!
    return 1
elif <argo.ischar>
    sysmessagered Selecione somente itens!
    return 1
elif (<argo.topobj>==<uid>)
    sysmessagered Nao pode furtar si mesmo.
    return 1
elif (<argo.cont.ischar>)
    sysmessagered Nao pode furtar itens expostos.
    return 1
elif (<argo.topobj.distance> > 1)
    sysmessagered Muito longe!
    return 1
elif <argo.topobj>==<argo>
    sysmessagered Nao precisa furtar isto!
    return 1
elif (<argo.attr>&attr_move_never)    
    sysmessagered Esta fixo e nao pode ser furtado!
    return 1
//elif (!<act.topobj.findlayer.layer_pack.rescount>)
// return 1
elif <serv.time> < <tag0.snoop_time>
    sysmessagered Aguarde antes de tentar novamente!
    tag0.snoop_time= <eval <serv.time>+30> //3 por seg
    return 1
else
    //0 to 10.0 stones
    LOCAL.mw=<eval <stealing>/10>

    //weight per item
    LOCAL.iw=<eval <argo.weight>/<argo.amount>>
    
    //max items that can be stolen
    if <LOCAL.iw>
        LOCAL.ia=<eval <LOCAL.mw>/<LOCAL.iw>>
    else
        LOCAL.ia=<argo.amount>
    endif
    
//    emoteyellow iw:<dLOCAL.iw> ia:<dLOCAL.ia>

    //if cant
    if !<LOCAL.ia>
        sysmessagered Muito pesado!
        return 1
    endif
endif

skill_gain skill_stealing
tag0.snoop_time= <eval <serv.time>+30> //3 por seg

if <belltest <stealing>,<SNOOP_CALC_DIF <argo.topobj>>>
    sysmessagegreen Voce furtou <argo.name>!
    
serv.newdupe <argo> 
	if <LOCAL.ia> >= <argo.amount> 
		new.amount=<argo.amount>
		argo.type = t_i_phantom
	else 
		new.amount=<LOCAL.ia> 
		argo.decrement <LOCAL.ia>
	endif
	new.bounce
else
    sysmessagered Voce falhou ao tentar furtar!
    argo.topobj.sysmessageyellow Voce nota alguem mexendo em sua mochila!
    TRIGGER @CallGuards,<DEF.TAT_AS_ARGN>,<DEF.TOWN_CAN_STEAL>
    SNOOP_WARN <argo.topobj>
    disturb
    
    //tem uma chance de mostrar pra todos q errou
    if <R1200> > <stealing>
        emotered tenta furtar
    endif        
endif

tag.snooping_cnt = 0
if <tag.snoop_cnt> < 20
    tag.snoop_cnt += 1
endif

return 1

//*****************************************************************************
// SNOOP_CALC_DIF <target_pc>
//*****************************************************************************
[FUNCTION SNOOP_CALC_DIF]
OBJ=<argn1>
//Deficuldade por tentativas. 5% por tentativa.
LOCAL.diff=<eval <tag0.snoop_cnt>*5>

//Dificuldade pela luz: 30%
if !(<REGION.FLAGS>&region_flag_underground)
    LOCAL.diff += <eval 30-<DEF.luz_<dvar.thour>>>
endif

//Dificuldade pela DEX do alvo 50%
LOCAL.diff += <BETWEEN 0,50,<obj.dex>,100>

//Dificuldade pelo Snooping do alvo 20%
LOCAL.diff += <BETWEEN 0,20,<obj.snooping>,1000>

//Modificaor para NPCs: +10%
if (<obj.NPC>)
    LOCAL.diff += 10
endif

//Modificaor para NPCs guardas: +40%
if (<obj.IsTEvent.e_town_guard>)
    LOCAL.diff += 40
endif

//Modificao para stealth/hidding: -30%
if <flags>&statf_hidden
     LOCAL.diff -= 20
endif

//Correção de valor negativo
if (<local.diff> < 0)
    LOCAL.diff=0
elif (<local.diff> > 200)
    LOCAL.diff=2000
else
    LOCAL.diff = <eval <LOCAL.diff>*10>
endif

RETURN <LOCAL.diff>

//*****************************************************************************
// SNOOP_WARN <target_pc>
//*****************************************************************************
[FUNCTION SNOOP_WARN]
OBJ=<argn1>
if (<obj.IsTEvent.e_town_guard>)
    obj.emotered cotovelada
    damage <R10,30> DAM_PHYSICAL <obj.UID>
    obj.timerf 1,say,Aha! Acha que eh <sex esperto/esperta>, <tag.name>?
    obj.timerf 3,say,Voce esta <sex banido/banida> de <serv.area.<get_town>.name>. Ouviu? <sex Banido/Banida>!
    TOWN_BAN_PLAYER <UID>
    faint
    timerf 8,f_wakeup
    return 1000
elif (<region.tag0.town_type>)
    if (<tag0.comcapuz>)
        //Consegue tirar o capuz?
        if (<R0,<str>> > <obj.str>)
            obj.emoteyellow aranca o capuz
            obj.timerf 1,say,<tag.name>! Eu sabia! Guardas!
            capuz
            TOWN_BAN_PLAYER <UID>
        endif
    else
        TOWN_BAN_PLAYER <UID>
    endif
    obj=<uid>
    forchars 8
        if (<IsTEvent.e_town_guard>) && (<obj.CanSeeLOS>)
            timerf 1,say Ladrao!
            act=<obj>
            GUARD_ATTACK <uid>
            return 1000
        endif
    endfor
endif

[EOF]

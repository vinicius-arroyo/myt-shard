//*****************************************************************************
//**                        Sistema de plantação v1.6                        **
//**                          Mystical Tales Shard                           **
//*****************************************************************************

//Tabela de valor MORE2 contido em cada objeto de semente para cada fruto a ser colhido.
//
//  MORE2       FRUTO       ITEMDEF
//
//  0001        batata      i_sem_batata
//  0002        milho       i_sem_milho
//  0003        ervilha     i_sem_ervilha
//  0004        trigo       i_sem_trigo
//  0005        cenoura     i_sem_cenoura
//  0006        alface      i_sem_alface
//  0007        ginsen      i_sem_ginseng
//  0008        mandrake    i_sem_mandrake
//  0009        alho        i_sem_alho
//  0010        nightshade  i_sem_nightshade
//  0011        macieira    i_sem_maca
//  0012        parreira    i_sem_uva
//  0013        bananeira   i_sem_banana
//  0014        algodoeiro  i_sem_algodao
//  0015        fumo        i_sem_fumo


//Tabela Pra Cores por tipo de reagente
//----------------------------------------
//Comidas   = 433
//Reagentes = 510
//Arvores   = 568
//Etc       = 343 (algodão, fumo, etc)
//----------------------------------------------

[PLEVEL 1]
tirarsemente 

//*****************************************************************************
//**                        Configurações do sistema                         **
//*****************************************************************************

//####################### Mude conforme a necessidade #########################

[DEFNAME Config_de_plantação]
// configurações gerais. Altere conforme necessário.

tempo_de_plantacao=60*60
//Tempo total de crescimento de uma planta para que ela se torne madura.
//No caso das árvores perenes, o valor será triplicado automaticamente.

tempo_de_decay_planta=15
//Tempo em segundos entre a colheita do ultimo fruto e a 'morte' da planta.
//O player só poderá tentar colher sementes durante este intervalo de tempo.

ferramenta_para_colher=;3921;3922;11552;11564;11553;11565;5120;
//DispID Decimal separados por ';' das ferramentas que devem estar na mão do player para ele conseguir colher
//os frutos. Coloque 0 (zero) para desabilitar a necessidade de uma ferramenta.

//Quantas sementes podem ter em uma sacola
sementes_limite=20

//*****************************************************************************
//**                                TYPEDEF                                  **
//*****************************************************************************

//*********************
[TYPEDEF t_seed]
//*********************
//Sementes plantáveis
ON=@CLICK
MESSAGE <name> (<eval <more1>>)
return 1

ON=@CLIENTTOOLTIP
f_sendTooltip <name>,Quantidade: <eval <more1>>
return 1

ON=@dclick
target Selecione a terra arada:
return 1

ON=@TARGON_GROUND
TARGET Voce nao pode plantar nisso. Selecione onde plantar:
RETURN 1

ON=@TARGON_CHAR
TARGET Voce nao pode plantar em criaturas. Selecione onde plantar:
RETURN 1

ON=@TARGON_ITEM
if (<src.targ.baseid>==i_terra_arada)
    if (<src.targ.distance> > 1)
        SRC.SYSMESSAGEred Voce esta muito longe.
        return 1
    endif
    if (<src.targ.link> != 04fffffff)
        SRC.SYSMESSAGEred Esta terra ja esta sendo usada. Use outra.
        return 1
    endif
//-?-QUE TIPO DE SEMENTE É? QUE PLANTA VAI NASCER?
    if (!<IsEmpty <tag.planta>>)
        serv.newitem <tag.planta>
        
    /////////////////////////////////
    ///         ATENÇÃO!!         ///
    /// Este bloco pode ser apaga-///
    ///do depois de 1/03/2010.    ///
    /////////////////////////////////
    elseif (<more2>==0001)
        serv.newitem i_plan_batata1
    elseif (<more2>==0002)
        serv.newitem i_plan_milho1
    elseif (<more2>==0003)
        serv.newitem i_plan_ervilha1
    elseif (<more2>==0004)
        serv.newitem i_plan_trigo1
    elseif (<more2>==0005)
        serv.newitem i_plan_cenoura1
    elseif (<more2>==0006)
        serv.newitem i_plan_alface1
    elseif (<more2>==0007)
        serv.newitem i_plan_ginsen1
    elseif (<more2>==0008)
        serv.newitem i_plan_mandrake1
    elseif (<more2>==0009)
        serv.newitem i_plan_alho1
    elseif (<more2>==0010)
        serv.newitem i_plan_nightshade1
    elseif (<more2>==0011)
        serv.newitem i_plan_maca1
    elseif (<more2>==0012)
        serv.newitem i_plan_uva1
    elseif (<more2>==0013)
        serv.newitem i_plan_banana1
    elseif (<more2>==0014)
        serv.newitem i_plan_algodao1
    /////////////////////////////////
    /// Apagar o bloco até aqui.  ///
    /////////////////////////////////
    else
        //tipo indefinido
        src.sysmessageyellow Erro: Semente inexistente.
        remove
        return 1
    endif
    new.p=<src.targp.x>,<src.targp.y>,<eval <src.targp.z>+1>,<src.targp.m>
    new.link=<src.targ.uid>
    src.targ.link=<new.uid>
    new.timer=<EVAL <DEF.tempo_de_plantacao>/2>
    new.moreX=<src.camping>
    more1=(<more1>-1)
    update
    src.act=0
    src.f_planta
    if (<more1>==0)
        remove
    endif
    return 1
else
    src.sysmessagered Voce nao pode plantar nisso.
    return 1
endif


//////////////////////////////
//tornando as bags stackaveis
//////////////////////////////
ON=@DropOn_Self
if (<argo.tag.planta> == <I.tag.planta>)
   if (<eval <I.more1> + <argo.more1>> > <DEF.sementes_limite>)
      argo.more1 = (<eval <I.more1> + <argo.more1>>) - <DEF.sementes_limite>
      I.more1 = <DEF.sementes_limite>
   else
      I.more1 += <argo.more1>
      argo.remove
   endif
endif
return 1


//*********************
[TYPEDEF t_broto]
//*********************
//Caindo em desuso. Só utilizado para árvores persistentes
ON=@TIMER
plant_timerBroto
return 1

ON=@DCLICK
src.sysmessageyellow Voce nao pode colher o broto.
return 1

//*********************
[TYPEDEF t_planta]
//*********************
//Plantas que vão crescendo, podem ser colhida e morrer.
//TDATA1        = DISPID do broto
//TDATA2        = DISPID do jovem
//TDATA3        = DEIPID do maduro
//TDATA4        = % chance de se pegar sementes
//MORE1L        = Minimo de frutos que ela dará
//MORE1H        = Máximo de frutos que ela dará
//MORE2L        = Mínimo de sementes que ela poderá dar
//MORE2H        = Máximo de sementes que ela poderá dar
//MOREX         = Skill Camping do cara que plantou (scripter não preenche. Variável do sistema)
//MOREY         = Que estágio está (0:broto 1:jovem 2:maduro 3:morto) (scripter não preenche. Variável do sistema)
//MOREZ         = Quantidade de frutos que há na planta. (scripter não preenche. Variável do sistema)
//TAG.FRUTO     = DEFNAME do fruto que será colhido.
//TAG.SEMENTE   = DEFNAME da semente que poderá ser colhida.
ON=@CLICK
DOSWITCH <morey>
    local.s = <name> (broto)
    local.s = <name> (jovem)
    local.s = <name> (<morez> frutos)
    local.s = <name> (morreu)   //Mas ainda colhe sementes
    local.s = <name> (morreu)   //Mas ainda colhe sementes
ENDDO
message <local.s>
return 1

ON=@CLIENTTOOLTIP
DOSWITCH <morey>
    local.s = broto
    local.s = jovem
    local.s = <morez> frutos
    local.s = morreu    //Mais ainda colhe sementes
    local.s = morreu
ENDDO
f_sendTooltip <name>,<local.s>
return 1

ON=@DCLICK
if (!<morem>)//Aída é colhivel?
    DOSWITCH (0<morey>)
        src.sysmessageyellow Nao ha frutos. Eh apenas um brotinho.
        src.sysmessageyellow Nao ha frutos. A planta ainda esta jovem.
        src.f_colhe <uid>
        src.f_sementes <uid>
    ENDDO
endif
RETURN 1

ON=@TIMER
DOSWITCH <morey>
    BEGIN
        morey += 1
        DISPID=<TDATA2>
        UPDATE
        TIMER=<eval <DEF.tempo_de_plantacao>/2>
    END
    BEGIN
        morey += 1
        DISPID=<TDATA3>
        TIMER=<eval <DEF.tempo_de_plantacao>/2>
        morez=<R<more1l>,<BETWEEN <more1l>,<more1h>,<morex>,100.0>>
        UPDATE
    END
    BEGIN
        RETURN 1
    END
    BEGIN
        emotered morreu
        timer=4
        morey += 1
    END
    BEGIN
        if (<link>)
            link.more1 -= 1
            link.link=04fffffff
            if (!<link.more1>)
                link.remove
            endif
        endif
        remove
    END
ENDDO
return 1

//*********************
[TYPEDEF t_arvore]
//*********************
//Árvore persistente. Regenera frutos e não morre.
ON=@CREATE
attr=010
timer=<def.tempo_de_plantacao>
more1=0
more2=0

ON=@TIMER
more2=<R3,7>
return 1

ON=@DCLICK
if ( <src.targ.distance> > 1)
    src.sysmessageyellow Voce esta muito longe.
    return 1
elif (<src.ctag0.busy> > <serv.time>)
    src.sysmessageyellow Deve esperar antes de tentar novamente.
    return 1
elif (<more2>)
    src.ctag.busy=<eval <serv.time>+10>
    src.f_colhe_arvore <uid>
    more2 -= 1
    if (!<more2>)
        timer=<def.tempo_de_plantacao>
    endif
    update
else
    src.sysmessageyellow Nao ha frutos nessa arvore.
    if (!<more2>) && (<timer>== -1)
        timer=<def.tempo_de_plantacao>
    endif    
endif
return 1



//*****************************************************************************
//**                           Functions e Memorys                           **
//*****************************************************************************

//*********************
[ITEMDEF i_mry_planta]
//*********************
//Animação de plantação
ID=I_MEMORY
TYPE=T_EQ_SCRIPT
NAME=Planta

ON=@CREATE
SRC.ACT.EQUIP
TIMER=1

ON=@EQUIP
if (!<cont.isgm>)
    cont.flags |= 000000004
endif
return 1

ON=@DCLICK
SRC.ACT.EQUIP
return 1

ON=@TIMER
    sfx 0145
    cont.anim 12
    if (<cont.IsGM>)
        more1=5
    endif
if (<more1>==5)
    if (!<cont.isgm>)
        cont.flags ^= 000000004
    endif
    remove
    return 1
else
    more1=(<more1>+1)
    TIMER=1
    return 1
endif

//*********************
[FUNCTION F_PLANTA]
//*********************
//Função chamada pela semente
src.newitem=i_mry_planta
src.act.equip
RETURN 1

//*********************
[FUNCTION f_colhe]
//*********************
//Dclick na planta <argv0> para colher os frutos
obj=<argv0>
if (<src.distance <obj.P>> > 1)
    src.sysmessageyellow Voce esta muito longe.
    return 1
elif (<src.ctag0.busy> > <serv.time>)
    src.sysmessageyellow Deve esperar antes de tentar novamente.
    return 1
elif (<IsEmpty <DEF.ferramenta_para_colher>>) || ( ( (STRMATCH(*<src.findlayer.layer_hand1.dispiddec>*,<DEF.ferramenta_para_colher>)) || (STRMATCH(*<src.findlayer.layer_hand2.dispiddec>*,<DEF.ferramenta_para_colher>)) ) && (<src.findlayer.layer_hand1.dispiddec>) )
    src.newitem=<obj.tag.fruto>
    src.anim 12
    src.bounce <new>
    src.sfx 004f
    src.emoteyellow colhendo <src.act.name>
    src.skill_gain SKILL_CAMPING
else
    src.sysmessagered Voce nao esta equipado com ferramenta correta.
    src.emotered estragou um fruto
endif
obj.morez -= 1
obj.timer=<DEF.tempo_de_decay_planta>
if (!<obj.morez>)
    obj.morey += 1
endif
src.timerf 2 uid.<obj>.use
return 1

//*********************
[FUNCTION f_colhe_arvore]
//*********************
//Dclick na árvore persistente <argv0> para colher frutos
obj=<argv0>
if (<IsEmpty <DEF.ferramenta_para_colher>>) || ( ( (STRMATCH(*<src.findlayer.layer_hand1.dispiddec>*,<DEF.ferramenta_para_colher>)) || (STRMATCH(*<src.findlayer.layer_hand2.dispiddec>*,<DEF.ferramenta_para_colher>)) ) && (<src.findlayer.layer_hand1.dispiddec>) )
    src.newitem=<obj.TDATA1>
    src.anim 12
    src.bounce <new>
    src.sfx 004f
    src.emoteyellow colhendo <src.act.name>
else
    src.sysmessagered Voce nao esta equipado com ferramenta correta.
    src.emotered estragou um fruto
endif
return 1

//*********************
[FUNCTION f_sementes]
//*********************
//Dclick na planta <argv0> para colher sementes, quando não há mais frutos
obj=<argv0>
if (<src.distance <obj.P>> > 1)
    src.sysmessageyellow Voce esta muito longe.
    return 1
elif (<src.ctag0.busy> > <serv.time>)
    src.sysmessageyellow Deve esperar antes de tentar novamente.
    return 1
elif ( <obj.TDATA4> >= <r100>)
    if (<IsEmpty <DEF.ferramenta_para_colher>>) || ( ( (STRMATCH(*<src.findlayer.layer_hand1.dispiddec>*,<DEF.ferramenta_para_colher>)) || (STRMATCH(*<src.findlayer.layer_hand2.dispiddec>*,<DEF.ferramenta_para_colher>)) ) && (<src.findlayer.layer_hand1.dispiddec>) )
        serv.newitem=<obj.tag.semente>
        new.more1=<R<obj.more2l>,<BETWEEN <obj.more2l>,<obj.more2h>,<obj.morex>,100.0>>
        src.bounce <new>
        src.anim 12
        src.sfx 004f
        src.emoteyellow colhendo <new.name>
        src.sysmessageyellow Voce retirou <new.more1> sementes da planta.

//-------------------[Verificando se player ja tem essa semente e adicionando a sacola]---------------------

        FORCONTID <obj.tag.semente> 10
            if (<uid> != <new>)
                if (<eval <new.more1> + <more1> > < <DEF.sementes_limite>)
                    more1 += <eval <new.more1>>
        //          new.more1 = 0
                    new.remove
                else
                    new.more1 = <eval <more1> + <new.more1>> - <DEF.sementes_limite>
                    more1 = <DEF.sementes_limite>
                    if (<new.more1> == 0)
                        new.remove
                    endif
                endif
            endif
        ENDFOR

    else
        src.sysmessagered Voce nao esta equipado com ferramenta correta.
    endif
else
    src.anim 12
    src.sfx 004f
    src.sysmessageyellow Voce nao tirou novas sementes da planta.
endif
obj.morem = 1
obj.timerd=1
return 1

//*********************
[FUNCTION plant_checkTile]
//*********************
//Start de uso do arado
sysmessageyellow Selecione aonde arar:
TARGETFG _plant_checkTile

[FUNCTION _plant_checkTile]
LOCAL.t <serv.map(<targp.x>,<targp.y>).type>
LOCAL.s <serv.map(<targp.x>,<targp.y>).statics(0).name>
//serv.log <serv.map(<targp.x>,<targp.y>).statics(0).name>/<serv.map(<targp.x>,<targp.y>).statics(0).ID>
IF (<distance <targp>> > 1)
 sysmessageyellow Muito longe.
 return 0
ELIF (<region.flags>&region_flag_nobuilding)
 sysmessageyellow Nao se pode plantar ou construir nessa area por ordens superiores.
 return 0
ELIF (<LOCAL.t>!=t_grass)
 sysmessageyellow Voce so pode fazer isso na grama.
 return 0
ELIF (<region.flags>&region_flag_underground) || (<flags>&statf_indoors)
 sysmessageyellow Aqui nao bate sol. Um vegetal nao pode crescer aqui.
 return 0
ELIF (!<ISNUM <LOCAL.s>>)
 sysmessageyellow Nao eh um bom lugar para plantar.
 return 0
ELIF (<flags>&0c0c4240e)
 sysmessageyellow Voce nao esta em condicoes de trabalhar.
 return 0
ELIF (<ARGO.UID>)
 IF (<argo.baseid>==i_terra_arada)
  sysmessageyellow Esta terra ja foi arada.
  return 0
 ELSE
  sysmessageyellow <argo.name> vai atrapalhar o plantio.
  return 0
 ENDIF
ELSE
 OBJ=<UID>
 FORITEMS 2
  IF (<p.x>==<obj.targp.x>) && (<p.y>==<obj.targp.y>) && (<p.z>-<obj.z> <= 20) && (<p.z>-<obj.z> >= -5)
   obj.sysmessageyellow <name> vai atrapalhar o plantio.
   RETURN 0
  ENDIF
 ENDFOR
ENDIF
plant_arar <targp>
RETURN 1

//*********************
[FUNCTION plant_arar]
//*********************
//Passados testes da função 'plant_checkTile', começar a arar a terra
cTAG.cp=<argn1>,<argn2>,<argn3>
if (<IsGM>)
 cTAG.ctimer=1
else
 cTAG.ctimer=<BETWEEN2 1,10,<camping>,1000>
endif
cTAG.p.x=<p.x>
cTAG.p.y=<p.y>
_plant_arar

[FUNCTION _plant_arar]
if (<p.x>!=<cTAG0.p.x>) || (<p.y>!=<cTAG0.p.y>)
 sysmessagered Voce parou de arar
 return 1
endif
sfx 0145
anim 12
cTAG.ctimer -= 1
IF (<cTAG0.cTIMER>)
 TIMERF 1,_plant_arar
ELSE
 serv.newitem i_terra_arada
 new.p=<cTAG.cp>
// new.attr=010
 new.more1=<BETWEEN 4,10,<camping>,1000>
 if (<ctag0.craft_tool>)
  uid.<ctag0.craft_tool>.hits -= 1
  if (!<uid.<ctag0.craft_tool>.hits>)
   uid.<ctag0.craft_tool>.remove
   sysmessagered Seu arado foi arruinado.
  else
   uid.<ctag0.craft_tool>.update
  endif
  ctag.craft_tool= 
 endif
 timer=-1
ENDIF

//*********************
[FUNCTION plant_timerBroto]
//*********************
//Caindo em desuso. Só utilizado pelo t_broto das árvores persistentes
if (!<IsEmpty <tag.planta>>)
 serv.newitem <tag.planta>
 new.p=<p>
 new.timer=<eval <DEF.tempo_de_plantacao>/2>
 new.link=<link>
 link.link=<new.uid>
 remove
endif



//===========================[Função pra separar as sementes das bags]============================

//------------------[Target]
//******************
[FUNCTION tirarsemente]
//******************
//Usado pelo player pra retirar semente das bags
Targetf f_tirarsemente <argn1>
return 1

//-------------------[Função]
//******************
[FUNCTION f_tirarsemente]
//******************
if (<argo.tag0.planta> != 0)
  if (<argo.more1> < <argn1>
     src.sysmessage a sacola nao tem tantas sementes assim.
     return 1
  else
     argo.more1 -= <argn>
     Serv.newitem <argo.id>, 1, <Src.Findlayer.21>
     new.name = <argo.name>
     new.type = <argo.type>
     new.tag.planta = <argo.tag.planta>
     new.more1 = <argn1>
     new.color = <argo.color>

     if (<new.more1> == 0)
        new.remove
     endif
  endif 
else
  src.sysmessage selecione uma sacola de sementes para fazer isso.
endif
return 1

//==============================[Fim da função]


//*****************************************************************************
//**                             Terra e Arado                               **
//*****************************************************************************


[ITEMDEF i_terra_arada]
//Terra para plantar
NAME=terra arada
ID=0914
DEFNAME=i_terra_arada
TYPE=t_script
FLIP=0
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Terra arada

ON=@CREATE
attr=010
more1=<r2,3>
timer=-1

ON=@TIMER
if (<more1> < 1)
 remove
endif
return 1

on=@click
message <name>
if (<src.camping> > 199)
 message [Camping] Mais <eval <more1>> <QVAL <more1> < 2?plantacao:plantacoes>
endif
return 1

ON=@CLIENTTOOLTIP
if (<src.camping> > 199)
 f_sendTooltip <name>,[Camping] Mais <eval <more1>> <QVAL <more1> < 2?plantação:plantações>
endif
return 1


[ITEMDEF i_arado]
//Arado
NAME=arado
ID=01507
DEFNAME=i_arado
RESOURCES=2 i_ingot_iron, 1 i_log
SKILLMAKE=32.0 Tinkering
TYPE=t_crafting_tool
WEIGHT=8
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Arado
TDATA4=200

ON=Create
MAXHITS={10 40}
HITS={5 <MAXHITS>}

ON=@DCLICK
src.plant_checkTile
return 1

//*****************************************************************************
//**                                Sementes                                 **
//*****************************************************************************

//########################### Vegetais Comestíveis#############################


[ITEMDEF i_sem_batata]
NAME=sementes de batata
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de batata

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 1,6,<src.camping>,1000>
ELSE
    MORE1=<R1,6>
ENDIF
TAG.PLANTA=i_plan_batata1
COLOR=433


[ITEMDEF i_sem_milho]
NAME=sementes de milho
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de milho

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 2,6,<src.camping>,1000>
ELSE
    MORE1=<R2,6>
ENDIF
TAG.PLANTA=i_plan_milho1
COLOR=433


[ITEMDEF i_sem_ervilha]
NAME=sementes de ervilha
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de ervilha

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 2,6,<src.camping>,1000>
ELSE
    MORE1=<R2,6>
ENDIF
TAG.PLANTA=i_plan_ervilha1
COLOR=433


[ITEMDEF i_sem_trigo]
NAME=sementes de trigo
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de trigo

ON=@CREATE
MORE1=7
TAG.PLANTA=i_plan_trigo1
COLOR=433


[ITEMDEF i_sem_cenoura]
NAME=sementes de cenoura
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de cenoura

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 2,6,<src.camping>,1000>
ELSE
    MORE1=<R2,6>
ENDIF
TAG.PLANTA=i_plan_cenoura1
COLOR=433


[ITEMDEF i_sem_alface]
NAME=sementes de alface
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de alface

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 2,6,<src.camping>,1000>
ELSE
    MORE1=<R2,6>
ENDIF
TAG.PLANTA=i_plan_alface1
COLOR=433


//################################ Reagentes ##################################

[ITEMDEF i_sem_ginseng]
NAME=sementes de ginseng
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de Ginseng

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 4,8,<src.camping>,1000>
ELSE
    MORE1=<R4,8>
ENDIF
TAG.PLANTA=i_plan_ginsen1
COLOR=510


[ITEMDEF i_sem_mandrake]
NAME=sementes de mandragora
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de mandrake

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 4,8,<src.camping>,1000>
ELSE
    MORE1=<R4,8>
ENDIF
TAG.PLANTA=i_plan_mandrake1
COLOR=510


[ITEMDEF i_sem_alho]
NAME=sementes de alho
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de alho

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 4,8,<src.camping>,1000>
ELSE
    MORE1=<R4,8>
ENDIF
TAG.PLANTA=i_plan_alho1
COLOR=510


[ITEMDEF i_sem_nightshade]
NAME=sementes de sombradanoite
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de nightshade

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 4,8,<src.camping>,1000>
ELSE
    MORE1=<R4,8>
ENDIF
TAG.PLANTA=i_plan_nightshade1
COLOR=510

//########################### arvores persistentes ############################

[ITEMDEF i_sem_maca]
NAME=semente de macieira
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de maçã

ON=@CREATE
MORE1=1
TAG.PLANTA=i_plan_maca1
COLOR=568


[ITEMDEF i_sem_uva]
NAME=semente de parreira
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de uva

ON=@CREATE
MORE1=<BETWEEN 1,6,<src.camping>,1000>
TAG.PLANTA=i_plan_uva1
COLOR=568


[ITEMDEF i_sem_banana]
NAME=semente de bananeira
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de banana

ON=@CREATE
MORE1=<BETWEEN 1,6,<src.camping>,1000>
TAG.PLANTA=i_plan_banana1
COLOR=568

//################################### Outros #############################


[ITEMDEF i_sem_algodao]
NAME=sementes de pe de algodao
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de algodão

ON=@CREATE
IF (!<IsEmpty <src>>)
    MORE1=<BETWEEN 1,6,<src.camping>,1000>
ELSE
    MORE1=<R1,6>
ENDIF
TAG.PLANTA=i_plan_algodao1
COLOR=343

//*** fumo ***

[ITEMDEF i_sem_fumo]
NAME=Sementes de erva de fumo
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de erva de fumo

ON=@CREATE
IF (!<IsEmpty <src>>)    //Criado por alguém que está colhendo?
    MORE1=<BETWEEN 2,4,<src.camping>,1000>
ELSE                             //Ng criou. Spawn ou loot
    MORE1=<R2,4>
ENDIF
TAG.PLANTA=i_plan_fumo1
COLOR=343


//*****************************************************************************
//**                         Plantas e Suas Evoluções                        **
//*****************************************************************************

//############################ Vegetais Comestíveis ###########################

//****batata****

[ITEMDEF i_plan_batata1]
ID=0c5e
TYPE=t_planta
NAME=batata
TDATA1=0c5e //DISPID broto
TDATA2=0c5f //DISPID jovem
TDATA3=0c60 //DISPID maduro
TDATA4=75   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 2  //Mínimo de frutos
MORE1H = 3  //Máximo de frutos
MORE2L = 1  //Mínimo de sêmem
MORE2H = 4  //Máximo de sêmem
TAG.FRUTO   I_BATATA
TAG.SEMENTE i_sem_batata 

//****milho****

[ITEMDEF i_plan_milho1]
ID=0cb5
TYPE=t_planta
NAME=milho
TDATA1=0cb5 //DISPID broto
TDATA2=0c7e //DISPID jovem
TDATA3=0c7d //DISPID maduro
TDATA4=75   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 1  //Mínimo de frutos
MORE1H = 1  //Máximo de frutos
MORE2L = 1  //Mínimo de sêmem
MORE2H = 4  //Máximo de sêmem
TAG.FRUTO   I_FRUIT_CORN
TAG.SEMENTE i_sem_milho


//****ervilha****

[ITEMDEF i_plan_ervilha1]
ID=0cb5
TYPE=t_planta
NAME=ervilha
TDATA1=0cb5 //DISPID broto
TDATA2=0cb0 //DISPID jovem
TDATA3=0c8d //DISPID maduro
TDATA4=65   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 2  //Mínimo de frutos
MORE1H = 4  //Máximo de frutos
MORE2L = 2  //Mínimo de sêmem
MORE2H = 4  //Máximo de sêmem
TAG.FRUTO   I_ERVILHA
TAG.SEMENTE i_sem_ervilha


//****trigo****

[ITEMDEF i_plan_trigo1]
ID=01EBF
TYPE=t_planta
NAME=trigo
TDATA1=01EBF //DISPID broto
TDATA2=0c59  //DISPID jovem
TDATA3=0c5a  //DISPID maduro
TDATA4=95    //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 1  //Mínimo de frutos
MORE1H = 1  //Máximo de frutos
MORE2L = 1  //Mínimo de sêmem
MORE2H = 3  //Máximo de sêmem
TAG.FRUTO   i_wheat_sheaf
TAG.SEMENTE i_sem_trigo


//****cenoura****

[ITEMDEF i_plan_cenoura1]
ID=0c68
TYPE=t_planta
NAME=cenoura
TDATA1=0c68 //DISPID broto
TDATA2=0c69 //DISPID jovem
TDATA3=0c76 //DISPID maduro
TDATA4=50   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 3  //Mínimo de frutos
MORE1H = 6  //Máximo de frutos
MORE2L = 2  //Mínimo de sêmem
MORE2H = 4  //Máximo de sêmem
TAG.FRUTO   i_fruit_carrot
TAG.SEMENTE i_sem_cenoura

//****alface****

[ITEMDEF i_plan_alface1]
ID=0c71
TYPE=t_planta
NAME=alface
TDATA1=0c71 //DISPID broto
TDATA2=0c71 //DISPID jovem
TDATA3=0c71 //DISPID maduro
TDATA4=62   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 1  //Mínimo de frutos
MORE1H = 1  //Máximo de frutos
MORE2L = 2  //Mínimo de sêmem
MORE2H = 4  //Máximo de sêmem
TAG.FRUTO   i_fruit_lettuce
TAG.SEMENTE i_sem_alface


//################################ Reagentes ##################################

//****ginsen****

[ITEMDEF i_plan_ginsen1]
ID=0cb5
TYPE=t_planta
NAME=ginseng
TDATA1=0cb5 //DISPID broto
TDATA2=0cb0 //DISPID jovem
TDATA3=018e9//DISPID maduro
TDATA4=55   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 1  //Mínimo de frutos
MORE1H = 5  //Máximo de frutos
MORE2L = 2  //Mínimo de sêmem
MORE2H = 3  //Máximo de sêmem
TAG.FRUTO   i_r_ginseng
TAG.SEMENTE i_sem_ginseng

//****mandrake root****

[ITEMDEF i_plan_mandrake1]
ID=018df
TYPE=t_planta
NAME=mandragora
TDATA1=018df//DISPID broto
TDATA2=018df//DISPID jovem
TDATA3=018df//DISPID maduro
TDATA4=55   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 1  //Mínimo de frutos
MORE1H = 4  //Máximo de frutos
MORE2L = 2  //Mínimo de sêmem
MORE2H = 3  //Máximo de sêmem
TAG.FRUTO   i_r_raiz_de_mandrake
TAG.SEMENTE i_sem_mandrake


//****alho****

[ITEMDEF i_plan_alho1]
ID=018e1
TYPE=t_planta
NAME=alho
TDATA1=018e1//DISPID broto
TDATA2=018e1//DISPID jovem
TDATA3=018e1//DISPID maduro
TDATA4=67   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 2  //Mínimo de frutos
MORE1H = 6  //Máximo de frutos
MORE2L = 2  //Mínimo de sêmem
MORE2H = 3  //Máximo de sêmem
TAG.FRUTO   i_r_alho
TAG.SEMENTE i_sem_alho

//****nightshade****

[ITEMDEF i_plan_nightshade1]
ID=0cb5
TYPE=t_planta
NAME=sombra da noite
TDATA1=0cb5 //DISPID broto
TDATA2=018e5//DISPID jovem
TDATA3=018e5//DISPID maduro
TDATA4=80   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 1  //Mínimo de frutos
MORE1H = 2  //Máximo de frutos
MORE2L = 1  //Mínimo de sêmem
MORE2H = 6  //Máximo de sêmem
TAG.FRUTO   i_r_nightshade
TAG.SEMENTE i_sem_nightshade


//################################### Arvores persistentes #############################

// **** macieira ****

[ITEMDEF i_plan_maca1]
ID=0cb5
TYPE=t_broto
NAME=macieira (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_maca2

[ITEMDEF i_plan_maca2]
ID=0CC7
TYPE=t_broto
NAME=macieira (pequena arvore)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_maca3

[ITEMDEF i_plan_maca3]
NAME=macieira
ID=0cd3
type=t_script

on=@create
attr=010
timerf 1,timerd,1

on=@timer
serv.newitem=i_arvore_macieira2
new.p=<p>
return 1

[ITEMDEF i_arvore_macieira2]
NAME=macieira
ID=0cd4
TYPE=t_arvore
TDATA1=i_fruit_apple

// **** parreira ****

[ITEMDEF i_plan_uva1]
ID=0B1
TYPE=t_broto
NAME=poste da parreira
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_uva2

ON=@DCLICK
return 1

[ITEMDEF i_plan_uva2]
ID=0CEA
TYPE=t_broto
NAME=parreira (pequena arvore)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_uva3
return 1

[ITEMDEF i_plan_uva3]
NAME=parreira
ID=0ced
type=t_arvore
TDATA1=i_fruit_grapes

[ITEMDEF i_plan_banana1]
ID=0CAB
TYPE=t_broto
NAME=bananeira (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_banana2

[ITEMDEF i_plan_banana2]
ID=0CA8
TYPE=t_broto
NAME=bananeira (pequena arvore)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_banana3

ON=@DCLICK
src.sysmessage Esta arvore e muito jovem para dar frutos.
return 1

[ITEMDEF i_plan_banana3]
NAME=bananeira
ID=0CAA
type=t_arvore
TDATA1=i_fruit_banana

//################################### Outros #############################


//*** algodao ***

[ITEMDEF i_plan_algodao1]
ID=0cb5
TYPE=t_planta
NAME=pe de algodao
TDATA1=0cb5 //DISPID broto
TDATA2=0c51 //DISPID jovem
TDATA3=0c53//DISPID maduro
TDATA4=60   //% chance de colhe sêmem

ON=@create
attr=010
MORE1L = 2  //Mínimo de frutos
MORE1H = 6  //Máximo de frutos
MORE2L = 2  //Mínimo de sêmem
MORE2H = 6  //Máximo de sêmem
TAG.FRUTO   i_cotton
TAG.SEMENTE i_sem_algodao


//*** fumo ***

[ITEMDEF i_plan_fumo1]
ID=0cb5
TYPE=t_planta
NAME=Erva de fumo
TDATA1=0cb5 //DISPID broto
TDATA2=0cc5 //DISPID jovem
TDATA3=0cc7//DISPID maduro
TDATA4=60   //% chance de colher sêmem

ON=@create
attr=010
MORE1L = 2  //Mínimo de frutos
MORE1H = 4  //Máximo de frutos
MORE2L = 1  //Mínimo de sêmem
MORE2H = 2  //Máximo de sêmem
TAG.FRUTO   i_fumo
TAG.SEMENTE i_sem_fumo


//*****************************************************************************
//**                               Novos frutos                              **
//*****************************************************************************



[ITEMDEF I_BATATA]
ID=01728
NAME=batata
WEIGHT=.1
VALUE=3
TYPE=T_FOOD_RAW
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Batata
DEFNAME2=BATATA

ON=@Create
COLOR=000f7

[ITEMDEF I_ERVILHA]
ID=0c7f
NAME=ervilha
TYPE=T_FOOD_RAW
VALUE={2 4}
WEIGHT=0.2
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Ervilha

ON=@CREATE
COLOR=00111

[ITEMDEF 09D0]
DEFNAME=i_fruit_apple
NAME=maca
VALUE=1
TYPE=T_FRUIT
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Maçã
[ITEMDEF 09d1]
DEFNAME=i_fruit_grapes
NAME=uvas
VALUE=1
TYPE=T_FRUIT
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Cacho de uvas
DUPELIST=0d1a

[ITEMDEF 09d2]
DEFNAME=i_fruit_peach
NAME=pessego
VALUE=1
TYPE=T_FRUIT
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Pêssego
DUPELIST=0172c

[ITEMDEF 0c70]
// DEFANAME2=HEAD_OF_LETTUCE
//Head of Lettuce
DEFNAME=i_fruit_lettuce
NAME=alface
TYPE=T_FOLIAGE
VALUE=2
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Alface
DUPELIST=0c71

[ITEMDEF 0c7f]
//Ear of Corn
// DEFNAME2=CORN
DEFNAME=i_fruit_corn
NAME=milho
RESOURCES=I_CROP_CORN
TYPE=T_CROPS
VALUE={2 4}
WEIGHT=0.2
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Milho
DUPELIST=0c80,0c81,0c82

[ITEMDEF 0c77]
//Carrot
DEFNAME=i_fruit_carrot
NAME=cenoura%s
RESOURCES=1 i_crop_carrot
TYPE=T_CROPS
TDATA1=i_crop_carrot
VALUE={2 4}
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Cenoura
DUPELIST=0c78

[ITEMDEF 0df9]
DEFNAME=i_cotton
NAME=algodao
VALUE=3
TYPE=t_COTTON
CATEGORY=MyT - Items by Professions
SUBSECTION=Tailor
DESCRIPTION=Algodão
DUPELIST=0def
WEIGHT=0.2

[ITEMDEF 01ebd]
//Food for animals only? Is there a distinction?
DEFNAME=i_wheat_sheaf
NAME=trigo
TYPE=t_grain
VALUE=5
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Trigo

//*****************************************************************************
//**                            Itens de emergência                          **
//*****************************************************************************


// Caixa de sementes
//
// Spawna sementes dentro dela de tanto em tanto tempo
//
//  morex - numero de sementes de reagentes
//  morey - numero de sementes de comida
//  morez - numero de semente de oturos
//  morem - numero de sementes de arvores persistentes
//  more2 - tempo de regeneração

[ITEMDEF i_caixa_sementes]
NAME=cesta de sementes
ID=i_basket_bushel
TYPE=t_container

CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=cesta de sementes

on=@create
 morex=3
 morey=2
 morez=2
 more2=60*60*24*1 //1 dias

on=@dclick
 local.next_spawn=<eval <more1>+<more2>>
 if (<eval <SERV.TIME>/10> > <local.next_spawn>)
  empty
  serv.newitem i_sem_alface
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_cenoura
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_ervilha
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_trigo
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_milho
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_batata
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_alho
  new.cont=<uid>
  new.dupe <eval <morex>-1>
  serv.newitem i_sem_mandrake
  new.cont=<uid>
  new.dupe <eval <morex>-1>
  serv.newitem i_sem_nightshade
  new.cont=<uid>
  new.dupe <eval <morex>-1>
  serv.newitem i_sem_ginseng
  new.cont=<uid>
  new.dupe <eval <morex>-1>
  serv.newitem i_sem_algodao
  new.cont=<uid>
  new.dupe <eval <morez>-1>
  more1=<eval <SERV.TIME>/10>
  update
 endif

 

//**********************************************************************************************
//**                    Script de plantação do Mystical Tales Shard v1.7                      ** 
//**                               Por: Galthar, O Errante                                    **
//**                         e-mail: galtar at terra dot com dot br                           **
//**                                                                                          **
//**                               24 de junho de 2003, 20:58hs (v1.2)                        **
//**                               28 de outubro de 2007, 23:25hs (v1.5)                      **
//**                               24 de novembro de 2007, 1:16hs (v1.6)                      **
//**                               28 de janeiro de 2010, 15:30hs (v1.6.1)                    **
//**                               01 de fevereiro de 2010, 02:28hs (v1.7)                    **
//**                                                                                          **
//**                                    "Deus é O Cara!!!"                                    **
//**********************************************************************************************
//**                                                                                          **
//**            Por favor, não altere este arquivo sem o consentimento do autor               **
//**                                                                                          **
//**********************************************************************************************

[EOF]

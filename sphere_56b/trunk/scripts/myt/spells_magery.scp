//###################################################################################
//                         Mystical Tales Shard
//                      MAGIAS DE CAMINHO "Destruição"
//                          Galthar, o Errante
//                                04/2009
//###################################################################################
//
//                              RANGE: 100 a 150
//
//###################################################################################

[comment spells_magery]
nº      Ataque
100     Dardos de fogo
104     Fireball
106     Parede de fogo
108     Combustão
110     Cela de fogo
112     Flamestrike
116     Explosão
118     Chuva de meteoros
        
        Buff
107     Chama Mística
105     Cauterizar
109     Tocha mágica
        
        Debuff
113     Esquentar arma
103     Sensibilidade ao fogo
        
        Summon
114     Horror do abismo
117     Homem Lagarto
119     Elemental do fogo
        
        Poly
111     Fogo Fátuo
        
        RP
115     Transformar em cobra
102     Quimadura
101     Fogos de artifício


Arc     Magia
Ben     Bem
Cae     Arma
Dart    Ódio
Eke     Para (motivo)
Fex     Criar
Grov    Madeira, floresta, natureza
Hag     Negar
Ixen    Fogo
Jax     Muro
Kot     Demonio
Lit     Armadura
Maj     Dar, conseder
Noch    Ganho, dinehiro, abôno
Oka     Casa
Pok     Parar
Qarac   Ultimo, anterior
Rhy     Bife, carne
Sve     Raio, trovão
Tur     Grande
Ukris   Dizer, falar, comandar
Val     Queimar
Wux     Você
Xin     Pequeno
Yesh    Conjurar, chamar
Zyak    Consequencia

//***********************************************************
//      SPELL fire darts
//***********************************************************

[ITEMDEF i_scroll_firedarts]
ID=01f2d
NAME=Pergaminho da Flecha Magica
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 20.0, MAGERY 10.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Dardos de fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=100

[Spell 100]
DEFNAME=s_firedarts
NAME=Flecha Magica
SOUND=054
RUNES=IJ
CAST_TIME=7
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa
RUNE_ITEM=2244
SCROLL_ITEM=i_scroll_firedarts
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted | spellflag_dir_anim | spellflag_fx_bolt | spellflag_fx_targ 
EFFECT_ID=i_fx_fireball_small
EFFECT=5,10
MANAUSE=5
SKILLREQ=MAGERY 10.0
INTERRUPT=25.0,100.0

on=@Effect
EFFECT 0 i_fx_fireball_small 6 16 1
local.dam=<EFFECTSPELL s_firedarts,<argn2>,<src.uid>>
local.dam += <eval <src.evaluatingintel>/150>
DAMAGE <local.dam>,DAM_FIRE,<src.uid>


src.skill_gain skill_evalint
return 1

//***********************************************************
//      SPELL FIRE Fireworks
//***********************************************************
[ITEMDEF i_scroll_fireworks]
ID=01f2d
NAME=Pergaminho dos Fogos de Artificio
RESOURCES=i_carta,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 15.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Fogos de artificio

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=101

[Spell 101]
DEFNAME=s_fireworks
NAME=Fogos de Artificio
SOUND=
RUNES=ABS
CAST_TIME=30
RESOURCES=i_r_cinza_sulfurosa
RUNE_ITEM=2282
SCROLL_ITEM=i_scroll_fireworks
FLAGS=spellflag_scripted
EFFECT_ID=
EFFECT=5,20
MANAUSE=10
SKILLREQ=MAGERY 10.0
INTERRUPT=100.0,100.0

on=@start
sfx 0357

on=@Effect
src.f_Fireworks <EFFECTSPELL s_fireworks,<src.magery>,<uid>>

src.skill_gain skill_evalint
return 1

[FUNCTION f_Fireworks]
IF !<argn> || ( <argn> > 100 )
 return 1
ENDIF
local.item={i_fx_bless_effect 1 i_fx_sparkle 1 i_fx_heal_effect 1 i_fx_sparkle_2 1 i_fx_explode 1 i_fx_explosion_3 1 i_fx_glow_spike 1}
local.sound=<hval {011C 1 011E 1 0308 1 04CF 1}>
newitem <local.item>
new.type t_normal
new.color=04EC
new.movenear <uid> 3
new.timer <R1,4>
new.attr=02
new.sfx <local.sound>
timerf <R1,<R1,2>>,f_Fireworks,<eval <argn>-1>
new.update

//***********************************************************
//      SPELL FIRE Queimadura
//***********************************************************
[ITEMDEF i_scroll_queimadura]
ID=01f2d
NAME=Pergaminho da Queimadura
RESOURCES=i_carta,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 15.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Queimadura

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=102

[Spell 102]
DEFNAME=s_queimadura
NAME=Queimadura
SOUND=0359
RUNES=VED
CAST_TIME=20
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
RUNE_ITEM=2267
SCROLL_ITEM=i_scroll_queimadura
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_harm
EFFECT_ID=
EFFECT=5,10
DURATION=60,5*60
MANAUSE=10
SKILLREQ=MAGERY 20.0
INTERRUPT=100.0,100.0

on=@START
if <act.restest i_mry_queimadura>
src.sysmessageyellow Esse alvo ja tem esse efeito. Espere <act.FINDID.i_mry_queimadura.TIMER> segundos.
return 1
endif


on=@SUCCESS
serv.newitem i_fx_smoke
new.attr = attr_static|attr_decay
new.timer = 1
new.p = <act.p>


on=@Effect
local.effect=<EFFECTSPELL s_queimadura,<argn2>,<src.uid>>
local.effect += <eval <src.evaluatingintel>/150>


local.time=<DURATIONSPELL s_queimadura,<argn2>,<src.uid>>

newitem=i_mry_queimadura
new.tag.modstr=-<local.effect>
new.link <uid>
trysrc <uid> src.new.equip
new.timer=<local.time>

IF (<hits> > <maxhits>)
     hits=<maxhits>


src.skill_gain skill_evalint
return 0 



[ITEMDEF i_mry_queimadura]
NAME=Queimadura
ID=2267
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=e_BuffStats

ON=@Create
ATTR=attr_cursed2

on=@equip
cont.sysmessagegreen A queimadura doi muito!

on=@unequip
cont.sysmessagegreen A dor comeca a aliviar.

on=@timer
remove
return 1



//***********************************************************
//      SPELL Fire Weakness
//***********************************************************
[ITEMDEF i_scroll_fire_weakness]
ID=01f2d
NAME=Pergaminho da Sensibilidade ao Fogo
RESOURCES=i_carta,i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 30.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Sensibilidade ao fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=103

[ITEMDEF i_rune_fire_weakness]
NAME=Fire Weakness
ID=020A4
TYPE=t_eq_script
LAYER=layer_special

on=@equip
cont.sysmessagered Voce esta extremamente sensivel ao fogo!
cont.tag0.aggravate |= <DEF.DAM_FIRE>
ATTR=attr_cursed2

on=@unequip
cont.sysmessagered Sua sensibilidade ao fogo passou.
cont.sfx 0208
cont.tag0.aggravate &= ~<DEF.DAM_FIRE>

on=@timer
remove
return 1

[Spell 103]
DEFNAME=s_fire_weakness
NAME=Sensibilidade ao fogo
RUNES=YDV
CAST_TIME=15
SOUND=0349
RESOURCES=i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_cinza_sulfurosa
RUNE_ITEM=2283
FLAGS=spellflag_targ_char | spellflag_scripted | spellflag_harm | spellflag_scripted
SCROLL_ITEM=i_scroll_fire_weakness
RESOURCES=i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_asa_morcego,i_r_cinza_sulfurosa
EFFECT_ID=0
EFFECT=
DURATION=60,5*60
MANAUSE=14
SKILLREQ=MAGERY 20.0
INTERRUPT=20.0,100.0

on=@EFFECT
IF (<tag0.aggravate>&DAM_FIRE) //Já tem dano agravado por fogo?
 if (<restest i_rune_fire_weakness>) //O que agrava o dano é essa magia?
  findid.i_rune_fire_weakness.timerd 1 //Então cancele a magia e fim.
 endif //Se ele já tiver agravamento por fogo, pode ser natural ou ter outra fonte. Abortar.
ELIF (<tag0.immune>&DAM_FIRE) || (<tag0.regendam>&DAM_FIRE) //Ele pode sofrer agravante por fogo?
 src.sysmessagered <name> eh imune a fogo!
ELSE //Ele ainda não tem agravamento de dano para fogo, não é imune e nem se regenera com fogo.
 newitem i_rune_fire_weakness
 trysrc <uid> src.new.equip
 new.timer=<DURATIONSPELL 103,<argn2>,<src.uid>>
 effect 3 i_fx_curse 0 33 0
ENDIF

src.skill_gain skill_evalint
return 1


//***********************************************************
//      SPELL Fireball
//***********************************************************
[ITEMDEF i_scroll_fire_ball]
ID=01f2d
NAME=Pergaminho da Bola de Fogo
RESOURCES=i_carta,i_r_raiz_de_mandrake,i_r_perola_negra,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 45.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Bola de fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=104

[Spell 104]
DEFNAME=s_fire_ball
NAME=Bola de fogo
SOUND=snd_SPELL_FIREBALL
RUNES=VI
CAST_TIME=10
RESOURCES=i_r_raiz_de_mandrake,i_r_perola_negra,i_r_cinza_sulfurosa
RUNE_ITEM=2257
SCROLL_ITEM=i_scroll_FIRE_BALL
FLAGS=SPELLFLAG_TARG_OBJ | SPELLFLAG_DIR_ANIM | SPELLFLAG_HARM
EFFECT=10,15
MANAUSE=10
SKILLREQ=MAGERY 30.0
INTERRUPT=100.0,100.0

on=@Effect
EFFECT 0 i_fx_fireball 6 16 1
local.dam=<EFFECTSPELL s_fire_ball,<argn2>,<src.uid>>
local.dam += <eval <src.evaluatingintel>/130>
DAMAGE <local.dam>,DAM_FIRE|DAM_PHYSICAL,<src.uid>

src.skill_gain skill_evalint
return 1

//***********************************************************
//      SPELL Cauterizar
//***********************************************************
[ITEMDEF i_scroll_cauterizar]
ID=01f2d
NAME=Pergaminho do Cauterizar
RESOURCES=i_carta,i_r_ginseng,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Cauterizar

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=105

[FUNCTION _s_cauterizar]
if (<flags>&statf_dead)
	sysmessagered O alvo não resistiu a cauterização
else
	hits += <argn1>
	if (<hits> >= <maxhits>)
	hits = <maxhits>
endif		
		
[Spell 105]
DEFNAME=s_cauterizar
NAME=Cauterizar
SOUND=0356
RUNES=MXG
CAST_TIME=30
RESOURCES=i_r_ginseng,i_r_cinza_sulfurosa
RUNE_ITEM=2273
SCROLL_ITEM=i_scroll_cauterizar
FLAGS=SPELLFLAG_TARG_CHAR | SPELLFLAG_GOOD 
EFFECT_ID=0
EFFECT=10,20
DURATION=0.0
MANAUSE=5
SKILLREQ=MAGERY 30.0
INTERRUPT=100.0,100.0

on=@start
if (<src.act.distance <UID>> > 1)
 sysmessageyellow Muito longe...
 return 1

endif
sfx 0227

on=@Effect
EFFECT 3 i_fx_smoke 0 64 0
local.dam=<EFFECTSPELL s_cauterizar,<argn2>,<src.uid>>
damage <LOCAL.DAM>,DAM_FIRE,<UID> 
local.dam += <eval <src.evaluatingintel>/30>
cry
timerf 1, _s_cauterizar <local.dam>

src.skill_gain skill_evalint
return 1

//***********************************************************
//      SPELL Fire Wall
//***********************************************************

[ITEMDEF i_scroll_fire_wall]
ID=01f2d
NAME=Pergaminho da Parede de Fogo
RESOURCES=i_carta,i_r_teia_de_aranha,i_r_asa_morcego,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Parede de fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=106

[Spell 106]
DEFNAME=s_parede_fogo
NAME=Parede de fogo
RUNES=FJI
CAST_TIME=25
RUNE_ITEM=2267
SCROLL_ITEM=i_scroll_fire_wall
RESOURCES=i_r_teia_de_aranha,i_r_asa_morcego,i_r_cinza_sulfurosa
FLAGS=SPELLFLAG_TARG_XYZ | SPELLFLAG_HARM | spellflag_scripted
EFFECT_ID=0
DURATION=40,120
EFFECT=5,10
MANAUSE=14
SKILLREQ=MAGERY 40.0
INTERRUPT=25.0,100.0

on=@start
sfx snd_elec_2

on=@Success
if (<belltest <magery>,80.0>) || !(<R9>)
 LOCAL.CREATEOBJECT1 = <eval i_fogo_armadilha_lo>
 LOCAL.CREATEOBJECT2 = <eval i_fogo_armadilha_ns>
else
 LOCAL.CREATEOBJECT1 = <eval i_fx_field_fire>
 LOCAL.CREATEOBJECT2 = <eval i_fx_field_fire_ns>
 endif
call src.f_line_field <src.EFFECTSPELL 106,<argn2>,<src.uid>>,<src.DURATIONSPELL 106,<argn2>,<src.uid>>



//***********************************************************
//      SPELL Mystic Flame
//***********************************************************
[ITEMDEF i_scroll_mysticFlame]
ID=01f2d
NAME=Pergaminho da Chama Mistica
RESOURCES=i_carta,i_r_asa_morcego,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Chama Mistica

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=107

[Spell 107]
DEFNAME=s_mysticFlame
NAME=Chama mistica
SOUND=0
RUNES=VC
CAST_TIME=28
RESOURCES=i_r_asa_morcego,i_r_cinza_sulfurosa
RUNE_ITEM=21015
SCROLL_ITEM=i_scroll_mysticFlame
FLAGS=SPELLFLAG_TARG_ITEM | spellflag_good | spellflag_eval_boosts
EFFECT_ID=0
EFFECT=2,10
DURATION=5*60,30*60
MANAUSE=25
SKILLREQ=MAGERY 60.0
INTERRUPT=100.0,100.0

on=@start
sfx 05c1

on=@SUCCESS
if (<act.type>==t_wand)
src.sysmessageyellow Itens de foco magico, como varinhas, nao podem receber esse efeito.
return 1
endif

//soh consegui fazer ele checar aqui (se achar que é melhor lá embaixo pode mudar. Evita colocar carga de flamestrike na t_wand.

on=@Effect
if (!<IsWeapon <UID>>) || !(STRMATCH(*ingot*,<serv.itemdef.<baseid>.resources>)
 src.sysmessageyellow Apenas armas de metal.
 return 1
ELIF (<more2>)
 src.sysmessageyellow Esta arma ja possui um tratamento especial
 return 1
ELIF (<src.distance <topobj>> > 1)
 src.sysmessageyellow Muito longe...
 return 1
endif
src.sfx 0356
f_mysticFlame <EFFECTSPELL s_mysticFlame,<src.magery>>

src.skill_gain skill_evalint
return 1

[FUNCTION f_mysticFlame]
tag.hcolor=<color>
more2=1
morex=51
damfire += <SRC.EFFECTSPELL s_mysticflame, <src.magery>, <src.uid>>
color=04ec
attr=<attr>|020
timerf <SRC.DURATIONSPELL s_mysticflame, <src.magery>, <src.uid>>,_f_mysticFlame,<damfire>
updatex

[FUNCTION _f_mysticFlame]
color=<tag.hcolor>
tag.hcolor
more2=0
morex=0
damfire -= <argn>
attr &= ~020
sfx 0208
updatex

//***********************************************************
//      SPELL Combustion
//***********************************************************
[ITEMDEF i_scroll_combustion]
ID=01f2d
NAME=Pergaminho da Combustao
RESOURCES=i_carta,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 60.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Combustao

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=108

[Spell 108]
DEFNAME=s_combustion
NAME=Combustao
SOUND=snd_SPELL_FIRE_BOLT
RUNES=WV
RESOURCES=i_r_cinza_sulfurosa
CAST_TIME=20
RUNE_ITEM=20736
SCROLL_ITEM=i_scroll_combustion
FLAGS=SPELLFLAG_TARG_CHAR | SPELLFLAG_DIR_ANIM | SPELLFLAG_HARM 
EFFECT_ID=0
EFFECT=5,15
DURATION=
MANAUSE=10
SKILLREQ=MAGERY 50.0
INTERRUPT=25.0,100.0

on=@start
if (<src.act.distance <UID>> > 3)
 sysmessageyellow Muito longe...
 return 1
endif

on=@SUCCESS
serv.newitem i_fx_glow
new.color = 00152
new.attr = attr_static|attr_decay
new.timer = 2
new.p = <act.p>


on=@Effect
src.act.f_pega_fogo, <EFFECTSPELL s_combustion,<src.magery>,<src.uid>>

src.skill_gain skill_evalint
return 1




//***********************************************************
//      SPELL MAGIC TORCH
//***********************************************************
[ITEMDEF i_scroll_magictorch]
ID=01f2d
NAME=Pergaminho da Tocha Magica
RESOURCES=i_carta,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 45.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Tocha Magica

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=109

[Spell 109]
DEFNAME=s_magictorch
NAME=Tocha magica
SOUND=054
RUNES=FI
CAST_TIME=35
RESOURCES=i_r_cinza_sulfurosa
RUNE_ITEM=2245
SCROLL_ITEM=i_scroll_magictorch
FLAGS=SPELLFLAG_GOOD | spellflag_scripted
EFFECT_ID=0
DURATION=5*60,30*60
MANAUSE=12
SKILLREQ=MAGERY 50.0
INTERRUPT=100.0,100.0

on=@start
if <src.restest i_torch_lit>
    src.sysmessageyellow Essa magia ja esta ativa.
    return 1
endif
sfx 05c1

on=@Effect
EFFECT 3 i_fx_sparkle_2 0 16 0 032
serv.newitem i_torch_lit
new.color 044eb
src.equip <new>
new.attr=08030
local.t=<DURATIONSPELL s_magicTorch,<argn2>>
new.timerf <LOCAL.t>,remove
src.timerf <local.t>,sfx,04b8


src.skill_gain skill_evalint
return 1


//***********************************************************
//      SPELL FIRE CELL
//***********************************************************
[ITEMDEF i_scroll_fire_cell]
ID=01f2d
NAME=Pergaminho da Cela do Fogo
RESOURCES=i_carta,i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_asa_morcego,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Cela de fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=110

[Spell 110]
DEFNAME=s_CelaFogo
NAME=Cela de Fogo
SOUND=snd_SPELL_FIRE_FIELD
RESOURCES=i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_asa_morcego,i_r_cinza_sulfurosa
RUNES=FXOI
CAST_TIME=50
RUNE_ITEM=2267
SCROLL_ITEM=i_scroll_fire_cell
FLAGS=SPELLFLAG_HARM | spellflag_scripted | spellflag_targ_char
EFFECT_ID=0
DURATION=45,90
MANAUSE=25
SKILLREQ=MAGERY 60.0
INTERRUPT=100.0,100.0

on=@Effect
f_cela_fogo <DURATIONSPELL s_CelaFogo,<src.Magery>>

src.skill_gain skill_evalint
return 1


//***********************************************************
//      SPELL FIRE Polymorph Fogo Fatuo
//***********************************************************
[ITEMDEF i_scroll_poly_fogo_fatuo]
ID=01f2d
NAME=Pergaminho da Forma Elemental
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
SKILLMAKE=INSCRIPTION 25.0, MAGERY 65.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Poly Fogo Fatuo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=111

[Spell 111]
DEFNAME=s_polymorph_fogo_fatuo
NAME=Forma Elemental
SOUND=snd_spell_fire_field
RUNES=WIHR
CAST_TIME=30
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
RUNE_ITEM=2280
SCROLL_ITEM=i_scroll_poly_fogo_fatuo
FLAGS=spellflag_scripted | spellflag_playeronly
EFFECT_ID=
EFFECT=
DURATION=60,5*60,
MANAUSE=25
SKILLREQ=MAGERY 60.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_spell_fire_field

on=@Effect
LOCAL.timer=<DURATIONSPELL s_polymorph_fogo_fatuo,<magery>,<UID>>
src.f_polymorph c_fogo_fatuo_poly,04EC,<LOCAL.timer>
src.haste <local.timer>

src.skill_gain skill_evalint
return 1

//***********************************************************
//      SPELL FIRE Flamestrike
//***********************************************************
[ITEMDEF i_scroll_flame_strike]
ID=01f2d
NAME=Pergaminho do Jato de Chamas
RESOURCES=i_carta,i_r_musgo_sangue,i_r_nightshade,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Jato de chamas

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=112

[Spell 112]
DEFNAME=s_flame_strike
NAME=Jato de chamas
SOUND=snd_SPELL_FLAMESTRIKE
RUNES=TV
CAST_TIME=2.0
RESOURCES=i_r_musgo_sangue,i_r_nightshade,i_r_cinza_sulfurosa
RUNE_ITEM=2290
SCROLL_ITEM=i_scroll_flame_strike
FLAGS=SPELLFLAG_TARG_OBJ|SPELLFLAG_HARM| SPELLFLAG_FX_TARG| SPELLFLAG_RESIST 
//| spellflag_eval_boosts
EFFECT_ID=03709
EFFECT=35,45
DURATION=0.0
MANAUSE=15
SKILLREQ=MAGERY 70.0
INTERRUPT=100.0,50.0


on=@Effect
EFFECT 3 i_fire_column 0 16 0
local.dam=<EFFECTSPELL s_flame_strike,<argn2>,<src.uid>>
local.dam += <eval <src.evaluatingintel>/100>
DAMAGE <local.dam>,DAM_FIRE,<src.uid>

src.skill_gain skill_evalint
return 1


//***********************************************************
//      SPELL Heat Weapon
//***********************************************************
[ITEMDEF i_scroll_heatWeapon]
ID=01f2d
NAME=Pergaminho do Esquentar Arma
RESOURCES=i_carta,i_r_olho_morto_vivo,i_r_perola_negra,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 62.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Esquentar arma

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=113

[Spell 113]
DEFNAME=s_heatWeapon
NAME=Esquentar arma
SOUND=snd_SPELL_FIREBALL
RUNES=HWC
CAST_TIME=40
RESOURCES=i_r_raiz_de_mandrake,i_r_teia_de_aranha,i_r_cinza_sulfurosa
RUNE_ITEM=23002
SCROLL_ITEM=i_scroll_heatWeapon
FLAGS=SPELLFLAG_TARG_OBJ | SPELLFLAG_HARM | SPELLFLAG_RESIST
EFFECT_ID=i_fire
EFFECT=3,10
DURATION=0.0
MANAUSE=22
SKILLREQ=MAGERY 70.0
INTERRUPT=50.0,100.0

on=@Effect
EFFECT 3 i_fire 0 16 0
DAMAGESPELL s_heatWeapon,dam_fire,<argn2>,<src.uid>
if (<weapon>)
 if (STRMATCH(*ingot*,<weapon.resources>)) && !(<weapon.attr>&08d10)
  f_heatWeapon <eval <EFFECTSPELL s_heatWeapon,<argn2>>*3>
  sysmessagered Sua arma esta muito quente para segurar!
 else
  src.sysmessagered A arma do seu oponente nao pode ser esquentada!
 endif
else
 src.sysmessagered Seu oponente nao esta armado!
endif


src.skill_gain skill_evalint
return 1

[FUNCTION f_heatWeapon]
obj=<weapon>
obj.tag.hcolor=<obj.color>
obj.tag.hname=<obj.name>
obj.name=<obj.name> (quente)
obj.color=04ec
obj.attr=<obj.attr>|010
obj.p=<topobj.p>
obj.timerf <argn>,_f_heatWeapon
obj.timer -1

[FUNCTION _f_heatWeapon]
color=<tag.hcolor>
tag.hcolor
name=<tag.hname>
tag.hname
attr &= ~010
attr |= 2
timer=600
emotered esfriou



//***********************************************************
//      SPELL FIRE Summon Horror do Abismo
//***********************************************************
[ITEMDEF i_scroll_horror_abismo]
ID=01f2d
NAME=Pergaminho de Conjurar Reptil das Chamas
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_raiz_de_mandrake,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 25.0, MAGERY 90.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Horror do Abismo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=114

[Spell 114]
DEFNAME=s_horror_abismo
NAME=Conjurar Reptil das Chamas
SOUND=
RUNES=YXK
CAST_TIME=20
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_raiz_de_mandrake,i_r_teia_de_aranha
RUNE_ITEM=23008
SCROLL_ITEM=i_scroll_horror_abismo
FLAGS=spellflag_targ_xyz | spellflag_targ_char | spellflag_scripted 
EFFECT_ID=0
EFFECT=10,40
DURATION=2*60,4*60
MANAUSE=25
SKILLREQ=MAGERY 80.0
INTERRUPT=100.0,100.0

on=@Start
SFX 0215

on=@success
LOCAL.TIME=<DURATIONSPELL s_horror_abismo,<argn2>,<UID>>
LOCAL.EFFECT=<EFFECTSPELL s_horror_abismo,<argn2>,<UID>>
obj=<src.f_summon_creature c_horror_abismo_sum,<LOCAL.TIME>>
obj.Wrestling += <eval(<local.effect>*5)>
obj.tag0.moddam += <eval <local.effect>/10>
obj.resfire = <eval(<local.effect>*5)/4>


src.skill_gain skill_evalint
return 0



//***********************************************************
//      SPELL FIRE Polymorph Cobra
//***********************************************************
[ITEMDEF i_scroll_poly_cobra]
ID=01f2d
NAME=Pergaminho da Forma de Cobra
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
SKILLMAKE=INSCRIPTION 25.0, MAGERY 85.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Poly Cobra

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=115

[Spell 115]
DEFNAME=s_polymorph_cobra
NAME=Forma de Cobra
SOUND=snd_spell_fire_field
RUNES=WXR
CAST_TIME=80
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
RUNE_ITEM=21019
SCROLL_ITEM=i_scroll_poly_cobra
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_harm | SPELLFLAG_RESIST 
EFFECT_ID=
EFFECT=
DURATION=10,30
MANAUSE=40
SKILLREQ=MAGERY 80.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_spell_fire_field

on=@Effect
LOCAL.timer=<DURATIONSPELL s_polymorph_cobra,<src.magery>,<src.UID>>
f_polymorph c_snake,0,<LOCAL.timer>

src.skill_gain skill_evalint
return 1



//***********************************************************
//      SPELL FIRE Explosion
//***********************************************************
[ITEMDEF i_scroll_explosao]
ID=01f2d
NAME=Pergaminho da Explosao
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Jato de chamas

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=116

[Spell 116]
DEFNAME=s_explosao
NAME=Explosao
SOUND=snd_SPELL_EXPLOSION
RUNES=VOF
CAST_TIME=30
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
RUNE_ITEM=2282
SCROLL_ITEM=i_scroll_explosao
FLAGS=SPELLFLAG_TARG_CHAR |SPELLFLAG_DIR_ANIM| SPELLFLAG_HARM |SPELLFLAG_FX_TARG| SPELLFLAG_RESIST | SPELLFLAG_SCRIPTED
EFFECT_ID=i_fx_explode
EFFECT=35,45
DURATION=
MANAUSE=25
SKILLREQ=MAGERY 90.0
INTERRUPT=100.0,100.0

ON=@EFFECT
local.dam=<EFFECTSPELL 116,<argn2>,<src.uid>>
obj=<src.uid>
ref1=<uid>
damage <local.dam>,dam_fire,<obj>
effect 3 i_fx_explosion_2 0 16 0
forchars <eval <local.dam>/20>
 if (<uid>!=<obj>) && (<uid>!=<ref1>)
  damage <eval <local.dam>/<distance <ref1>>>,dam_fire,<obj>
  effect 3 i_fx_explosion_3 0 16 0
 endif
end
sfx snd_SPELL_EXPLOSION

src.skill_gain skill_evalint
return 1



//***********************************************************
//      SPELL FIRE Summon Homem Lagarto
//***********************************************************
[ITEMDEF i_scroll_homem_lagarto]
ID=01f2d
NAME=Pergaminho da Horda das Chamas
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_raiz_de_mandrake,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 25.0, MAGERY 110.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Horda das Chamas

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=117

[Spell 117]
DEFNAME=s_homem_lagarto
NAME=Horda das Chamas
SOUND=
RUNES=YAR
CAST_TIME=30
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_raiz_de_mandrake,i_r_teia_de_aranha
RUNE_ITEM=2302
SCROLL_ITEM=i_scroll_homem_lagarto
FLAGS=spellflag_scripted|SPELLFLAG_HARM | spellflag_targ_char |
EFFECT_ID=0
EFFECT=
DURATION=30,60
MANAUSE=30
SKILLREQ=MAGERY 90.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_elec_2


on=@Select
if (<argn3>==3)
 sysmessageyellow Para atacar quem?
endif

on=@Effect
local.crit=<MAXIMUM <eval <src.evaluatingintel>/200>,2>
local.time=<DURATIONSPELL s_homem_lagarto,<argn2>,<src.uid>>
for i 1 <local.crit>
 obj=<src.f_summon_creature <hval {c_lizardman_sum 3, c_elem_fire_sum 1}>, <local.time>>
 obj.attack <uid>
end


src.skill_gain skill_evalint
return 1


//***********************************************************
//      SPELL FIRE Meteor Shower
//***********************************************************
[ITEMDEF i_scroll_chuva_meteoros]
ID=01f2d
NAME=Pergaminho da Chuva de Meteoros
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_asa_morcego,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Chuva de Meteoros

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=118

[ITEMDEF i_rune_chuva_meteoros]
NAME=chuva de meteoros
ID=i_gold
TYPE=T_EXPLOSION

//act.morex=10 // damage
//act.morey=090 // wFlags DAMAGE_GENERAL
//act.morez=4 // iDist

on=@create
attr=04092

on=@timer
if (!<tag0.do>)
 newitem <hval {01364 0136b}>
 new.color=04ec
 new.name=meteorito
 new.attr=012
 new.timer <R10,65>
 new.p=<p>
 new.effect 2 i_fire_column 0 16 0
 new.effect 2 i_fire 0 3000 0
 sfx {011b 1 011D 1 0306 1 0207 1}
 tag.do=1
else
 timerf 2 remove
 return 1
endif


[Spell 118]
// NOTE: Does Meteor Swarm have a duration or does it just send out one blast of 'rocks' ??? IF a duration then certainly no more the 30secs IMO.
DEFNAME=s_chuva_meteoros
NAME=Chuva de Meteoros
SOUND=011b
RUNES=FKDY
CAST_TIME=60
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_asa_morcego,i_r_teia_de_aranha
RUNE_ITEM=2294
SCROLL_ITEM=i_scroll_chuva_meteoros
FLAGS=SPELLFLAG_TARG_XYZ | SPELLFLAG_HARM | spellflag_scripted
EFFECT_ID=0
EFFECT=20,30
DURATION=0.0
MANAUSE=30
SKILLREQ=MAGERY 100.0
INTERRUPT=100.0,100.0

on=@Start
if (<src.flags>&statf_indoors) || (<region.flags>&region_flag_underground)
 src.sysmessageyellow Impossivel em lugares cobertos
 return 1
endif

on=@Success
src.newitem i_rune_chuva_meteoros
new.p=<src.actp>
new.f_spl_chuva_meteoros <argn2>

[FUNCTION f_spl_chuva_meteoros]
local.rad=<eval <src.EFFECTSPELL 118,<argn1>,<src.uid>>/6>
local.dam=<eval <src.EFFECTSPELL 118,<argn1>,<src.uid>>+6>
morex=<local.dam>
morey=(DAM_PHYSICAL|DAM_FIRE)
morez=3
timerd <R2,6>
src.effect 4 2
local.ii=<eval <src.EFFECTSPELL 118,<argn1>,<src.uid>>/5>
for i 1 <local.ii>
 newitem <DEFNAME>
 new.morex <R<eval <local.dam>/2>,<local.dam>>
 new.morey=(<DEF.DAM_PHYSICAL>|<DEF.DAM_FIRE>)
 new.morez=<R2,3>
 new.link=<src>
 new.movenear <uid>,<R1,<local.rad>>
 new.timerd <R2,<R10,32>>
endif


//***********************************************************
//      SPELL Summon Fire Elemental
//***********************************************************
[ITEMDEF i_scroll_conj_elem_fire]
ID=01f2d
NAME=Pergaminho do Conjurar Elemental do Fogo
RESOURCES=i_carta,i_r_teia_de_aranha,i_r_asa_morcego,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Elemntal do fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=119

[Spell 119]
DEFNAME=s_conj_elem_fire
NAME=Conjurar Elem. do Fogo
RUNES=YRV
CAST_TIME=50
RESOURCES=i_r_teia_de_aranha,i_r_asa_morcego,i_r_cinza_sulfurosa
RUNE_ITEM=2302
FLAGS=spellflag_targ_xyz | spellflag_scripted 
SCROLL_ITEM=i_scroll_conj_elem_fire
RESOURCES=i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_asa_morcego,i_r_cinza_sulfurosa
EFFECT_ID=0
DURATION=2*60,4*60
EFFECT=10,40
MANAUSE=30
SKILLREQ=MAGERY 100.0
INTERRUPT=25.0,100.0

on=@start
sfx snd_elec_2

on=@success
LOCAL.TIME=<DURATIONSPELL s_conj_elem_fire,<argn2>,<src.UID>>
LOCAL.EFFECT=<EFFECTSPELL s_conj_elem_fire,<argn2>,<src.UID>>
obj=<src.f_summon_creature c_elem_fire_sum,<LOCAL.TIME>>
obj.Wrestling = <eval(<local.effect>*20)>
obj.tag0.moddam += <eval <local.effect>/5>
obj.respoison = <eval <local.effect>*2>
obj.resenergy = <eval <local.effect>*2>
obj.modar += <eval <local.effect>>

src.skill_gain skill_evalint
return 0



[EOF]

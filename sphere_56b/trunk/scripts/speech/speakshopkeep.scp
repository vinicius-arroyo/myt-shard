//****************************************************************************
// SPHERE by : Menasoft ©1997-2017
// www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: Wednesday, Aug 16, 2017
//
VERSION=0.56d

[SPEECH spk_shopkeep]
ON=*sell*
	IF ((<SRC.FAME.INFAMOUS>) || (<SRC.FAME.OUTLAW>))
		IF (<COMPLEXITY.HIGH>)
			DORAND 3
				SAY Doubtless aught thou hast to sell would be stolen goods.
				SAY I trust not aught that ye sell, given thy reputation.
				SAY Thou wouldst doubtless try to cheat me, wert thou trying to sell me aught.
			ENDDO
		ELIF (<COMPLEXITY.MEDIUM>)
			DORAND 3
				SAY I am not a fence, so sell me not thy stolen goods.
				SAY Thou'rt untrustworthy--anything thou sellest must be bad.
				SAY Thou wishest to sell aught? I might buy, though I suspect thou'rt a cheat.
			ENDDO
		ELSE
			DORAND 3
				SAY Thou'rt wantin' to sell to me? I can't afford the price thou wants... the price thou'rt willin' to break me head to get...
				SAY Thou wants to sell somethin'? 'Tis probably stolen.
				SAY Thou'rt a cheat, but I might buy from thee anyway.
			ENDDO
		ENDIF
	ELSE
		IF (<COMPLEXITY.HIGH>)
			DORAND 7
				SAY If thou hast aught to sell, mayhap I am interested--give it to me and I shall value it for thee. Otherwise thou art welcome to buy my goods.
				SAY Depending on what thou hast to sell, I may be interested in purchasing it to add to my stock. Hand it to me and I shall decide its worth.
				SAY If thou art interested in selling an item to me, then thou must give it to me and I'll decide if I need it in my stock. Otherwise, thou can buy anything that I sell here.
				SAY I might be willing to purchase some supplies from thee. Thou wouldst need to hand them to me for appraisal. But if thou wouldst like to buy something, just say so.
				SAY Let me see what thou art offering to sell me, if indeed that is thy intent. Or thou couldst buy something of mine.
				SAY I am a <JOB>. I could purchase those things that I supply.
				SAY If thou dost wish to sell something to me, hand it over and I shall determine its value. Otherwise thou art welcome to buy some of my items.
			ENDDO
		ELIF (<COMPLEXITY.MEDIUM>)
			DORAND 7
				SAY I may be interested in what thou have-let me see it. Otherwise thou art welcome to buy my goods.
				SAY I could be interested in purchasing what thou has to add to my stock. Hand it to me and I shall decide its worth.
				SAY If thou art interested in selling an item to me, then thou must give it to me and I'll decide if I need it in my stock. Or thou can buy anything that I sell here.
				SAY I may be willing to purchase some supplies from thee. Hand them to me for appraisal. And if thou wouldst like to buy something from me, just say so.
				SAY Let me see what thou art offering to sell me, if indeed that is what thou dost mean. Or thou couldst buy something of mine.
				SAY I am a <JOB>. I could purchase those things that I supply.
				SAY If thou dost wish to sell something to me, hand it over and I shall determine its value. Otherwise thou art welcome to buy some of my items.
			ENDDO
		ELSE
			DORAND 7
				SAY What's thou got to sell? Give it here. Or buy somethin'. Or go 'way.
				SAY If I sell it, I buys it too. Let me see it. Oh, and if thou'rt wantin' to buy somethin', jus' say so.
				SAY Thee got summat to sell? Give it here.
				SAY If thou'rt wantin' to but somethin', then say the word.  Buy... that's the word. Or give me what thou'rt wantin' to sell me.
				SAY Thou wants to sell me somethin'? Hand it over and I'll see if it's worth nothin'.
				SAY I could take some supplies off thy hands. Give 'em to me for a once-over. And look around abit. Just say thou wants to buy somethin' and I'll see if I got it.
				SAY I could purchase those things that I supply.
			ENDDO
		ENDIF
	ENDIF
	SELL

ON=*buy*
	IF ((<SRC.FAME.INFAMOUS>) || (<SRC.FAME.OUTLAW>))
		IF ((<SRC.KARMA.WICKED>) || (<SRC.KARMA.BELLIGERANT>))
			IF (<COMPLEXITY.HIGH>)
				DORAND 3
					SAY If thou needest to buy something, I can supply things from my wares. No stealing needed.
					SAY Thou'rt more like to steal than to buy supplies, but nonetheless I can offer thee what I sell.
					SAY Life is bad enough that I have no qualms about selling my stock to a known <SRC.SEX scoundrel/hussy>.
				ENDDO
			ELIF (<COMPLEXITY.MEDIUM>)
				DORAND 3
					SAY 'Twould be just my ill-luck if thou were to steal from me.
					SAY Prithee, steal not! I can sell thee supplies.
					SAY Well, I could hardly get in worse trouble... even if I do help a criminal such as thee. I can sell thee supplies.
				ENDDO
			ELSE
				DORAND 3
					SAY Aww, no stealin', by thy <SRC.SEX lord/lady>ship's leave!
					SAY Sure, I can sell thee stuff even if thou'rt a bit whiffy.
					SAY I can sell thee things if thou ain't hurtin' me.
				ENDDO
			ENDIF
		ELSE
			IF (<COMPLEXITY.HIGH>)
				DORAND 3
					SAY Thou must not steal from me, mind! I can sell thee supplies.
					SAY If thou needest supplies, why art thou not looking to steal them? I can sell them to thee if thou dost behave.
					SAY I trust thee not, but will offer to sell thee supplies.
				ENDDO
			ELIF (<COMPLEXITY.MEDIUM>)
				DORAND 3
					SAY Prithee, harm me not! I am but a merchant of supplies!
					SAY Thou'rt speaking of buying, yes? Not of stealing?
					SAY Thou'rt unsavory, but I am willing to sell to thee.
				ENDDO
			ELSE
				DORAND 3
					SAY Don' hurt me! I can sell thee stuff.
					SAY Please, don' steal nothin'.
					SAY If thou don' beat me up, I can sell to thee.
				ENDDO
			ENDIF
		ENDIF
	ELSE
		IF ((<SRC.KARMA.WICKED>) || (<SRC.KARMA.BELLIGERANT>))
			IF (<COMPLEXITY.HIGH>)
				DORAND 3
					SAY Thou canst buy supplies from me if thou art willing to waste thy time with one my mine humble station.
					SAY If thou'rt willing to interact with one as miserable as I, thou canst buy supplies from me.
					SAY I can sell thee supplies. 'Tis hardly a hardship--not at all like the rest of my hopeless life.
				ENDDO
			ELIF (<COMPLEXITY.MEDIUM>)
				DORAND 3
					SAY Thou meanest one such as thee dost not have everything thou needest already?
					SAY If thou wilt buy from one such as me, I have much to sell.
					SAY Making money may be th'only thing to cheer my dismal life.
				ENDDO
			ELSE
				DORAND 3
					SAY Thou wishes to buy from me? Thee? A big <SRC.SEX lord/lady> type?
					SAY I'm a little worm to thee, but I can make thee spend money in my shop!
					SAY I could use anythin' to cheer me up. Want to buy from me?
				ENDDO
			ENDIF
		ELSE
			IF (<COMPLEXITY.HIGH>)
				DORAND 3
					SAY I sell many wares and thou'rt welcome to buy from me!
					SAY If thou seekest supplies, thou canst buy from me.
					SAY If thou seekest to buy, I can sell to thee.
				ENDDO
			ELIF (<COMPLEXITY.MEDIUM>)
				DORAND 3
					SAY Thou mayst purchase supplies from me.
					SAY I would gladly sell to thee of my many wares.
					SAY My shop sells many things.
				ENDDO
			ELSE
				DORAND 3
					SAY I'm here to sell thee supplies.
					SAY I got lots to sell!
					SAY Thou surely wants all the things that I can sell thee.
				ENDDO
			ENDIF
		ENDIF
	ENDIF
	BUY

[SPEECH spk_banker]
ON=*bank*
	SRC.BANKSELF

ON=*balance*
ON=*statement*
	IF (<SERV.FeatureTOL> & 02)	//FEATURE_TOL_VIRTUALGOLD
		SAY Thy current bank balance is <eval <SRC.VIRTUALGOLD>/1000000000> platinum and <eval <SRC.VIRTUALGOLD>%1000000000> gold.
	ELSE
		SAY Thy current bank balance is <SRC.BANKBALANCE> gold.
	ENDIF

ON=*check*
	IF !(<DEF0.i_bank_check>) || (<SERV.FeatureTOL> & 02)	//FEATURE_TOL_VIRTUALGOLD
		return 1
	ENDIF
	ARGS=<dSTRARG <STREAT <ARGS>>>
	IF (<ARGS> < 5000)
		SAY We cannot create checks for such a paltry amount of gold!
		return 1
	ELIF (<ARGS> > 1000000)
		SAY Our policies prevent us from creating checks worth that much!
		return 1
	ELIF (<ARGS> > <SRC.GOLD>)
		SAY Ah, art thou trying to fool me? Thou hast not so much gold!
		return 1
	ENDIF
	SRC.GOLD -= <ARGS>
	SERV.NEWITEM i_bank_check
	NEW.MORE=<ARGS>
	NEW.CONT <SRC.FINDLAYER.layer_bankbox>
	SAY Into your bank box I have placed a check in the amount of: <ARGS>

ON=*withdraw*
	ARGS=<dSTRARG <STREAT <ARGS>>>
	IF (<ARGS> <= 0)
		SAY Thou must tell me how much thou wishest to withdraw.
		return 1
	ELIF (<ARGS> > <SERV.ItemsMaxAmount>)
		SAY Thou canst not withdraw so much at one time!
		return 1
	ELIF (<SERV.FeatureTOL> & 02)	//FEATURE_TOL_VIRTUALGOLD
		IF (<ARGS> > <SRC.VIRTUALGOLD>)
			SAY Ah, art thou trying to fool me? Thou hast not so much gold!
			return 1
		ENDIF
		SRC.VIRTUALGOLD -= <ARGS>
	ELSE
		IF (<ARGS> > <SRC.BANKBALANCE>)
			SAY Ah, art thou trying to fool me? Thou hast not so much gold!
			return 1
		ENDIF
		SRC.GOLD -= <ARGS>
	ENDIF
	SRC.NEWGOLD <ARGS>
	SAY Thou hast withdrawn gold from thy account.

[SPEECH spk_bodysculptor]
ON=*buy*
	IF (<SRC.f_isgarg>)
		SRC.SYSMESSAGE Gargoyles cannot use the services of a body sculptor.
		return 1
	ELIF !((<SRC.CLIENTISKR>) || (<SRC.CLIENTISSA>))
		SRC.SYSMESSAGE Your legacy self looks too good to change.
		return 1
	ENDIF
	SDIALOG d_body_sculptor

[SPEECH spk_hairstylist]
ON=*buy*
	IF (<SRC.FLAGS> & statf_dead)
		SRC.SYSMESSAGE You cannot use the services of a hairstylist right now.
		return 1
	ELIF (<SRC.FLAGS> & statf_polymorph)
		SRC.SYSMESSAGE Your current form does not allow for new styles.
		return 1
	ELIF (<SRC.f_isgarg>)
		SRC.SYSMESSAGE A hairstylist cannot style your horns!
		return 1
	ENDIF
	SDIALOG d_hairstylist

[EOF]
